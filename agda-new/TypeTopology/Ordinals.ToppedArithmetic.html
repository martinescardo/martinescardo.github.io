<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Ordinals.ToppedArithmetic</title><link rel="stylesheet" href="Agda.css"><script type="text/javascript" src="highlight-hover.js"></script></head><body><pre class="Agda"><a id="1" class="Background">Martin Escardo, 29 June 2018

To get closure under sums constructively, we need to restrict to
particular kinds of ordinals. Having a top element is a simple
sufficient condition, which holds in the applications we have in mind
(for compact ordinals).  Classically, ordinals with a top element are
precisely the successor ordinals. Constructively, ℕ∞ is an example of
an ordinal with a top element, which &quot;is not&quot; a successor ordinal, as
its top element is not isolated.

TODO. Generalize this from 𝓤₀ to an arbitrary universe. The
(practical) problem is that the type of natural numbers is defined at
𝓤₀. We could (1) either using universe lifting, or (2) define the type
in any universe (like we did for the the types 𝟘 and 𝟙). But (1) is
cumbersome and (2) requires much work in other modules.


</a><a id="800" class="Markup">\begin{code}</a>

<a id="814" class="Symbol">{-#</a> <a id="818" class="Keyword">OPTIONS</a> <a id="826" class="Pragma">--safe</a> <a id="833" class="Pragma">--without-K</a> <a id="845" class="Symbol">#-}</a>

<a id="850" class="Keyword">open</a> <a id="855" class="Keyword">import</a> <a id="862" href="UF.FunExt.html" class="Module">UF.FunExt</a>

<a id="873" class="Keyword">module</a> <a id="880" href="Ordinals.ToppedArithmetic.html" class="Module">Ordinals.ToppedArithmetic</a>
        <a id="914" class="Symbol">(</a><a id="915" href="Ordinals.ToppedArithmetic.html#915" class="Bound">fe</a> <a id="918" class="Symbol">:</a> <a id="920" href="UF.FunExt.html#995" class="Function">FunExt</a><a id="926" class="Symbol">)</a>
       <a id="935" class="Keyword">where</a>

<a id="942" class="Keyword">open</a> <a id="947" class="Keyword">import</a> <a id="954" href="UF.Subsingletons.html" class="Module">UF.Subsingletons</a>

<a id="972" class="Keyword">open</a> <a id="977" class="Keyword">import</a> <a id="984" href="MLTT.Spartan.html" class="Module">MLTT.Spartan</a>
<a id="997" class="Keyword">open</a> <a id="1002" class="Keyword">import</a> <a id="1009" href="CoNaturals.Type.html" class="Module">CoNaturals.Type</a>
<a id="1025" class="Keyword">open</a> <a id="1030" class="Keyword">import</a> <a id="1037" href="TypeTopology.SquashedSum.html" class="Module">TypeTopology.SquashedSum</a> <a id="1062" href="Ordinals.ToppedArithmetic.html#915" class="Bound">fe</a>
<a id="1065" class="Keyword">open</a> <a id="1070" class="Keyword">import</a> <a id="1077" href="Notation.CanonicalMap.html" class="Module">Notation.CanonicalMap</a>

<a id="1100" class="Keyword">open</a> <a id="1105" class="Keyword">import</a> <a id="1112" href="Ordinals.Type.html" class="Module">Ordinals.Type</a>
<a id="1126" class="Keyword">open</a> <a id="1131" class="Keyword">import</a> <a id="1138" href="Ordinals.Arithmetic.html" class="Module">Ordinals.Arithmetic</a> <a id="1158" href="Ordinals.ToppedArithmetic.html#915" class="Bound">fe</a>
<a id="1161" class="Keyword">open</a> <a id="1166" class="Keyword">import</a> <a id="1173" href="Ordinals.WellOrderArithmetic.html" class="Module">Ordinals.WellOrderArithmetic</a>
<a id="1202" class="Keyword">open</a> <a id="1207" class="Keyword">import</a> <a id="1214" href="Ordinals.ToppedType.html" class="Module">Ordinals.ToppedType</a> <a id="1234" href="Ordinals.ToppedArithmetic.html#915" class="Bound">fe</a>
<a id="1237" class="Keyword">open</a> <a id="1242" class="Keyword">import</a> <a id="1249" href="Ordinals.Injectivity.html" class="Module">Ordinals.Injectivity</a>
<a id="1270" class="Keyword">open</a> <a id="1275" class="Keyword">import</a> <a id="1282" href="Ordinals.Underlying.html" class="Module">Ordinals.Underlying</a>

<a id="Ordᵀ"></a><a id="1303" href="Ordinals.ToppedArithmetic.html#1303" class="Function">Ordᵀ</a> <a id="1308" class="Symbol">=</a> <a id="1310" href="Ordinals.ToppedType.html#865" class="Function">Ordinalᵀ</a> <a id="1319" href="Agda.Primitive.html#915" class="Primitive">𝓤₀</a>

<a id="succₒ"></a><a id="1323" href="Ordinals.ToppedArithmetic.html#1323" class="Function">succₒ</a> <a id="1329" class="Symbol">:</a> <a id="1331" href="Ordinals.Type.html#658" class="Function">Ordinal</a> <a id="1339" href="MLTT.Universes.html#3834" class="Generalizable">𝓤</a> <a id="1341" class="Symbol">→</a> <a id="1343" href="Ordinals.ToppedType.html#865" class="Function">Ordinalᵀ</a> <a id="1352" href="MLTT.Universes.html#3834" class="Generalizable">𝓤</a>
<a id="1354" href="Ordinals.ToppedArithmetic.html#1323" class="Function">succₒ</a> <a id="1360" href="Ordinals.ToppedArithmetic.html#1360" class="Bound">α</a> <a id="1362" class="Symbol">=</a> <a id="1364" href="Ordinals.ToppedArithmetic.html#1360" class="Bound">α</a> <a id="1366" href="Ordinals.Arithmetic.html#1823" class="Function Operator">+ₒ</a> <a id="1369" href="Ordinals.Arithmetic.html#904" class="Function">𝟙ₒ</a>  <a id="1373" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a>
          <a id="1385" href="Ordinals.WellOrderArithmetic.html#4095" class="Function">plus.top-preservation</a>
           <a id="1418" class="Symbol">(</a><a id="1419" href="Ordinals.Underlying.html#344" class="Field">underlying-order</a> <a id="1436" href="Ordinals.ToppedArithmetic.html#1360" class="Bound">α</a><a id="1437" class="Symbol">)</a>
           <a id="1450" class="Symbol">(</a><a id="1451" href="Ordinals.Underlying.html#344" class="Field">underlying-order</a> <a id="1468" href="Ordinals.Arithmetic.html#904" class="Function">𝟙ₒ</a><a id="1470" class="Symbol">)</a>
           <a id="1483" class="Symbol">(</a><a id="1484" href="Ordinals.WellOrderArithmetic.html#1108" class="Function">prop.topped</a> <a id="1496" href="MLTT.Unit.html#129" class="Record">𝟙</a> <a id="1498" href="UF.Subsingletons.html#2858" class="Function">𝟙-is-prop</a> <a id="1508" href="MLTT.Unit.html#162" class="InductiveConstructor">⋆</a><a id="1509" class="Symbol">)</a>

<a id="succₒ-is-trichotomous"></a><a id="1512" href="Ordinals.ToppedArithmetic.html#1512" class="Function">succₒ-is-trichotomous</a> <a id="1534" class="Symbol">:</a> <a id="1536" class="Symbol">(</a><a id="1537" href="Ordinals.ToppedArithmetic.html#1537" class="Bound">α</a> <a id="1539" class="Symbol">:</a> <a id="1541" href="Ordinals.Type.html#658" class="Function">Ordinal</a> <a id="1549" href="MLTT.Universes.html#3834" class="Generalizable">𝓤</a><a id="1550" class="Symbol">)</a>
                      <a id="1574" class="Symbol">→</a> <a id="1576" href="Ordinals.Type.html#1307" class="Function">is-trichotomous</a> <a id="1592" href="Ordinals.ToppedArithmetic.html#1537" class="Bound">α</a>
                      <a id="1616" class="Symbol">→</a> <a id="1618" href="Ordinals.Type.html#1307" class="Function">is-trichotomous</a> <a id="1634" href="Notation.CanonicalMap.html#502" class="Function Operator">[</a> <a id="1636" href="Ordinals.ToppedArithmetic.html#1323" class="Function">succₒ</a> <a id="1642" href="Ordinals.ToppedArithmetic.html#1537" class="Bound">α</a> <a id="1644" href="Notation.CanonicalMap.html#502" class="Function Operator">]</a>
<a id="1646" href="Ordinals.ToppedArithmetic.html#1512" class="Function">succₒ-is-trichotomous</a> <a id="1668" href="Ordinals.ToppedArithmetic.html#1668" class="Bound">α</a> <a id="1670" href="Ordinals.ToppedArithmetic.html#1670" class="Bound">t</a> <a id="1672" class="Symbol">=</a> <a id="1674" href="Ordinals.Arithmetic.html#2024" class="Function">+ₒ-is-trichotomous</a> <a id="1693" href="Ordinals.ToppedArithmetic.html#1668" class="Bound">α</a> <a id="1695" href="Ordinals.Arithmetic.html#904" class="Function">𝟙ₒ</a> <a id="1698" href="Ordinals.ToppedArithmetic.html#1670" class="Bound">t</a> <a id="1700" href="Ordinals.Arithmetic.html#1147" class="Function">𝟙ₒ-is-trichotomous</a>

<a id="𝟙ᵒ"></a><a id="1720" href="Ordinals.ToppedArithmetic.html#1720" class="Function">𝟙ᵒ</a> <a id="𝟚ᵒ"></a><a id="1723" href="Ordinals.ToppedArithmetic.html#1723" class="Function">𝟚ᵒ</a> <a id="1726" class="Symbol">:</a> <a id="1728" href="Ordinals.ToppedType.html#865" class="Function">Ordinalᵀ</a> <a id="1737" href="MLTT.Universes.html#3834" class="Generalizable">𝓤</a>
<a id="1739" href="Ordinals.ToppedArithmetic.html#1720" class="Function">𝟙ᵒ</a> <a id="1742" class="Symbol">=</a> <a id="1744" href="Ordinals.Arithmetic.html#904" class="Function">𝟙ₒ</a> <a id="1747" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="1749" href="Ordinals.WellOrderArithmetic.html#1108" class="Function">prop.topped</a> <a id="1761" href="MLTT.Unit.html#129" class="Record">𝟙</a> <a id="1763" href="UF.Subsingletons.html#2858" class="Function">𝟙-is-prop</a> <a id="1773" href="MLTT.Unit.html#162" class="InductiveConstructor">⋆</a>
<a id="1775" href="Ordinals.ToppedArithmetic.html#1723" class="Function">𝟚ᵒ</a> <a id="1778" class="Symbol">=</a> <a id="1780" href="Ordinals.ToppedArithmetic.html#1323" class="Function">succₒ</a> <a id="1786" href="Ordinals.Arithmetic.html#904" class="Function">𝟙ₒ</a>

<a id="ℕ∞ᵒ"></a><a id="1790" href="Ordinals.ToppedArithmetic.html#1790" class="Function">ℕ∞ᵒ</a> <a id="1794" class="Symbol">:</a> <a id="1796" href="Ordinals.ToppedArithmetic.html#1303" class="Function">Ordᵀ</a>
<a id="1801" href="Ordinals.ToppedArithmetic.html#1790" class="Function">ℕ∞ᵒ</a> <a id="1805" class="Symbol">=</a> <a id="1807" class="Symbol">(</a><a id="1808" href="Ordinals.Arithmetic.html#1481" class="Function">ℕ∞ₒ</a> <a id="1812" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="1814" href="CoNaturals.GenericConvergentSequence.html#6051" class="Function">∞</a> <a id="1816" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="1818" href="CoNaturals.GenericConvergentSequence.html#23372" class="Function">∞-top</a><a id="1823" class="Symbol">)</a>

<a id="1826" class="Markup">\end{code}</a><a id="1836" class="Background">

Sum of an ordinal-indexed family of ordinals:

</a><a id="1885" class="Markup">\begin{code}</a>

<a id="∑"></a><a id="1899" href="Ordinals.ToppedArithmetic.html#1899" class="Function">∑</a> <a id="1901" class="Symbol">:</a> <a id="1903" class="Symbol">(</a><a id="1904" href="Ordinals.ToppedArithmetic.html#1904" class="Bound">τ</a> <a id="1906" class="Symbol">:</a> <a id="1908" href="Ordinals.ToppedType.html#865" class="Function">Ordinalᵀ</a> <a id="1917" href="MLTT.Universes.html#3834" class="Generalizable">𝓤</a><a id="1918" class="Symbol">)</a> <a id="1920" class="Symbol">→</a> <a id="1922" class="Symbol">(</a><a id="1923" href="Ordinals.Underlying.html#315" class="Field Operator">⟨</a> <a id="1925" href="Ordinals.ToppedArithmetic.html#1904" class="Bound">τ</a> <a id="1927" href="Ordinals.Underlying.html#315" class="Field Operator">⟩</a> <a id="1929" class="Symbol">→</a> <a id="1931" href="Ordinals.ToppedType.html#865" class="Function">Ordinalᵀ</a> <a id="1940" href="MLTT.Universes.html#3834" class="Generalizable">𝓤</a><a id="1941" class="Symbol">)</a> <a id="1943" class="Symbol">→</a> <a id="1945" href="Ordinals.ToppedType.html#865" class="Function">Ordinalᵀ</a> <a id="1954" href="MLTT.Universes.html#3834" class="Generalizable">𝓤</a>
<a id="1956" href="Ordinals.ToppedArithmetic.html#1899" class="Function">∑</a> <a id="1958" class="Symbol">{</a><a id="1959" href="Ordinals.ToppedArithmetic.html#1959" class="Bound">𝓤</a><a id="1960" class="Symbol">}</a> <a id="1962" class="Symbol">((</a><a id="1964" href="Ordinals.ToppedArithmetic.html#1964" class="Bound">X</a> <a id="1966" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="1968" href="Ordinals.ToppedArithmetic.html#1968" class="Bound Operator">_&lt;_</a> <a id="1972" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="1974" href="Ordinals.ToppedArithmetic.html#1974" class="Bound">o</a><a id="1975" class="Symbol">)</a> <a id="1977" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="1979" href="Ordinals.ToppedArithmetic.html#1979" class="Bound">t</a><a id="1980" class="Symbol">)</a> <a id="1982" href="Ordinals.ToppedArithmetic.html#1982" class="Bound">υ</a> <a id="1984" class="Symbol">=</a> <a id="1986" class="Symbol">((</a><a id="1988" href="MLTT.Sigma.html#423" class="Function">Σ</a> <a id="1990" href="Ordinals.ToppedArithmetic.html#1990" class="Bound">x</a> <a id="1992" href="MLTT.Sigma.html#423" class="Function">꞉</a> <a id="1994" href="Ordinals.ToppedArithmetic.html#1964" class="Bound">X</a> <a id="1996" href="MLTT.Sigma.html#423" class="Function">,</a> <a id="1998" href="Ordinals.Underlying.html#315" class="Field Operator">⟨</a> <a id="2000" href="Ordinals.ToppedArithmetic.html#1982" class="Bound">υ</a> <a id="2002" href="Ordinals.ToppedArithmetic.html#1990" class="Bound">x</a> <a id="2004" href="Ordinals.Underlying.html#315" class="Field Operator">⟩</a><a id="2005" class="Symbol">)</a> <a id="2007" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a>
                              <a id="2039" href="Ordinals.WellOrderArithmetic.html#18835" class="Function">Sum.order</a> <a id="2049" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a>
                              <a id="2081" href="Ordinals.WellOrderArithmetic.html#24747" class="Function">Sum.well-order</a> <a id="2096" href="Ordinals.ToppedArithmetic.html#1974" class="Bound">o</a> <a id="2098" class="Symbol">(λ</a> <a id="2101" href="Ordinals.ToppedArithmetic.html#2101" class="Bound">x</a> <a id="2103" class="Symbol">→</a> <a id="2105" href="Ordinals.ToppedType.html#1547" class="Function">tis-well-ordered</a> <a id="2122" class="Symbol">(</a><a id="2123" href="Ordinals.ToppedArithmetic.html#1982" class="Bound">υ</a> <a id="2125" href="Ordinals.ToppedArithmetic.html#2101" class="Bound">x</a><a id="2126" class="Symbol">)))</a> <a id="2130" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a>
                          <a id="2158" href="Ordinals.WellOrderArithmetic.html#25211" class="Function">Sum.top-preservation</a> <a id="2179" href="Ordinals.ToppedArithmetic.html#1979" class="Bound">t</a>
 <a id="2182" class="Keyword">where</a>
  <a id="2190" href="Ordinals.ToppedArithmetic.html#2190" class="Function Operator">_≺_</a> <a id="2194" class="Symbol">:</a> <a id="2196" class="Symbol">{</a><a id="2197" href="Ordinals.ToppedArithmetic.html#2197" class="Bound">x</a> <a id="2199" class="Symbol">:</a> <a id="2201" href="Ordinals.ToppedArithmetic.html#1964" class="Bound">X</a><a id="2202" class="Symbol">}</a> <a id="2204" class="Symbol">→</a> <a id="2206" href="Ordinals.Underlying.html#315" class="Field Operator">⟨</a> <a id="2208" href="Ordinals.ToppedArithmetic.html#1982" class="Bound">υ</a> <a id="2210" href="Ordinals.ToppedArithmetic.html#2197" class="Bound">x</a> <a id="2212" href="Ordinals.Underlying.html#315" class="Field Operator">⟩</a> <a id="2214" class="Symbol">→</a> <a id="2216" href="Ordinals.Underlying.html#315" class="Field Operator">⟨</a> <a id="2218" href="Ordinals.ToppedArithmetic.html#1982" class="Bound">υ</a> <a id="2220" href="Ordinals.ToppedArithmetic.html#2197" class="Bound">x</a> <a id="2222" href="Ordinals.Underlying.html#315" class="Field Operator">⟩</a> <a id="2224" class="Symbol">→</a> <a id="2226" href="Ordinals.ToppedArithmetic.html#1959" class="Bound">𝓤</a> <a id="2228" href="MLTT.Universes.html#3977" class="Function Operator">̇</a>
  <a id="2232" href="Ordinals.ToppedArithmetic.html#2232" class="Bound">y</a> <a id="2234" href="Ordinals.ToppedArithmetic.html#2190" class="Function Operator">≺</a> <a id="2236" href="Ordinals.ToppedArithmetic.html#2236" class="Bound">z</a> <a id="2238" class="Symbol">=</a> <a id="2240" href="Ordinals.ToppedArithmetic.html#2232" class="Bound">y</a> <a id="2242" href="Ordinals.Underlying.html#344" class="Field">≺⟨</a> <a id="2245" href="Ordinals.ToppedArithmetic.html#1982" class="Bound">υ</a> <a id="2247" class="Symbol">_</a> <a id="2249" href="Ordinals.Underlying.html#344" class="Field">⟩</a> <a id="2251" href="Ordinals.ToppedArithmetic.html#2236" class="Bound">z</a>

  <a id="2256" class="Keyword">module</a> <a id="2263" href="Ordinals.ToppedArithmetic.html#2263" class="Module">Sum</a> <a id="2267" class="Symbol">=</a> <a id="2269" href="Ordinals.WellOrderArithmetic.html#22473" class="Module">sum-top</a> <a id="2277" href="Ordinals.ToppedArithmetic.html#915" class="Bound">fe</a> <a id="2280" href="Ordinals.ToppedArithmetic.html#1968" class="Bound Operator">_&lt;_</a> <a id="2284" href="Ordinals.ToppedArithmetic.html#2190" class="Function Operator">_≺_</a> <a id="2288" class="Symbol">(λ</a> <a id="2291" href="Ordinals.ToppedArithmetic.html#2291" class="Bound">x</a> <a id="2293" class="Symbol">→</a> <a id="2295" href="Ordinals.ToppedType.html#1973" class="Function">top</a> <a id="2299" class="Symbol">(</a><a id="2300" href="Ordinals.ToppedArithmetic.html#1982" class="Bound">υ</a> <a id="2302" href="Ordinals.ToppedArithmetic.html#2291" class="Bound">x</a><a id="2303" class="Symbol">))</a> <a id="2306" class="Symbol">(λ</a> <a id="2309" href="Ordinals.ToppedArithmetic.html#2309" class="Bound">x</a> <a id="2311" class="Symbol">→</a> <a id="2313" href="Ordinals.ToppedType.html#2027" class="Function">top-is-top</a> <a id="2324" class="Symbol">(</a><a id="2325" href="Ordinals.ToppedArithmetic.html#1982" class="Bound">υ</a> <a id="2327" href="Ordinals.ToppedArithmetic.html#2309" class="Bound">x</a><a id="2328" class="Symbol">))</a>

<a id="∑-is-trichotomous"></a><a id="2332" href="Ordinals.ToppedArithmetic.html#2332" class="Function">∑-is-trichotomous</a> <a id="2350" class="Symbol">:</a> <a id="2352" class="Symbol">(</a><a id="2353" href="Ordinals.ToppedArithmetic.html#2353" class="Bound">τ</a> <a id="2355" class="Symbol">:</a> <a id="2357" href="Ordinals.ToppedType.html#865" class="Function">Ordinalᵀ</a> <a id="2366" href="MLTT.Universes.html#3834" class="Generalizable">𝓤</a><a id="2367" class="Symbol">)</a> <a id="2369" class="Symbol">(</a><a id="2370" href="Ordinals.ToppedArithmetic.html#2370" class="Bound">υ</a> <a id="2372" class="Symbol">:</a> <a id="2374" href="Ordinals.Underlying.html#315" class="Field Operator">⟨</a> <a id="2376" href="Ordinals.ToppedArithmetic.html#2353" class="Bound">τ</a> <a id="2378" href="Ordinals.Underlying.html#315" class="Field Operator">⟩</a> <a id="2380" class="Symbol">→</a> <a id="2382" href="Ordinals.ToppedType.html#865" class="Function">Ordinalᵀ</a> <a id="2391" href="MLTT.Universes.html#3834" class="Generalizable">𝓤</a><a id="2392" class="Symbol">)</a>
                  <a id="2412" class="Symbol">→</a> <a id="2414" href="Ordinals.Type.html#1307" class="Function">is-trichotomous</a> <a id="2430" href="Notation.CanonicalMap.html#502" class="Function Operator">[</a> <a id="2432" href="Ordinals.ToppedArithmetic.html#2353" class="Bound">τ</a> <a id="2434" href="Notation.CanonicalMap.html#502" class="Function Operator">]</a>
                  <a id="2454" class="Symbol">→</a> <a id="2456" class="Symbol">((</a><a id="2458" href="Ordinals.ToppedArithmetic.html#2458" class="Bound">x</a> <a id="2460" class="Symbol">:</a> <a id="2462" href="Ordinals.Underlying.html#315" class="Field Operator">⟨</a> <a id="2464" href="Ordinals.ToppedArithmetic.html#2353" class="Bound">τ</a> <a id="2466" href="Ordinals.Underlying.html#315" class="Field Operator">⟩</a><a id="2467" class="Symbol">)</a> <a id="2469" class="Symbol">→</a> <a id="2471" href="Ordinals.Type.html#1307" class="Function">is-trichotomous</a> <a id="2487" href="Notation.CanonicalMap.html#502" class="Function Operator">[</a> <a id="2489" href="Ordinals.ToppedArithmetic.html#2370" class="Bound">υ</a> <a id="2491" href="Ordinals.ToppedArithmetic.html#2458" class="Bound">x</a> <a id="2493" href="Notation.CanonicalMap.html#502" class="Function Operator">]</a><a id="2494" class="Symbol">)</a>
                  <a id="2514" class="Symbol">→</a> <a id="2516" href="Ordinals.Type.html#1307" class="Function">is-trichotomous</a> <a id="2532" href="Notation.CanonicalMap.html#502" class="Function Operator">[</a> <a id="2534" href="Ordinals.ToppedArithmetic.html#1899" class="Function">∑</a> <a id="2536" href="Ordinals.ToppedArithmetic.html#2353" class="Bound">τ</a> <a id="2538" href="Ordinals.ToppedArithmetic.html#2370" class="Bound">υ</a> <a id="2540" href="Notation.CanonicalMap.html#502" class="Function Operator">]</a>
<a id="2542" href="Ordinals.ToppedArithmetic.html#2332" class="Function">∑-is-trichotomous</a> <a id="2560" href="Ordinals.ToppedArithmetic.html#2560" class="Bound">τ</a> <a id="2562" href="Ordinals.ToppedArithmetic.html#2562" class="Bound">υ</a> <a id="2564" class="Symbol">=</a> <a id="2566" href="Ordinals.WellOrderArithmetic.html#22005" class="Function">sum.trichotomy-preservation</a> <a id="2594" class="Symbol">_</a> <a id="2596" class="Symbol">_</a>

<a id="2599" class="Markup">\end{code}</a><a id="2609" class="Background">

Addition and multiplication can be reduced to ∑, given the ordinal 𝟚ᵒ
defined above:

</a><a id="2697" class="Markup">\begin{code}</a>

<a id="_+ᵒ_"></a><a id="2711" href="Ordinals.ToppedArithmetic.html#2711" class="Function Operator">_+ᵒ_</a> <a id="2716" class="Symbol">:</a> <a id="2718" href="Ordinals.ToppedType.html#865" class="Function">Ordinalᵀ</a> <a id="2727" href="MLTT.Universes.html#3834" class="Generalizable">𝓤</a> <a id="2729" class="Symbol">→</a> <a id="2731" href="Ordinals.ToppedType.html#865" class="Function">Ordinalᵀ</a> <a id="2740" href="MLTT.Universes.html#3834" class="Generalizable">𝓤</a> <a id="2742" class="Symbol">→</a> <a id="2744" href="Ordinals.ToppedType.html#865" class="Function">Ordinalᵀ</a> <a id="2753" href="MLTT.Universes.html#3834" class="Generalizable">𝓤</a>
<a id="2755" href="Ordinals.ToppedArithmetic.html#2755" class="Bound">τ</a> <a id="2757" href="Ordinals.ToppedArithmetic.html#2711" class="Function Operator">+ᵒ</a> <a id="2760" href="Ordinals.ToppedArithmetic.html#2760" class="Bound">υ</a> <a id="2762" class="Symbol">=</a> <a id="2764" href="Ordinals.ToppedArithmetic.html#1899" class="Function">∑</a> <a id="2766" href="Ordinals.ToppedArithmetic.html#1723" class="Function">𝟚ᵒ</a> <a id="2769" class="Symbol">(</a><a id="2770" href="MLTT.Plus.html#397" class="Function">cases</a> <a id="2776" class="Symbol">(λ</a> <a id="2779" href="Ordinals.ToppedArithmetic.html#2779" class="Bound">_</a> <a id="2781" class="Symbol">→</a> <a id="2783" href="Ordinals.ToppedArithmetic.html#2755" class="Bound">τ</a><a id="2784" class="Symbol">)</a> <a id="2786" class="Symbol">(λ</a> <a id="2789" href="Ordinals.ToppedArithmetic.html#2789" class="Bound">_</a> <a id="2791" class="Symbol">→</a> <a id="2793" href="Ordinals.ToppedArithmetic.html#2760" class="Bound">υ</a><a id="2794" class="Symbol">))</a>

<a id="+ᵒ-is-trichotomous"></a><a id="2798" href="Ordinals.ToppedArithmetic.html#2798" class="Function">+ᵒ-is-trichotomous</a> <a id="2817" class="Symbol">:</a> <a id="2819" class="Symbol">(</a><a id="2820" href="Ordinals.ToppedArithmetic.html#2820" class="Bound">τ</a> <a id="2822" href="Ordinals.ToppedArithmetic.html#2822" class="Bound">υ</a> <a id="2824" class="Symbol">:</a> <a id="2826" href="Ordinals.ToppedType.html#865" class="Function">Ordinalᵀ</a> <a id="2835" href="MLTT.Universes.html#3834" class="Generalizable">𝓤</a><a id="2836" class="Symbol">)</a>
                   <a id="2857" class="Symbol">→</a> <a id="2859" href="Ordinals.Type.html#1307" class="Function">is-trichotomous</a> <a id="2875" href="Notation.CanonicalMap.html#502" class="Function Operator">[</a> <a id="2877" href="Ordinals.ToppedArithmetic.html#2820" class="Bound">τ</a> <a id="2879" href="Notation.CanonicalMap.html#502" class="Function Operator">]</a>
                   <a id="2900" class="Symbol">→</a> <a id="2902" href="Ordinals.Type.html#1307" class="Function">is-trichotomous</a> <a id="2918" href="Notation.CanonicalMap.html#502" class="Function Operator">[</a> <a id="2920" href="Ordinals.ToppedArithmetic.html#2822" class="Bound">υ</a> <a id="2922" href="Notation.CanonicalMap.html#502" class="Function Operator">]</a>
                   <a id="2943" class="Symbol">→</a> <a id="2945" href="Ordinals.Type.html#1307" class="Function">is-trichotomous</a> <a id="2961" href="Notation.CanonicalMap.html#502" class="Function Operator">[</a> <a id="2963" href="Ordinals.ToppedArithmetic.html#2820" class="Bound">τ</a> <a id="2965" href="Ordinals.ToppedArithmetic.html#2711" class="Function Operator">+ᵒ</a> <a id="2968" href="Ordinals.ToppedArithmetic.html#2822" class="Bound">υ</a> <a id="2970" href="Notation.CanonicalMap.html#502" class="Function Operator">]</a>
<a id="2972" href="Ordinals.ToppedArithmetic.html#2798" class="Function">+ᵒ-is-trichotomous</a> <a id="2991" href="Ordinals.ToppedArithmetic.html#2991" class="Bound">τ</a> <a id="2993" href="Ordinals.ToppedArithmetic.html#2993" class="Bound">υ</a> <a id="2995" href="Ordinals.ToppedArithmetic.html#2995" class="Bound">t</a> <a id="2997" href="Ordinals.ToppedArithmetic.html#2997" class="Bound">u</a> <a id="2999" class="Symbol">=</a> <a id="3001" href="Ordinals.ToppedArithmetic.html#2332" class="Function">∑-is-trichotomous</a> <a id="3019" href="Ordinals.ToppedArithmetic.html#1723" class="Function">𝟚ᵒ</a> <a id="3022" class="Symbol">(</a><a id="3023" href="MLTT.Plus.html#397" class="Function">cases</a> <a id="3029" class="Symbol">(λ</a> <a id="3032" href="Ordinals.ToppedArithmetic.html#3032" class="Bound">_</a> <a id="3034" class="Symbol">→</a> <a id="3036" href="Ordinals.ToppedArithmetic.html#2991" class="Bound">τ</a><a id="3037" class="Symbol">)</a> <a id="3039" class="Symbol">(λ</a> <a id="3042" href="Ordinals.ToppedArithmetic.html#3042" class="Bound">_</a> <a id="3044" class="Symbol">→</a> <a id="3046" href="Ordinals.ToppedArithmetic.html#2993" class="Bound">υ</a><a id="3047" class="Symbol">))</a>
                              <a id="3080" href="Ordinals.Arithmetic.html#2770" class="Function">𝟚ₒ-is-trichotomous</a>
                              <a id="3129" class="Symbol">(</a><a id="3130" href="MLTT.Plus.html#187" class="Function">dep-cases</a> <a id="3140" class="Symbol">(λ</a> <a id="3143" href="Ordinals.ToppedArithmetic.html#3143" class="Bound">_</a> <a id="3145" class="Symbol">→</a> <a id="3147" href="Ordinals.ToppedArithmetic.html#2995" class="Bound">t</a><a id="3148" class="Symbol">)</a> <a id="3150" class="Symbol">(λ</a> <a id="3153" href="Ordinals.ToppedArithmetic.html#3153" class="Bound">_</a> <a id="3155" class="Symbol">→</a> <a id="3157" href="Ordinals.ToppedArithmetic.html#2997" class="Bound">u</a><a id="3158" class="Symbol">))</a>

<a id="_×ᵒ_"></a><a id="3162" href="Ordinals.ToppedArithmetic.html#3162" class="Function Operator">_×ᵒ_</a> <a id="3167" class="Symbol">:</a> <a id="3169" href="Ordinals.ToppedType.html#865" class="Function">Ordinalᵀ</a> <a id="3178" href="MLTT.Universes.html#3834" class="Generalizable">𝓤</a> <a id="3180" class="Symbol">→</a> <a id="3182" href="Ordinals.ToppedType.html#865" class="Function">Ordinalᵀ</a> <a id="3191" href="MLTT.Universes.html#3834" class="Generalizable">𝓤</a> <a id="3193" class="Symbol">→</a> <a id="3195" href="Ordinals.ToppedType.html#865" class="Function">Ordinalᵀ</a> <a id="3204" href="MLTT.Universes.html#3834" class="Generalizable">𝓤</a>
<a id="3206" href="Ordinals.ToppedArithmetic.html#3206" class="Bound">τ</a> <a id="3208" href="Ordinals.ToppedArithmetic.html#3162" class="Function Operator">×ᵒ</a> <a id="3211" href="Ordinals.ToppedArithmetic.html#3211" class="Bound">υ</a> <a id="3213" class="Symbol">=</a> <a id="3215" href="Ordinals.ToppedArithmetic.html#1899" class="Function">∑</a> <a id="3217" href="Ordinals.ToppedArithmetic.html#3206" class="Bound">τ</a>  <a id="3220" class="Symbol">(λ</a> <a id="3223" class="Symbol">(</a><a id="3224" href="Ordinals.ToppedArithmetic.html#3224" class="Bound">_</a> <a id="3226" class="Symbol">:</a> <a id="3228" href="Ordinals.Underlying.html#315" class="Field Operator">⟨</a> <a id="3230" href="Ordinals.ToppedArithmetic.html#3206" class="Bound">τ</a> <a id="3232" href="Ordinals.Underlying.html#315" class="Field Operator">⟩</a><a id="3233" class="Symbol">)</a> <a id="3235" class="Symbol">→</a> <a id="3237" href="Ordinals.ToppedArithmetic.html#3211" class="Bound">υ</a><a id="3238" class="Symbol">)</a>

<a id="×ᵒ-is-trichotomous"></a><a id="3241" href="Ordinals.ToppedArithmetic.html#3241" class="Function">×ᵒ-is-trichotomous</a> <a id="3260" class="Symbol">:</a> <a id="3262" class="Symbol">(</a><a id="3263" href="Ordinals.ToppedArithmetic.html#3263" class="Bound">τ</a> <a id="3265" href="Ordinals.ToppedArithmetic.html#3265" class="Bound">υ</a> <a id="3267" class="Symbol">:</a> <a id="3269" href="Ordinals.ToppedType.html#865" class="Function">Ordinalᵀ</a> <a id="3278" href="MLTT.Universes.html#3834" class="Generalizable">𝓤</a><a id="3279" class="Symbol">)</a>
                   <a id="3300" class="Symbol">→</a> <a id="3302" href="Ordinals.Type.html#1307" class="Function">is-trichotomous</a> <a id="3318" href="Notation.CanonicalMap.html#502" class="Function Operator">[</a> <a id="3320" href="Ordinals.ToppedArithmetic.html#3263" class="Bound">τ</a> <a id="3322" href="Notation.CanonicalMap.html#502" class="Function Operator">]</a>
                   <a id="3343" class="Symbol">→</a> <a id="3345" href="Ordinals.Type.html#1307" class="Function">is-trichotomous</a> <a id="3361" href="Notation.CanonicalMap.html#502" class="Function Operator">[</a> <a id="3363" href="Ordinals.ToppedArithmetic.html#3265" class="Bound">υ</a> <a id="3365" href="Notation.CanonicalMap.html#502" class="Function Operator">]</a>
                   <a id="3386" class="Symbol">→</a> <a id="3388" href="Ordinals.Type.html#1307" class="Function">is-trichotomous</a> <a id="3404" href="Notation.CanonicalMap.html#502" class="Function Operator">[</a> <a id="3406" href="Ordinals.ToppedArithmetic.html#3263" class="Bound">τ</a> <a id="3408" href="Ordinals.ToppedArithmetic.html#3162" class="Function Operator">×ᵒ</a> <a id="3411" href="Ordinals.ToppedArithmetic.html#3265" class="Bound">υ</a> <a id="3413" href="Notation.CanonicalMap.html#502" class="Function Operator">]</a>
<a id="3415" href="Ordinals.ToppedArithmetic.html#3241" class="Function">×ᵒ-is-trichotomous</a> <a id="3434" href="Ordinals.ToppedArithmetic.html#3434" class="Bound">τ</a> <a id="3436" href="Ordinals.ToppedArithmetic.html#3436" class="Bound">υ</a> <a id="3438" href="Ordinals.ToppedArithmetic.html#3438" class="Bound">t</a> <a id="3440" href="Ordinals.ToppedArithmetic.html#3440" class="Bound">u</a> <a id="3442" class="Symbol">=</a> <a id="3444" href="Ordinals.ToppedArithmetic.html#2332" class="Function">∑-is-trichotomous</a> <a id="3462" href="Ordinals.ToppedArithmetic.html#3434" class="Bound">τ</a> <a id="3464" class="Symbol">(λ</a> <a id="3467" href="Ordinals.ToppedArithmetic.html#3467" class="Bound">_</a> <a id="3469" class="Symbol">→</a> <a id="3471" href="Ordinals.ToppedArithmetic.html#3436" class="Bound">υ</a><a id="3472" class="Symbol">)</a> <a id="3474" href="Ordinals.ToppedArithmetic.html#3438" class="Bound">t</a> <a id="3476" class="Symbol">(λ</a> <a id="3479" href="Ordinals.ToppedArithmetic.html#3479" class="Bound">_</a> <a id="3481" class="Symbol">→</a> <a id="3483" href="Ordinals.ToppedArithmetic.html#3440" class="Bound">u</a><a id="3484" class="Symbol">)</a>

<a id="3487" class="Markup">\end{code}</a><a id="3497" class="Background">

Extension of a family X → Ordᵀ along an embedding j : X → A to get a
family A → Ordᵀ. (This can also be done for Ord-valued families.)
This uses the module UF.InjectiveTypes to calculate Y / j.

Sum of a countable family with an added non-isolated top element. We
first extend the family to ℕ∞ and then take the ordinal-indexed sum of
ordinals defined above.

</a><a id="3859" class="Markup">\begin{code}</a>

<a id="3873" class="Keyword">open</a> <a id="3878" href="Ordinals.Injectivity.html#2945" class="Module">topped-ordinals-injectivity</a> <a id="3906" href="Ordinals.ToppedArithmetic.html#915" class="Bound">fe</a>

<a id="∑¹"></a><a id="3910" href="Ordinals.ToppedArithmetic.html#3910" class="Function">∑¹</a> <a id="3913" class="Symbol">:</a> <a id="3915" class="Symbol">(</a><a id="3916" href="MLTT.Natural-Numbers-Type.html#96" class="Datatype">ℕ</a> <a id="3918" class="Symbol">→</a> <a id="3920" href="Ordinals.ToppedArithmetic.html#1303" class="Function">Ordᵀ</a><a id="3924" class="Symbol">)</a> <a id="3926" class="Symbol">→</a> <a id="3928" href="Ordinals.ToppedArithmetic.html#1303" class="Function">Ordᵀ</a>
<a id="3933" href="Ordinals.ToppedArithmetic.html#3910" class="Function">∑¹</a> <a id="3936" href="Ordinals.ToppedArithmetic.html#3936" class="Bound">τ</a> <a id="3938" class="Symbol">=</a> <a id="3940" href="Ordinals.ToppedArithmetic.html#1899" class="Function">∑</a> <a id="3942" href="Ordinals.ToppedArithmetic.html#1790" class="Function">ℕ∞ᵒ</a> <a id="3946" class="Symbol">(</a><a id="3947" href="Ordinals.ToppedArithmetic.html#3936" class="Bound">τ</a> <a id="3949" href="Ordinals.Injectivity.html#3074" class="Function Operator">↗</a> <a id="3951" href="CoNaturals.GenericConvergentSequence.html#7742" class="Function">embedding-ℕ-to-ℕ∞</a> <a id="3969" class="Symbol">(</a><a id="3970" href="Ordinals.ToppedArithmetic.html#915" class="Bound">fe</a> <a id="3973" href="Agda.Primitive.html#915" class="Primitive">𝓤₀</a> <a id="3976" href="Agda.Primitive.html#915" class="Primitive">𝓤₀</a><a id="3978" class="Symbol">))</a>

<a id="3982" class="Markup">\end{code}</a><a id="3992" class="Background">

And now with an isolated top element:

</a><a id="4033" class="Markup">\begin{code}</a>

<a id="∑₁"></a><a id="4047" href="Ordinals.ToppedArithmetic.html#4047" class="Function">∑₁</a> <a id="4050" class="Symbol">:</a> <a id="4052" class="Symbol">(</a><a id="4053" href="MLTT.Natural-Numbers-Type.html#96" class="Datatype">ℕ</a> <a id="4055" class="Symbol">→</a> <a id="4057" href="Ordinals.ToppedArithmetic.html#1303" class="Function">Ordᵀ</a><a id="4061" class="Symbol">)</a> <a id="4063" class="Symbol">→</a> <a id="4065" href="Ordinals.ToppedArithmetic.html#1303" class="Function">Ordᵀ</a>
<a id="4070" href="Ordinals.ToppedArithmetic.html#4047" class="Function">∑₁</a> <a id="4073" href="Ordinals.ToppedArithmetic.html#4073" class="Bound">τ</a> <a id="4075" class="Symbol">=</a> <a id="4077" href="Ordinals.ToppedArithmetic.html#1899" class="Function">∑</a> <a id="4079" class="Symbol">(</a><a id="4080" href="Ordinals.ToppedArithmetic.html#1323" class="Function">succₒ</a> <a id="4086" href="Ordinals.Arithmetic.html#1479" class="Function">ω</a><a id="4087" class="Symbol">)</a> <a id="4089" class="Symbol">(</a><a id="4090" href="Ordinals.ToppedArithmetic.html#4073" class="Bound">τ</a> <a id="4092" href="Ordinals.Injectivity.html#3074" class="Function Operator">↗</a> <a id="4094" class="Symbol">(</a><a id="4095" href="TypeTopology.SquashedSum.html#2464" class="Function">over</a> <a id="4100" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="4102" href="TypeTopology.SquashedSum.html#2503" class="Function">over-embedding</a><a id="4116" class="Symbol">))</a>

<a id="4120" class="Markup">\end{code}</a><a id="4130" class="Background">

Added 4th May 2022.

</a><a id="4153" class="Markup">\begin{code}</a>

<a id="4167" class="Keyword">module</a> <a id="Omega"></a><a id="4174" href="Ordinals.ToppedArithmetic.html#4174" class="Module">Omega</a> <a id="4180" class="Symbol">{</a><a id="4181" href="Ordinals.ToppedArithmetic.html#4181" class="Bound">𝓤</a><a id="4182" class="Symbol">}</a> <a id="4184" class="Symbol">(</a><a id="4185" href="Ordinals.ToppedArithmetic.html#4185" class="Bound">pe</a> <a id="4188" class="Symbol">:</a> <a id="4190" href="UF.Subsingletons.html#7674" class="Function">propext</a> <a id="4198" href="Ordinals.ToppedArithmetic.html#4181" class="Bound">𝓤</a><a id="4199" class="Symbol">)</a> <a id="4201" class="Keyword">where</a>

 <a id="4209" class="Keyword">open</a> <a id="4214" class="Keyword">import</a> <a id="4221" href="Ordinals.OrdinalOfTruthValues.html" class="Module">Ordinals.OrdinalOfTruthValues</a> <a id="4251" href="Ordinals.ToppedArithmetic.html#915" class="Bound">fe</a> <a id="4254" href="Ordinals.ToppedArithmetic.html#4181" class="Bound">𝓤</a> <a id="4256" href="Ordinals.ToppedArithmetic.html#4185" class="Bound">pe</a>
 <a id="4260" class="Keyword">open</a> <a id="4265" class="Keyword">import</a> <a id="4272" href="Ordinals.Notions.html" class="Module">Ordinals.Notions</a>
 <a id="4290" class="Keyword">open</a> <a id="4295" class="Keyword">import</a> <a id="4302" href="UF.SubtypeClassifier.html" class="Module">UF.SubtypeClassifier</a>

 <a id="Omega.Ωᵒ"></a><a id="4325" href="Ordinals.ToppedArithmetic.html#4325" class="Function">Ωᵒ</a> <a id="4328" class="Symbol">:</a> <a id="4330" href="Ordinals.ToppedType.html#865" class="Function">Ordinalᵀ</a> <a id="4339" class="Symbol">(</a><a id="4340" href="Ordinals.ToppedArithmetic.html#4181" class="Bound">𝓤</a> <a id="4342" href="Agda.Primitive.html#931" class="Primitive Operator">⁺</a><a id="4343" class="Symbol">)</a>
 <a id="4346" href="Ordinals.ToppedArithmetic.html#4325" class="Function">Ωᵒ</a> <a id="4349" class="Symbol">=</a> <a id="4351" href="Ordinals.OrdinalOfTruthValues.html#607" class="Function">Ωₒ</a> <a id="4354" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="4356" href="UF.SubtypeClassifier.html#1306" class="Function">⊤</a> <a id="4358" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="4360" href="Ordinals.ToppedArithmetic.html#4373" class="Function">h</a>
  <a id="4364" class="Keyword">where</a>
   <a id="4373" href="Ordinals.ToppedArithmetic.html#4373" class="Function">h</a> <a id="4375" class="Symbol">:</a> <a id="4377" href="Ordinals.Notions.html#10413" class="Function">is-top</a> <a id="4384" class="Symbol">(</a><a id="4385" href="Ordinals.Underlying.html#344" class="Field">underlying-order</a> <a id="4402" href="Ordinals.OrdinalOfTruthValues.html#607" class="Function">Ωₒ</a><a id="4404" class="Symbol">)</a> <a id="4406" href="UF.SubtypeClassifier.html#1306" class="Function">⊤</a>
   <a id="4411" href="Ordinals.ToppedArithmetic.html#4373" class="Function">h</a> <a id="4413" href="Ordinals.ToppedArithmetic.html#4413" class="Bound">y</a> <a id="4415" class="Symbol">(</a><a id="4416" href="Ordinals.ToppedArithmetic.html#4416" class="Bound">p</a> <a id="4418" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="4420" class="Symbol">_)</a> <a id="4423" class="Symbol">=</a> <a id="4425" href="UF.SubtypeClassifier.html#1463" class="Function">⊥-is-not-⊤</a> <a id="4436" class="Symbol">(</a><a id="4437" href="Ordinals.ToppedArithmetic.html#4416" class="Bound">p</a> <a id="4439" href="MLTT.Id.html#1055" class="Function Operator">⁻¹</a><a id="4441" class="Symbol">)</a>

<a id="4444" class="Markup">\end{code}</a><a id="4454" class="Background">
</a></pre></body></html>