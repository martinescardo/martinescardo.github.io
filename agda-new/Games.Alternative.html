<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Games.Alternative</title><link rel="stylesheet" href="Agda.css"><script type="text/javascript" src="highlight-hover.js"></script></head><body><pre class="Agda"><a id="1" class="Background">Martin Escardo, Paulo Oliva, 9th July 2024.

Alternative, equivalent, inductive definition of the type Game of
games, which may have some practical advantages, such as more modular
definitions of games.

</a><a id="205" class="Markup">\begin{code}</a>

<a id="219" class="Symbol">{-#</a> <a id="223" class="Keyword">OPTIONS</a> <a id="231" class="Pragma">--safe</a> <a id="238" class="Pragma">--without-K</a> <a id="250" class="Symbol">#-}</a>

<a id="255" class="Keyword">open</a> <a id="260" class="Keyword">import</a> <a id="267" href="MLTT.Spartan.html" class="Module">MLTT.Spartan</a> <a id="280" class="Keyword">hiding</a> <a id="287" class="Symbol">(</a><a id="288" href="MLTT.Id.html#438" class="Function">J</a><a id="289" class="Symbol">)</a>

<a id="292" class="Keyword">module</a> <a id="299" href="Games.Alternative.html" class="Module">Games.Alternative</a> <a id="317" class="Symbol">(</a><a id="318" href="Games.Alternative.html#318" class="Bound">R</a> <a id="320" class="Symbol">:</a> <a id="322" href="Notation.General.html#372" class="Function">Type</a><a id="326" class="Symbol">)</a> <a id="328" class="Keyword">where</a>

<a id="335" class="Keyword">open</a> <a id="340" class="Keyword">import</a> <a id="347" href="UF.Equiv.html" class="Module">UF.Equiv</a>
<a id="356" class="Keyword">open</a> <a id="361" class="Keyword">import</a> <a id="368" href="UF.FunExt.html" class="Module">UF.FunExt</a>

<a id="379" class="Keyword">open</a> <a id="384" class="Keyword">import</a> <a id="391" href="Games.TypeTrees.html" class="Module">Games.TypeTrees</a>
<a id="407" class="Keyword">open</a> <a id="412" class="Keyword">import</a> <a id="419" href="Games.FiniteHistoryDependent.html" class="Module">Games.FiniteHistoryDependent</a> <a id="448" href="Games.Alternative.html#318" class="Bound">R</a>
             <a id="463" class="Keyword">renaming</a> <a id="472" class="Symbol">(</a><a id="473" href="Games.FiniteHistoryDependent.html#3132" class="Record">Game</a> <a id="478" class="Symbol">to</a> <a id="481" class="Record">Game&#39;</a> <a id="487" class="Symbol">;</a>
                       <a id="512" href="Games.FiniteHistoryDependent.html#3164" class="InductiveConstructor">game</a> <a id="517" class="Symbol">to</a> <a id="520" class="InductiveConstructor">game&#39;</a><a id="525" class="Symbol">)</a>

<a id="528" class="Keyword">open</a> <a id="533" class="Keyword">import</a> <a id="540" href="MonadOnTypes.K.html" class="Module">MonadOnTypes.K</a>

<a id="556" class="Keyword">open</a> <a id="561" href="MonadOnTypes.K.html#438" class="Module">K-definitions</a> <a id="575" href="Games.Alternative.html#318" class="Bound">R</a>

<a id="578" class="Keyword">data</a> <a id="Game"></a><a id="583" href="Games.Alternative.html#583" class="Datatype">Game</a> <a id="588" class="Symbol">:</a> <a id="590" href="Notation.General.html#384" class="Function">Typeâ‚</a> <a id="596" class="Keyword">where</a>
 <a id="Game.leaf"></a><a id="603" href="Games.Alternative.html#603" class="InductiveConstructor">leaf</a>   <a id="610" class="Symbol">:</a> <a id="612" href="Games.Alternative.html#318" class="Bound">R</a> <a id="614" class="Symbol">â†’</a> <a id="616" href="Games.Alternative.html#583" class="Datatype">Game</a>
 <a id="Game.branch"></a><a id="622" href="Games.Alternative.html#622" class="InductiveConstructor">branch</a> <a id="629" class="Symbol">:</a> <a id="631" class="Symbol">(</a><a id="632" href="Games.Alternative.html#632" class="Bound">X</a> <a id="634" class="Symbol">:</a> <a id="636" href="Notation.General.html#372" class="Function">Type</a><a id="640" class="Symbol">)</a> <a id="642" class="Symbol">â†’</a> <a id="644" href="MonadOnTypes.K.html#471" class="Function">K</a> <a id="646" href="Games.Alternative.html#632" class="Bound">X</a> <a id="648" class="Symbol">â†’</a> <a id="650" class="Symbol">(</a><a id="651" href="Games.Alternative.html#632" class="Bound">X</a> <a id="653" class="Symbol">â†’</a> <a id="655" href="Games.Alternative.html#583" class="Datatype">Game</a><a id="659" class="Symbol">)</a> <a id="661" class="Symbol">â†’</a> <a id="663" href="Games.Alternative.html#583" class="Datatype">Game</a>

<a id="leaf&#39;"></a><a id="669" href="Games.Alternative.html#669" class="Function">leaf&#39;</a> <a id="675" class="Symbol">:</a> <a id="677" href="Games.Alternative.html#318" class="Bound">R</a> <a id="679" class="Symbol">â†’</a> <a id="681" href="Games.Alternative.html#481" class="Record">Game&#39;</a>
<a id="687" href="Games.Alternative.html#669" class="Function">leaf&#39;</a> <a id="693" href="Games.Alternative.html#693" class="Bound">r</a> <a id="695" class="Symbol">=</a> <a id="697" href="Games.Alternative.html#520" class="InductiveConstructor">game&#39;</a> <a id="703" href="Games.TypeTrees.html#773" class="InductiveConstructor">[]</a> <a id="706" class="Symbol">(Î»</a> <a id="709" href="Games.Alternative.html#709" class="Bound">âŸ¨âŸ©</a> <a id="712" class="Symbol">â†’</a> <a id="714" href="Games.Alternative.html#693" class="Bound">r</a><a id="715" class="Symbol">)</a> <a id="717" href="Games.TypeTrees.html#1310" class="InductiveConstructor">âŸ¨âŸ©</a>

<a id="branch&#39;"></a><a id="721" href="Games.Alternative.html#721" class="Function">branch&#39;</a> <a id="729" class="Symbol">:</a> <a id="731" class="Symbol">(</a><a id="732" href="Games.Alternative.html#732" class="Bound">X</a> <a id="734" class="Symbol">:</a> <a id="736" href="Notation.General.html#372" class="Function">Type</a><a id="740" class="Symbol">)</a> <a id="742" class="Symbol">â†’</a> <a id="744" href="MonadOnTypes.K.html#471" class="Function">K</a> <a id="746" href="Games.Alternative.html#732" class="Bound">X</a> <a id="748" class="Symbol">â†’</a> <a id="750" class="Symbol">(</a><a id="751" href="Games.Alternative.html#732" class="Bound">X</a> <a id="753" class="Symbol">â†’</a> <a id="755" href="Games.Alternative.html#481" class="Record">Game&#39;</a><a id="760" class="Symbol">)</a> <a id="762" class="Symbol">â†’</a> <a id="764" href="Games.Alternative.html#481" class="Record">Game&#39;</a>
<a id="770" href="Games.Alternative.html#721" class="Function">branch&#39;</a> <a id="778" href="Games.Alternative.html#778" class="Bound">X</a> <a id="780" href="Games.Alternative.html#780" class="Bound">Ï•</a> <a id="782" href="Games.Alternative.html#782" class="Bound">Gf</a> <a id="785" class="Symbol">=</a> <a id="787" href="Games.Alternative.html#520" class="InductiveConstructor">game&#39;</a> <a id="793" class="Symbol">(</a><a id="794" href="Games.Alternative.html#778" class="Bound">X</a> <a id="796" href="Games.TypeTrees.html#783" class="InductiveConstructor Operator">âˆ·</a> <a id="798" class="Symbol">(</a><a id="799" href="Games.FiniteHistoryDependent.html#3178" class="Field">game-tree</a> <a id="809" href="MLTT.Pi.html#513" class="Function Operator">âˆ˜</a> <a id="811" href="Games.Alternative.html#782" class="Bound">Gf</a><a id="813" class="Symbol">))</a>
                       <a id="839" class="Symbol">(Î»</a> <a id="842" class="Symbol">(</a><a id="843" href="Games.Alternative.html#843" class="Bound">x</a> <a id="845" href="Games.TypeTrees.html#1332" class="InductiveConstructor Operator">::</a> <a id="848" href="Games.Alternative.html#848" class="Bound">xs</a><a id="850" class="Symbol">)</a> <a id="852" class="Symbol">â†’</a> <a id="854" href="Games.FiniteHistoryDependent.html#3194" class="Field">payoff-function</a> <a id="870" class="Symbol">(</a><a id="871" href="Games.Alternative.html#782" class="Bound">Gf</a> <a id="874" href="Games.Alternative.html#843" class="Bound">x</a><a id="875" class="Symbol">)</a> <a id="877" href="Games.Alternative.html#848" class="Bound">xs</a><a id="879" class="Symbol">)</a>
                       <a id="904" class="Symbol">(</a><a id="905" href="Games.Alternative.html#780" class="Bound">Ï•</a> <a id="907" href="Games.TypeTrees.html#1332" class="InductiveConstructor Operator">::</a> <a id="910" class="Symbol">(</a><a id="911" href="Games.FiniteHistoryDependent.html#3234" class="Field">quantifier-tree</a> <a id="927" href="MLTT.Pi.html#513" class="Function Operator">âˆ˜</a> <a id="929" href="Games.Alternative.html#782" class="Bound">Gf</a><a id="931" class="Symbol">))</a>

<a id="from-Game"></a><a id="935" href="Games.Alternative.html#935" class="Function">from-Game</a> <a id="945" class="Symbol">:</a> <a id="947" href="Games.Alternative.html#583" class="Datatype">Game</a> <a id="952" class="Symbol">â†’</a> <a id="954" href="Games.Alternative.html#481" class="Record">Game&#39;</a>
<a id="960" href="Games.Alternative.html#935" class="Function">from-Game</a> <a id="970" class="Symbol">(</a><a id="971" href="Games.Alternative.html#603" class="InductiveConstructor">leaf</a> <a id="976" href="Games.Alternative.html#976" class="Bound">r</a><a id="977" class="Symbol">)</a>        <a id="986" class="Symbol">=</a> <a id="988" href="Games.Alternative.html#669" class="Function">leaf&#39;</a> <a id="994" href="Games.Alternative.html#976" class="Bound">r</a>
<a id="996" href="Games.Alternative.html#935" class="Function">from-Game</a> <a id="1006" class="Symbol">(</a><a id="1007" href="Games.Alternative.html#622" class="InductiveConstructor">branch</a> <a id="1014" href="Games.Alternative.html#1014" class="Bound">X</a> <a id="1016" href="Games.Alternative.html#1016" class="Bound">Ï•</a> <a id="1018" href="Games.Alternative.html#1018" class="Bound">Gf</a><a id="1020" class="Symbol">)</a> <a id="1022" class="Symbol">=</a> <a id="1024" href="Games.Alternative.html#721" class="Function">branch&#39;</a> <a id="1032" href="Games.Alternative.html#1014" class="Bound">X</a> <a id="1034" href="Games.Alternative.html#1016" class="Bound">Ï•</a> <a id="1036" class="Symbol">(</a><a id="1037" href="Games.Alternative.html#935" class="Function">from-Game</a> <a id="1047" href="MLTT.Pi.html#513" class="Function Operator">âˆ˜</a> <a id="1049" href="Games.Alternative.html#1018" class="Bound">Gf</a><a id="1051" class="Symbol">)</a>

<a id="1054" class="Markup">\end{code}</a><a id="1064" class="Background">

We need the curried version h of the conversion function to-Game
defined below to convince the termination checker that the following
recursion does terminate.

</a><a id="1227" class="Markup">\begin{code}</a>

<a id="to-Game"></a><a id="1241" href="Games.Alternative.html#1241" class="Function">to-Game</a> <a id="1249" class="Symbol">:</a> <a id="1251" href="Games.Alternative.html#481" class="Record">Game&#39;</a> <a id="1257" class="Symbol">â†’</a> <a id="1259" href="Games.Alternative.html#583" class="Datatype">Game</a>
<a id="1264" href="Games.Alternative.html#1241" class="Function">to-Game</a> <a id="1272" class="Symbol">(</a><a id="1273" href="Games.Alternative.html#520" class="InductiveConstructor">game&#39;</a> <a id="1279" href="Games.Alternative.html#1279" class="Bound">Xt</a> <a id="1282" href="Games.Alternative.html#1282" class="Bound">q</a> <a id="1284" href="Games.Alternative.html#1284" class="Bound">Ï•t</a><a id="1286" class="Symbol">)</a> <a id="1288" class="Symbol">=</a> <a id="1290" href="Games.Alternative.html#1309" class="Function">h</a> <a id="1292" href="Games.Alternative.html#1279" class="Bound">Xt</a> <a id="1295" href="Games.Alternative.html#1282" class="Bound">q</a> <a id="1297" href="Games.Alternative.html#1284" class="Bound">Ï•t</a>
 <a id="1301" class="Keyword">where</a>
  <a id="1309" href="Games.Alternative.html#1309" class="Function">h</a> <a id="1311" class="Symbol">:</a> <a id="1313" class="Symbol">(</a><a id="1314" href="Games.Alternative.html#1314" class="Bound">Xt</a> <a id="1317" class="Symbol">:</a> <a id="1319" href="Games.TypeTrees.html#755" class="Datatype">ğ‘»</a><a id="1320" class="Symbol">)</a> <a id="1322" class="Symbol">â†’</a> <a id="1324" class="Symbol">(</a><a id="1325" href="Games.TypeTrees.html#942" class="Function">Path</a> <a id="1330" href="Games.Alternative.html#1314" class="Bound">Xt</a> <a id="1333" class="Symbol">â†’</a> <a id="1335" href="Games.Alternative.html#318" class="Bound">R</a><a id="1336" class="Symbol">)</a> <a id="1338" class="Symbol">â†’</a> <a id="1340" href="Games.FiniteHistoryDependent.html#2364" class="Function">ğ“š</a> <a id="1342" href="Games.Alternative.html#1314" class="Bound">Xt</a> <a id="1345" class="Symbol">â†’</a> <a id="1347" href="Games.Alternative.html#583" class="Datatype">Game</a>
  <a id="1354" href="Games.Alternative.html#1309" class="Function">h</a> <a id="1356" href="Games.TypeTrees.html#773" class="InductiveConstructor">[]</a>       <a id="1365" href="Games.Alternative.html#1365" class="Bound">q</a> <a id="1367" href="Games.TypeTrees.html#1310" class="InductiveConstructor">âŸ¨âŸ©</a>        <a id="1377" class="Symbol">=</a> <a id="1379" href="Games.Alternative.html#603" class="InductiveConstructor">leaf</a> <a id="1384" class="Symbol">(</a><a id="1385" href="Games.Alternative.html#1365" class="Bound">q</a> <a id="1387" href="Games.TypeTrees.html#1310" class="InductiveConstructor">âŸ¨âŸ©</a><a id="1389" class="Symbol">)</a>
  <a id="1393" href="Games.Alternative.html#1309" class="Function">h</a> <a id="1395" class="Symbol">(</a><a id="1396" href="Games.Alternative.html#1396" class="Bound">X</a> <a id="1398" href="Games.TypeTrees.html#783" class="InductiveConstructor Operator">âˆ·</a> <a id="1400" href="Games.Alternative.html#1400" class="Bound">Xf</a><a id="1402" class="Symbol">)</a> <a id="1404" href="Games.Alternative.html#1404" class="Bound">q</a> <a id="1406" class="Symbol">(</a><a id="1407" href="Games.Alternative.html#1407" class="Bound">Ï•</a> <a id="1409" href="Games.TypeTrees.html#1332" class="InductiveConstructor Operator">::</a> <a id="1412" href="Games.Alternative.html#1412" class="Bound">Ï•f</a><a id="1414" class="Symbol">)</a> <a id="1416" class="Symbol">=</a> <a id="1418" href="Games.Alternative.html#622" class="InductiveConstructor">branch</a> <a id="1425" href="Games.Alternative.html#1396" class="Bound">X</a> <a id="1427" href="Games.Alternative.html#1407" class="Bound">Ï•</a> <a id="1429" class="Symbol">(Î»</a> <a id="1432" href="Games.Alternative.html#1432" class="Bound">x</a> <a id="1434" class="Symbol">â†’</a> <a id="1436" href="Games.Alternative.html#1309" class="Function">h</a> <a id="1438" class="Symbol">(</a><a id="1439" href="Games.Alternative.html#1400" class="Bound">Xf</a> <a id="1442" href="Games.Alternative.html#1432" class="Bound">x</a><a id="1443" class="Symbol">)</a> <a id="1445" class="Symbol">(</a><a id="1446" href="Games.TypeTrees.html#1658" class="Function">subpred</a> <a id="1454" href="Games.Alternative.html#1404" class="Bound">q</a> <a id="1456" href="Games.Alternative.html#1432" class="Bound">x</a><a id="1457" class="Symbol">)</a> <a id="1459" class="Symbol">(</a><a id="1460" href="Games.Alternative.html#1412" class="Bound">Ï•f</a> <a id="1463" href="Games.Alternative.html#1432" class="Bound">x</a><a id="1464" class="Symbol">))</a>

<a id="1468" class="Markup">\end{code}</a><a id="1478" class="Background">

The equations we would have liked to use to define the function
to-Game are the following:

</a><a id="1572" class="Markup">\begin{code}</a>

<a id="to-Game-base"></a><a id="1586" href="Games.Alternative.html#1586" class="Function">to-Game-base</a>
 <a id="1600" class="Symbol">:</a> <a id="1602" class="Symbol">(</a><a id="1603" href="Games.Alternative.html#1603" class="Bound">q</a> <a id="1605" class="Symbol">:</a> <a id="1607" href="Games.TypeTrees.html#942" class="Function">Path</a> <a id="1612" class="InductiveConstructor">[]</a> <a id="1615" class="Symbol">â†’</a> <a id="1617" href="Games.Alternative.html#318" class="Bound">R</a><a id="1618" class="Symbol">)</a>
 <a id="1621" class="Symbol">â†’</a> <a id="1623" href="Games.Alternative.html#1241" class="Function">to-Game</a> <a id="1631" class="Symbol">(</a><a id="1632" href="Games.Alternative.html#520" class="InductiveConstructor">game&#39;</a> <a id="1638" class="InductiveConstructor">[]</a> <a id="1641" href="Games.Alternative.html#1603" class="Bound">q</a> <a id="1643" href="Games.TypeTrees.html#1310" class="InductiveConstructor">âŸ¨âŸ©</a><a id="1645" class="Symbol">)</a> <a id="1647" href="MLTT.Id.html#193" class="Datatype Operator">ï¼</a> <a id="1649" href="Games.Alternative.html#603" class="InductiveConstructor">leaf</a> <a id="1654" class="Symbol">(</a><a id="1655" href="Games.Alternative.html#1603" class="Bound">q</a> <a id="1657" href="Games.TypeTrees.html#1310" class="InductiveConstructor">âŸ¨âŸ©</a><a id="1659" class="Symbol">)</a>
<a id="1661" href="Games.Alternative.html#1586" class="Function">to-Game-base</a> <a id="1674" href="Games.Alternative.html#1674" class="Bound">q</a> <a id="1676" class="Symbol">=</a> <a id="1678" href="MLTT.Identity-Type.html#157" class="InductiveConstructor">refl</a>

<a id="to-Game-step"></a><a id="1684" href="Games.Alternative.html#1684" class="Function">to-Game-step</a>
 <a id="1698" class="Symbol">:</a> <a id="1700" class="Symbol">(</a><a id="1701" href="Games.Alternative.html#1701" class="Bound">X</a> <a id="1703" class="Symbol">:</a> <a id="1705" href="Notation.General.html#372" class="Function">Type</a><a id="1709" class="Symbol">)</a> <a id="1711" class="Symbol">(</a><a id="1712" href="Games.Alternative.html#1712" class="Bound">Xf</a> <a id="1715" class="Symbol">:</a> <a id="1717" href="Games.Alternative.html#1701" class="Bound">X</a> <a id="1719" class="Symbol">â†’</a> <a id="1721" href="Games.TypeTrees.html#755" class="Datatype">ğ‘»</a><a id="1722" class="Symbol">)</a> <a id="1724" class="Symbol">(</a><a id="1725" href="Games.Alternative.html#1725" class="Bound">Ï•</a> <a id="1727" class="Symbol">:</a> <a id="1729" href="MonadOnTypes.K.html#471" class="Function">K</a> <a id="1731" href="Games.Alternative.html#1701" class="Bound">X</a><a id="1732" class="Symbol">)</a> <a id="1734" class="Symbol">(</a><a id="1735" href="Games.Alternative.html#1735" class="Bound">Ï•f</a> <a id="1738" class="Symbol">:</a> <a id="1740" class="Symbol">(</a><a id="1741" href="Games.Alternative.html#1741" class="Bound">x</a> <a id="1743" class="Symbol">:</a> <a id="1745" href="Games.Alternative.html#1701" class="Bound">X</a><a id="1746" class="Symbol">)</a> <a id="1748" class="Symbol">â†’</a> <a id="1750" href="Games.FiniteHistoryDependent.html#2364" class="Function">ğ“š</a> <a id="1752" class="Symbol">(</a><a id="1753" href="Games.Alternative.html#1712" class="Bound">Xf</a> <a id="1756" href="Games.Alternative.html#1741" class="Bound">x</a><a id="1757" class="Symbol">))</a> <a id="1760" class="Symbol">(</a><a id="1761" href="Games.Alternative.html#1761" class="Bound">q</a> <a id="1763" class="Symbol">:</a> <a id="1765" href="Games.TypeTrees.html#942" class="Function">Path</a> <a id="1770" class="Symbol">(</a><a id="1771" href="Games.Alternative.html#1701" class="Bound">X</a> <a id="1773" class="InductiveConstructor Operator">âˆ·</a> <a id="1775" href="Games.Alternative.html#1712" class="Bound">Xf</a><a id="1777" class="Symbol">)</a> <a id="1779" class="Symbol">â†’</a> <a id="1781" href="Games.Alternative.html#318" class="Bound">R</a><a id="1782" class="Symbol">)</a>
 <a id="1785" class="Symbol">â†’</a> <a id="1787" href="Games.Alternative.html#1241" class="Function">to-Game</a> <a id="1795" class="Symbol">(</a><a id="1796" href="Games.Alternative.html#520" class="InductiveConstructor">game&#39;</a> <a id="1802" class="Symbol">(</a><a id="1803" href="Games.Alternative.html#1701" class="Bound">X</a> <a id="1805" class="InductiveConstructor Operator">âˆ·</a> <a id="1807" href="Games.Alternative.html#1712" class="Bound">Xf</a><a id="1809" class="Symbol">)</a> <a id="1811" href="Games.Alternative.html#1761" class="Bound">q</a> <a id="1813" class="Symbol">(</a><a id="1814" href="Games.Alternative.html#1725" class="Bound">Ï•</a> <a id="1816" href="Games.TypeTrees.html#1332" class="InductiveConstructor Operator">::</a> <a id="1819" href="Games.Alternative.html#1735" class="Bound">Ï•f</a><a id="1821" class="Symbol">))</a>
 <a id="1825" href="MLTT.Id.html#193" class="Datatype Operator">ï¼</a> <a id="1827" href="Games.Alternative.html#622" class="InductiveConstructor">branch</a> <a id="1834" href="Games.Alternative.html#1701" class="Bound">X</a> <a id="1836" href="Games.Alternative.html#1725" class="Bound">Ï•</a> <a id="1838" class="Symbol">(Î»</a> <a id="1841" href="Games.Alternative.html#1841" class="Bound">x</a> <a id="1843" class="Symbol">â†’</a> <a id="1845" href="Games.Alternative.html#1241" class="Function">to-Game</a> <a id="1853" class="Symbol">(</a><a id="1854" href="Games.Alternative.html#520" class="InductiveConstructor">game&#39;</a> <a id="1860" class="Symbol">(</a><a id="1861" href="Games.Alternative.html#1712" class="Bound">Xf</a> <a id="1864" href="Games.Alternative.html#1841" class="Bound">x</a><a id="1865" class="Symbol">)</a> <a id="1867" class="Symbol">(</a><a id="1868" href="Games.TypeTrees.html#1658" class="Function">subpred</a> <a id="1876" href="Games.Alternative.html#1761" class="Bound">q</a> <a id="1878" href="Games.Alternative.html#1841" class="Bound">x</a><a id="1879" class="Symbol">)</a> <a id="1881" class="Symbol">(</a><a id="1882" href="Games.Alternative.html#1735" class="Bound">Ï•f</a> <a id="1885" href="Games.Alternative.html#1841" class="Bound">x</a><a id="1886" class="Symbol">)))</a>
<a id="1890" href="Games.Alternative.html#1684" class="Function">to-Game-step</a> <a id="1903" href="Games.Alternative.html#1903" class="Bound">X</a> <a id="1905" href="Games.Alternative.html#1905" class="Bound">Xf</a> <a id="1908" href="Games.Alternative.html#1908" class="Bound">Ï•</a> <a id="1910" href="Games.Alternative.html#1910" class="Bound">Ï•f</a> <a id="1913" href="Games.Alternative.html#1913" class="Bound">q</a> <a id="1915" class="Symbol">=</a> <a id="1917" href="MLTT.Identity-Type.html#157" class="InductiveConstructor">refl</a>

<a id="1923" class="Markup">\end{code}</a><a id="1933" class="Background">

We also have the following equivalent definitional equalities
expressed in terms of leaf&#39; and branch&#39;:

</a><a id="2039" class="Markup">\begin{code}</a>

<a id="to-Game-base&#39;"></a><a id="2053" href="Games.Alternative.html#2053" class="Function">to-Game-base&#39;</a> <a id="2067" class="Symbol">:</a> <a id="2069" class="Symbol">(</a><a id="2070" href="Games.Alternative.html#2070" class="Bound">r</a> <a id="2072" class="Symbol">:</a> <a id="2074" href="Games.Alternative.html#318" class="Bound">R</a><a id="2075" class="Symbol">)</a> <a id="2077" class="Symbol">â†’</a> <a id="2079" href="Games.Alternative.html#1241" class="Function">to-Game</a> <a id="2087" class="Symbol">(</a><a id="2088" href="Games.Alternative.html#669" class="Function">leaf&#39;</a> <a id="2094" href="Games.Alternative.html#2070" class="Bound">r</a><a id="2095" class="Symbol">)</a> <a id="2097" href="MLTT.Id.html#193" class="Datatype Operator">ï¼</a> <a id="2099" href="Games.Alternative.html#603" class="InductiveConstructor">leaf</a> <a id="2104" href="Games.Alternative.html#2070" class="Bound">r</a>
<a id="2106" href="Games.Alternative.html#2053" class="Function">to-Game-base&#39;</a> <a id="2120" href="Games.Alternative.html#2120" class="Bound">r</a> <a id="2122" class="Symbol">=</a> <a id="2124" href="MLTT.Identity-Type.html#157" class="InductiveConstructor">refl</a>

<a id="2130" class="Keyword">module</a> <a id="2137" href="Games.Alternative.html#2137" class="Module">_</a>
         <a id="2148" class="Symbol">(</a><a id="2149" href="Games.Alternative.html#2149" class="Bound">X</a> <a id="2151" class="Symbol">:</a> <a id="2153" href="Notation.General.html#372" class="Function">Type</a><a id="2157" class="Symbol">)</a>
         <a id="2168" class="Symbol">(</a><a id="2169" href="Games.Alternative.html#2169" class="Bound">Xf</a> <a id="2172" class="Symbol">:</a> <a id="2174" href="Games.Alternative.html#2149" class="Bound">X</a> <a id="2176" class="Symbol">â†’</a> <a id="2178" href="Games.TypeTrees.html#755" class="Datatype">ğ‘»</a><a id="2179" class="Symbol">)</a>
         <a id="2190" class="Symbol">(</a><a id="2191" href="Games.Alternative.html#2191" class="Bound">Ï•</a> <a id="2193" class="Symbol">:</a> <a id="2195" href="MonadOnTypes.K.html#471" class="Function">K</a> <a id="2197" href="Games.Alternative.html#2149" class="Bound">X</a><a id="2198" class="Symbol">)</a>
         <a id="2209" class="Symbol">(</a><a id="2210" href="Games.Alternative.html#2210" class="Bound">Ï•f</a> <a id="2213" class="Symbol">:</a> <a id="2215" class="Symbol">(</a><a id="2216" href="Games.Alternative.html#2216" class="Bound">x</a> <a id="2218" class="Symbol">:</a> <a id="2220" href="Games.Alternative.html#2149" class="Bound">X</a><a id="2221" class="Symbol">)</a> <a id="2223" class="Symbol">â†’</a> <a id="2225" href="Games.FiniteHistoryDependent.html#2364" class="Function">ğ“š</a> <a id="2227" class="Symbol">(</a><a id="2228" href="Games.Alternative.html#2169" class="Bound">Xf</a> <a id="2231" href="Games.Alternative.html#2216" class="Bound">x</a><a id="2232" class="Symbol">))</a>
         <a id="2244" class="Symbol">(</a><a id="2245" href="Games.Alternative.html#2245" class="Bound">q</a> <a id="2247" class="Symbol">:</a> <a id="2249" href="Games.TypeTrees.html#942" class="Function">Path</a> <a id="2254" class="Symbol">(</a><a id="2255" href="Games.Alternative.html#2149" class="Bound">X</a> <a id="2257" href="Games.TypeTrees.html#783" class="InductiveConstructor Operator">âˆ·</a> <a id="2259" href="Games.Alternative.html#2169" class="Bound">Xf</a><a id="2261" class="Symbol">)</a> <a id="2263" class="Symbol">â†’</a> <a id="2265" href="Games.Alternative.html#318" class="Bound">R</a><a id="2266" class="Symbol">)</a>
       <a id="2275" class="Keyword">where</a>

 <a id="2283" href="Games.Alternative.html#2283" class="Function">subgame</a> <a id="2291" class="Symbol">:</a> <a id="2293" href="Games.Alternative.html#2149" class="Bound">X</a> <a id="2295" class="Symbol">â†’</a> <a id="2297" href="Games.Alternative.html#481" class="Record">Game&#39;</a>
 <a id="2304" href="Games.Alternative.html#2283" class="Function">subgame</a> <a id="2312" href="Games.Alternative.html#2312" class="Bound">x</a> <a id="2314" class="Symbol">=</a> <a id="2316" href="Games.Alternative.html#520" class="InductiveConstructor">game&#39;</a> <a id="2322" class="Symbol">(</a><a id="2323" href="Games.Alternative.html#2169" class="Bound">Xf</a> <a id="2326" href="Games.Alternative.html#2312" class="Bound">x</a><a id="2327" class="Symbol">)</a> <a id="2329" class="Symbol">(</a><a id="2330" href="Games.TypeTrees.html#1658" class="Function">subpred</a> <a id="2338" href="Games.Alternative.html#2245" class="Bound">q</a> <a id="2340" href="Games.Alternative.html#2312" class="Bound">x</a><a id="2341" class="Symbol">)</a> <a id="2343" class="Symbol">(</a><a id="2344" href="Games.Alternative.html#2210" class="Bound">Ï•f</a> <a id="2347" href="Games.Alternative.html#2312" class="Bound">x</a><a id="2348" class="Symbol">)</a>

 <a id="2352" href="Games.Alternative.html#2352" class="Function">to-Game-step&#39;</a> <a id="2366" class="Symbol">:</a> <a id="2368" href="Games.Alternative.html#1241" class="Function">to-Game</a> <a id="2376" class="Symbol">(</a><a id="2377" href="Games.Alternative.html#721" class="Function">branch&#39;</a> <a id="2385" href="Games.Alternative.html#2149" class="Bound">X</a> <a id="2387" href="Games.Alternative.html#2191" class="Bound">Ï•</a> <a id="2389" href="Games.Alternative.html#2283" class="Function">subgame</a><a id="2396" class="Symbol">)</a> <a id="2398" href="MLTT.Id.html#193" class="Datatype Operator">ï¼</a> <a id="2400" href="Games.Alternative.html#622" class="InductiveConstructor">branch</a> <a id="2407" href="Games.Alternative.html#2149" class="Bound">X</a> <a id="2409" href="Games.Alternative.html#2191" class="Bound">Ï•</a> <a id="2411" class="Symbol">(</a><a id="2412" href="Games.Alternative.html#1241" class="Function">to-Game</a> <a id="2420" href="MLTT.Pi.html#513" class="Function Operator">âˆ˜</a> <a id="2422" href="Games.Alternative.html#2283" class="Function">subgame</a><a id="2429" class="Symbol">)</a>
 <a id="2432" href="Games.Alternative.html#2352" class="Function">to-Game-step&#39;</a> <a id="2446" class="Symbol">=</a> <a id="2448" href="MLTT.Identity-Type.html#157" class="InductiveConstructor">refl</a>

<a id="2454" class="Markup">\end{code}</a><a id="2464" class="Background">

The above conversion functions are mutually inverse and so the types
Game and Game&#39; are equivalent, assuming function extensionality.

</a><a id="2601" class="Markup">\begin{code}</a>

<a id="2615" class="Keyword">module</a> <a id="2622" href="Games.Alternative.html#2622" class="Module">_</a> <a id="2624" class="Symbol">(</a><a id="2625" href="Games.Alternative.html#2625" class="Bound">fe</a> <a id="2628" class="Symbol">:</a> <a id="2630" href="UF.FunExt.html#1047" class="Function">Fun-Ext</a><a id="2637" class="Symbol">)</a> <a id="2639" class="Keyword">where</a>

 <a id="2647" href="Games.Alternative.html#2647" class="Function">from-to-Game</a> <a id="2660" class="Symbol">:</a> <a id="2662" href="Games.Alternative.html#935" class="Function">from-Game</a> <a id="2672" href="MLTT.Pi.html#513" class="Function Operator">âˆ˜</a> <a id="2674" href="Games.Alternative.html#1241" class="Function">to-Game</a> <a id="2682" href="MLTT.Id.html#1889" class="Function Operator">âˆ¼</a> <a id="2684" href="MLTT.Pi.html#444" class="Function">id</a>
 <a id="2688" href="Games.Alternative.html#2647" class="Function">from-to-Game</a> <a id="2701" class="Symbol">(</a><a id="2702" href="Games.Alternative.html#520" class="InductiveConstructor">game&#39;</a> <a id="2708" href="Games.Alternative.html#2708" class="Bound">Xt</a> <a id="2711" href="Games.Alternative.html#2711" class="Bound">q</a> <a id="2713" href="Games.Alternative.html#2713" class="Bound">Ï•t</a><a id="2715" class="Symbol">)</a> <a id="2717" class="Symbol">=</a> <a id="2719" href="Games.Alternative.html#2740" class="Function">h</a> <a id="2721" href="Games.Alternative.html#2708" class="Bound">Xt</a> <a id="2724" href="Games.Alternative.html#2711" class="Bound">q</a> <a id="2726" href="Games.Alternative.html#2713" class="Bound">Ï•t</a>
  <a id="2731" class="Keyword">where</a>
   <a id="2740" href="Games.Alternative.html#2740" class="Function">h</a> <a id="2742" class="Symbol">:</a> <a id="2744" class="Symbol">(</a><a id="2745" href="Games.Alternative.html#2745" class="Bound">Xt</a> <a id="2748" class="Symbol">:</a> <a id="2750" href="Games.TypeTrees.html#755" class="Datatype">ğ‘»</a><a id="2751" class="Symbol">)</a>
       <a id="2760" class="Symbol">(</a><a id="2761" href="Games.Alternative.html#2761" class="Bound">q</a> <a id="2763" class="Symbol">:</a> <a id="2765" href="Games.TypeTrees.html#942" class="Function">Path</a> <a id="2770" href="Games.Alternative.html#2745" class="Bound">Xt</a> <a id="2773" class="Symbol">â†’</a> <a id="2775" href="Games.Alternative.html#318" class="Bound">R</a><a id="2776" class="Symbol">)</a>
       <a id="2785" class="Symbol">(</a><a id="2786" href="Games.Alternative.html#2786" class="Bound">Ï•t</a> <a id="2789" class="Symbol">:</a> <a id="2791" href="Games.FiniteHistoryDependent.html#2364" class="Function">ğ“š</a> <a id="2793" href="Games.Alternative.html#2745" class="Bound">Xt</a><a id="2795" class="Symbol">)</a>
     <a id="2802" class="Symbol">â†’</a> <a id="2804" href="Games.Alternative.html#935" class="Function">from-Game</a> <a id="2814" class="Symbol">(</a><a id="2815" href="Games.Alternative.html#1241" class="Function">to-Game</a> <a id="2823" class="Symbol">(</a><a id="2824" href="Games.Alternative.html#520" class="InductiveConstructor">game&#39;</a> <a id="2830" href="Games.Alternative.html#2745" class="Bound">Xt</a> <a id="2833" href="Games.Alternative.html#2761" class="Bound">q</a> <a id="2835" href="Games.Alternative.html#2786" class="Bound">Ï•t</a><a id="2837" class="Symbol">))</a> <a id="2840" href="MLTT.Id.html#193" class="Datatype Operator">ï¼</a> <a id="2842" href="Games.Alternative.html#520" class="InductiveConstructor">game&#39;</a> <a id="2848" href="Games.Alternative.html#2745" class="Bound">Xt</a> <a id="2851" href="Games.Alternative.html#2761" class="Bound">q</a> <a id="2853" href="Games.Alternative.html#2786" class="Bound">Ï•t</a>
   <a id="2859" href="Games.Alternative.html#2740" class="Function">h</a> <a id="2861" href="Games.TypeTrees.html#773" class="InductiveConstructor">[]</a>       <a id="2870" href="Games.Alternative.html#2870" class="Bound">q</a> <a id="2872" href="Games.TypeTrees.html#1310" class="InductiveConstructor">âŸ¨âŸ©</a>       <a id="2881" class="Symbol">=</a> <a id="2883" href="MLTT.Identity-Type.html#157" class="InductiveConstructor">refl</a>
   <a id="2891" href="Games.Alternative.html#2740" class="Function">h</a> <a id="2893" class="Symbol">(</a><a id="2894" href="Games.Alternative.html#2894" class="Bound">X</a> <a id="2896" href="Games.TypeTrees.html#783" class="InductiveConstructor Operator">âˆ·</a> <a id="2898" href="Games.Alternative.html#2898" class="Bound">Xf</a><a id="2900" class="Symbol">)</a> <a id="2902" href="Games.Alternative.html#2902" class="Bound">q</a> <a id="2904" class="Symbol">(</a><a id="2905" href="Games.Alternative.html#2905" class="Bound">Ï•</a> <a id="2907" href="Games.TypeTrees.html#1332" class="InductiveConstructor Operator">::</a> <a id="2910" href="Games.Alternative.html#2910" class="Bound">Ï•f</a><a id="2912" class="Symbol">)</a> <a id="2914" class="Symbol">=</a>
    <a id="2920" href="Games.Alternative.html#935" class="Function">from-Game</a> <a id="2930" class="Symbol">(</a><a id="2931" href="Games.Alternative.html#1241" class="Function">to-Game</a> <a id="2939" class="Symbol">(</a><a id="2940" href="Games.Alternative.html#520" class="InductiveConstructor">game&#39;</a> <a id="2946" class="Symbol">(</a><a id="2947" href="Games.Alternative.html#2894" class="Bound">X</a> <a id="2949" href="Games.TypeTrees.html#783" class="InductiveConstructor Operator">âˆ·</a> <a id="2951" href="Games.Alternative.html#2898" class="Bound">Xf</a><a id="2953" class="Symbol">)</a> <a id="2955" href="Games.Alternative.html#2902" class="Bound">q</a> <a id="2957" class="Symbol">(</a><a id="2958" href="Games.Alternative.html#2905" class="Bound">Ï•</a> <a id="2960" href="Games.TypeTrees.html#1332" class="InductiveConstructor Operator">::</a> <a id="2963" href="Games.Alternative.html#2910" class="Bound">Ï•f</a><a id="2965" class="Symbol">)))</a> <a id="2969" href="MLTT.Id.html#3170" class="Function Operator">ï¼âŸ¨reflâŸ©</a>
    <a id="2981" href="Games.Alternative.html#935" class="Function">from-Game</a> <a id="2991" class="Symbol">(</a><a id="2992" href="Games.Alternative.html#622" class="InductiveConstructor">branch</a> <a id="2999" href="Games.Alternative.html#2894" class="Bound">X</a> <a id="3001" href="Games.Alternative.html#2905" class="Bound">Ï•</a> <a id="3003" class="Symbol">(</a><a id="3004" href="Games.Alternative.html#1241" class="Function">to-Game</a> <a id="3012" href="MLTT.Pi.html#513" class="Function Operator">âˆ˜</a> <a id="3014" href="Games.Alternative.html#3229" class="Function">Gf</a><a id="3016" class="Symbol">))</a>            <a id="3030" href="MLTT.Id.html#3170" class="Function Operator">ï¼âŸ¨reflâŸ©</a>
    <a id="3042" href="Games.Alternative.html#721" class="Function">branch&#39;</a> <a id="3050" href="Games.Alternative.html#2894" class="Bound">X</a> <a id="3052" href="Games.Alternative.html#2905" class="Bound">Ï•</a> <a id="3054" href="Games.Alternative.html#3232" class="Function">Hf</a>                                   <a id="3091" href="MLTT.Id.html#2703" class="Function Operator">ï¼âŸ¨</a> <a id="3094" href="Games.Alternative.html#3387" class="Function">I</a> <a id="3096" href="MLTT.Id.html#2703" class="Function Operator">âŸ©</a>
    <a id="3102" href="Games.Alternative.html#721" class="Function">branch&#39;</a> <a id="3110" href="Games.Alternative.html#2894" class="Bound">X</a> <a id="3112" href="Games.Alternative.html#2905" class="Bound">Ï•</a> <a id="3114" href="Games.Alternative.html#3229" class="Function">Gf</a>                                   <a id="3151" href="MLTT.Id.html#3170" class="Function Operator">ï¼âŸ¨reflâŸ©</a>
    <a id="3163" href="Games.Alternative.html#520" class="InductiveConstructor">game&#39;</a> <a id="3169" class="Symbol">(</a><a id="3170" href="Games.Alternative.html#2894" class="Bound">X</a> <a id="3172" href="Games.TypeTrees.html#783" class="InductiveConstructor Operator">âˆ·</a> <a id="3174" href="Games.Alternative.html#2898" class="Bound">Xf</a><a id="3176" class="Symbol">)</a> <a id="3178" href="Games.Alternative.html#2902" class="Bound">q</a> <a id="3180" class="Symbol">(</a><a id="3181" href="Games.Alternative.html#2905" class="Bound">Ï•</a> <a id="3183" href="Games.TypeTrees.html#1332" class="InductiveConstructor Operator">::</a> <a id="3186" href="Games.Alternative.html#2910" class="Bound">Ï•f</a><a id="3188" class="Symbol">)</a>                       <a id="3212" href="MLTT.Id.html#2785" class="Function Operator">âˆ</a>
    <a id="3218" class="Keyword">where</a>
     <a id="3229" href="Games.Alternative.html#3229" class="Function">Gf</a> <a id="3232" href="Games.Alternative.html#3232" class="Function">Hf</a> <a id="3235" class="Symbol">:</a> <a id="3237" href="Games.Alternative.html#2894" class="Bound">X</a> <a id="3239" class="Symbol">â†’</a> <a id="3241" href="Games.Alternative.html#481" class="Record">Game&#39;</a>
     <a id="3252" href="Games.Alternative.html#3229" class="Function">Gf</a> <a id="3255" href="Games.Alternative.html#3255" class="Bound">x</a> <a id="3257" class="Symbol">=</a> <a id="3259" href="Games.Alternative.html#2283" class="Function">subgame</a> <a id="3267" href="Games.Alternative.html#2894" class="Bound">X</a> <a id="3269" href="Games.Alternative.html#2898" class="Bound">Xf</a> <a id="3272" href="Games.Alternative.html#2905" class="Bound">Ï•</a> <a id="3274" href="Games.Alternative.html#2910" class="Bound">Ï•f</a> <a id="3277" href="Games.Alternative.html#2902" class="Bound">q</a> <a id="3279" href="Games.Alternative.html#3255" class="Bound">x</a>
     <a id="3286" href="Games.Alternative.html#3232" class="Function">Hf</a> <a id="3289" href="Games.Alternative.html#3289" class="Bound">x</a> <a id="3291" class="Symbol">=</a> <a id="3293" href="Games.Alternative.html#935" class="Function">from-Game</a> <a id="3303" class="Symbol">(</a><a id="3304" href="Games.Alternative.html#1241" class="Function">to-Game</a> <a id="3312" class="Symbol">(</a><a id="3313" href="Games.Alternative.html#3229" class="Function">Gf</a> <a id="3316" href="Games.Alternative.html#3289" class="Bound">x</a><a id="3317" class="Symbol">))</a>

     <a id="3326" href="Games.Alternative.html#3326" class="Function">IH</a> <a id="3329" class="Symbol">:</a> <a id="3331" href="Games.Alternative.html#3232" class="Function">Hf</a> <a id="3334" href="MLTT.Id.html#1889" class="Function Operator">âˆ¼</a> <a id="3336" href="Games.Alternative.html#3229" class="Function">Gf</a>
     <a id="3344" href="Games.Alternative.html#3326" class="Function">IH</a> <a id="3347" href="Games.Alternative.html#3347" class="Bound">x</a> <a id="3349" class="Symbol">=</a> <a id="3351" href="Games.Alternative.html#2740" class="Function">h</a> <a id="3353" class="Symbol">(</a><a id="3354" href="Games.Alternative.html#2898" class="Bound">Xf</a> <a id="3357" href="Games.Alternative.html#3347" class="Bound">x</a><a id="3358" class="Symbol">)</a> <a id="3360" class="Symbol">(</a><a id="3361" href="Games.TypeTrees.html#1658" class="Function">subpred</a> <a id="3369" href="Games.Alternative.html#2902" class="Bound">q</a> <a id="3371" href="Games.Alternative.html#3347" class="Bound">x</a><a id="3372" class="Symbol">)</a> <a id="3374" class="Symbol">(</a><a id="3375" href="Games.Alternative.html#2910" class="Bound">Ï•f</a> <a id="3378" href="Games.Alternative.html#3347" class="Bound">x</a><a id="3379" class="Symbol">)</a>

     <a id="3387" href="Games.Alternative.html#3387" class="Function">I</a> <a id="3389" class="Symbol">:</a> <a id="3391" href="Games.Alternative.html#721" class="Function">branch&#39;</a> <a id="3399" href="Games.Alternative.html#2894" class="Bound">X</a> <a id="3401" href="Games.Alternative.html#2905" class="Bound">Ï•</a> <a id="3403" href="Games.Alternative.html#3232" class="Function">Hf</a> <a id="3406" href="MLTT.Id.html#193" class="Datatype Operator">ï¼</a> <a id="3408" href="Games.Alternative.html#721" class="Function">branch&#39;</a> <a id="3416" href="Games.Alternative.html#2894" class="Bound">X</a> <a id="3418" href="Games.Alternative.html#2905" class="Bound">Ï•</a> <a id="3420" href="Games.Alternative.html#3229" class="Function">Gf</a>
     <a id="3428" href="Games.Alternative.html#3387" class="Function">I</a> <a id="3430" class="Symbol">=</a> <a id="3432" href="MLTT.Id.html#1136" class="Function">ap</a> <a id="3435" class="Symbol">(</a><a id="3436" href="Games.Alternative.html#721" class="Function">branch&#39;</a> <a id="3444" href="Games.Alternative.html#2894" class="Bound">X</a> <a id="3446" href="Games.Alternative.html#2905" class="Bound">Ï•</a><a id="3447" class="Symbol">)</a> <a id="3449" class="Symbol">(</a><a id="3450" href="UF.FunExt.html#1242" class="Function">dfunext</a> <a id="3458" href="Games.Alternative.html#2625" class="Bound">fe</a> <a id="3461" href="Games.Alternative.html#3326" class="Function">IH</a><a id="3463" class="Symbol">)</a>

 <a id="3467" href="Games.Alternative.html#3467" class="Function">to-from-Game</a> <a id="3480" class="Symbol">:</a> <a id="3482" href="Games.Alternative.html#1241" class="Function">to-Game</a> <a id="3490" href="MLTT.Pi.html#513" class="Function Operator">âˆ˜</a> <a id="3492" href="Games.Alternative.html#935" class="Function">from-Game</a> <a id="3502" href="MLTT.Id.html#1889" class="Function Operator">âˆ¼</a> <a id="3504" href="MLTT.Pi.html#444" class="Function">id</a>
 <a id="3508" href="Games.Alternative.html#3467" class="Function">to-from-Game</a> <a id="3521" class="Symbol">(</a><a id="3522" href="Games.Alternative.html#603" class="InductiveConstructor">leaf</a> <a id="3527" href="Games.Alternative.html#3527" class="Bound">x</a><a id="3528" class="Symbol">)</a>        <a id="3537" class="Symbol">=</a> <a id="3539" href="MLTT.Identity-Type.html#157" class="InductiveConstructor">refl</a>
 <a id="3545" href="Games.Alternative.html#3467" class="Function">to-from-Game</a> <a id="3558" class="Symbol">(</a><a id="3559" href="Games.Alternative.html#622" class="InductiveConstructor">branch</a> <a id="3566" href="Games.Alternative.html#3566" class="Bound">X</a> <a id="3568" href="Games.Alternative.html#3568" class="Bound">Ï•</a> <a id="3570" href="Games.Alternative.html#3570" class="Bound">Gf</a><a id="3572" class="Symbol">)</a> <a id="3574" class="Symbol">=</a>
  <a id="3578" href="Games.Alternative.html#1241" class="Function">to-Game</a> <a id="3586" class="Symbol">(</a><a id="3587" href="Games.Alternative.html#935" class="Function">from-Game</a> <a id="3597" class="Symbol">(</a><a id="3598" href="Games.Alternative.html#622" class="InductiveConstructor">branch</a> <a id="3605" href="Games.Alternative.html#3566" class="Bound">X</a> <a id="3607" href="Games.Alternative.html#3568" class="Bound">Ï•</a> <a id="3609" href="Games.Alternative.html#3570" class="Bound">Gf</a><a id="3611" class="Symbol">))</a>    <a id="3617" href="MLTT.Id.html#3170" class="Function Operator">ï¼âŸ¨reflâŸ©</a>
  <a id="3627" href="Games.Alternative.html#1241" class="Function">to-Game</a> <a id="3635" class="Symbol">(</a><a id="3636" href="Games.Alternative.html#721" class="Function">branch&#39;</a> <a id="3644" href="Games.Alternative.html#3566" class="Bound">X</a> <a id="3646" href="Games.Alternative.html#3568" class="Bound">Ï•</a> <a id="3648" class="Symbol">(</a><a id="3649" href="Games.Alternative.html#935" class="Function">from-Game</a> <a id="3659" href="MLTT.Pi.html#513" class="Function Operator">âˆ˜</a> <a id="3661" href="Games.Alternative.html#3570" class="Bound">Gf</a><a id="3663" class="Symbol">))</a> <a id="3666" href="MLTT.Id.html#3170" class="Function Operator">ï¼âŸ¨reflâŸ©</a>
  <a id="3676" href="Games.Alternative.html#622" class="InductiveConstructor">branch</a> <a id="3683" href="Games.Alternative.html#3566" class="Bound">X</a> <a id="3685" href="Games.Alternative.html#3568" class="Bound">Ï•</a> <a id="3687" class="Symbol">(</a><a id="3688" href="Games.Alternative.html#1241" class="Function">to-Game</a> <a id="3696" href="MLTT.Pi.html#513" class="Function Operator">âˆ˜</a> <a id="3698" href="Games.Alternative.html#935" class="Function">from-Game</a> <a id="3708" href="MLTT.Pi.html#513" class="Function Operator">âˆ˜</a> <a id="3710" href="Games.Alternative.html#3570" class="Bound">Gf</a><a id="3712" class="Symbol">)</a>  <a id="3715" href="MLTT.Id.html#2703" class="Function Operator">ï¼âŸ¨</a> <a id="3718" href="Games.Alternative.html#3776" class="Function">I</a> <a id="3720" href="MLTT.Id.html#2703" class="Function Operator">âŸ©</a>
  <a id="3724" href="Games.Alternative.html#622" class="InductiveConstructor">branch</a> <a id="3731" href="Games.Alternative.html#3566" class="Bound">X</a> <a id="3733" href="Games.Alternative.html#3568" class="Bound">Ï•</a> <a id="3735" href="Games.Alternative.html#3570" class="Bound">Gf</a>                          <a id="3763" href="MLTT.Id.html#2785" class="Function Operator">âˆ</a>
  <a id="3767" class="Keyword">where</a>
   <a id="3776" href="Games.Alternative.html#3776" class="Function">I</a> <a id="3778" class="Symbol">=</a> <a id="3780" href="MLTT.Id.html#1136" class="Function">ap</a> <a id="3783" class="Symbol">(</a><a id="3784" href="Games.Alternative.html#622" class="InductiveConstructor">branch</a> <a id="3791" href="Games.Alternative.html#3566" class="Bound">X</a> <a id="3793" href="Games.Alternative.html#3568" class="Bound">Ï•</a><a id="3794" class="Symbol">)</a> <a id="3796" class="Symbol">(</a><a id="3797" href="UF.FunExt.html#1242" class="Function">dfunext</a> <a id="3805" href="Games.Alternative.html#2625" class="Bound">fe</a> <a id="3808" class="Symbol">(</a><a id="3809" href="Games.Alternative.html#3467" class="Function">to-from-Game</a> <a id="3822" href="MLTT.Pi.html#513" class="Function Operator">âˆ˜</a> <a id="3824" href="Games.Alternative.html#3570" class="Bound">Gf</a><a id="3826" class="Symbol">))</a>

 <a id="3831" href="Games.Alternative.html#3831" class="Function">Game-is-equiv-to-Game&#39;</a> <a id="3854" class="Symbol">:</a> <a id="3856" href="Games.Alternative.html#583" class="Datatype">Game</a> <a id="3861" href="UF.Equiv.html#1318" class="Function Operator">â‰ƒ</a> <a id="3863" href="Games.Alternative.html#481" class="Record">Game&#39;</a>
 <a id="3870" href="Games.Alternative.html#3831" class="Function">Game-is-equiv-to-Game&#39;</a> <a id="3893" class="Symbol">=</a> <a id="3895" href="UF.Equiv.html#7569" class="Function">qinveq</a> <a id="3902" href="Games.Alternative.html#935" class="Function">from-Game</a> <a id="3912" class="Symbol">(</a><a id="3913" href="Games.Alternative.html#1241" class="Function">to-Game</a> <a id="3921" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="3923" href="Games.Alternative.html#3467" class="Function">to-from-Game</a> <a id="3936" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="3938" href="Games.Alternative.html#2647" class="Function">from-to-Game</a><a id="3950" class="Symbol">)</a>

<a id="3953" class="Markup">\end{code}</a><a id="3963" class="Background">
</a></pre></body></html>