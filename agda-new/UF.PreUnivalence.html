<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>UF.PreUnivalence</title><link rel="stylesheet" href="Agda.css"><script type="text/javascript" src="highlight-hover.js"></script></head><body><pre class="Agda"><a id="1" class="Background">Martin Escardo 23 February 2023

The pre-univalence axiom, first suggested by Evan Cavallo in November
2017 [1] and then again by Peter Lumsdaine in August 2022
verbally to me.

[1] https://groups.google.com/g/homotopytypetheory/c/bKti7krHM-c/m/vxRU3FwADQAJ

The preunivalence axiom is a common generalization of the univalence
axiom and the K axiom.

The strong preunivalence axiom is a variant that was stated by Fredrik Bakke
in the agda-unimath in February 2025 [2].

[2] https://unimath.github.io/agda-unimath/foundation.strong-preunivalence.html

</a><a id="554" class="Markup">\begin{code}</a>

<a id="568" class="Symbol">{-#</a> <a id="572" class="Keyword">OPTIONS</a> <a id="580" class="Pragma">--safe</a> <a id="587" class="Pragma">--without-K</a> <a id="599" class="Symbol">#-}</a>

<a id="604" class="Keyword">module</a> <a id="611" href="UF.PreUnivalence.html" class="Module">UF.PreUnivalence</a> <a id="628" class="Keyword">where</a>

<a id="635" class="Keyword">open</a> <a id="640" class="Keyword">import</a> <a id="647" href="MLTT.Spartan.html" class="Module">MLTT.Spartan</a>
<a id="660" class="Keyword">open</a> <a id="665" class="Keyword">import</a> <a id="672" href="UF.Base.html" class="Module">UF.Base</a>
<a id="680" class="Keyword">open</a> <a id="685" class="Keyword">import</a> <a id="692" href="UF.Embeddings.html" class="Module">UF.Embeddings</a>
<a id="706" class="Keyword">open</a> <a id="711" class="Keyword">import</a> <a id="718" href="UF.Equiv.html" class="Module">UF.Equiv</a>
<a id="727" class="Keyword">open</a> <a id="732" class="Keyword">import</a> <a id="739" href="UF.Equiv-FunExt.html" class="Module">UF.Equiv-FunExt</a>
<a id="755" class="Keyword">open</a> <a id="760" class="Keyword">import</a> <a id="767" href="UF.EquivalenceExamples.html" class="Module">UF.EquivalenceExamples</a>
<a id="790" class="Keyword">open</a> <a id="795" class="Keyword">import</a> <a id="802" href="UF.FunExt.html" class="Module">UF.FunExt</a>
<a id="812" class="Keyword">open</a> <a id="817" class="Keyword">import</a> <a id="824" href="UF.Retracts.html" class="Module">UF.Retracts</a>
<a id="836" class="Keyword">open</a> <a id="841" class="Keyword">import</a> <a id="848" href="UF.Sets.html" class="Module">UF.Sets</a>
<a id="856" class="Keyword">open</a> <a id="861" class="Keyword">import</a> <a id="868" href="UF.Subsingletons.html" class="Module">UF.Subsingletons</a>
<a id="885" class="Keyword">open</a> <a id="890" class="Keyword">import</a> <a id="897" href="UF.Univalence.html" class="Module">UF.Univalence</a>

<a id="is-preunivalent"></a><a id="912" href="UF.PreUnivalence.html#912" class="Function">is-preunivalent</a> <a id="928" class="Symbol">:</a> <a id="930" class="Symbol">∀</a> <a id="932" href="UF.PreUnivalence.html#932" class="Bound">𝓤</a> <a id="934" class="Symbol">→</a> <a id="936" href="UF.PreUnivalence.html#932" class="Bound">𝓤</a> <a id="938" href="Agda.Primitive.html#931" class="Primitive Operator">⁺</a> <a id="940" href="MLTT.Universes.html#3977" class="Function Operator">̇</a>
<a id="942" href="UF.PreUnivalence.html#912" class="Function">is-preunivalent</a> <a id="958" href="UF.PreUnivalence.html#958" class="Bound">𝓤</a> <a id="960" class="Symbol">=</a> <a id="962" class="Symbol">(</a><a id="963" href="UF.PreUnivalence.html#963" class="Bound">X</a> <a id="965" href="UF.PreUnivalence.html#965" class="Bound">Y</a> <a id="967" class="Symbol">:</a> <a id="969" href="UF.PreUnivalence.html#958" class="Bound">𝓤</a> <a id="971" href="MLTT.Universes.html#3977" class="Function Operator">̇</a> <a id="973" class="Symbol">)</a> <a id="975" class="Symbol">→</a> <a id="977" href="UF.Embeddings.html#625" class="Function">is-embedding</a> <a id="990" class="Symbol">(</a><a id="991" href="UF.Equiv.html#3374" class="Function">idtoeq</a> <a id="998" href="UF.PreUnivalence.html#963" class="Bound">X</a> <a id="1000" href="UF.PreUnivalence.html#965" class="Bound">Y</a><a id="1001" class="Symbol">)</a>

<a id="Preunivalence"></a><a id="1004" href="UF.PreUnivalence.html#1004" class="Function">Preunivalence</a> <a id="1018" class="Symbol">:</a> <a id="1020" href="Agda.Primitive.html#512" class="Primitive">𝓤ω</a>
<a id="1023" href="UF.PreUnivalence.html#1004" class="Function">Preunivalence</a> <a id="1037" class="Symbol">=</a> <a id="1039" class="Symbol">(</a><a id="1040" href="UF.PreUnivalence.html#1040" class="Bound">𝓤</a> <a id="1042" class="Symbol">:</a> <a id="1044" href="Agda.Primitive.html#742" class="Postulate">Universe</a><a id="1052" class="Symbol">)</a> <a id="1054" class="Symbol">→</a> <a id="1056" href="UF.PreUnivalence.html#912" class="Function">is-preunivalent</a> <a id="1072" href="UF.PreUnivalence.html#1040" class="Bound">𝓤</a>

<a id="univalence-gives-preunivalence"></a><a id="1075" href="UF.PreUnivalence.html#1075" class="Function">univalence-gives-preunivalence</a> <a id="1106" class="Symbol">:</a> <a id="1108" href="UF.Univalence.html#412" class="Function">is-univalent</a> <a id="1121" href="MLTT.Universes.html#3834" class="Generalizable">𝓤</a>
                               <a id="1154" class="Symbol">→</a> <a id="1156" href="UF.PreUnivalence.html#912" class="Function">is-preunivalent</a> <a id="1172" href="MLTT.Universes.html#3834" class="Generalizable">𝓤</a>
<a id="1174" href="UF.PreUnivalence.html#1075" class="Function">univalence-gives-preunivalence</a> <a id="1205" href="UF.PreUnivalence.html#1205" class="Bound">ua</a> <a id="1208" href="UF.PreUnivalence.html#1208" class="Bound">X</a> <a id="1210" href="UF.PreUnivalence.html#1210" class="Bound">Y</a> <a id="1212" class="Symbol">=</a> <a id="1214" href="UF.Embeddings.html#3459" class="Function">equivs-are-embeddings</a>
                                         <a id="1277" class="Symbol">(</a><a id="1278" href="UF.Equiv.html#3374" class="Function">idtoeq</a> <a id="1285" href="UF.PreUnivalence.html#1208" class="Bound">X</a> <a id="1287" href="UF.PreUnivalence.html#1210" class="Bound">Y</a><a id="1288" class="Symbol">)</a>
                                         <a id="1331" class="Symbol">(</a><a id="1332" href="UF.PreUnivalence.html#1205" class="Bound">ua</a> <a id="1335" href="UF.PreUnivalence.html#1208" class="Bound">X</a> <a id="1337" href="UF.PreUnivalence.html#1210" class="Bound">Y</a><a id="1338" class="Symbol">)</a>

<a id="Univalence-gives-Preunivalence"></a><a id="1341" href="UF.PreUnivalence.html#1341" class="Function">Univalence-gives-Preunivalence</a> <a id="1372" class="Symbol">:</a> <a id="1374" href="UF.Univalence.html#494" class="Function">Univalence</a> <a id="1385" class="Symbol">→</a> <a id="1387" href="UF.PreUnivalence.html#1004" class="Function">Preunivalence</a>
<a id="1401" href="UF.PreUnivalence.html#1341" class="Function">Univalence-gives-Preunivalence</a> <a id="1432" href="UF.PreUnivalence.html#1432" class="Bound">ua</a> <a id="1435" href="UF.PreUnivalence.html#1435" class="Bound">𝓤</a> <a id="1437" class="Symbol">=</a> <a id="1439" href="UF.PreUnivalence.html#1075" class="Function">univalence-gives-preunivalence</a> <a id="1470" class="Symbol">(</a><a id="1471" href="UF.PreUnivalence.html#1432" class="Bound">ua</a> <a id="1474" href="UF.PreUnivalence.html#1435" class="Bound">𝓤</a><a id="1475" class="Symbol">)</a>

<a id="K-gives-preunivalence"></a><a id="1478" href="UF.PreUnivalence.html#1478" class="Function">K-gives-preunivalence</a> <a id="1500" class="Symbol">:</a> <a id="1502" href="UF.Sets.html#2309" class="Function">K-axiom</a> <a id="1510" href="MLTT.Universes.html#3834" class="Generalizable">𝓤</a>
                      <a id="1534" class="Symbol">→</a> <a id="1536" href="UF.Sets.html#2309" class="Function">K-axiom</a> <a id="1544" class="Symbol">(</a><a id="1545" href="MLTT.Universes.html#3834" class="Generalizable">𝓤</a> <a id="1547" href="Agda.Primitive.html#931" class="Primitive Operator">⁺</a><a id="1548" class="Symbol">)</a>
                      <a id="1572" class="Symbol">→</a> <a id="1574" href="UF.PreUnivalence.html#912" class="Function">is-preunivalent</a> <a id="1590" href="MLTT.Universes.html#3834" class="Generalizable">𝓤</a>
<a id="1592" href="UF.PreUnivalence.html#1478" class="Function">K-gives-preunivalence</a> <a id="1614" class="Symbol">{</a><a id="1615" href="UF.PreUnivalence.html#1615" class="Bound">𝓤</a><a id="1616" class="Symbol">}</a> <a id="1618" href="UF.PreUnivalence.html#1618" class="Bound">k</a> <a id="1620" href="UF.PreUnivalence.html#1620" class="Bound">k&#39;</a> <a id="1623" href="UF.PreUnivalence.html#1623" class="Bound">X</a> <a id="1625" href="UF.PreUnivalence.html#1625" class="Bound">Y</a> <a id="1627" href="UF.PreUnivalence.html#1627" class="Bound">e</a> <a id="1629" class="Symbol">(</a><a id="1630" href="UF.PreUnivalence.html#1630" class="Bound">p</a> <a id="1632" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="1634" class="Symbol">_)</a> <a id="1637" class="Symbol">(</a><a id="1638" href="UF.PreUnivalence.html#1638" class="Bound">p&#39;</a> <a id="1641" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="1643" class="Symbol">_)</a> <a id="1646" class="Symbol">=</a>
 <a id="1649" href="UF.Subsingletons.html#4964" class="Function">to-subtype-＝</a> <a id="1662" class="Symbol">(λ</a> <a id="1665" href="UF.PreUnivalence.html#1665" class="Bound">_</a> <a id="1667" class="Symbol">→</a> <a id="1669" href="UF.PreUnivalence.html#1618" class="Bound">k</a> <a id="1671" class="Symbol">(</a><a id="1672" href="UF.PreUnivalence.html#1623" class="Bound">X</a> <a id="1674" href="UF.Equiv.html#1318" class="Function Operator">≃</a> <a id="1676" href="UF.PreUnivalence.html#1625" class="Bound">Y</a><a id="1677" class="Symbol">))</a> <a id="1680" class="Symbol">(</a><a id="1681" href="UF.PreUnivalence.html#1620" class="Bound">k&#39;</a> <a id="1684" class="Symbol">(</a><a id="1685" href="UF.PreUnivalence.html#1615" class="Bound">𝓤</a> <a id="1687" href="MLTT.Universes.html#3977" class="Function Operator">̇</a> <a id="1689" class="Symbol">)</a><a id="1690" href="UF.PreUnivalence.html#1630" class="Bound">p</a> <a id="1692" href="UF.PreUnivalence.html#1638" class="Bound">p&#39;</a><a id="1694" class="Symbol">)</a>

<a id="K-gives-Preunivalence"></a><a id="1697" href="UF.PreUnivalence.html#1697" class="Function">K-gives-Preunivalence</a> <a id="1719" class="Symbol">:</a> <a id="1721" href="UF.Sets.html#2366" class="Function">K-Axiom</a> <a id="1729" class="Symbol">→</a> <a id="1731" href="UF.PreUnivalence.html#1004" class="Function">Preunivalence</a>
<a id="1745" href="UF.PreUnivalence.html#1697" class="Function">K-gives-Preunivalence</a> <a id="1767" href="UF.PreUnivalence.html#1767" class="Bound">k</a> <a id="1769" href="UF.PreUnivalence.html#1769" class="Bound">𝓤</a> <a id="1771" class="Symbol">=</a> <a id="1773" href="UF.PreUnivalence.html#1478" class="Function">K-gives-preunivalence</a> <a id="1795" class="Symbol">(</a><a id="1796" href="UF.PreUnivalence.html#1767" class="Bound">k</a> <a id="1798" href="UF.PreUnivalence.html#1769" class="Bound">𝓤</a><a id="1799" class="Symbol">)</a> <a id="1801" class="Symbol">(</a><a id="1802" href="UF.PreUnivalence.html#1767" class="Bound">k</a> <a id="1804" class="Symbol">(</a><a id="1805" href="UF.PreUnivalence.html#1769" class="Bound">𝓤</a> <a id="1807" href="Agda.Primitive.html#931" class="Primitive Operator">⁺</a><a id="1808" class="Symbol">))</a>

<a id="1812" class="Markup">\end{code}</a><a id="1822" class="Background">

Added by Evan Cavallo on 13th March 2025. The strong preunivalence axiom and the
fact that it implies the preunivalence axiom are due to Fredrik Bakke.

</a><a id="1977" class="Markup">\begin{code}</a>

<a id="is-strong-preunivalent"></a><a id="1991" href="UF.PreUnivalence.html#1991" class="Function">is-strong-preunivalent</a> <a id="2014" class="Symbol">:</a> <a id="2016" class="Symbol">∀</a> <a id="2018" href="UF.PreUnivalence.html#2018" class="Bound">𝓤</a> <a id="2020" href="UF.PreUnivalence.html#2020" class="Bound">𝓥</a> <a id="2022" class="Symbol">→</a> <a id="2024" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="2028" class="Symbol">(</a><a id="2029" href="UF.PreUnivalence.html#2018" class="Bound">𝓤</a> <a id="2031" href="Agda.Primitive.html#931" class="Primitive Operator">⁺</a> <a id="2033" href="Agda.Primitive.html#961" class="Primitive Operator">⊔</a> <a id="2035" href="UF.PreUnivalence.html#2020" class="Bound">𝓥</a> <a id="2037" href="Agda.Primitive.html#931" class="Primitive Operator">⁺</a><a id="2038" class="Symbol">)</a>
<a id="2040" href="UF.PreUnivalence.html#1991" class="Function">is-strong-preunivalent</a> <a id="2063" href="UF.PreUnivalence.html#2063" class="Bound">𝓤</a> <a id="2065" href="UF.PreUnivalence.html#2065" class="Bound">𝓥</a> <a id="2067" class="Symbol">=</a> <a id="2069" class="Symbol">(</a><a id="2070" href="UF.PreUnivalence.html#2070" class="Bound">X</a> <a id="2072" class="Symbol">:</a> <a id="2074" href="UF.PreUnivalence.html#2063" class="Bound">𝓤</a> <a id="2076" href="MLTT.Universes.html#3977" class="Function Operator">̇</a> <a id="2078" class="Symbol">)</a> <a id="2080" class="Symbol">→</a> <a id="2082" href="UF.Sets.html#709" class="Function">is-set</a> <a id="2089" class="Symbol">(</a><a id="2090" href="MLTT.Sigma.html#423" class="Function">Σ</a> <a id="2092" href="UF.PreUnivalence.html#2092" class="Bound">Y</a> <a id="2094" href="MLTT.Sigma.html#423" class="Function">꞉</a> <a id="2096" href="UF.PreUnivalence.html#2065" class="Bound">𝓥</a> <a id="2098" href="MLTT.Universes.html#3977" class="Function Operator">̇</a>  <a id="2101" href="MLTT.Sigma.html#423" class="Function">,</a> <a id="2103" href="UF.PreUnivalence.html#2070" class="Bound">X</a> <a id="2105" href="UF.Equiv.html#1318" class="Function Operator">≃</a> <a id="2107" href="UF.PreUnivalence.html#2092" class="Bound">Y</a><a id="2108" class="Symbol">)</a>

<a id="strong-preunivalence-gives-preunivalence"></a><a id="2111" href="UF.PreUnivalence.html#2111" class="Function">strong-preunivalence-gives-preunivalence</a> <a id="2152" class="Symbol">:</a> <a id="2154" href="UF.PreUnivalence.html#1991" class="Function">is-strong-preunivalent</a> <a id="2177" href="MLTT.Universes.html#3834" class="Generalizable">𝓤</a> <a id="2179" href="MLTT.Universes.html#3834" class="Generalizable">𝓤</a>
                                         <a id="2222" class="Symbol">→</a> <a id="2224" href="UF.PreUnivalence.html#912" class="Function">is-preunivalent</a> <a id="2240" href="MLTT.Universes.html#3834" class="Generalizable">𝓤</a>
<a id="2242" href="UF.PreUnivalence.html#2111" class="Function">strong-preunivalence-gives-preunivalence</a> <a id="2283" href="UF.PreUnivalence.html#2283" class="Bound">spua</a> <a id="2288" href="UF.PreUnivalence.html#2288" class="Bound">X</a> <a id="2290" class="Symbol">=</a>
 <a id="2293" href="UF.Embeddings.html#17871" class="Function">NatΣ-is-embedding-converse</a> <a id="2320" class="Symbol">(</a><a id="2321" href="UF.PreUnivalence.html#2288" class="Bound">X</a> <a id="2323" href="MLTT.Id.html#193" class="Datatype Operator">＝_</a><a id="2325" class="Symbol">)</a> <a id="2327" class="Symbol">(</a><a id="2328" href="UF.PreUnivalence.html#2288" class="Bound">X</a> <a id="2330" href="UF.Equiv.html#1318" class="Function Operator">≃_</a><a id="2332" class="Symbol">)</a> <a id="2334" class="Symbol">(</a><a id="2335" href="UF.Equiv.html#3374" class="Function">idtoeq</a> <a id="2342" href="UF.PreUnivalence.html#2288" class="Bound">X</a><a id="2343" class="Symbol">)</a>
  <a id="2347" class="Symbol">(</a><a id="2348" href="UF.Embeddings.html#15879" class="Function">maps-of-props-into-sets-are-embeddings</a>
   <a id="2390" class="Symbol">(</a><a id="2391" href="UF.Base.html#679" class="Function">NatΣ</a> <a id="2396" class="Symbol">(</a><a id="2397" href="UF.Equiv.html#3374" class="Function">idtoeq</a> <a id="2404" href="UF.PreUnivalence.html#2288" class="Bound">X</a><a id="2405" class="Symbol">))</a>
    <a id="2412" class="Symbol">(</a><a id="2413" href="UF.Subsingletons.html#3787" class="Function">singleton-types-are-props</a> <a id="2439" href="UF.PreUnivalence.html#2288" class="Bound">X</a><a id="2440" class="Symbol">)</a>
     <a id="2447" class="Symbol">(</a><a id="2448" href="UF.PreUnivalence.html#2283" class="Bound">spua</a> <a id="2453" href="UF.PreUnivalence.html#2288" class="Bound">X</a><a id="2454" class="Symbol">))</a>

<a id="funext-and-preunivalence-give-strong-preunivalence"></a><a id="2458" href="UF.PreUnivalence.html#2458" class="Function">funext-and-preunivalence-give-strong-preunivalence</a> <a id="2509" class="Symbol">:</a> <a id="2511" href="UF.FunExt.html#850" class="Function">funext</a> <a id="2518" href="MLTT.Universes.html#3834" class="Generalizable">𝓤</a> <a id="2520" href="MLTT.Universes.html#3834" class="Generalizable">𝓤</a>
                                                   <a id="2573" class="Symbol">→</a> <a id="2575" href="UF.FunExt.html#850" class="Function">funext</a> <a id="2582" href="MLTT.Universes.html#3836" class="Generalizable">𝓥</a> <a id="2584" href="MLTT.Universes.html#3834" class="Generalizable">𝓤</a>
                                                   <a id="2637" class="Symbol">→</a> <a id="2639" href="UF.FunExt.html#850" class="Function">funext</a> <a id="2646" href="MLTT.Universes.html#3836" class="Generalizable">𝓥</a> <a id="2648" href="MLTT.Universes.html#3836" class="Generalizable">𝓥</a>
                                                   <a id="2701" class="Symbol">→</a> <a id="2703" href="UF.PreUnivalence.html#912" class="Function">is-preunivalent</a> <a id="2719" href="MLTT.Universes.html#3836" class="Generalizable">𝓥</a>
                                                   <a id="2772" class="Symbol">→</a> <a id="2774" href="UF.PreUnivalence.html#1991" class="Function">is-strong-preunivalent</a> <a id="2797" href="MLTT.Universes.html#3834" class="Generalizable">𝓤</a> <a id="2799" href="MLTT.Universes.html#3836" class="Generalizable">𝓥</a>
<a id="2801" href="UF.PreUnivalence.html#2458" class="Function">funext-and-preunivalence-give-strong-preunivalence</a>
 <a id="2853" class="Symbol">{</a><a id="2854" href="UF.PreUnivalence.html#2854" class="Bound">𝓤</a><a id="2855" class="Symbol">}</a> <a id="2857" class="Symbol">{</a><a id="2858" href="UF.PreUnivalence.html#2858" class="Bound">𝓥</a><a id="2859" class="Symbol">}</a> <a id="2861" href="UF.PreUnivalence.html#2861" class="Bound">feuu</a> <a id="2866" href="UF.PreUnivalence.html#2866" class="Bound">fevu</a> <a id="2871" href="UF.PreUnivalence.html#2871" class="Bound">fevv</a> <a id="2876" href="UF.PreUnivalence.html#2876" class="Bound">preua</a> <a id="2882" href="UF.PreUnivalence.html#2882" class="Bound">X</a> <a id="2884" class="Symbol">{</a><a id="2885" href="UF.PreUnivalence.html#2885" class="Bound">Y</a> <a id="2887" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="2889" href="UF.PreUnivalence.html#2889" class="Bound">α</a><a id="2890" class="Symbol">}</a> <a id="2892" class="Symbol">{</a><a id="2893" href="UF.PreUnivalence.html#2893" class="Bound">Y&#39;</a> <a id="2896" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="2898" href="UF.PreUnivalence.html#2898" class="Bound">α&#39;</a><a id="2900" class="Symbol">}</a> <a id="2902" class="Symbol">=</a>
 <a id="2905" href="UF.Retracts.html#2703" class="Function">retract-of-prop</a>
  <a id="2923" class="Symbol">(</a><a id="2924" href="UF.Base.html#14933" class="Function">to-Σ-＝</a> <a id="2931" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="2933" href="UF.Base.html#14767" class="Function">from-Σ-＝</a> <a id="2942" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="2944" href="UF.Base.html#15667" class="Function">tofrom-Σ-＝</a><a id="2954" class="Symbol">)</a>
   <a id="2959" class="Symbol">(</a><a id="2960" href="UF.Equiv.html#8620" class="Function">equiv-to-prop</a>
    <a id="2978" class="Symbol">(</a><a id="2979" href="UF.EquivalenceExamples.html#3378" class="Function">Σ-cong</a> <a id="2986" class="Symbol">λ</a> <a id="2988" href="UF.PreUnivalence.html#2988" class="Bound">p</a> <a id="2990" class="Symbol">→</a>
     <a id="2997" class="Symbol">(_</a> <a id="3000" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="3002" href="UF.EquivalenceExamples.html#33942" class="Function">∙-is-equiv-left</a> <a id="3018" class="Symbol">(</a><a id="3019" href="UF.PreUnivalence.html#3112" class="Function">transport-eq</a> <a id="3032" href="UF.PreUnivalence.html#2988" class="Bound">p</a><a id="3033" class="Symbol">))</a> <a id="3036" href="UF.Equiv.html#2640" class="Function Operator">●</a> <a id="3038" href="UF.PreUnivalence.html#3239" class="Function">shift-equiv</a> <a id="3050" href="UF.PreUnivalence.html#2889" class="Bound">α</a> <a id="3052" class="Symbol">(</a><a id="3053" href="UF.Equiv.html#3374" class="Function">idtoeq</a> <a id="3060" class="Symbol">_</a> <a id="3062" class="Symbol">_</a> <a id="3064" href="UF.PreUnivalence.html#2988" class="Bound">p</a><a id="3065" class="Symbol">)</a> <a id="3067" href="UF.PreUnivalence.html#2898" class="Bound">α&#39;</a><a id="3069" class="Symbol">)</a>
    <a id="3075" class="Symbol">(</a><a id="3076" href="UF.PreUnivalence.html#2876" class="Bound">preua</a> <a id="3082" class="Symbol">_</a> <a id="3084" class="Symbol">_</a> <a id="3086" class="Symbol">(</a><a id="3087" href="UF.Equiv.html#7797" class="Function">≃-sym</a> <a id="3093" href="UF.PreUnivalence.html#2889" class="Bound">α</a> <a id="3095" href="UF.Equiv.html#2640" class="Function Operator">●</a> <a id="3097" href="UF.PreUnivalence.html#2898" class="Bound">α&#39;</a><a id="3099" class="Symbol">)))</a>
 <a id="3104" class="Keyword">where</a>
  <a id="3112" href="UF.PreUnivalence.html#3112" class="Function">transport-eq</a> <a id="3125" class="Symbol">:</a> <a id="3127" class="Symbol">(</a><a id="3128" href="UF.PreUnivalence.html#3128" class="Bound">p</a> <a id="3130" class="Symbol">:</a> <a id="3132" href="UF.PreUnivalence.html#2885" class="Bound">Y</a> <a id="3134" href="MLTT.Id.html#193" class="Datatype Operator">＝</a> <a id="3136" href="UF.PreUnivalence.html#2893" class="Bound">Y&#39;</a><a id="3138" class="Symbol">)</a> <a id="3140" class="Symbol">→</a> <a id="3142" href="UF.PreUnivalence.html#2889" class="Bound">α</a> <a id="3144" href="UF.Equiv.html#2640" class="Function Operator">●</a> <a id="3146" href="UF.Equiv.html#3374" class="Function">idtoeq</a> <a id="3153" href="UF.PreUnivalence.html#2885" class="Bound">Y</a> <a id="3155" href="UF.PreUnivalence.html#2893" class="Bound">Y&#39;</a> <a id="3158" href="UF.PreUnivalence.html#3128" class="Bound">p</a> <a id="3160" href="MLTT.Id.html#193" class="Datatype Operator">＝</a> <a id="3162" href="MLTT.Id.html#735" class="Function">transport</a> <a id="3172" class="Symbol">(</a><a id="3173" href="UF.PreUnivalence.html#2882" class="Bound">X</a> <a id="3175" href="UF.Equiv.html#1318" class="Function Operator">≃_</a><a id="3177" class="Symbol">)</a> <a id="3179" href="UF.PreUnivalence.html#3128" class="Bound">p</a> <a id="3181" href="UF.PreUnivalence.html#2889" class="Bound">α</a>
  <a id="3185" href="UF.PreUnivalence.html#3112" class="Function">transport-eq</a> <a id="3198" href="MLTT.Identity-Type.html#157" class="InductiveConstructor">refl</a> <a id="3203" class="Symbol">=</a> <a id="3205" href="UF.Equiv-FunExt.html#8066" class="Function">≃-refl-right&#39;</a> <a id="3219" href="UF.PreUnivalence.html#2866" class="Bound">fevu</a> <a id="3224" href="UF.PreUnivalence.html#2871" class="Bound">fevv</a> <a id="3229" href="UF.PreUnivalence.html#2861" class="Bound">feuu</a> <a id="3234" href="UF.PreUnivalence.html#2889" class="Bound">α</a>

  <a id="3239" href="UF.PreUnivalence.html#3239" class="Function">shift-equiv</a> <a id="3251" class="Symbol">:</a> <a id="3253" class="Symbol">{</a><a id="3254" href="UF.PreUnivalence.html#3254" class="Bound">A</a> <a id="3256" class="Symbol">:</a> <a id="3258" href="UF.PreUnivalence.html#2854" class="Bound">𝓤</a> <a id="3260" href="MLTT.Universes.html#3977" class="Function Operator">̇</a> <a id="3262" class="Symbol">}</a> <a id="3264" class="Symbol">{</a><a id="3265" href="UF.PreUnivalence.html#3265" class="Bound">B</a> <a id="3267" class="Symbol">:</a> <a id="3269" href="UF.PreUnivalence.html#2858" class="Bound">𝓥</a> <a id="3271" href="MLTT.Universes.html#3977" class="Function Operator">̇</a> <a id="3273" class="Symbol">}</a> <a id="3275" class="Symbol">{</a><a id="3276" href="UF.PreUnivalence.html#3276" class="Bound">C</a> <a id="3278" class="Symbol">:</a> <a id="3280" href="UF.PreUnivalence.html#2858" class="Bound">𝓥</a> <a id="3282" href="MLTT.Universes.html#3977" class="Function Operator">̇</a> <a id="3284" class="Symbol">}</a>
              <a id="3300" class="Symbol">→</a> <a id="3302" class="Symbol">(</a><a id="3303" href="UF.PreUnivalence.html#3303" class="Bound">e</a> <a id="3305" class="Symbol">:</a> <a id="3307" href="UF.PreUnivalence.html#3254" class="Bound">A</a> <a id="3309" href="UF.Equiv.html#1318" class="Function Operator">≃</a> <a id="3311" href="UF.PreUnivalence.html#3265" class="Bound">B</a><a id="3312" class="Symbol">)</a> <a id="3314" class="Symbol">(</a><a id="3315" href="UF.PreUnivalence.html#3315" class="Bound">e&#39;</a> <a id="3318" class="Symbol">:</a> <a id="3320" href="UF.PreUnivalence.html#3265" class="Bound">B</a> <a id="3322" href="UF.Equiv.html#1318" class="Function Operator">≃</a> <a id="3324" href="UF.PreUnivalence.html#3276" class="Bound">C</a><a id="3325" class="Symbol">)</a> <a id="3327" class="Symbol">(</a><a id="3328" href="UF.PreUnivalence.html#3328" class="Bound">e&#39;&#39;</a> <a id="3332" class="Symbol">:</a> <a id="3334" href="UF.PreUnivalence.html#3254" class="Bound">A</a> <a id="3336" href="UF.Equiv.html#1318" class="Function Operator">≃</a> <a id="3338" href="UF.PreUnivalence.html#3276" class="Bound">C</a><a id="3339" class="Symbol">)</a>
              <a id="3355" class="Symbol">→</a> <a id="3357" class="Symbol">(</a><a id="3358" href="UF.PreUnivalence.html#3303" class="Bound">e</a> <a id="3360" href="UF.Equiv.html#2640" class="Function Operator">●</a> <a id="3362" href="UF.PreUnivalence.html#3315" class="Bound">e&#39;</a> <a id="3365" href="MLTT.Id.html#193" class="Datatype Operator">＝</a> <a id="3367" href="UF.PreUnivalence.html#3328" class="Bound">e&#39;&#39;</a><a id="3370" class="Symbol">)</a> <a id="3372" href="UF.Equiv.html#1318" class="Function Operator">≃</a> <a id="3374" class="Symbol">(</a><a id="3375" href="UF.PreUnivalence.html#3315" class="Bound">e&#39;</a> <a id="3378" href="MLTT.Id.html#193" class="Datatype Operator">＝</a> <a id="3380" href="UF.Equiv.html#7797" class="Function">≃-sym</a> <a id="3386" href="UF.PreUnivalence.html#3303" class="Bound">e</a> <a id="3388" href="UF.Equiv.html#2640" class="Function Operator">●</a> <a id="3390" href="UF.PreUnivalence.html#3328" class="Bound">e&#39;&#39;</a><a id="3393" class="Symbol">)</a>
  <a id="3397" href="UF.PreUnivalence.html#3239" class="Function">shift-equiv</a> <a id="3409" href="UF.PreUnivalence.html#3409" class="Bound">e</a> <a id="3411" href="UF.PreUnivalence.html#3411" class="Bound">e&#39;</a> <a id="3414" href="UF.PreUnivalence.html#3414" class="Bound">e&#39;&#39;</a> <a id="3418" class="Symbol">=</a>
   <a id="3423" class="Symbol">(</a><a id="3424" href="UF.PreUnivalence.html#3409" class="Bound">e</a> <a id="3426" href="UF.Equiv.html#2640" class="Function Operator">●</a> <a id="3428" href="UF.PreUnivalence.html#3411" class="Bound">e&#39;</a> <a id="3431" href="MLTT.Id.html#193" class="Datatype Operator">＝</a> <a id="3433" href="UF.PreUnivalence.html#3414" class="Bound">e&#39;&#39;</a><a id="3436" class="Symbol">)</a>
    <a id="3442" href="UF.Equiv.html#2729" class="Function Operator">≃⟨</a> <a id="3445" class="Symbol">_</a> <a id="3447" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="3449" href="UF.Embeddings.html#6838" class="Function">ap-is-equiv</a> <a id="3461" class="Symbol">(</a><a id="3462" href="UF.Equiv.html#7797" class="Function">≃-sym</a> <a id="3468" href="UF.PreUnivalence.html#3409" class="Bound">e</a> <a id="3470" href="UF.Equiv.html#2640" class="Function Operator">●_</a><a id="3472" class="Symbol">)</a> <a id="3474" class="Symbol">(</a><a id="3475" href="MLTT.Sigma-Type.html#209" class="Field">pr₂</a> <a id="3479" href="UF.PreUnivalence.html#3820" class="Function">q</a><a id="3480" class="Symbol">)</a> <a id="3482" href="UF.Equiv.html#2729" class="Function Operator">⟩</a>
   <a id="3487" class="Symbol">(</a><a id="3488" href="UF.Equiv.html#7797" class="Function">≃-sym</a> <a id="3494" href="UF.PreUnivalence.html#3409" class="Bound">e</a> <a id="3496" href="UF.Equiv.html#2640" class="Function Operator">●</a> <a id="3498" class="Symbol">(</a><a id="3499" href="UF.PreUnivalence.html#3409" class="Bound">e</a> <a id="3501" href="UF.Equiv.html#2640" class="Function Operator">●</a> <a id="3503" href="UF.PreUnivalence.html#3411" class="Bound">e&#39;</a><a id="3505" class="Symbol">)</a> <a id="3507" href="MLTT.Id.html#193" class="Datatype Operator">＝</a> <a id="3509" href="UF.Equiv.html#7797" class="Function">≃-sym</a> <a id="3515" href="UF.PreUnivalence.html#3409" class="Bound">e</a> <a id="3517" href="UF.Equiv.html#2640" class="Function Operator">●</a> <a id="3519" href="UF.PreUnivalence.html#3414" class="Bound">e&#39;&#39;</a><a id="3522" class="Symbol">)</a>
    <a id="3528" href="UF.Equiv.html#2729" class="Function Operator">≃⟨</a> <a id="3531" href="UF.EquivalenceExamples.html#24891" class="Function">＝-cong-l</a> <a id="3540" class="Symbol">_</a> <a id="3542" class="Symbol">_</a> <a id="3544" class="Symbol">(</a><a id="3545" href="UF.Equiv-FunExt.html#6603" class="Function">≃-assoc&#39;</a> <a id="3554" href="UF.PreUnivalence.html#2871" class="Bound">fevv</a> <a id="3559" href="UF.PreUnivalence.html#2871" class="Bound">fevv</a> <a id="3564" href="UF.PreUnivalence.html#2871" class="Bound">fevv</a> <a id="3569" class="Symbol">(</a><a id="3570" href="UF.Equiv.html#7797" class="Function">≃-sym</a> <a id="3576" href="UF.PreUnivalence.html#3409" class="Bound">e</a><a id="3577" class="Symbol">)</a> <a id="3579" href="UF.PreUnivalence.html#3409" class="Bound">e</a> <a id="3581" href="UF.PreUnivalence.html#3411" class="Bound">e&#39;</a><a id="3583" class="Symbol">)</a> <a id="3585" href="UF.Equiv.html#2729" class="Function Operator">⟩</a>
   <a id="3590" class="Symbol">((</a><a id="3592" href="UF.Equiv.html#7797" class="Function">≃-sym</a> <a id="3598" href="UF.PreUnivalence.html#3409" class="Bound">e</a> <a id="3600" href="UF.Equiv.html#2640" class="Function Operator">●</a> <a id="3602" href="UF.PreUnivalence.html#3409" class="Bound">e</a><a id="3603" class="Symbol">)</a> <a id="3605" href="UF.Equiv.html#2640" class="Function Operator">●</a> <a id="3607" href="UF.PreUnivalence.html#3411" class="Bound">e&#39;</a> <a id="3610" href="MLTT.Id.html#193" class="Datatype Operator">＝</a> <a id="3612" href="UF.Equiv.html#7797" class="Function">≃-sym</a> <a id="3618" href="UF.PreUnivalence.html#3409" class="Bound">e</a> <a id="3620" href="UF.Equiv.html#2640" class="Function Operator">●</a> <a id="3622" href="UF.PreUnivalence.html#3414" class="Bound">e&#39;&#39;</a><a id="3625" class="Symbol">)</a>
    <a id="3631" href="UF.Equiv.html#2729" class="Function Operator">≃⟨</a> <a id="3634" href="UF.EquivalenceExamples.html#24891" class="Function">＝-cong-l</a> <a id="3643" class="Symbol">_</a> <a id="3645" class="Symbol">_</a> <a id="3647" class="Symbol">(</a><a id="3648" href="MLTT.Id.html#1136" class="Function">ap</a> <a id="3651" class="Symbol">(</a><a id="3652" href="UF.Equiv.html#2640" class="Function Operator">_●</a> <a id="3655" href="UF.PreUnivalence.html#3411" class="Bound">e&#39;</a><a id="3657" class="Symbol">)</a> <a id="3659" class="Symbol">(</a><a id="3660" href="UF.Equiv-FunExt.html#9796" class="Function">≃-sym-left-inverse&#39;</a> <a id="3680" href="UF.PreUnivalence.html#2871" class="Bound">fevv</a> <a id="3685" href="UF.PreUnivalence.html#3409" class="Bound">e</a><a id="3686" class="Symbol">))</a> <a id="3689" href="UF.Equiv.html#2729" class="Function Operator">⟩</a>
   <a id="3694" class="Symbol">(</a><a id="3695" href="UF.Equiv.html#1517" class="Function">≃-refl</a> <a id="3702" class="Symbol">_</a> <a id="3704" href="UF.Equiv.html#2640" class="Function Operator">●</a> <a id="3706" href="UF.PreUnivalence.html#3411" class="Bound">e&#39;</a> <a id="3709" href="MLTT.Id.html#193" class="Datatype Operator">＝</a> <a id="3711" href="UF.Equiv.html#7797" class="Function">≃-sym</a> <a id="3717" href="UF.PreUnivalence.html#3409" class="Bound">e</a> <a id="3719" href="UF.Equiv.html#2640" class="Function Operator">●</a> <a id="3721" href="UF.PreUnivalence.html#3414" class="Bound">e&#39;&#39;</a><a id="3724" class="Symbol">)</a>
    <a id="3730" href="UF.Equiv.html#2729" class="Function Operator">≃⟨</a> <a id="3733" href="UF.EquivalenceExamples.html#24891" class="Function">＝-cong-l</a> <a id="3742" class="Symbol">_</a> <a id="3744" class="Symbol">_</a> <a id="3746" class="Symbol">(</a><a id="3747" href="UF.Equiv-FunExt.html#7823" class="Function">≃-refl-left&#39;</a> <a id="3760" href="UF.PreUnivalence.html#2871" class="Bound">fevv</a> <a id="3765" href="UF.PreUnivalence.html#2871" class="Bound">fevv</a> <a id="3770" href="UF.PreUnivalence.html#2871" class="Bound">fevv</a> <a id="3775" href="UF.PreUnivalence.html#3411" class="Bound">e&#39;</a><a id="3777" class="Symbol">)</a> <a id="3779" href="UF.Equiv.html#2729" class="Function Operator">⟩</a>
   <a id="3784" class="Symbol">(</a><a id="3785" href="UF.PreUnivalence.html#3411" class="Bound">e&#39;</a> <a id="3788" href="MLTT.Id.html#193" class="Datatype Operator">＝</a> <a id="3790" href="UF.Equiv.html#7797" class="Function">≃-sym</a> <a id="3796" href="UF.PreUnivalence.html#3409" class="Bound">e</a> <a id="3798" href="UF.Equiv.html#2640" class="Function Operator">●</a> <a id="3800" href="UF.PreUnivalence.html#3414" class="Bound">e&#39;&#39;</a><a id="3803" class="Symbol">)</a> <a id="3805" href="UF.Equiv.html#2815" class="Function Operator">■</a>
   <a id="3810" class="Keyword">where</a>
    <a id="3820" href="UF.PreUnivalence.html#3820" class="Function">q</a> <a id="3822" class="Symbol">=</a> <a id="3824" href="UF.Equiv-FunExt.html#10929" class="Function">≃-cong-left&#39;</a> <a id="3837" href="UF.PreUnivalence.html#2866" class="Bound">fevu</a> <a id="3842" href="UF.PreUnivalence.html#2871" class="Bound">fevv</a> <a id="3847" href="UF.PreUnivalence.html#2861" class="Bound">feuu</a> <a id="3852" href="UF.PreUnivalence.html#2871" class="Bound">fevv</a> <a id="3857" href="UF.PreUnivalence.html#2871" class="Bound">fevv</a> <a id="3862" href="UF.PreUnivalence.html#3409" class="Bound">e</a>

<a id="3865" class="Markup">\end{code}</a><a id="3875" class="Background">
</a></pre></body></html>