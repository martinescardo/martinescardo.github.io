<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Groups.Aut</title><link rel="stylesheet" href="Agda.css"><script type="text/javascript" src="highlight-hover.js"></script></head><body><pre class="Agda"><a id="1" class="Background">--------------------------------------------------------------------------------
Ettore Aldrovandi ealdrovandi@fsu.edu
Keri D&#39;Angelo kd349@cornell.edu

June 2022
--------------------------------------------------------------------------------

If X is a set, Aut X, defined in UF-Equiv, is a group.

We assume functional extensionality at level 𝓤.

</a><a id="350" class="Markup">\begin{code}</a>

<a id="364" class="Symbol">{-#</a> <a id="368" class="Keyword">OPTIONS</a> <a id="376" class="Pragma">--safe</a> <a id="383" class="Pragma">--without-K</a> <a id="395" class="Symbol">#-}</a>

<a id="400" class="Keyword">open</a> <a id="405" class="Keyword">import</a> <a id="412" href="Groups.Type.html" class="Module">Groups.Type</a> <a id="424" class="Keyword">renaming</a> <a id="433" class="Symbol">(</a><a id="434" href="Groups.Type.html#10041" class="Function Operator">_≅_</a> <a id="438" class="Symbol">to</a> <a id="441" class="Function Operator">_≣_</a><a id="444" class="Symbol">)</a>
<a id="446" class="Keyword">open</a> <a id="451" class="Keyword">import</a> <a id="458" href="MLTT.Spartan.html" class="Module">MLTT.Spartan</a>
<a id="471" class="Keyword">open</a> <a id="476" class="Keyword">import</a> <a id="483" href="UF.Base.html" class="Module">UF.Base</a> <a id="491" class="Keyword">hiding</a> <a id="498" class="Symbol">(</a><a id="499" href="UF.Base.html#1301" class="Function Operator">_≈_</a><a id="502" class="Symbol">)</a>
<a id="504" class="Keyword">open</a> <a id="509" class="Keyword">import</a> <a id="516" href="UF.Equiv.html" class="Module">UF.Equiv</a>
<a id="525" class="Keyword">open</a> <a id="530" class="Keyword">import</a> <a id="537" href="UF.Equiv-FunExt.html" class="Module">UF.Equiv-FunExt</a>
<a id="553" class="Keyword">open</a> <a id="558" class="Keyword">import</a> <a id="565" href="UF.FunExt.html" class="Module">UF.FunExt</a>
<a id="575" class="Keyword">open</a> <a id="580" class="Keyword">import</a> <a id="587" href="UF.Sets.html" class="Module">UF.Sets</a>
<a id="595" class="Keyword">open</a> <a id="600" class="Keyword">import</a> <a id="607" href="UF.Sets-Properties.html" class="Module">UF.Sets-Properties</a>
<a id="626" class="Keyword">open</a> <a id="631" class="Keyword">import</a> <a id="638" href="UF.Subsingletons-Properties.html" class="Module">UF.Subsingletons-Properties</a>

<a id="667" class="Keyword">module</a> <a id="674" href="Groups.Aut.html" class="Module">Groups.Aut</a> <a id="685" class="Keyword">where</a>

<a id="692" class="Markup">\end{code}</a><a id="702" class="Background">

In the group structure below the definition matches that of function
composition. This notation is used in UF.Equiv.

Note, however, that writing the variables this way introduces an
&quot;opposite&quot; operation. We define it formally in Groups.Opposite and we
must take it into account whenever using this group structure on
Aut(X).

</a><a id="1031" class="Markup">\begin{code}</a>
<a id="1044" class="Keyword">module</a> <a id="1051" href="Groups.Aut.html#1051" class="Module">_</a> <a id="1053" class="Symbol">(</a><a id="1054" href="Groups.Aut.html#1054" class="Bound">fe</a> <a id="1057" class="Symbol">:</a> <a id="1059" href="UF.FunExt.html#850" class="Function">funext</a> <a id="1066" href="MLTT.Universes.html#3834" class="Generalizable">𝓤</a> <a id="1068" href="MLTT.Universes.html#3834" class="Generalizable">𝓤</a><a id="1069" class="Symbol">)</a> <a id="1071" class="Symbol">(</a><a id="1072" href="Groups.Aut.html#1072" class="Bound">X</a> <a id="1074" class="Symbol">:</a> <a id="1076" href="MLTT.Universes.html#3834" class="Generalizable">𝓤</a> <a id="1078" href="MLTT.Universes.html#3977" class="Function Operator">̇</a> <a id="1080" class="Symbol">)(</a><a id="1082" href="Groups.Aut.html#1082" class="Bound">i</a> <a id="1084" class="Symbol">:</a> <a id="1086" href="UF.Sets.html#709" class="Function">is-set</a> <a id="1093" href="Groups.Aut.html#1072" class="Bound">X</a><a id="1094" class="Symbol">)</a> <a id="1096" class="Keyword">where</a>

  <a id="1105" href="Groups.Aut.html#1105" class="Function">is-set-Aut</a> <a id="1116" class="Symbol">:</a> <a id="1118" href="UF.Sets.html#709" class="Function">is-set</a> <a id="1125" class="Symbol">(</a><a id="1126" href="UF.Equiv.html#1380" class="Function">Aut</a> <a id="1130" href="Groups.Aut.html#1072" class="Bound">X</a><a id="1131" class="Symbol">)</a>
  <a id="1135" href="Groups.Aut.html#1105" class="Function">is-set-Aut</a> <a id="1146" class="Symbol">=</a> <a id="1148" href="UF.Sets-Properties.html#2769" class="Function">Σ-is-set</a>
                  <a id="1175" class="Symbol">(</a><a id="1176" href="UF.Sets-Properties.html#1260" class="Function">Π-is-set</a> <a id="1185" href="Groups.Aut.html#1054" class="Bound">fe</a> <a id="1188" class="Symbol">(λ</a> <a id="1191" href="Groups.Aut.html#1191" class="Bound">_</a> <a id="1193" class="Symbol">→</a> <a id="1195" href="Groups.Aut.html#1082" class="Bound">i</a><a id="1196" class="Symbol">))</a>
                  <a id="1217" class="Symbol">λ</a> <a id="1219" href="Groups.Aut.html#1219" class="Bound">f</a> <a id="1221" class="Symbol">→</a> <a id="1223" href="UF.Subsingletons-Properties.html#345" class="Function">props-are-sets</a> <a id="1238" class="Symbol">(</a><a id="1239" href="UF.Equiv-FunExt.html#6389" class="Function">being-equiv-is-prop&#39;&#39;</a> <a id="1261" href="Groups.Aut.html#1054" class="Bound">fe</a> <a id="1264" href="Groups.Aut.html#1219" class="Bound">f</a><a id="1265" class="Symbol">)</a>


  <a id="1271" href="Groups.Aut.html#1271" class="Function">group-structure-Aut</a> <a id="1291" class="Symbol">:</a> <a id="1293" href="UF.Equiv.html#1380" class="Function">Aut</a> <a id="1297" href="Groups.Aut.html#1072" class="Bound">X</a> <a id="1299" class="Symbol">→</a> <a id="1301" href="UF.Equiv.html#1380" class="Function">Aut</a> <a id="1305" href="Groups.Aut.html#1072" class="Bound">X</a> <a id="1307" class="Symbol">→</a> <a id="1309" href="UF.Equiv.html#1380" class="Function">Aut</a> <a id="1313" href="Groups.Aut.html#1072" class="Bound">X</a>
  <a id="1317" href="Groups.Aut.html#1271" class="Function">group-structure-Aut</a> <a id="1337" href="Groups.Aut.html#1337" class="Bound">f</a> <a id="1339" href="Groups.Aut.html#1339" class="Bound">g</a> <a id="1341" class="Symbol">=</a> <a id="1343" href="Groups.Aut.html#1337" class="Bound">f</a> <a id="1345" href="UF.Equiv.html#2640" class="Function Operator">●</a> <a id="1347" href="Groups.Aut.html#1339" class="Bound">g</a>

  <a id="1352" class="Keyword">private</a>
    <a id="1364" href="Groups.Aut.html#1364" class="Function Operator">_·_</a> <a id="1368" class="Symbol">=</a> <a id="1370" href="Groups.Aut.html#1271" class="Function">group-structure-Aut</a>

<a id="1391" class="Markup">\end{code}</a><a id="1401" class="Background">

In the following proof of the group axioms, the associativity proof
reproduces that of ≃-assoc in UF-Equiv-FunExt, instead of calling
≃-assoc directly, because the latter uses FunExt and we use funext 𝓤 𝓤
here.  Similarly for the proof of the inverse, which reproduces those
of ≃-sym-is-linv and ≃-sym-is-rinv.

In both cases the key is to use being-equiv-is-prop&#39;&#39; in place of
being-equiv-is-prop.

</a><a id="1803" class="Markup">\begin{code}</a>
  <a id="1818" href="Groups.Aut.html#1818" class="Function">group-axioms-Aut</a> <a id="1835" class="Symbol">:</a> <a id="1837" href="Groups.Type.html#1072" class="Function">group-axioms</a> <a id="1850" class="Symbol">(</a><a id="1851" href="UF.Equiv.html#1380" class="Function">Aut</a> <a id="1855" href="Groups.Aut.html#1072" class="Bound">X</a><a id="1856" class="Symbol">)</a> <a id="1858" href="Groups.Aut.html#1364" class="Function Operator">_·_</a>
  <a id="1864" href="Groups.Aut.html#1818" class="Function">group-axioms-Aut</a> <a id="1881" class="Symbol">=</a> <a id="1883" href="Groups.Aut.html#1105" class="Function">is-set-Aut</a> <a id="1894" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="1896" href="Groups.Aut.html#1944" class="Function">assoc-Aut</a> <a id="1906" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="1908" href="Groups.Aut.html#2332" class="Function">e</a> <a id="1910" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="1912" href="Groups.Aut.html#2378" class="Function">ln-e</a> <a id="1917" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="1919" href="Groups.Aut.html#2454" class="Function">rn-e</a> <a id="1924" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="1926" href="Groups.Aut.html#2532" class="Function">φ</a>
    <a id="1932" class="Keyword">where</a>
      <a id="1944" href="Groups.Aut.html#1944" class="Function">assoc-Aut</a> <a id="1954" class="Symbol">:</a> <a id="1956" href="Notation.General.html#1977" class="Function">associative</a> <a id="1968" href="Groups.Aut.html#1364" class="Function Operator">_·_</a>
      <a id="1978" href="Groups.Aut.html#1944" class="Function">assoc-Aut</a> <a id="1988" class="Symbol">(</a><a id="1989" href="Groups.Aut.html#1989" class="Bound">f</a> <a id="1991" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="1993" href="Groups.Aut.html#1993" class="Bound">i</a><a id="1994" class="Symbol">)</a> <a id="1996" class="Symbol">(</a><a id="1997" href="Groups.Aut.html#1997" class="Bound">g</a> <a id="1999" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="2001" href="Groups.Aut.html#2001" class="Bound">j</a><a id="2002" class="Symbol">)</a> <a id="2004" class="Symbol">(</a><a id="2005" href="Groups.Aut.html#2005" class="Bound">h</a> <a id="2007" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="2009" href="Groups.Aut.html#2009" class="Bound">k</a><a id="2010" class="Symbol">)</a> <a id="2012" class="Symbol">=</a> <a id="2014" href="UF.Base.html#14933" class="Function">to-Σ-＝</a> <a id="2021" class="Symbol">(</a><a id="2022" href="Groups.Aut.html#2053" class="Function">p</a> <a id="2024" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="2026" href="Groups.Aut.html#2239" class="Function">q</a><a id="2027" class="Symbol">)</a>
        <a id="2037" class="Keyword">where</a>
          <a id="2053" href="Groups.Aut.html#2053" class="Function">p</a> <a id="2055" class="Symbol">:</a> <a id="2057" class="Symbol">(</a><a id="2058" href="Groups.Aut.html#2005" class="Bound">h</a> <a id="2060" href="MLTT.Pi.html#513" class="Function Operator">∘</a> <a id="2062" href="Groups.Aut.html#1997" class="Bound">g</a><a id="2063" class="Symbol">)</a> <a id="2065" href="MLTT.Pi.html#513" class="Function Operator">∘</a> <a id="2067" href="Groups.Aut.html#1989" class="Bound">f</a> <a id="2069" href="MLTT.Id.html#193" class="Datatype Operator">＝</a> <a id="2071" href="Groups.Aut.html#2005" class="Bound">h</a> <a id="2073" href="MLTT.Pi.html#513" class="Function Operator">∘</a> <a id="2075" class="Symbol">(</a><a id="2076" href="Groups.Aut.html#1997" class="Bound">g</a> <a id="2078" href="MLTT.Pi.html#513" class="Function Operator">∘</a> <a id="2080" href="Groups.Aut.html#1989" class="Bound">f</a><a id="2081" class="Symbol">)</a>
          <a id="2093" href="Groups.Aut.html#2053" class="Function">p</a> <a id="2095" class="Symbol">=</a> <a id="2097" href="MLTT.Identity-Type.html#157" class="InductiveConstructor">refl</a>

          <a id="2113" href="Groups.Aut.html#2113" class="Function">d</a> <a id="2115" href="Groups.Aut.html#2115" class="Function">e</a> <a id="2117" class="Symbol">:</a> <a id="2119" href="UF.Equiv.html#402" class="Function">is-equiv</a> <a id="2128" class="Symbol">(</a><a id="2129" href="Groups.Aut.html#2005" class="Bound">h</a> <a id="2131" href="MLTT.Pi.html#513" class="Function Operator">∘</a> <a id="2133" href="Groups.Aut.html#1997" class="Bound">g</a> <a id="2135" href="MLTT.Pi.html#513" class="Function Operator">∘</a> <a id="2137" href="Groups.Aut.html#1989" class="Bound">f</a><a id="2138" class="Symbol">)</a>
          <a id="2150" href="Groups.Aut.html#2113" class="Function">d</a> <a id="2152" class="Symbol">=</a> <a id="2154" href="UF.Equiv.html#1615" class="Function">∘-is-equiv</a> <a id="2165" href="Groups.Aut.html#1993" class="Bound">i</a> <a id="2167" class="Symbol">(</a><a id="2168" href="UF.Equiv.html#1615" class="Function">∘-is-equiv</a> <a id="2179" href="Groups.Aut.html#2001" class="Bound">j</a> <a id="2181" href="Groups.Aut.html#2009" class="Bound">k</a><a id="2182" class="Symbol">)</a>
          <a id="2194" href="Groups.Aut.html#2115" class="Function">e</a> <a id="2196" class="Symbol">=</a> <a id="2198" href="UF.Equiv.html#1615" class="Function">∘-is-equiv</a> <a id="2209" class="Symbol">(</a><a id="2210" href="UF.Equiv.html#1615" class="Function">∘-is-equiv</a> <a id="2221" href="Groups.Aut.html#1993" class="Bound">i</a> <a id="2223" href="Groups.Aut.html#2001" class="Bound">j</a><a id="2224" class="Symbol">)</a> <a id="2226" href="Groups.Aut.html#2009" class="Bound">k</a>

          <a id="2239" href="Groups.Aut.html#2239" class="Function">q</a> <a id="2241" class="Symbol">:</a> <a id="2243" href="MLTT.Id.html#735" class="Function">transport</a> <a id="2253" href="UF.Equiv.html#402" class="Function">is-equiv</a> <a id="2262" href="Groups.Aut.html#2053" class="Function">p</a> <a id="2264" href="Groups.Aut.html#2115" class="Function">e</a> <a id="2266" href="MLTT.Id.html#193" class="Datatype Operator">＝</a> <a id="2268" href="Groups.Aut.html#2113" class="Function">d</a>
          <a id="2280" href="Groups.Aut.html#2239" class="Function">q</a> <a id="2282" class="Symbol">=</a> <a id="2284" href="UF.Equiv-FunExt.html#6389" class="Function">being-equiv-is-prop&#39;&#39;</a> <a id="2306" href="Groups.Aut.html#1054" class="Bound">fe</a> <a id="2309" class="Symbol">(</a><a id="2310" href="Groups.Aut.html#2005" class="Bound">h</a> <a id="2312" href="MLTT.Pi.html#513" class="Function Operator">∘</a> <a id="2314" href="Groups.Aut.html#1997" class="Bound">g</a> <a id="2316" href="MLTT.Pi.html#513" class="Function Operator">∘</a> <a id="2318" href="Groups.Aut.html#1989" class="Bound">f</a><a id="2319" class="Symbol">)</a> <a id="2321" class="Symbol">_</a> <a id="2323" class="Symbol">_</a>

      <a id="2332" href="Groups.Aut.html#2332" class="Function">e</a> <a id="2334" class="Symbol">:</a> <a id="2336" href="UF.Equiv.html#1380" class="Function">Aut</a> <a id="2340" href="Groups.Aut.html#1072" class="Bound">X</a>
      <a id="2348" href="Groups.Aut.html#2332" class="Function">e</a> <a id="2350" class="Symbol">=</a> <a id="2352" href="MLTT.Pi.html#444" class="Function">id</a> <a id="2355" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="2357" href="UF.Equiv.html#1413" class="Function">id-is-equiv</a> <a id="2369" href="Groups.Aut.html#1072" class="Bound">X</a>

      <a id="2378" href="Groups.Aut.html#2378" class="Function">ln-e</a> <a id="2383" class="Symbol">:</a> <a id="2385" href="Notation.General.html#1799" class="Function">left-neutral</a> <a id="2398" href="Groups.Aut.html#2332" class="Function">e</a> <a id="2400" href="Groups.Aut.html#1364" class="Function Operator">_·_</a>
      <a id="2410" href="Groups.Aut.html#2378" class="Function">ln-e</a> <a id="2415" class="Symbol">=</a> <a id="2417" class="Symbol">λ</a> <a id="2419" href="Groups.Aut.html#2419" class="Bound">f</a> <a id="2421" class="Symbol">→</a> <a id="2423" href="UF.Equiv-FunExt.html#7823" class="Function">≃-refl-left&#39;</a> <a id="2436" href="Groups.Aut.html#1054" class="Bound">fe</a> <a id="2439" href="Groups.Aut.html#1054" class="Bound">fe</a> <a id="2442" href="Groups.Aut.html#1054" class="Bound">fe</a> <a id="2445" href="Groups.Aut.html#2419" class="Bound">f</a>

      <a id="2454" href="Groups.Aut.html#2454" class="Function">rn-e</a> <a id="2459" class="Symbol">:</a> <a id="2461" href="Notation.General.html#1887" class="Function">right-neutral</a> <a id="2475" href="Groups.Aut.html#2332" class="Function">e</a> <a id="2477" href="Groups.Aut.html#1364" class="Function Operator">_·_</a>
      <a id="2487" href="Groups.Aut.html#2454" class="Function">rn-e</a> <a id="2492" class="Symbol">=</a> <a id="2494" class="Symbol">λ</a> <a id="2496" href="Groups.Aut.html#2496" class="Bound">f</a> <a id="2498" class="Symbol">→</a> <a id="2500" href="UF.Equiv-FunExt.html#8066" class="Function">≃-refl-right&#39;</a> <a id="2514" href="Groups.Aut.html#1054" class="Bound">fe</a> <a id="2517" href="Groups.Aut.html#1054" class="Bound">fe</a> <a id="2520" href="Groups.Aut.html#1054" class="Bound">fe</a> <a id="2523" href="Groups.Aut.html#2496" class="Bound">f</a>

      <a id="2532" href="Groups.Aut.html#2532" class="Function">φ</a> <a id="2534" class="Symbol">:</a> <a id="2536" class="Symbol">(</a><a id="2537" href="Groups.Aut.html#2537" class="Bound">f</a> <a id="2539" class="Symbol">:</a> <a id="2541" href="UF.Equiv.html#1380" class="Function">Aut</a> <a id="2545" href="Groups.Aut.html#1072" class="Bound">X</a><a id="2546" class="Symbol">)</a>
        <a id="2556" class="Symbol">→</a> <a id="2558" class="Symbol">(</a><a id="2559" href="MLTT.Sigma.html#423" class="Function">Σ</a> <a id="2561" href="Groups.Aut.html#2561" class="Bound">f&#39;</a> <a id="2564" href="MLTT.Sigma.html#423" class="Function">꞉</a> <a id="2566" href="UF.Equiv.html#1380" class="Function">Aut</a> <a id="2570" href="Groups.Aut.html#1072" class="Bound">X</a> <a id="2572" href="MLTT.Sigma.html#423" class="Function">,</a> <a id="2574" class="Symbol">(</a><a id="2575" href="Groups.Aut.html#2561" class="Bound">f&#39;</a> <a id="2578" href="Groups.Aut.html#1364" class="Function Operator">·</a> <a id="2580" href="Groups.Aut.html#2537" class="Bound">f</a> <a id="2582" href="MLTT.Id.html#193" class="Datatype Operator">＝</a> <a id="2584" href="Groups.Aut.html#2332" class="Function">e</a><a id="2585" class="Symbol">)</a> <a id="2587" href="MLTT.Sigma.html#558" class="Function Operator">×</a> <a id="2589" class="Symbol">(</a><a id="2590" href="Groups.Aut.html#2537" class="Bound">f</a> <a id="2592" href="Groups.Aut.html#1364" class="Function Operator">·</a> <a id="2594" href="Groups.Aut.html#2561" class="Bound">f&#39;</a> <a id="2597" href="MLTT.Id.html#193" class="Datatype Operator">＝</a> <a id="2599" href="Groups.Aut.html#2332" class="Function">e</a><a id="2600" class="Symbol">))</a>
      <a id="2609" href="MLTT.Sigma-Type.html#198" class="Field">pr₁</a> <a id="2613" class="Symbol">(</a><a id="2614" href="Groups.Aut.html#2532" class="Function">φ</a> <a id="2616" href="Groups.Aut.html#2616" class="Bound">f</a><a id="2617" class="Symbol">)</a> <a id="2619" class="Symbol">=</a> <a id="2621" href="UF.Equiv.html#8089" class="Function">≃-sym</a> <a id="2627" href="Groups.Aut.html#2616" class="Bound">f</a>
      <a id="2635" href="MLTT.Sigma-Type.html#198" class="Field">pr₁</a> <a id="2639" class="Symbol">(</a><a id="2640" href="MLTT.Sigma-Type.html#209" class="Field">pr₂</a> <a id="2644" class="Symbol">(</a><a id="2645" href="Groups.Aut.html#2532" class="Function">φ</a> <a id="2647" class="Symbol">(</a><a id="2648" href="Groups.Aut.html#2648" class="Bound">∣f∣</a> <a id="2652" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="2654" href="Groups.Aut.html#2654" class="Bound">is</a><a id="2656" class="Symbol">)))</a> <a id="2660" class="Symbol">=</a> <a id="2662" href="UF.Base.html#14933" class="Function">to-Σ-＝</a> <a id="2669" class="Symbol">(</a><a id="2670" href="Groups.Aut.html#2731" class="Function">p</a>  <a id="2673" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="2675" href="UF.Equiv-FunExt.html#6389" class="Function">being-equiv-is-prop&#39;&#39;</a> <a id="2697" href="Groups.Aut.html#1054" class="Bound">fe</a> <a id="2700" class="Symbol">_</a> <a id="2702" class="Symbol">_</a> <a id="2704" class="Symbol">_)</a>
        <a id="2715" class="Keyword">where</a>
          <a id="2731" href="Groups.Aut.html#2731" class="Function">p</a> <a id="2733" class="Symbol">:</a> <a id="2735" href="Groups.Aut.html#2648" class="Bound">∣f∣</a> <a id="2739" href="MLTT.Pi.html#513" class="Function Operator">∘</a> <a id="2741" href="UF.Equiv.html#498" class="Function">inverse</a> <a id="2749" href="Groups.Aut.html#2648" class="Bound">∣f∣</a> <a id="2753" href="Groups.Aut.html#2654" class="Bound">is</a> <a id="2756" href="MLTT.Id.html#193" class="Datatype Operator">＝</a> <a id="2758" href="MLTT.Pi.html#444" class="Function">id</a>
          <a id="2771" href="Groups.Aut.html#2731" class="Function">p</a> <a id="2773" class="Symbol">=</a> <a id="2775" href="UF.FunExt.html#1242" class="Function">dfunext</a> <a id="2783" href="Groups.Aut.html#1054" class="Bound">fe</a> <a id="2786" class="Symbol">(</a><a id="2787" href="UF.Equiv.html#5785" class="Function">inverses-are-sections</a> <a id="2809" href="Groups.Aut.html#2648" class="Bound">∣f∣</a> <a id="2813" href="Groups.Aut.html#2654" class="Bound">is</a><a id="2815" class="Symbol">)</a>
      <a id="2823" href="MLTT.Sigma-Type.html#209" class="Field">pr₂</a> <a id="2827" class="Symbol">(</a><a id="2828" href="MLTT.Sigma-Type.html#209" class="Field">pr₂</a> <a id="2832" class="Symbol">(</a><a id="2833" href="Groups.Aut.html#2532" class="Function">φ</a> <a id="2835" class="Symbol">(</a><a id="2836" href="Groups.Aut.html#2836" class="Bound">∣f∣</a> <a id="2840" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="2842" href="Groups.Aut.html#2842" class="Bound">is</a><a id="2844" class="Symbol">)))</a> <a id="2848" class="Symbol">=</a> <a id="2850" href="UF.Base.html#14933" class="Function">to-Σ-＝</a> <a id="2857" class="Symbol">(</a><a id="2858" href="Groups.Aut.html#2918" class="Function">p</a> <a id="2860" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="2862" href="UF.Equiv-FunExt.html#6389" class="Function">being-equiv-is-prop&#39;&#39;</a> <a id="2884" href="Groups.Aut.html#1054" class="Bound">fe</a> <a id="2887" class="Symbol">_</a> <a id="2889" class="Symbol">_</a> <a id="2891" class="Symbol">_)</a>
        <a id="2902" class="Keyword">where</a>
          <a id="2918" href="Groups.Aut.html#2918" class="Function">p</a> <a id="2920" class="Symbol">:</a> <a id="2922" href="UF.Equiv.html#498" class="Function">inverse</a> <a id="2930" href="Groups.Aut.html#2836" class="Bound">∣f∣</a> <a id="2934" href="Groups.Aut.html#2842" class="Bound">is</a> <a id="2937" href="MLTT.Pi.html#513" class="Function Operator">∘</a> <a id="2939" href="Groups.Aut.html#2836" class="Bound">∣f∣</a> <a id="2943" href="MLTT.Id.html#193" class="Datatype Operator">＝</a> <a id="2945" href="MLTT.Pi.html#444" class="Function">id</a>
          <a id="2958" href="Groups.Aut.html#2918" class="Function">p</a> <a id="2960" class="Symbol">=</a> <a id="2962" href="UF.FunExt.html#1242" class="Function">dfunext</a> <a id="2970" href="Groups.Aut.html#1054" class="Bound">fe</a> <a id="2973" class="Symbol">(</a><a id="2974" href="UF.Equiv.html#4819" class="Function">inverses-are-retractions</a> <a id="2999" href="Groups.Aut.html#2836" class="Bound">∣f∣</a> <a id="3003" href="Groups.Aut.html#2842" class="Bound">is</a><a id="3005" class="Symbol">)</a>

  <a id="3010" href="Groups.Aut.html#3010" class="Function">Group-structure-Aut</a> <a id="3030" class="Symbol">:</a> <a id="3032" href="Groups.Type.html#3691" class="Function">Group-structure</a> <a id="3048" class="Symbol">(</a><a id="3049" href="UF.Equiv.html#1380" class="Function">Aut</a> <a id="3053" href="Groups.Aut.html#1072" class="Bound">X</a><a id="3054" class="Symbol">)</a>
  <a id="3058" href="Groups.Aut.html#3010" class="Function">Group-structure-Aut</a> <a id="3078" class="Symbol">=</a> <a id="3080" href="Groups.Aut.html#1364" class="Function Operator">_·_</a> <a id="3084" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="3086" href="Groups.Aut.html#1818" class="Function">group-axioms-Aut</a>

  <a id="3106" href="Groups.Aut.html#3106" class="Function">𝔸ut</a> <a id="3110" class="Symbol">:</a> <a id="3112" href="Groups.Type.html#3789" class="Function">Group</a> <a id="3118" href="Groups.Aut.html#1066" class="Bound">𝓤</a>
  <a id="3122" href="Groups.Aut.html#3106" class="Function">𝔸ut</a> <a id="3126" class="Symbol">=</a> <a id="3128" href="UF.Equiv.html#1380" class="Function">Aut</a> <a id="3132" href="Groups.Aut.html#1072" class="Bound">X</a> <a id="3134" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="3136" href="Groups.Aut.html#3010" class="Function">Group-structure-Aut</a>

<a id="3157" class="Markup">\end{code}</a><a id="3167" class="Background">

If φ is an equivalence between X and Y, then it induces a morphism
from Aut X to Aut Y.

</a><a id="3258" class="Markup">\begin{code}</a>

<a id="𝓐ut"></a><a id="3272" href="Groups.Aut.html#3272" class="Function">𝓐ut</a> <a id="3276" class="Symbol">:</a> <a id="3278" class="Symbol">{</a><a id="3279" href="Groups.Aut.html#3279" class="Bound">X</a> <a id="3281" class="Symbol">:</a> <a id="3283" href="MLTT.Universes.html#3834" class="Generalizable">𝓤</a> <a id="3285" href="MLTT.Universes.html#3977" class="Function Operator">̇</a> <a id="3287" class="Symbol">}</a> <a id="3289" class="Symbol">{</a><a id="3290" href="Groups.Aut.html#3290" class="Bound">Y</a> <a id="3292" class="Symbol">:</a> <a id="3294" href="MLTT.Universes.html#3836" class="Generalizable">𝓥</a> <a id="3296" href="MLTT.Universes.html#3977" class="Function Operator">̇</a> <a id="3298" class="Symbol">}</a> <a id="3300" class="Symbol">→</a> <a id="3302" class="Symbol">(</a><a id="3303" href="Groups.Aut.html#3279" class="Bound">X</a> <a id="3305" href="UF.Equiv.html#1318" class="Function Operator">≃</a> <a id="3307" href="Groups.Aut.html#3290" class="Bound">Y</a><a id="3308" class="Symbol">)</a> <a id="3310" class="Symbol">→</a> <a id="3312" href="UF.Equiv.html#1380" class="Function">Aut</a> <a id="3316" href="Groups.Aut.html#3279" class="Bound">X</a> <a id="3318" class="Symbol">→</a> <a id="3320" href="UF.Equiv.html#1380" class="Function">Aut</a> <a id="3324" href="Groups.Aut.html#3290" class="Bound">Y</a>
<a id="3326" href="Groups.Aut.html#3272" class="Function">𝓐ut</a> <a id="3330" href="Groups.Aut.html#3330" class="Bound">φ</a> <a id="3332" class="Symbol">=</a> <a id="3334" class="Symbol">λ</a> <a id="3336" href="Groups.Aut.html#3336" class="Bound">f</a> <a id="3338" class="Symbol">→</a> <a id="3340" class="Symbol">(</a><a id="3341" href="UF.Equiv.html#8089" class="Function">≃-sym</a> <a id="3347" href="Groups.Aut.html#3330" class="Bound">φ</a> <a id="3349" href="UF.Equiv.html#2640" class="Function Operator">●</a> <a id="3351" href="Groups.Aut.html#3336" class="Bound">f</a> <a id="3353" class="Symbol">)</a> <a id="3355" href="UF.Equiv.html#2640" class="Function Operator">●</a> <a id="3357" href="Groups.Aut.html#3330" class="Bound">φ</a>

<a id="3360" class="Markup">\end{code}</a><a id="3370" class="Background">

This morphism is a homomorphism for the group
structures defined above.

</a><a id="3445" class="Markup">\begin{code}</a>

<a id="3459" class="Keyword">module</a> <a id="3466" href="Groups.Aut.html#3466" class="Module">_</a> <a id="3468" class="Symbol">(</a><a id="3469" href="Groups.Aut.html#3469" class="Bound">fe</a> <a id="3472" class="Symbol">:</a> <a id="3474" href="UF.FunExt.html#995" class="Function">FunExt</a><a id="3480" class="Symbol">)</a>
         <a id="3491" class="Symbol">(</a><a id="3492" href="Groups.Aut.html#3492" class="Bound">X</a> <a id="3494" class="Symbol">:</a> <a id="3496" href="MLTT.Universes.html#3834" class="Generalizable">𝓤</a> <a id="3498" href="MLTT.Universes.html#3977" class="Function Operator">̇</a> <a id="3500" class="Symbol">)(</a><a id="3502" href="Groups.Aut.html#3502" class="Bound">i</a> <a id="3504" class="Symbol">:</a> <a id="3506" href="UF.Sets.html#709" class="Function">is-set</a> <a id="3513" href="Groups.Aut.html#3492" class="Bound">X</a><a id="3514" class="Symbol">)</a>
         <a id="3525" class="Symbol">(</a><a id="3526" href="Groups.Aut.html#3526" class="Bound">Y</a> <a id="3528" class="Symbol">:</a> <a id="3530" href="MLTT.Universes.html#3836" class="Generalizable">𝓥</a> <a id="3532" href="MLTT.Universes.html#3977" class="Function Operator">̇</a> <a id="3534" class="Symbol">)(</a><a id="3536" href="Groups.Aut.html#3536" class="Bound">j</a> <a id="3538" class="Symbol">:</a> <a id="3540" href="UF.Sets.html#709" class="Function">is-set</a> <a id="3547" href="Groups.Aut.html#3526" class="Bound">Y</a><a id="3548" class="Symbol">)</a>
         <a id="3559" class="Symbol">(</a><a id="3560" href="Groups.Aut.html#3560" class="Bound">φ</a> <a id="3562" class="Symbol">:</a> <a id="3564" href="Groups.Aut.html#3492" class="Bound">X</a> <a id="3566" href="UF.Equiv.html#1318" class="Function Operator">≃</a> <a id="3568" href="Groups.Aut.html#3526" class="Bound">Y</a><a id="3569" class="Symbol">)</a>  <a id="3572" class="Keyword">where</a>

   <a id="3582" href="Groups.Aut.html#3582" class="Function">is-hom-𝓐ut</a> <a id="3593" class="Symbol">:</a> <a id="3595" href="Groups.Type.html#5625" class="Function">is-hom</a> <a id="3602" class="Symbol">(</a><a id="3603" href="Groups.Aut.html#3106" class="Function">𝔸ut</a> <a id="3607" class="Symbol">(</a><a id="3608" href="Groups.Aut.html#3469" class="Bound">fe</a> <a id="3611" href="Groups.Aut.html#3496" class="Bound">𝓤</a> <a id="3613" href="Groups.Aut.html#3496" class="Bound">𝓤</a><a id="3614" class="Symbol">)</a> <a id="3616" href="Groups.Aut.html#3492" class="Bound">X</a> <a id="3618" href="Groups.Aut.html#3502" class="Bound">i</a><a id="3619" class="Symbol">)</a> <a id="3621" class="Symbol">(</a><a id="3622" href="Groups.Aut.html#3106" class="Function">𝔸ut</a> <a id="3626" class="Symbol">(</a><a id="3627" href="Groups.Aut.html#3469" class="Bound">fe</a> <a id="3630" href="Groups.Aut.html#3530" class="Bound">𝓥</a> <a id="3632" href="Groups.Aut.html#3530" class="Bound">𝓥</a><a id="3633" class="Symbol">)</a> <a id="3635" href="Groups.Aut.html#3526" class="Bound">Y</a> <a id="3637" href="Groups.Aut.html#3536" class="Bound">j</a><a id="3638" class="Symbol">)</a> <a id="3640" class="Symbol">(</a><a id="3641" href="Groups.Aut.html#3272" class="Function">𝓐ut</a> <a id="3645" href="Groups.Aut.html#3560" class="Bound">φ</a><a id="3646" class="Symbol">)</a>
   <a id="3651" href="Groups.Aut.html#3582" class="Function">is-hom-𝓐ut</a> <a id="3662" class="Symbol">{</a><a id="3663" href="Groups.Aut.html#3663" class="Bound">g</a><a id="3664" class="Symbol">}</a> <a id="3666" class="Symbol">{</a><a id="3667" href="Groups.Aut.html#3667" class="Bound">f</a><a id="3668" class="Symbol">}</a> <a id="3670" class="Symbol">=</a> <a id="3672" class="Symbol">(</a><a id="3673" href="UF.Equiv.html#8089" class="Function">≃-sym</a> <a id="3679" href="Groups.Aut.html#3560" class="Bound">φ</a> <a id="3681" href="UF.Equiv.html#2640" class="Function Operator">●</a> <a id="3683" class="Symbol">(</a><a id="3684" href="Groups.Aut.html#3663" class="Bound">g</a> <a id="3686" href="UF.Equiv.html#2640" class="Function Operator">●</a> <a id="3688" href="Groups.Aut.html#3667" class="Bound">f</a> <a id="3690" class="Symbol">))</a> <a id="3693" href="UF.Equiv.html#2640" class="Function Operator">●</a> <a id="3695" href="Groups.Aut.html#3560" class="Bound">φ</a>                   <a id="3715" href="MLTT.Id.html#2703" class="Function Operator">＝⟨</a>  <a id="3719" href="MLTT.Id.html#1136" class="Function">ap</a> <a id="3722" class="Symbol">(</a><a id="3723" href="UF.Equiv.html#2640" class="Function Operator">_●</a> <a id="3726" href="Groups.Aut.html#3560" class="Bound">φ</a><a id="3727" class="Symbol">)</a> <a id="3729" class="Symbol">(</a><a id="3730" href="MLTT.Id.html#1136" class="Function">ap</a> <a id="3733" class="Symbol">(</a><a id="3734" href="UF.Equiv.html#8089" class="Function">≃-sym</a> <a id="3740" href="Groups.Aut.html#3560" class="Bound">φ</a> <a id="3742" href="UF.Equiv.html#2640" class="Function Operator">●_</a><a id="3744" class="Symbol">)</a> <a id="3746" href="Groups.Aut.html#4219" class="Function">p</a><a id="3747" class="Symbol">)</a> <a id="3749" href="MLTT.Id.html#2703" class="Function Operator">⟩</a>
                         <a id="3776" class="Symbol">(</a><a id="3777" href="UF.Equiv.html#8089" class="Function">≃-sym</a> <a id="3783" href="Groups.Aut.html#3560" class="Bound">φ</a> <a id="3785" href="UF.Equiv.html#2640" class="Function Operator">●</a> <a id="3787" class="Symbol">((</a><a id="3789" href="Groups.Aut.html#3663" class="Bound">g</a> <a id="3791" href="UF.Equiv.html#2640" class="Function Operator">●</a> <a id="3793" href="Groups.Aut.html#3560" class="Bound">φ</a><a id="3794" class="Symbol">)</a> <a id="3796" href="UF.Equiv.html#2640" class="Function Operator">●</a> <a id="3798" class="Symbol">(</a><a id="3799" href="UF.Equiv.html#8089" class="Function">≃-sym</a> <a id="3805" href="Groups.Aut.html#3560" class="Bound">φ</a> <a id="3807" href="UF.Equiv.html#2640" class="Function Operator">●</a> <a id="3809" href="Groups.Aut.html#3667" class="Bound">f</a><a id="3810" class="Symbol">)))</a> <a id="3814" href="UF.Equiv.html#2640" class="Function Operator">●</a> <a id="3816" href="Groups.Aut.html#3560" class="Bound">φ</a> <a id="3818" href="MLTT.Id.html#2703" class="Function Operator">＝⟨</a>  <a id="3822" href="MLTT.Id.html#1136" class="Function">ap</a> <a id="3825" class="Symbol">(</a><a id="3826" href="UF.Equiv.html#2640" class="Function Operator">_●</a> <a id="3829" href="Groups.Aut.html#3560" class="Bound">φ</a><a id="3830" class="Symbol">)</a> <a id="3832" class="Symbol">(</a><a id="3833" href="UF.Equiv-FunExt.html#7112" class="Function">≃-assoc</a> <a id="3841" href="Groups.Aut.html#3469" class="Bound">fe</a> <a id="3844" class="Symbol">(</a><a id="3845" href="UF.Equiv.html#8089" class="Function">≃-sym</a> <a id="3851" href="Groups.Aut.html#3560" class="Bound">φ</a><a id="3852" class="Symbol">)</a> <a id="3854" class="Symbol">(</a><a id="3855" href="Groups.Aut.html#3663" class="Bound">g</a> <a id="3857" href="UF.Equiv.html#2640" class="Function Operator">●</a> <a id="3859" href="Groups.Aut.html#3560" class="Bound">φ</a><a id="3860" class="Symbol">)</a> <a id="3862" class="Symbol">(</a><a id="3863" href="UF.Equiv.html#8089" class="Function">≃-sym</a> <a id="3869" href="Groups.Aut.html#3560" class="Bound">φ</a> <a id="3871" href="UF.Equiv.html#2640" class="Function Operator">●</a> <a id="3873" href="Groups.Aut.html#3667" class="Bound">f</a><a id="3874" class="Symbol">))</a> <a id="3877" href="MLTT.Id.html#2703" class="Function Operator">⟩</a>
                         <a id="3904" class="Symbol">((</a><a id="3906" href="UF.Equiv.html#8089" class="Function">≃-sym</a> <a id="3912" href="Groups.Aut.html#3560" class="Bound">φ</a> <a id="3914" href="UF.Equiv.html#2640" class="Function Operator">●</a> <a id="3916" class="Symbol">(</a><a id="3917" href="Groups.Aut.html#3663" class="Bound">g</a> <a id="3919" href="UF.Equiv.html#2640" class="Function Operator">●</a> <a id="3921" href="Groups.Aut.html#3560" class="Bound">φ</a><a id="3922" class="Symbol">))</a> <a id="3925" href="UF.Equiv.html#2640" class="Function Operator">●</a> <a id="3927" class="Symbol">(</a><a id="3928" href="UF.Equiv.html#8089" class="Function">≃-sym</a> <a id="3934" href="Groups.Aut.html#3560" class="Bound">φ</a> <a id="3936" href="UF.Equiv.html#2640" class="Function Operator">●</a> <a id="3938" href="Groups.Aut.html#3667" class="Bound">f</a><a id="3939" class="Symbol">))</a> <a id="3942" href="UF.Equiv.html#2640" class="Function Operator">●</a> <a id="3944" href="Groups.Aut.html#3560" class="Bound">φ</a> <a id="3946" href="MLTT.Id.html#2703" class="Function Operator">＝⟨</a>  <a id="3950" class="Symbol">(</a><a id="3951" href="UF.Equiv-FunExt.html#7112" class="Function">≃-assoc</a> <a id="3959" href="Groups.Aut.html#3469" class="Bound">fe</a> <a id="3962" class="Symbol">(</a><a id="3963" href="UF.Equiv.html#8089" class="Function">≃-sym</a> <a id="3969" href="Groups.Aut.html#3560" class="Bound">φ</a> <a id="3971" href="UF.Equiv.html#2640" class="Function Operator">●</a> <a id="3973" class="Symbol">(</a><a id="3974" href="Groups.Aut.html#3663" class="Bound">g</a> <a id="3976" href="UF.Equiv.html#2640" class="Function Operator">●</a> <a id="3978" href="Groups.Aut.html#3560" class="Bound">φ</a><a id="3979" class="Symbol">))</a> <a id="3982" class="Symbol">(</a><a id="3983" href="UF.Equiv.html#8089" class="Function">≃-sym</a> <a id="3989" href="Groups.Aut.html#3560" class="Bound">φ</a> <a id="3991" href="UF.Equiv.html#2640" class="Function Operator">●</a> <a id="3993" href="Groups.Aut.html#3667" class="Bound">f</a><a id="3994" class="Symbol">)</a> <a id="3996" href="Groups.Aut.html#3560" class="Bound">φ</a><a id="3997" class="Symbol">)</a> <a id="3999" href="MLTT.Id.html#1055" class="Function Operator">⁻¹</a> <a id="4002" href="MLTT.Id.html#2703" class="Function Operator">⟩</a>
                         <a id="4029" class="Symbol">(</a><a id="4030" href="UF.Equiv.html#8089" class="Function">≃-sym</a> <a id="4036" href="Groups.Aut.html#3560" class="Bound">φ</a> <a id="4038" href="UF.Equiv.html#2640" class="Function Operator">●</a> <a id="4040" class="Symbol">(</a><a id="4041" href="Groups.Aut.html#3663" class="Bound">g</a> <a id="4043" href="UF.Equiv.html#2640" class="Function Operator">●</a> <a id="4045" href="Groups.Aut.html#3560" class="Bound">φ</a><a id="4046" class="Symbol">))</a> <a id="4049" href="UF.Equiv.html#2640" class="Function Operator">●</a> <a id="4051" class="Symbol">((</a><a id="4053" href="UF.Equiv.html#8089" class="Function">≃-sym</a> <a id="4059" href="Groups.Aut.html#3560" class="Bound">φ</a> <a id="4061" href="UF.Equiv.html#2640" class="Function Operator">●</a> <a id="4063" href="Groups.Aut.html#3667" class="Bound">f</a><a id="4064" class="Symbol">)</a> <a id="4066" href="UF.Equiv.html#2640" class="Function Operator">●</a> <a id="4068" href="Groups.Aut.html#3560" class="Bound">φ</a><a id="4069" class="Symbol">)</a> <a id="4071" href="MLTT.Id.html#2703" class="Function Operator">＝⟨</a>  <a id="4075" href="MLTT.Id.html#1136" class="Function">ap</a> <a id="4078" class="Symbol">(</a><a id="4079" href="UF.Equiv.html#2640" class="Function Operator">_●</a> <a id="4082" class="Symbol">((</a><a id="4084" href="UF.Equiv.html#8089" class="Function">≃-sym</a> <a id="4090" href="Groups.Aut.html#3560" class="Bound">φ</a> <a id="4092" href="UF.Equiv.html#2640" class="Function Operator">●</a> <a id="4094" href="Groups.Aut.html#3667" class="Bound">f</a><a id="4095" class="Symbol">)</a> <a id="4097" href="UF.Equiv.html#2640" class="Function Operator">●</a> <a id="4099" href="Groups.Aut.html#3560" class="Bound">φ</a><a id="4100" class="Symbol">))</a> <a id="4103" class="Symbol">(</a><a id="4104" href="UF.Equiv-FunExt.html#7112" class="Function">≃-assoc</a> <a id="4112" href="Groups.Aut.html#3469" class="Bound">fe</a> <a id="4115" class="Symbol">(</a><a id="4116" href="UF.Equiv.html#8089" class="Function">≃-sym</a> <a id="4122" href="Groups.Aut.html#3560" class="Bound">φ</a><a id="4123" class="Symbol">)</a> <a id="4125" href="Groups.Aut.html#3663" class="Bound">g</a> <a id="4127" href="Groups.Aut.html#3560" class="Bound">φ</a><a id="4128" class="Symbol">)</a> <a id="4130" href="MLTT.Id.html#2703" class="Function Operator">⟩</a>
                         <a id="4157" class="Symbol">((</a><a id="4159" href="UF.Equiv.html#8089" class="Function">≃-sym</a> <a id="4165" href="Groups.Aut.html#3560" class="Bound">φ</a> <a id="4167" href="UF.Equiv.html#2640" class="Function Operator">●</a> <a id="4169" href="Groups.Aut.html#3663" class="Bound">g</a><a id="4170" class="Symbol">)</a> <a id="4172" href="UF.Equiv.html#2640" class="Function Operator">●</a> <a id="4174" href="Groups.Aut.html#3560" class="Bound">φ</a><a id="4175" class="Symbol">)</a> <a id="4177" href="UF.Equiv.html#2640" class="Function Operator">●</a> <a id="4179" class="Symbol">((</a><a id="4181" href="UF.Equiv.html#8089" class="Function">≃-sym</a> <a id="4187" href="Groups.Aut.html#3560" class="Bound">φ</a> <a id="4189" href="UF.Equiv.html#2640" class="Function Operator">●</a> <a id="4191" href="Groups.Aut.html#3667" class="Bound">f</a><a id="4192" class="Symbol">)</a> <a id="4194" href="UF.Equiv.html#2640" class="Function Operator">●</a> <a id="4196" href="Groups.Aut.html#3560" class="Bound">φ</a><a id="4197" class="Symbol">)</a> <a id="4199" href="MLTT.Id.html#2785" class="Function Operator">∎</a>
     <a id="4206" class="Keyword">where</a>
       <a id="4219" href="Groups.Aut.html#4219" class="Function">p</a> <a id="4221" class="Symbol">=</a> <a id="4223" href="Groups.Aut.html#3663" class="Bound">g</a> <a id="4225" href="UF.Equiv.html#2640" class="Function Operator">●</a> <a id="4227" href="Groups.Aut.html#3667" class="Bound">f</a>                    <a id="4248" href="MLTT.Id.html#2703" class="Function Operator">＝⟨</a> <a id="4251" href="MLTT.Id.html#1136" class="Function">ap</a> <a id="4254" class="Symbol">(</a><a id="4255" href="UF.Equiv.html#2640" class="Function Operator">_●</a> <a id="4258" href="Groups.Aut.html#3667" class="Bound">f</a><a id="4259" class="Symbol">)</a> <a id="4261" class="Symbol">(</a><a id="4262" href="UF.Equiv-FunExt.html#9618" class="Function">≃-refl-right</a> <a id="4275" href="Groups.Aut.html#3469" class="Bound">fe</a> <a id="4278" href="Groups.Aut.html#3663" class="Bound">g</a><a id="4279" class="Symbol">)</a> <a id="4281" href="MLTT.Id.html#1055" class="Function Operator">⁻¹</a> <a id="4284" href="MLTT.Id.html#2703" class="Function Operator">⟩</a>
           <a id="4297" class="Symbol">(</a><a id="4298" href="Groups.Aut.html#3663" class="Bound">g</a> <a id="4300" href="UF.Equiv.html#2640" class="Function Operator">●</a> <a id="4302" href="UF.Equiv.html#1517" class="Function">≃-refl</a> <a id="4309" href="Groups.Aut.html#3492" class="Bound">X</a><a id="4310" class="Symbol">)</a> <a id="4312" href="UF.Equiv.html#2640" class="Function Operator">●</a> <a id="4314" href="Groups.Aut.html#3667" class="Bound">f</a>       <a id="4322" href="MLTT.Id.html#2703" class="Function Operator">＝⟨</a> <a id="4325" href="MLTT.Id.html#1136" class="Function">ap</a> <a id="4328" class="Symbol">(</a><a id="4329" href="UF.Equiv.html#2640" class="Function Operator">_●</a> <a id="4332" href="Groups.Aut.html#3667" class="Bound">f</a><a id="4333" class="Symbol">)</a> <a id="4335" class="Symbol">(</a><a id="4336" href="MLTT.Id.html#1136" class="Function">ap</a> <a id="4339" class="Symbol">(</a><a id="4340" href="Groups.Aut.html#3663" class="Bound">g</a> <a id="4342" href="UF.Equiv.html#2640" class="Function Operator">●_</a><a id="4344" class="Symbol">)</a> <a id="4346" class="Symbol">(</a><a id="4347" href="UF.Equiv-FunExt.html#10743" class="Function">≃-sym-right-inverse</a> <a id="4367" href="Groups.Aut.html#3469" class="Bound">fe</a> <a id="4370" href="Groups.Aut.html#3560" class="Bound">φ</a><a id="4371" class="Symbol">)</a> <a id="4373" href="MLTT.Id.html#1055" class="Function Operator">⁻¹</a><a id="4375" class="Symbol">)</a> <a id="4377" href="MLTT.Id.html#2703" class="Function Operator">⟩</a>
           <a id="4390" class="Symbol">(</a><a id="4391" href="Groups.Aut.html#3663" class="Bound">g</a> <a id="4393" href="UF.Equiv.html#2640" class="Function Operator">●</a> <a id="4395" class="Symbol">(</a><a id="4396" href="Groups.Aut.html#3560" class="Bound">φ</a> <a id="4398" href="UF.Equiv.html#2640" class="Function Operator">●</a> <a id="4400" href="UF.Equiv.html#8089" class="Function">≃-sym</a> <a id="4406" href="Groups.Aut.html#3560" class="Bound">φ</a><a id="4407" class="Symbol">))</a> <a id="4410" href="UF.Equiv.html#2640" class="Function Operator">●</a> <a id="4412" href="Groups.Aut.html#3667" class="Bound">f</a>  <a id="4415" href="MLTT.Id.html#2703" class="Function Operator">＝⟨</a> <a id="4418" href="MLTT.Id.html#1136" class="Function">ap</a> <a id="4421" class="Symbol">(</a><a id="4422" href="UF.Equiv.html#2640" class="Function Operator">_●</a> <a id="4425" href="Groups.Aut.html#3667" class="Bound">f</a><a id="4426" class="Symbol">)</a> <a id="4428" class="Symbol">(</a><a id="4429" href="UF.Equiv-FunExt.html#7112" class="Function">≃-assoc</a> <a id="4437" href="Groups.Aut.html#3469" class="Bound">fe</a> <a id="4440" href="Groups.Aut.html#3663" class="Bound">g</a> <a id="4442" href="Groups.Aut.html#3560" class="Bound">φ</a> <a id="4444" class="Symbol">(</a><a id="4445" href="UF.Equiv.html#8089" class="Function">≃-sym</a> <a id="4451" href="Groups.Aut.html#3560" class="Bound">φ</a><a id="4452" class="Symbol">)</a> <a id="4454" class="Symbol">)</a> <a id="4456" href="MLTT.Id.html#2703" class="Function Operator">⟩</a>
           <a id="4469" class="Symbol">((</a><a id="4471" href="Groups.Aut.html#3663" class="Bound">g</a> <a id="4473" href="UF.Equiv.html#2640" class="Function Operator">●</a> <a id="4475" href="Groups.Aut.html#3560" class="Bound">φ</a><a id="4476" class="Symbol">)</a> <a id="4478" href="UF.Equiv.html#2640" class="Function Operator">●</a> <a id="4480" href="UF.Equiv.html#8089" class="Function">≃-sym</a> <a id="4486" href="Groups.Aut.html#3560" class="Bound">φ</a><a id="4487" class="Symbol">)</a> <a id="4489" href="UF.Equiv.html#2640" class="Function Operator">●</a> <a id="4491" href="Groups.Aut.html#3667" class="Bound">f</a>  <a id="4494" href="MLTT.Id.html#2703" class="Function Operator">＝⟨</a> <a id="4497" class="Symbol">(</a><a id="4498" href="UF.Equiv-FunExt.html#7112" class="Function">≃-assoc</a> <a id="4506" href="Groups.Aut.html#3469" class="Bound">fe</a> <a id="4509" class="Symbol">(</a><a id="4510" href="Groups.Aut.html#3663" class="Bound">g</a> <a id="4512" href="UF.Equiv.html#2640" class="Function Operator">●</a> <a id="4514" href="Groups.Aut.html#3560" class="Bound">φ</a><a id="4515" class="Symbol">)</a> <a id="4517" class="Symbol">(</a><a id="4518" href="UF.Equiv.html#8089" class="Function">≃-sym</a> <a id="4524" href="Groups.Aut.html#3560" class="Bound">φ</a><a id="4525" class="Symbol">)</a> <a id="4527" href="Groups.Aut.html#3667" class="Bound">f</a><a id="4528" class="Symbol">)</a> <a id="4530" href="MLTT.Id.html#1055" class="Function Operator">⁻¹</a>   <a id="4535" href="MLTT.Id.html#2703" class="Function Operator">⟩</a>
           <a id="4548" class="Symbol">(</a><a id="4549" href="Groups.Aut.html#3663" class="Bound">g</a> <a id="4551" href="UF.Equiv.html#2640" class="Function Operator">●</a> <a id="4553" href="Groups.Aut.html#3560" class="Bound">φ</a><a id="4554" class="Symbol">)</a> <a id="4556" href="UF.Equiv.html#2640" class="Function Operator">●</a> <a id="4558" class="Symbol">(</a><a id="4559" href="UF.Equiv.html#8089" class="Function">≃-sym</a> <a id="4565" href="Groups.Aut.html#3560" class="Bound">φ</a> <a id="4567" href="UF.Equiv.html#2640" class="Function Operator">●</a> <a id="4569" href="Groups.Aut.html#3667" class="Bound">f</a><a id="4570" class="Symbol">)</a> <a id="4572" href="MLTT.Id.html#2785" class="Function Operator">∎</a>

<a id="4575" class="Markup">\end{code}</a><a id="4585" class="Background">
</a></pre></body></html>