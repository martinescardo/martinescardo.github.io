<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>TypeTopology.SequentiallyHausdorff</title><link rel="stylesheet" href="Agda.css"><script type="text/javascript" src="highlight-hover.js"></script></head><body><pre class="Agda"><a id="1" class="Background">Martin Escardo, 17th July 2024.

Sequentially Hausdorff types.

Motivated by https://grossack.site/2024/07/03/life-in-johnstones-topological-topos

The idea in this file, and many files in TypeTopology, is that we are
working in an arbitrary topos, which may or may not be a topological
topos, and we are interested in proving things synthetically in
topological toposes.

</a><a id="374" class="Markup">\begin{code}</a>

<a id="388" class="Symbol">{-#</a> <a id="392" class="Keyword">OPTIONS</a> <a id="400" class="Pragma">--safe</a> <a id="407" class="Pragma">--without-K</a> <a id="419" class="Symbol">#-}</a>

<a id="424" class="Keyword">open</a> <a id="429" class="Keyword">import</a> <a id="436" href="UF.FunExt.html" class="Module">UF.FunExt</a>

<a id="447" class="Keyword">module</a> <a id="454" href="TypeTopology.SequentiallyHausdorff.html" class="Module">TypeTopology.SequentiallyHausdorff</a>
        <a id="497" class="Symbol">(</a><a id="498" href="TypeTopology.SequentiallyHausdorff.html#498" class="Bound">feâ‚€</a> <a id="502" class="Symbol">:</a> <a id="504" href="UF.FunExt.html#956" class="Function">funextâ‚€</a><a id="511" class="Symbol">)</a>
       <a id="520" class="Keyword">where</a>

<a id="527" class="Keyword">open</a> <a id="532" class="Keyword">import</a> <a id="539" href="CoNaturals.Type.html" class="Module">CoNaturals.Type</a>
<a id="555" class="Keyword">open</a> <a id="560" class="Keyword">import</a> <a id="567" href="MLTT.Spartan.html" class="Module">MLTT.Spartan</a>
<a id="580" class="Keyword">open</a> <a id="585" class="Keyword">import</a> <a id="592" href="Notation.CanonicalMap.html" class="Module">Notation.CanonicalMap</a>
<a id="614" class="Keyword">open</a> <a id="619" class="Keyword">import</a> <a id="626" href="Taboos.BasicDiscontinuity.html" class="Module">Taboos.BasicDiscontinuity</a> <a id="652" href="TypeTopology.SequentiallyHausdorff.html#498" class="Bound">feâ‚€</a>
<a id="656" class="Keyword">open</a> <a id="661" class="Keyword">import</a> <a id="668" href="Taboos.WLPO.html" class="Module">Taboos.WLPO</a>

<a id="681" class="Markup">\end{code}</a><a id="691" class="Background">

A topological space is sequentially Hausdorff if every sequence
converges to at most one point. In our synthetic setting, this can be
formulated as follows, following the above blog post by Chris
Grossack.

</a><a id="900" class="Markup">\begin{code}</a>

<a id="is-sequentially-Hausdorff"></a><a id="914" href="TypeTopology.SequentiallyHausdorff.html#914" class="Function">is-sequentially-Hausdorff</a> <a id="940" class="Symbol">:</a> <a id="942" href="MLTT.Universes.html#3833" class="Generalizable">ğ“¤</a> <a id="944" href="MLTT.Universes.html#3976" class="Function Operator">Ì‡</a> <a id="946" class="Symbol">â†’</a> <a id="948" href="MLTT.Universes.html#3833" class="Generalizable">ğ“¤</a> <a id="950" href="MLTT.Universes.html#3976" class="Function Operator">Ì‡</a>
<a id="952" href="TypeTopology.SequentiallyHausdorff.html#914" class="Function">is-sequentially-Hausdorff</a> <a id="978" href="TypeTopology.SequentiallyHausdorff.html#978" class="Bound">X</a> <a id="980" class="Symbol">=</a> <a id="982" class="Symbol">(</a><a id="983" href="TypeTopology.SequentiallyHausdorff.html#983" class="Bound">f</a> <a id="985" href="TypeTopology.SequentiallyHausdorff.html#985" class="Bound">g</a> <a id="987" class="Symbol">:</a> <a id="989" href="CoNaturals.GenericConvergentSequence.html#1332" class="Function">â„•âˆ</a> <a id="992" class="Symbol">â†’</a> <a id="994" href="TypeTopology.SequentiallyHausdorff.html#978" class="Bound">X</a><a id="995" class="Symbol">)</a>
                            <a id="1025" class="Symbol">â†’</a> <a id="1027" class="Symbol">((</a><a id="1029" href="TypeTopology.SequentiallyHausdorff.html#1029" class="Bound">n</a> <a id="1031" class="Symbol">:</a> <a id="1033" href="MLTT.Natural-Numbers-Type.html#96" class="Datatype">â„•</a><a id="1034" class="Symbol">)</a> <a id="1036" class="Symbol">â†’</a> <a id="1038" href="TypeTopology.SequentiallyHausdorff.html#983" class="Bound">f</a> <a id="1040" class="Symbol">(</a><a id="1041" href="Notation.CanonicalMap.html#359" class="Field">Î¹</a> <a id="1043" href="TypeTopology.SequentiallyHausdorff.html#1029" class="Bound">n</a><a id="1044" class="Symbol">)</a> <a id="1046" href="MLTT.Id.html#193" class="Datatype Operator">ï¼</a> <a id="1048" href="TypeTopology.SequentiallyHausdorff.html#985" class="Bound">g</a> <a id="1050" class="Symbol">(</a><a id="1051" href="Notation.CanonicalMap.html#359" class="Field">Î¹</a> <a id="1053" href="TypeTopology.SequentiallyHausdorff.html#1029" class="Bound">n</a><a id="1054" class="Symbol">))</a>
                            <a id="1085" class="Symbol">â†’</a> <a id="1087" href="TypeTopology.SequentiallyHausdorff.html#983" class="Bound">f</a> <a id="1089" href="CoNaturals.GenericConvergentSequence.html#6051" class="Function">âˆ</a> <a id="1091" href="MLTT.Id.html#193" class="Datatype Operator">ï¼</a> <a id="1093" href="TypeTopology.SequentiallyHausdorff.html#985" class="Bound">g</a> <a id="1095" href="CoNaturals.GenericConvergentSequence.html#6051" class="Function">âˆ</a>

<a id="1098" class="Markup">\end{code}</a><a id="1108" class="Background">

If WLPO holds in our topos, then our topos is not topological, in any
conceivable sense, and no type with two distinct points is
sequentially Hausdorff.

</a><a id="1264" class="Markup">\begin{code}</a>

<a id="no-non-trivial-sequentially-Hausdorff-types-under-WLPO"></a><a id="1278" href="TypeTopology.SequentiallyHausdorff.html#1278" class="Function">no-non-trivial-sequentially-Hausdorff-types-under-WLPO</a>
 <a id="1334" class="Symbol">:</a> <a id="1336" href="Taboos.WLPO.html#1337" class="Function">WLPO</a>
 <a id="1342" class="Symbol">â†’</a> <a id="1344" class="Symbol">(</a><a id="1345" href="TypeTopology.SequentiallyHausdorff.html#1345" class="Bound">X</a> <a id="1347" class="Symbol">:</a> <a id="1349" href="MLTT.Universes.html#3833" class="Generalizable">ğ“¤</a> <a id="1351" href="MLTT.Universes.html#3976" class="Function Operator">Ì‡</a> <a id="1353" class="Symbol">)</a>
 <a id="1356" class="Symbol">â†’</a> <a id="1358" href="MLTT.Negation.html#689" class="Function">has-two-distinct-points</a> <a id="1382" href="TypeTopology.SequentiallyHausdorff.html#1345" class="Bound">X</a>
 <a id="1385" class="Symbol">â†’</a> <a id="1387" href="MLTT.Negation.html#278" class="Function Operator">Â¬</a> <a id="1389" href="TypeTopology.SequentiallyHausdorff.html#914" class="Function">is-sequentially-Hausdorff</a> <a id="1415" href="TypeTopology.SequentiallyHausdorff.html#1345" class="Bound">X</a>
<a id="1417" href="TypeTopology.SequentiallyHausdorff.html#1278" class="Function">no-non-trivial-sequentially-Hausdorff-types-under-WLPO</a>
 <a id="1473" href="TypeTopology.SequentiallyHausdorff.html#1473" class="Bound">wlpo</a> <a id="1478" href="TypeTopology.SequentiallyHausdorff.html#1478" class="Bound">X</a> <a id="1480" class="Symbol">((</a><a id="1482" href="TypeTopology.SequentiallyHausdorff.html#1482" class="Bound">xâ‚€</a> <a id="1485" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="1487" href="TypeTopology.SequentiallyHausdorff.html#1487" class="Bound">xâ‚</a><a id="1489" class="Symbol">)</a><a id="1490" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="1492" href="TypeTopology.SequentiallyHausdorff.html#1492" class="Bound">d</a><a id="1493" class="Symbol">)</a> <a id="1495" href="TypeTopology.SequentiallyHausdorff.html#1495" class="Bound">X-is-seq-Haus</a> <a id="1509" class="Symbol">=</a> <a id="1511" href="TypeTopology.SequentiallyHausdorff.html#2020" class="Function">III</a>
 <a id="1516" class="Keyword">where</a>
  <a id="1524" href="TypeTopology.SequentiallyHausdorff.html#1524" class="Function">f</a> <a id="1526" class="Symbol">:</a> <a id="1528" href="CoNaturals.GenericConvergentSequence.html#1332" class="Function">â„•âˆ</a> <a id="1531" class="Symbol">â†’</a> <a id="1533" href="TypeTopology.SequentiallyHausdorff.html#1478" class="Bound">X</a>
  <a id="1537" href="TypeTopology.SequentiallyHausdorff.html#1524" class="Function">f</a> <a id="1539" href="TypeTopology.SequentiallyHausdorff.html#1539" class="Bound">u</a> <a id="1541" class="Symbol">=</a> <a id="1543" href="TypeTopology.SequentiallyHausdorff.html#1482" class="Bound">xâ‚€</a>

  <a id="1549" href="TypeTopology.SequentiallyHausdorff.html#1549" class="Function">g&#39;</a> <a id="1552" class="Symbol">:</a> <a id="1554" class="Symbol">(</a><a id="1555" href="TypeTopology.SequentiallyHausdorff.html#1555" class="Bound">u</a> <a id="1557" class="Symbol">:</a> <a id="1559" href="CoNaturals.GenericConvergentSequence.html#1332" class="Function">â„•âˆ</a><a id="1561" class="Symbol">)</a> <a id="1563" class="Symbol">â†’</a> <a id="1565" class="Symbol">(</a><a id="1566" href="TypeTopology.SequentiallyHausdorff.html#1555" class="Bound">u</a> <a id="1568" href="MLTT.Id.html#193" class="Datatype Operator">ï¼</a> <a id="1570" href="CoNaturals.GenericConvergentSequence.html#6051" class="Function">âˆ</a><a id="1571" class="Symbol">)</a> <a id="1573" href="MLTT.Plus-Type.html#120" class="Datatype Operator">+</a> <a id="1575" class="Symbol">(</a><a id="1576" href="TypeTopology.SequentiallyHausdorff.html#1555" class="Bound">u</a> <a id="1578" href="MLTT.Negation.html#635" class="Function Operator">â‰ </a> <a id="1580" href="CoNaturals.GenericConvergentSequence.html#6051" class="Function">âˆ</a><a id="1581" class="Symbol">)</a> <a id="1583" class="Symbol">â†’</a> <a id="1585" href="TypeTopology.SequentiallyHausdorff.html#1478" class="Bound">X</a>
  <a id="1589" href="TypeTopology.SequentiallyHausdorff.html#1549" class="Function">g&#39;</a> <a id="1592" href="TypeTopology.SequentiallyHausdorff.html#1592" class="Bound">u</a> <a id="1594" class="Symbol">(</a><a id="1595" href="MLTT.Plus-Type.html#169" class="InductiveConstructor">inl</a> <a id="1599" class="Symbol">_)</a> <a id="1602" class="Symbol">=</a> <a id="1604" href="TypeTopology.SequentiallyHausdorff.html#1487" class="Bound">xâ‚</a>
  <a id="1609" href="TypeTopology.SequentiallyHausdorff.html#1549" class="Function">g&#39;</a> <a id="1612" href="TypeTopology.SequentiallyHausdorff.html#1612" class="Bound">u</a> <a id="1614" class="Symbol">(</a><a id="1615" href="MLTT.Plus-Type.html#186" class="InductiveConstructor">inr</a> <a id="1619" class="Symbol">_)</a> <a id="1622" class="Symbol">=</a> <a id="1624" href="TypeTopology.SequentiallyHausdorff.html#1482" class="Bound">xâ‚€</a>

  <a id="1630" href="TypeTopology.SequentiallyHausdorff.html#1630" class="Function">g</a> <a id="1632" class="Symbol">:</a> <a id="1634" href="CoNaturals.GenericConvergentSequence.html#1332" class="Function">â„•âˆ</a> <a id="1637" class="Symbol">â†’</a> <a id="1639" href="TypeTopology.SequentiallyHausdorff.html#1478" class="Bound">X</a>
  <a id="1643" href="TypeTopology.SequentiallyHausdorff.html#1630" class="Function">g</a> <a id="1645" href="TypeTopology.SequentiallyHausdorff.html#1645" class="Bound">u</a> <a id="1647" class="Symbol">=</a> <a id="1649" href="TypeTopology.SequentiallyHausdorff.html#1549" class="Function">g&#39;</a> <a id="1652" href="TypeTopology.SequentiallyHausdorff.html#1645" class="Bound">u</a> <a id="1654" class="Symbol">(</a><a id="1655" href="TypeTopology.SequentiallyHausdorff.html#1473" class="Bound">wlpo</a> <a id="1660" href="TypeTopology.SequentiallyHausdorff.html#1645" class="Bound">u</a><a id="1661" class="Symbol">)</a>

  <a id="1666" href="TypeTopology.SequentiallyHausdorff.html#1666" class="Function">I</a> <a id="1668" class="Symbol">:</a> <a id="1670" class="Symbol">(</a><a id="1671" href="TypeTopology.SequentiallyHausdorff.html#1671" class="Bound">n</a> <a id="1673" class="Symbol">:</a> <a id="1675" href="MLTT.Natural-Numbers-Type.html#96" class="Datatype">â„•</a><a id="1676" class="Symbol">)</a> <a id="1678" class="Symbol">(</a><a id="1679" href="TypeTopology.SequentiallyHausdorff.html#1679" class="Bound">c</a> <a id="1681" class="Symbol">:</a> <a id="1683" class="Symbol">(</a><a id="1684" href="Notation.CanonicalMap.html#359" class="Field">Î¹</a> <a id="1686" href="TypeTopology.SequentiallyHausdorff.html#1671" class="Bound">n</a> <a id="1688" href="MLTT.Id.html#193" class="Datatype Operator">ï¼</a> <a id="1690" href="CoNaturals.GenericConvergentSequence.html#6051" class="Function">âˆ</a><a id="1691" class="Symbol">)</a> <a id="1693" href="MLTT.Plus-Type.html#120" class="Datatype Operator">+</a> <a id="1695" class="Symbol">(</a><a id="1696" href="Notation.CanonicalMap.html#359" class="Field">Î¹</a> <a id="1698" href="TypeTopology.SequentiallyHausdorff.html#1671" class="Bound">n</a> <a id="1700" href="MLTT.Negation.html#635" class="Function Operator">â‰ </a> <a id="1702" href="CoNaturals.GenericConvergentSequence.html#6051" class="Function">âˆ</a><a id="1703" class="Symbol">))</a> <a id="1706" class="Symbol">â†’</a> <a id="1708" href="TypeTopology.SequentiallyHausdorff.html#1549" class="Function">g&#39;</a> <a id="1711" class="Symbol">(</a><a id="1712" href="Notation.CanonicalMap.html#359" class="Field">Î¹</a> <a id="1714" href="TypeTopology.SequentiallyHausdorff.html#1671" class="Bound">n</a><a id="1715" class="Symbol">)</a> <a id="1717" href="TypeTopology.SequentiallyHausdorff.html#1679" class="Bound">c</a> <a id="1719" href="MLTT.Id.html#193" class="Datatype Operator">ï¼</a> <a id="1721" href="TypeTopology.SequentiallyHausdorff.html#1482" class="Bound">xâ‚€</a>
  <a id="1726" href="TypeTopology.SequentiallyHausdorff.html#1666" class="Function">I</a> <a id="1728" href="TypeTopology.SequentiallyHausdorff.html#1728" class="Bound">n</a> <a id="1730" class="Symbol">(</a><a id="1731" href="MLTT.Plus-Type.html#169" class="InductiveConstructor">inl</a> <a id="1735" href="TypeTopology.SequentiallyHausdorff.html#1735" class="Bound">e</a><a id="1736" class="Symbol">)</a> <a id="1738" class="Symbol">=</a> <a id="1740" href="MLTT.Empty.html#356" class="Function">ğŸ˜-elim</a> <a id="1747" class="Symbol">(</a><a id="1748" href="CoNaturals.GenericConvergentSequence.html#12129" class="Function">âˆ-is-not-finite</a> <a id="1764" href="TypeTopology.SequentiallyHausdorff.html#1728" class="Bound">n</a> <a id="1766" class="Symbol">(</a><a id="1767" href="TypeTopology.SequentiallyHausdorff.html#1735" class="Bound">e</a> <a id="1769" href="MLTT.Id.html#1055" class="Function Operator">â»Â¹</a><a id="1771" class="Symbol">))</a>
  <a id="1776" href="TypeTopology.SequentiallyHausdorff.html#1666" class="Function">I</a> <a id="1778" href="TypeTopology.SequentiallyHausdorff.html#1778" class="Bound">n</a> <a id="1780" class="Symbol">(</a><a id="1781" href="MLTT.Plus-Type.html#186" class="InductiveConstructor">inr</a> <a id="1785" class="Symbol">_)</a> <a id="1788" class="Symbol">=</a> <a id="1790" href="MLTT.Identity-Type.html#157" class="InductiveConstructor">refl</a>

  <a id="1798" href="TypeTopology.SequentiallyHausdorff.html#1798" class="Function">a</a> <a id="1800" class="Symbol">:</a> <a id="1802" class="Symbol">(</a><a id="1803" href="TypeTopology.SequentiallyHausdorff.html#1803" class="Bound">n</a> <a id="1805" class="Symbol">:</a> <a id="1807" href="MLTT.Natural-Numbers-Type.html#96" class="Datatype">â„•</a><a id="1808" class="Symbol">)</a> <a id="1810" class="Symbol">â†’</a>  <a id="1813" href="TypeTopology.SequentiallyHausdorff.html#1524" class="Function">f</a> <a id="1815" class="Symbol">(</a><a id="1816" href="Notation.CanonicalMap.html#359" class="Field">Î¹</a> <a id="1818" href="TypeTopology.SequentiallyHausdorff.html#1803" class="Bound">n</a><a id="1819" class="Symbol">)</a> <a id="1821" href="MLTT.Id.html#193" class="Datatype Operator">ï¼</a> <a id="1823" href="TypeTopology.SequentiallyHausdorff.html#1630" class="Function">g</a> <a id="1825" class="Symbol">(</a><a id="1826" href="Notation.CanonicalMap.html#359" class="Field">Î¹</a> <a id="1828" href="TypeTopology.SequentiallyHausdorff.html#1803" class="Bound">n</a><a id="1829" class="Symbol">)</a>
  <a id="1833" href="TypeTopology.SequentiallyHausdorff.html#1798" class="Function">a</a> <a id="1835" href="TypeTopology.SequentiallyHausdorff.html#1835" class="Bound">n</a> <a id="1837" class="Symbol">=</a>  <a id="1840" href="TypeTopology.SequentiallyHausdorff.html#1524" class="Function">f</a> <a id="1842" class="Symbol">(</a><a id="1843" href="Notation.CanonicalMap.html#359" class="Field">Î¹</a> <a id="1845" href="TypeTopology.SequentiallyHausdorff.html#1835" class="Bound">n</a><a id="1846" class="Symbol">)</a> <a id="1848" href="MLTT.Id.html#2239" class="Function Operator">ï¼âŸ¨</a> <a id="1851" href="MLTT.Identity-Type.html#157" class="InductiveConstructor">refl</a> <a id="1856" href="MLTT.Id.html#2239" class="Function Operator">âŸ©</a>
         <a id="1867" href="TypeTopology.SequentiallyHausdorff.html#1482" class="Bound">xâ‚€</a>      <a id="1875" href="MLTT.Id.html#2239" class="Function Operator">ï¼âŸ¨</a> <a id="1878" class="Symbol">(</a><a id="1879" href="TypeTopology.SequentiallyHausdorff.html#1666" class="Function">I</a> <a id="1881" href="TypeTopology.SequentiallyHausdorff.html#1835" class="Bound">n</a> <a id="1883" class="Symbol">(</a><a id="1884" href="TypeTopology.SequentiallyHausdorff.html#1473" class="Bound">wlpo</a> <a id="1889" class="Symbol">(</a><a id="1890" href="Notation.CanonicalMap.html#359" class="Field">Î¹</a> <a id="1892" href="TypeTopology.SequentiallyHausdorff.html#1835" class="Bound">n</a><a id="1893" class="Symbol">)))</a><a id="1896" href="MLTT.Id.html#1055" class="Function Operator">â»Â¹</a> <a id="1899" href="MLTT.Id.html#2239" class="Function Operator">âŸ©</a>
         <a id="1910" href="TypeTopology.SequentiallyHausdorff.html#1630" class="Function">g</a> <a id="1912" class="Symbol">(</a><a id="1913" href="Notation.CanonicalMap.html#359" class="Field">Î¹</a> <a id="1915" href="TypeTopology.SequentiallyHausdorff.html#1835" class="Bound">n</a><a id="1916" class="Symbol">)</a> <a id="1918" href="MLTT.Id.html#2321" class="Function Operator">âˆ</a>

  <a id="1923" href="TypeTopology.SequentiallyHausdorff.html#1923" class="Function">II</a> <a id="1926" class="Symbol">:</a> <a id="1928" class="Symbol">(</a><a id="1929" href="TypeTopology.SequentiallyHausdorff.html#1929" class="Bound">c</a> <a id="1931" class="Symbol">:</a> <a id="1933" class="Symbol">(</a><a id="1934" href="CoNaturals.GenericConvergentSequence.html#6051" class="Function">âˆ</a> <a id="1936" href="MLTT.Id.html#193" class="Datatype Operator">ï¼</a> <a id="1938" href="CoNaturals.GenericConvergentSequence.html#6051" class="Function">âˆ</a><a id="1939" class="Symbol">)</a> <a id="1941" href="MLTT.Plus-Type.html#120" class="Datatype Operator">+</a> <a id="1943" class="Symbol">(</a><a id="1944" href="CoNaturals.GenericConvergentSequence.html#6051" class="Function">âˆ</a> <a id="1946" href="MLTT.Negation.html#635" class="Function Operator">â‰ </a> <a id="1948" href="CoNaturals.GenericConvergentSequence.html#6051" class="Function">âˆ</a><a id="1949" class="Symbol">))</a> <a id="1952" class="Symbol">â†’</a> <a id="1954" href="TypeTopology.SequentiallyHausdorff.html#1549" class="Function">g&#39;</a> <a id="1957" href="CoNaturals.GenericConvergentSequence.html#6051" class="Function">âˆ</a> <a id="1959" href="TypeTopology.SequentiallyHausdorff.html#1929" class="Bound">c</a> <a id="1961" href="MLTT.Id.html#193" class="Datatype Operator">ï¼</a> <a id="1963" href="TypeTopology.SequentiallyHausdorff.html#1487" class="Bound">xâ‚</a>
  <a id="1968" href="TypeTopology.SequentiallyHausdorff.html#1923" class="Function">II</a> <a id="1971" class="Symbol">(</a><a id="1972" href="MLTT.Plus-Type.html#169" class="InductiveConstructor">inl</a> <a id="1976" class="Symbol">_)</a> <a id="1979" class="Symbol">=</a> <a id="1981" href="MLTT.Identity-Type.html#157" class="InductiveConstructor">refl</a>
  <a id="1988" href="TypeTopology.SequentiallyHausdorff.html#1923" class="Function">II</a> <a id="1991" class="Symbol">(</a><a id="1992" href="MLTT.Plus-Type.html#186" class="InductiveConstructor">inr</a> <a id="1996" href="TypeTopology.SequentiallyHausdorff.html#1996" class="Bound">Î½</a><a id="1997" class="Symbol">)</a> <a id="1999" class="Symbol">=</a> <a id="2001" href="MLTT.Empty.html#356" class="Function">ğŸ˜-elim</a> <a id="2008" class="Symbol">(</a><a id="2009" href="TypeTopology.SequentiallyHausdorff.html#1996" class="Bound">Î½</a> <a id="2011" href="MLTT.Identity-Type.html#157" class="InductiveConstructor">refl</a><a id="2015" class="Symbol">)</a>

  <a id="2020" href="TypeTopology.SequentiallyHausdorff.html#2020" class="Function">III</a> <a id="2024" class="Symbol">:</a> <a id="2026" href="MLTT.Empty-Type.html#121" class="Datatype">ğŸ˜</a>
  <a id="2030" href="TypeTopology.SequentiallyHausdorff.html#2020" class="Function">III</a> <a id="2034" class="Symbol">=</a> <a id="2036" href="TypeTopology.SequentiallyHausdorff.html#1492" class="Bound">d</a> <a id="2038" class="Symbol">(</a><a id="2039" href="TypeTopology.SequentiallyHausdorff.html#1482" class="Bound">xâ‚€</a>  <a id="2043" href="MLTT.Id.html#2239" class="Function Operator">ï¼âŸ¨</a> <a id="2046" href="MLTT.Identity-Type.html#157" class="InductiveConstructor">refl</a> <a id="2051" href="MLTT.Id.html#2239" class="Function Operator">âŸ©</a>
           <a id="2064" href="TypeTopology.SequentiallyHausdorff.html#1524" class="Function">f</a> <a id="2066" href="CoNaturals.GenericConvergentSequence.html#6051" class="Function">âˆ</a> <a id="2068" href="MLTT.Id.html#2239" class="Function Operator">ï¼âŸ¨</a> <a id="2071" href="TypeTopology.SequentiallyHausdorff.html#1495" class="Bound">X-is-seq-Haus</a> <a id="2085" href="TypeTopology.SequentiallyHausdorff.html#1524" class="Function">f</a> <a id="2087" href="TypeTopology.SequentiallyHausdorff.html#1630" class="Function">g</a> <a id="2089" href="TypeTopology.SequentiallyHausdorff.html#1798" class="Function">a</a> <a id="2091" href="MLTT.Id.html#2239" class="Function Operator">âŸ©</a>
           <a id="2104" href="TypeTopology.SequentiallyHausdorff.html#1630" class="Function">g</a> <a id="2106" href="CoNaturals.GenericConvergentSequence.html#6051" class="Function">âˆ</a> <a id="2108" href="MLTT.Id.html#2239" class="Function Operator">ï¼âŸ¨</a> <a id="2111" href="TypeTopology.SequentiallyHausdorff.html#1923" class="Function">II</a> <a id="2114" class="Symbol">(</a><a id="2115" href="TypeTopology.SequentiallyHausdorff.html#1473" class="Bound">wlpo</a> <a id="2120" href="CoNaturals.GenericConvergentSequence.html#6051" class="Function">âˆ</a><a id="2121" class="Symbol">)</a> <a id="2123" href="MLTT.Id.html#2239" class="Function Operator">âŸ©</a>
           <a id="2136" href="TypeTopology.SequentiallyHausdorff.html#1487" class="Bound">xâ‚</a>  <a id="2140" href="MLTT.Id.html#2321" class="Function Operator">âˆ</a><a id="2141" class="Symbol">)</a>

<a id="2144" class="Markup">\end{code}</a><a id="2154" class="Background">

The reason WLPO doesn&#39;t hold topological in toposes is that the
negation of WLPO is a weak continuity principle [1], which holds in
topological toposes. So it makes sense to investigate which sets are
sequentially Hausdorff assuming Â¬ WLPO or stronger continuity
principles.

[1] https://doi.org/10.1017/S096012951300042X

To begin with, we show that all totally separated types are
sequentially Hausdorff in topological toposes.

</a><a id="2587" class="Markup">\begin{code}</a>

<a id="2601" class="Keyword">open</a> <a id="2606" class="Keyword">import</a> <a id="2613" href="TypeTopology.TotallySeparated.html" class="Module">TypeTopology.TotallySeparated</a>
<a id="2643" class="Keyword">open</a> <a id="2648" class="Keyword">import</a> <a id="2655" href="UF.DiscreteAndSeparated.html" class="Module">UF.DiscreteAndSeparated</a>

<a id="totally-separated-types-are-sequentially-Hausdorff"></a><a id="2680" href="TypeTopology.SequentiallyHausdorff.html#2680" class="Function">totally-separated-types-are-sequentially-Hausdorff</a>
 <a id="2732" class="Symbol">:</a> <a id="2734" href="MLTT.Negation.html#278" class="Function Operator">Â¬</a> <a id="2736" href="Taboos.WLPO.html#1337" class="Function">WLPO</a>
 <a id="2742" class="Symbol">â†’</a> <a id="2744" class="Symbol">(</a><a id="2745" href="TypeTopology.SequentiallyHausdorff.html#2745" class="Bound">X</a> <a id="2747" class="Symbol">:</a> <a id="2749" href="MLTT.Universes.html#3833" class="Generalizable">ğ“¤</a> <a id="2751" href="MLTT.Universes.html#3976" class="Function Operator">Ì‡</a> <a id="2753" class="Symbol">)</a>
 <a id="2756" class="Symbol">â†’</a> <a id="2758" href="TypeTopology.TotallySeparated.html#3866" class="Function">is-totally-separated</a> <a id="2779" href="TypeTopology.SequentiallyHausdorff.html#2745" class="Bound">X</a>
 <a id="2782" class="Symbol">â†’</a> <a id="2784" href="TypeTopology.SequentiallyHausdorff.html#914" class="Function">is-sequentially-Hausdorff</a> <a id="2810" href="TypeTopology.SequentiallyHausdorff.html#2745" class="Bound">X</a>
<a id="2812" href="TypeTopology.SequentiallyHausdorff.html#2680" class="Function">totally-separated-types-are-sequentially-Hausdorff</a> <a id="2863" href="TypeTopology.SequentiallyHausdorff.html#2863" class="Bound">nwlpo</a> <a id="2869" href="TypeTopology.SequentiallyHausdorff.html#2869" class="Bound">X</a> <a id="2871" href="TypeTopology.SequentiallyHausdorff.html#2871" class="Bound">X-is-ts</a> <a id="2879" href="TypeTopology.SequentiallyHausdorff.html#2879" class="Bound">f</a> <a id="2881" href="TypeTopology.SequentiallyHausdorff.html#2881" class="Bound">g</a> <a id="2883" href="TypeTopology.SequentiallyHausdorff.html#2883" class="Bound">a</a> <a id="2885" class="Symbol">=</a> <a id="2887" href="TypeTopology.SequentiallyHausdorff.html#3250" class="Function">II</a>
 <a id="2891" class="Keyword">where</a>
  <a id="2899" href="TypeTopology.SequentiallyHausdorff.html#2899" class="Function">I</a> <a id="2901" class="Symbol">:</a> <a id="2903" class="Symbol">(</a><a id="2904" href="TypeTopology.SequentiallyHausdorff.html#2904" class="Bound">p</a> <a id="2906" class="Symbol">:</a> <a id="2908" href="TypeTopology.SequentiallyHausdorff.html#2869" class="Bound">X</a> <a id="2910" class="Symbol">â†’</a> <a id="2912" href="MLTT.Two.html#124" class="Datatype">ğŸš</a><a id="2913" class="Symbol">)</a> <a id="2915" class="Symbol">â†’</a> <a id="2917" href="TypeTopology.SequentiallyHausdorff.html#2904" class="Bound">p</a> <a id="2919" class="Symbol">(</a><a id="2920" href="TypeTopology.SequentiallyHausdorff.html#2879" class="Bound">f</a> <a id="2922" href="CoNaturals.GenericConvergentSequence.html#6051" class="Function">âˆ</a><a id="2923" class="Symbol">)</a> <a id="2925" href="MLTT.Id.html#193" class="Datatype Operator">ï¼</a> <a id="2927" href="TypeTopology.SequentiallyHausdorff.html#2904" class="Bound">p</a> <a id="2929" class="Symbol">(</a><a id="2930" href="TypeTopology.SequentiallyHausdorff.html#2881" class="Bound">g</a> <a id="2932" href="CoNaturals.GenericConvergentSequence.html#6051" class="Function">âˆ</a><a id="2933" class="Symbol">)</a>
  <a id="2937" href="TypeTopology.SequentiallyHausdorff.html#2899" class="Function">I</a> <a id="2939" href="TypeTopology.SequentiallyHausdorff.html#2939" class="Bound">p</a> <a id="2941" class="Symbol">=</a> <a id="2943" href="TypeTopology.SequentiallyHausdorff.html#3174" class="Function">Iâ‚ƒ</a>
   <a id="2949" class="Keyword">where</a>
    <a id="2959" href="TypeTopology.SequentiallyHausdorff.html#2959" class="Function">Iâ‚€</a> <a id="2962" class="Symbol">:</a> <a id="2964" class="Symbol">(</a><a id="2965" href="TypeTopology.SequentiallyHausdorff.html#2965" class="Bound">n</a> <a id="2967" class="Symbol">:</a> <a id="2969" href="MLTT.Natural-Numbers-Type.html#96" class="Datatype">â„•</a><a id="2970" class="Symbol">)</a> <a id="2972" class="Symbol">â†’</a> <a id="2974" href="TypeTopology.SequentiallyHausdorff.html#2939" class="Bound">p</a> <a id="2976" class="Symbol">(</a><a id="2977" href="TypeTopology.SequentiallyHausdorff.html#2879" class="Bound">f</a> <a id="2979" class="Symbol">(</a><a id="2980" href="Notation.CanonicalMap.html#359" class="Field">Î¹</a> <a id="2982" href="TypeTopology.SequentiallyHausdorff.html#2965" class="Bound">n</a><a id="2983" class="Symbol">))</a> <a id="2986" href="MLTT.Id.html#193" class="Datatype Operator">ï¼</a> <a id="2988" href="TypeTopology.SequentiallyHausdorff.html#2939" class="Bound">p</a> <a id="2990" class="Symbol">(</a><a id="2991" href="TypeTopology.SequentiallyHausdorff.html#2881" class="Bound">g</a> <a id="2993" class="Symbol">(</a><a id="2994" href="Notation.CanonicalMap.html#359" class="Field">Î¹</a> <a id="2996" href="TypeTopology.SequentiallyHausdorff.html#2965" class="Bound">n</a><a id="2997" class="Symbol">))</a>
    <a id="3004" href="TypeTopology.SequentiallyHausdorff.html#2959" class="Function">Iâ‚€</a> <a id="3007" href="TypeTopology.SequentiallyHausdorff.html#3007" class="Bound">n</a> <a id="3009" class="Symbol">=</a> <a id="3011" href="MLTT.Id.html#1136" class="Function">ap</a> <a id="3014" href="TypeTopology.SequentiallyHausdorff.html#2939" class="Bound">p</a> <a id="3016" class="Symbol">(</a><a id="3017" href="TypeTopology.SequentiallyHausdorff.html#2883" class="Bound">a</a> <a id="3019" href="TypeTopology.SequentiallyHausdorff.html#3007" class="Bound">n</a><a id="3020" class="Symbol">)</a>

    <a id="3027" href="TypeTopology.SequentiallyHausdorff.html#3027" class="Function">Iâ‚</a> <a id="3030" class="Symbol">:</a> <a id="3032" href="TypeTopology.SequentiallyHausdorff.html#2939" class="Bound">p</a> <a id="3034" class="Symbol">(</a><a id="3035" href="TypeTopology.SequentiallyHausdorff.html#2879" class="Bound">f</a> <a id="3037" href="CoNaturals.GenericConvergentSequence.html#6051" class="Function">âˆ</a><a id="3038" class="Symbol">)</a> <a id="3040" href="MLTT.Negation.html#635" class="Function Operator">â‰ </a> <a id="3042" href="TypeTopology.SequentiallyHausdorff.html#2939" class="Bound">p</a> <a id="3044" class="Symbol">(</a><a id="3045" href="TypeTopology.SequentiallyHausdorff.html#2881" class="Bound">g</a> <a id="3047" href="CoNaturals.GenericConvergentSequence.html#6051" class="Function">âˆ</a><a id="3048" class="Symbol">)</a> <a id="3050" class="Symbol">â†’</a> <a id="3052" href="Taboos.WLPO.html#1337" class="Function">WLPO</a>
    <a id="3061" href="TypeTopology.SequentiallyHausdorff.html#3027" class="Function">Iâ‚</a> <a id="3064" class="Symbol">=</a> <a id="3066" href="Taboos.BasicDiscontinuity.html#3833" class="Function">disagreement-taboo</a> <a id="3085" class="Symbol">(</a><a id="3086" href="TypeTopology.SequentiallyHausdorff.html#2939" class="Bound">p</a> <a id="3088" href="MLTT.Pi.html#513" class="Function Operator">âˆ˜</a> <a id="3090" href="TypeTopology.SequentiallyHausdorff.html#2879" class="Bound">f</a><a id="3091" class="Symbol">)</a> <a id="3093" class="Symbol">(</a><a id="3094" href="TypeTopology.SequentiallyHausdorff.html#2939" class="Bound">p</a> <a id="3096" href="MLTT.Pi.html#513" class="Function Operator">âˆ˜</a> <a id="3098" href="TypeTopology.SequentiallyHausdorff.html#2881" class="Bound">g</a><a id="3099" class="Symbol">)</a> <a id="3101" href="TypeTopology.SequentiallyHausdorff.html#2959" class="Function">Iâ‚€</a>

    <a id="3109" href="TypeTopology.SequentiallyHausdorff.html#3109" class="Function">Iâ‚‚</a> <a id="3112" class="Symbol">:</a> <a id="3114" href="MLTT.Negation.html#278" class="Function Operator">Â¬</a> <a id="3116" class="Symbol">(</a><a id="3117" href="TypeTopology.SequentiallyHausdorff.html#2939" class="Bound">p</a> <a id="3119" class="Symbol">(</a><a id="3120" href="TypeTopology.SequentiallyHausdorff.html#2879" class="Bound">f</a> <a id="3122" href="CoNaturals.GenericConvergentSequence.html#6051" class="Function">âˆ</a><a id="3123" class="Symbol">)</a> <a id="3125" href="MLTT.Negation.html#635" class="Function Operator">â‰ </a> <a id="3127" href="TypeTopology.SequentiallyHausdorff.html#2939" class="Bound">p</a> <a id="3129" class="Symbol">(</a><a id="3130" href="TypeTopology.SequentiallyHausdorff.html#2881" class="Bound">g</a> <a id="3132" href="CoNaturals.GenericConvergentSequence.html#6051" class="Function">âˆ</a><a id="3133" class="Symbol">))</a>
    <a id="3140" href="TypeTopology.SequentiallyHausdorff.html#3109" class="Function">Iâ‚‚</a> <a id="3143" class="Symbol">=</a> <a id="3145" href="MLTT.Negation.html#1240" class="Function">contrapositive</a> <a id="3160" href="TypeTopology.SequentiallyHausdorff.html#3027" class="Function">Iâ‚</a> <a id="3163" href="TypeTopology.SequentiallyHausdorff.html#2863" class="Bound">nwlpo</a>

    <a id="3174" href="TypeTopology.SequentiallyHausdorff.html#3174" class="Function">Iâ‚ƒ</a> <a id="3177" class="Symbol">:</a> <a id="3179" href="TypeTopology.SequentiallyHausdorff.html#2939" class="Bound">p</a> <a id="3181" class="Symbol">(</a><a id="3182" href="TypeTopology.SequentiallyHausdorff.html#2879" class="Bound">f</a> <a id="3184" href="CoNaturals.GenericConvergentSequence.html#6051" class="Function">âˆ</a><a id="3185" class="Symbol">)</a> <a id="3187" href="MLTT.Id.html#193" class="Datatype Operator">ï¼</a> <a id="3189" href="TypeTopology.SequentiallyHausdorff.html#2939" class="Bound">p</a> <a id="3191" class="Symbol">(</a><a id="3192" href="TypeTopology.SequentiallyHausdorff.html#2881" class="Bound">g</a> <a id="3194" href="CoNaturals.GenericConvergentSequence.html#6051" class="Function">âˆ</a><a id="3195" class="Symbol">)</a>
    <a id="3201" href="TypeTopology.SequentiallyHausdorff.html#3174" class="Function">Iâ‚ƒ</a> <a id="3204" class="Symbol">=</a> <a id="3206" href="UF.DiscreteAndSeparated.html#7010" class="Function">ğŸš-is-Â¬Â¬-separated</a> <a id="3224" class="Symbol">(</a><a id="3225" href="TypeTopology.SequentiallyHausdorff.html#2939" class="Bound">p</a> <a id="3227" class="Symbol">(</a><a id="3228" href="TypeTopology.SequentiallyHausdorff.html#2879" class="Bound">f</a> <a id="3230" href="CoNaturals.GenericConvergentSequence.html#6051" class="Function">âˆ</a><a id="3231" class="Symbol">))</a> <a id="3234" class="Symbol">(</a><a id="3235" href="TypeTopology.SequentiallyHausdorff.html#2939" class="Bound">p</a> <a id="3237" class="Symbol">(</a><a id="3238" href="TypeTopology.SequentiallyHausdorff.html#2881" class="Bound">g</a> <a id="3240" href="CoNaturals.GenericConvergentSequence.html#6051" class="Function">âˆ</a><a id="3241" class="Symbol">))</a> <a id="3244" href="TypeTopology.SequentiallyHausdorff.html#3109" class="Function">Iâ‚‚</a>

  <a id="3250" href="TypeTopology.SequentiallyHausdorff.html#3250" class="Function">II</a> <a id="3253" class="Symbol">:</a> <a id="3255" href="TypeTopology.SequentiallyHausdorff.html#2879" class="Bound">f</a> <a id="3257" href="CoNaturals.GenericConvergentSequence.html#6051" class="Function">âˆ</a> <a id="3259" href="MLTT.Id.html#193" class="Datatype Operator">ï¼</a> <a id="3261" href="TypeTopology.SequentiallyHausdorff.html#2881" class="Bound">g</a> <a id="3263" href="CoNaturals.GenericConvergentSequence.html#6051" class="Function">âˆ</a>
  <a id="3267" href="TypeTopology.SequentiallyHausdorff.html#3250" class="Function">II</a> <a id="3270" class="Symbol">=</a> <a id="3272" href="TypeTopology.SequentiallyHausdorff.html#2871" class="Bound">X-is-ts</a> <a id="3280" href="TypeTopology.SequentiallyHausdorff.html#2899" class="Function">I</a>

<a id="3283" class="Markup">\end{code}</a><a id="3293" class="Background">

There are plenty of totally separated types. For example, the types ğŸš,
â„• and â„•âˆ are totally separated, and the totally separated types are
closed under products (and hence function spaces and more generally
form an exponential ideal) and under retracts, as proved in the above
import TypeTopology.TotallySeparated.

And here is an example of a non-sequentially Hausdorff space, which
was originally offered in the following imported module as an example
of a type which is not totally separated in general. This is the type

    â„•âˆâ‚‚ = Î£ u ê‰ â„•âˆ , (u ï¼ âˆ â†’ ğŸš),

which amounts to â„•âˆ with the point âˆ split into two copies

    âˆâ‚€ = (âˆ , Î» _ â†’ â‚€),
    âˆâ‚ = (âˆ , Î» _ â†’ â‚).

</a><a id="3964" class="Markup">\begin{code}</a>

<a id="3978" class="Keyword">open</a> <a id="3983" class="Keyword">import</a> <a id="3990" href="TypeTopology.FailureOfTotalSeparatedness.html" class="Module">TypeTopology.FailureOfTotalSeparatedness</a> <a id="4031" href="TypeTopology.SequentiallyHausdorff.html#498" class="Bound">feâ‚€</a>

<a id="â„•âˆâ‚‚-is-not-sequentially-Hausdorff"></a><a id="4036" href="TypeTopology.SequentiallyHausdorff.html#4036" class="Function">â„•âˆâ‚‚-is-not-sequentially-Hausdorff</a> <a id="4070" class="Symbol">:</a> <a id="4072" href="MLTT.Negation.html#278" class="Function Operator">Â¬</a> <a id="4074" href="TypeTopology.SequentiallyHausdorff.html#914" class="Function">is-sequentially-Hausdorff</a> <a id="4100" href="TypeTopology.FailureOfTotalSeparatedness.html#2456" class="Function">â„•âˆâ‚‚</a>
<a id="4104" href="TypeTopology.SequentiallyHausdorff.html#4036" class="Function">â„•âˆâ‚‚-is-not-sequentially-Hausdorff</a> <a id="4138" href="TypeTopology.SequentiallyHausdorff.html#4138" class="Bound">h</a> <a id="4140" class="Symbol">=</a> <a id="4142" href="TypeTopology.SequentiallyHausdorff.html#4462" class="Function">III</a>
 <a id="4147" class="Keyword">where</a>
  <a id="4155" href="TypeTopology.SequentiallyHausdorff.html#4155" class="Function">f</a> <a id="4157" href="TypeTopology.SequentiallyHausdorff.html#4157" class="Function">g</a> <a id="4159" class="Symbol">:</a> <a id="4161" href="CoNaturals.GenericConvergentSequence.html#1332" class="Function">â„•âˆ</a> <a id="4164" class="Symbol">â†’</a> <a id="4166" href="TypeTopology.FailureOfTotalSeparatedness.html#2456" class="Function">â„•âˆâ‚‚</a>
  <a id="4172" href="TypeTopology.SequentiallyHausdorff.html#4155" class="Function">f</a> <a id="4174" href="TypeTopology.SequentiallyHausdorff.html#4174" class="Bound">u</a> <a id="4176" class="Symbol">=</a> <a id="4178" href="TypeTopology.SequentiallyHausdorff.html#4174" class="Bound">u</a> <a id="4180" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="4182" class="Symbol">Î»</a> <a id="4184" class="Symbol">(</a><a id="4185" href="TypeTopology.SequentiallyHausdorff.html#4185" class="Bound">e</a> <a id="4187" class="Symbol">:</a> <a id="4189" href="TypeTopology.SequentiallyHausdorff.html#4174" class="Bound">u</a> <a id="4191" href="MLTT.Id.html#193" class="Datatype Operator">ï¼</a> <a id="4193" href="CoNaturals.GenericConvergentSequence.html#6051" class="Function">âˆ</a><a id="4194" class="Symbol">)</a> <a id="4196" class="Symbol">â†’</a> <a id="4198" href="MLTT.Two.html#140" class="InductiveConstructor">â‚€</a>
  <a id="4202" href="TypeTopology.SequentiallyHausdorff.html#4157" class="Function">g</a> <a id="4204" href="TypeTopology.SequentiallyHausdorff.html#4204" class="Bound">u</a> <a id="4206" class="Symbol">=</a> <a id="4208" href="TypeTopology.SequentiallyHausdorff.html#4204" class="Bound">u</a> <a id="4210" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="4212" class="Symbol">Î»</a> <a id="4214" class="Symbol">(</a><a id="4215" href="TypeTopology.SequentiallyHausdorff.html#4215" class="Bound">e</a> <a id="4217" class="Symbol">:</a> <a id="4219" href="TypeTopology.SequentiallyHausdorff.html#4204" class="Bound">u</a> <a id="4221" href="MLTT.Id.html#193" class="Datatype Operator">ï¼</a> <a id="4223" href="CoNaturals.GenericConvergentSequence.html#6051" class="Function">âˆ</a><a id="4224" class="Symbol">)</a> <a id="4226" class="Symbol">â†’</a> <a id="4228" href="MLTT.Two.html#147" class="InductiveConstructor">â‚</a>

  <a id="4233" href="TypeTopology.SequentiallyHausdorff.html#4233" class="Function">I</a> <a id="4235" class="Symbol">:</a> <a id="4237" class="Symbol">(</a><a id="4238" href="TypeTopology.SequentiallyHausdorff.html#4238" class="Bound">n</a> <a id="4240" class="Symbol">:</a> <a id="4242" href="MLTT.Natural-Numbers-Type.html#96" class="Datatype">â„•</a><a id="4243" class="Symbol">)</a> <a id="4245" class="Symbol">â†’</a> <a id="4247" class="Symbol">(Î»</a> <a id="4250" class="Symbol">(</a><a id="4251" href="TypeTopology.SequentiallyHausdorff.html#4251" class="Bound">e</a> <a id="4253" class="Symbol">:</a> <a id="4255" href="Notation.CanonicalMap.html#359" class="Field">Î¹</a> <a id="4257" href="TypeTopology.SequentiallyHausdorff.html#4238" class="Bound">n</a> <a id="4259" href="MLTT.Id.html#193" class="Datatype Operator">ï¼</a> <a id="4261" href="CoNaturals.GenericConvergentSequence.html#6051" class="Function">âˆ</a><a id="4262" class="Symbol">)</a> <a id="4264" class="Symbol">â†’</a> <a id="4266" href="MLTT.Two.html#140" class="InductiveConstructor">â‚€</a><a id="4267" class="Symbol">)</a> <a id="4269" href="MLTT.Id.html#193" class="Datatype Operator">ï¼</a> <a id="4271" class="Symbol">(Î»</a> <a id="4274" class="Symbol">(</a><a id="4275" href="TypeTopology.SequentiallyHausdorff.html#4275" class="Bound">e</a> <a id="4277" class="Symbol">:</a> <a id="4279" href="Notation.CanonicalMap.html#359" class="Field">Î¹</a> <a id="4281" href="TypeTopology.SequentiallyHausdorff.html#4238" class="Bound">n</a> <a id="4283" href="MLTT.Id.html#193" class="Datatype Operator">ï¼</a> <a id="4285" href="CoNaturals.GenericConvergentSequence.html#6051" class="Function">âˆ</a><a id="4286" class="Symbol">)</a> <a id="4288" class="Symbol">â†’</a> <a id="4290" href="MLTT.Two.html#147" class="InductiveConstructor">â‚</a><a id="4291" class="Symbol">)</a>
  <a id="4295" href="TypeTopology.SequentiallyHausdorff.html#4233" class="Function">I</a> <a id="4297" href="TypeTopology.SequentiallyHausdorff.html#4297" class="Bound">n</a> <a id="4299" class="Symbol">=</a> <a id="4301" href="UF.FunExt.html#1242" class="Function">dfunext</a> <a id="4309" href="TypeTopology.SequentiallyHausdorff.html#498" class="Bound">feâ‚€</a> <a id="4313" class="Symbol">(Î»</a> <a id="4316" class="Symbol">(</a><a id="4317" href="TypeTopology.SequentiallyHausdorff.html#4317" class="Bound">e</a> <a id="4319" class="Symbol">:</a> <a id="4321" href="Notation.CanonicalMap.html#359" class="Field">Î¹</a> <a id="4323" href="TypeTopology.SequentiallyHausdorff.html#4297" class="Bound">n</a> <a id="4325" href="MLTT.Id.html#193" class="Datatype Operator">ï¼</a> <a id="4327" href="CoNaturals.GenericConvergentSequence.html#6051" class="Function">âˆ</a><a id="4328" class="Symbol">)</a> <a id="4330" class="Symbol">â†’</a> <a id="4332" href="MLTT.Empty.html#356" class="Function">ğŸ˜-elim</a> <a id="4339" class="Symbol">(</a><a id="4340" href="CoNaturals.GenericConvergentSequence.html#12129" class="Function">âˆ-is-not-finite</a> <a id="4356" href="TypeTopology.SequentiallyHausdorff.html#4297" class="Bound">n</a> <a id="4358" class="Symbol">(</a><a id="4359" href="TypeTopology.SequentiallyHausdorff.html#4317" class="Bound">e</a> <a id="4361" href="MLTT.Id.html#1055" class="Function Operator">â»Â¹</a><a id="4363" class="Symbol">)))</a>

  <a id="4370" href="TypeTopology.SequentiallyHausdorff.html#4370" class="Function">a</a> <a id="4372" class="Symbol">:</a> <a id="4374" class="Symbol">(</a><a id="4375" href="TypeTopology.SequentiallyHausdorff.html#4375" class="Bound">n</a> <a id="4377" class="Symbol">:</a> <a id="4379" href="MLTT.Natural-Numbers-Type.html#96" class="Datatype">â„•</a><a id="4380" class="Symbol">)</a> <a id="4382" class="Symbol">â†’</a> <a id="4384" href="TypeTopology.SequentiallyHausdorff.html#4155" class="Function">f</a> <a id="4386" class="Symbol">(</a><a id="4387" href="Notation.CanonicalMap.html#359" class="Field">Î¹</a> <a id="4389" href="TypeTopology.SequentiallyHausdorff.html#4375" class="Bound">n</a><a id="4390" class="Symbol">)</a> <a id="4392" href="MLTT.Id.html#193" class="Datatype Operator">ï¼</a> <a id="4394" href="TypeTopology.SequentiallyHausdorff.html#4157" class="Function">g</a> <a id="4396" class="Symbol">(</a><a id="4397" href="Notation.CanonicalMap.html#359" class="Field">Î¹</a> <a id="4399" href="TypeTopology.SequentiallyHausdorff.html#4375" class="Bound">n</a><a id="4400" class="Symbol">)</a>
  <a id="4404" href="TypeTopology.SequentiallyHausdorff.html#4370" class="Function">a</a> <a id="4406" href="TypeTopology.SequentiallyHausdorff.html#4406" class="Bound">n</a> <a id="4408" class="Symbol">=</a> <a id="4410" href="MLTT.Id.html#1136" class="Function">ap</a> <a id="4413" class="Symbol">(</a><a id="4414" href="Notation.CanonicalMap.html#359" class="Field">Î¹</a> <a id="4416" href="TypeTopology.SequentiallyHausdorff.html#4406" class="Bound">n</a> <a id="4418" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,_</a><a id="4420" class="Symbol">)</a> <a id="4422" class="Symbol">(</a><a id="4423" href="TypeTopology.SequentiallyHausdorff.html#4233" class="Function">I</a> <a id="4425" href="TypeTopology.SequentiallyHausdorff.html#4406" class="Bound">n</a><a id="4426" class="Symbol">)</a>

  <a id="4431" href="TypeTopology.SequentiallyHausdorff.html#4431" class="Function">II</a> <a id="4434" class="Symbol">:</a> <a id="4436" href="TypeTopology.FailureOfTotalSeparatedness.html#2497" class="Function">âˆâ‚€</a> <a id="4439" href="MLTT.Id.html#193" class="Datatype Operator">ï¼</a> <a id="4441" href="TypeTopology.FailureOfTotalSeparatedness.html#2526" class="Function">âˆâ‚</a>
  <a id="4446" href="TypeTopology.SequentiallyHausdorff.html#4431" class="Function">II</a> <a id="4449" class="Symbol">=</a> <a id="4451" href="TypeTopology.SequentiallyHausdorff.html#4138" class="Bound">h</a> <a id="4453" href="TypeTopology.SequentiallyHausdorff.html#4155" class="Function">f</a> <a id="4455" href="TypeTopology.SequentiallyHausdorff.html#4157" class="Function">g</a> <a id="4457" href="TypeTopology.SequentiallyHausdorff.html#4370" class="Function">a</a>

  <a id="4462" href="TypeTopology.SequentiallyHausdorff.html#4462" class="Function">III</a> <a id="4466" class="Symbol">:</a> <a id="4468" href="MLTT.Empty-Type.html#121" class="Datatype">ğŸ˜</a>
  <a id="4472" href="TypeTopology.SequentiallyHausdorff.html#4462" class="Function">III</a> <a id="4476" class="Symbol">=</a> <a id="4478" href="TypeTopology.FailureOfTotalSeparatedness.html#4369" class="Function">âˆâ‚€-and-âˆâ‚-different</a> <a id="4498" href="TypeTopology.SequentiallyHausdorff.html#4431" class="Function">II</a>

<a id="4502" class="Markup">\end{code}</a><a id="4512" class="Background">

The following was already proved in TypeTopology.FailureOfTotalSeparatedness.

</a><a id="4593" class="Markup">\begin{code}</a>

<a id="â„•âˆâ‚‚-is-not-totally-separated-in-topological-toposes"></a><a id="4607" href="TypeTopology.SequentiallyHausdorff.html#4607" class="Function">â„•âˆâ‚‚-is-not-totally-separated-in-topological-toposes</a>
 <a id="4660" class="Symbol">:</a> <a id="4662" href="MLTT.Negation.html#278" class="Function Operator">Â¬</a> <a id="4664" href="Taboos.WLPO.html#1337" class="Function">WLPO</a>
 <a id="4670" class="Symbol">â†’</a> <a id="4672" href="MLTT.Negation.html#278" class="Function Operator">Â¬</a> <a id="4674" href="TypeTopology.TotallySeparated.html#3866" class="Function">is-totally-separated</a> <a id="4695" href="TypeTopology.FailureOfTotalSeparatedness.html#2456" class="Function">â„•âˆâ‚‚</a>
<a id="4699" href="TypeTopology.SequentiallyHausdorff.html#4607" class="Function">â„•âˆâ‚‚-is-not-totally-separated-in-topological-toposes</a> <a id="4751" href="TypeTopology.SequentiallyHausdorff.html#4751" class="Bound">nwlpo</a> <a id="4757" href="TypeTopology.SequentiallyHausdorff.html#4757" class="Bound">ts</a> <a id="4760" class="Symbol">=</a>
 <a id="4763" href="TypeTopology.SequentiallyHausdorff.html#4036" class="Function">â„•âˆâ‚‚-is-not-sequentially-Hausdorff</a>
  <a id="4799" class="Symbol">(</a><a id="4800" href="TypeTopology.SequentiallyHausdorff.html#2680" class="Function">totally-separated-types-are-sequentially-Hausdorff</a> <a id="4851" href="TypeTopology.SequentiallyHausdorff.html#4751" class="Bound">nwlpo</a> <a id="4857" href="TypeTopology.FailureOfTotalSeparatedness.html#2456" class="Function">â„•âˆâ‚‚</a> <a id="4861" href="TypeTopology.SequentiallyHausdorff.html#4757" class="Bound">ts</a><a id="4863" class="Symbol">)</a>

<a id="4866" class="Markup">\end{code}</a><a id="4876" class="Background">

The proof given here is the same, but factored in two steps, by
considering sequentially Hausdorff spaces as an intermediate step.
</a></pre></body></html>