-- Let's define equality of natural numbers ourselves.

data â„• : Set where
 zero : â„•
 succ : â„• â†’ â„•

_+_ : â„• â†’ â„• â†’ â„•
m + zero   = m
m + succ n = succ(m + n)

induction : {A : â„• â†’ Set}
         â†’  A zero
         â†’ (âˆ€(k : â„•) â†’ A k â†’ A(succ k))
         â†’ âˆ€(n : â„•) â†’ A n 
induction base step zero     = base
induction base step (succ n) = step n (induction base step n)

-- Set with no elements (empty set):
data ğŸ˜ : Set where
  -- nothing here

from-ğŸ˜ : {X : Set} â†’ ğŸ˜ â†’ X
from-ğŸ˜ () -- nothing available in ğŸ˜ to map to X (it would have been
          -- better if Agda allowed us to write an empty set of
          -- equations)

-- Set with one element (unit type):
data ğŸ™ : Set where
 â‹† : ğŸ™

infix 3 _â‰£_

_â‰£_ : â„• â†’ â„• â†’ Set
zero     â‰£ zero     = ğŸ™ 
(succ m) â‰£ zero     = ğŸ˜ 
zero     â‰£ (succ n) = ğŸ˜ 
(succ m) â‰£ (succ n) = m â‰£ n 

Reflâ„• : âˆ€ n â†’ n â‰£ n
Reflâ„• zero     = â‹†
Reflâ„• (succ n) = IH
 -- Notice that we needed to inhabit the set ((succ m) â‰£ (succ n))
 -- in this case, but we instead inhabited the set (m â‰£ n) using IH.
 -- This works because, by definition, ((succ m) â‰£ (succ n)) is (m â‰£ n).
 where
  IH : n â‰£ n
  IH = Reflâ„• n

-- We next show that _â‰£_ is the least reflexive relation on â„•:

weak-Jâ„• : (A : â„• â†’ â„• â†’ Set) â†’ (âˆ€ n â†’ A n n) â†’  âˆ€ m n â†’ m â‰£ n â†’ A m n
weak-Jâ„• A Ï† zero zero â‹† = Ï† zero
weak-Jâ„• A Ï† zero (succ n) ()
weak-Jâ„• A Ï† (succ m) zero ()
weak-Jâ„• A Ï† (succ m) (succ n) e = weak-Jâ„• A' Ï†' m n e
 where
  A' : â„• â†’ â„• â†’ Set 
  A' m n = A (succ m) (succ n)
  Ï†' : âˆ€ n â†’ A' n n
  Ï†' n = Ï†(succ n)

-- If you don't like "()" patterns (I don't), you can use the function
-- from-ğŸ˜ (defined above using "()" patterns):

weak-Jâ„•' : (A : â„• â†’ â„• â†’ Set) â†’ (âˆ€ n â†’ A n n) â†’  âˆ€ m n â†’ m â‰£ n â†’ A m n
weak-Jâ„•' A Ï† zero     zero â‹†     = Ï† zero
weak-Jâ„•' A Ï† zero    (succ n) e  = from-ğŸ˜ e
weak-Jâ„•' A Ï† (succ m) zero e     = from-ğŸ˜ e
weak-Jâ„•' A Ï† (succ m) (succ n) e = weak-Jâ„•' (Î» m n â†’ A(succ m)(succ n))
                                             (Î» n â†’ Ï†(succ n))
                                             m n e

-- There is a stronger, dependent(ly typed) version of weak-Jâ„•:

Jâ„• : (A : (m n : â„•) â†’ m â‰£ n â†’ Set)

   â†’ (âˆ€ n â†’ A n n (Reflâ„• n)) â†’  âˆ€ m n â†’ âˆ€(e : m â‰£ n) â†’ A m n e

Jâ„• A Ï† zero     zero â‹†     = Ï† zero
Jâ„• A Ï† zero    (succ n) ()
Jâ„• A Ï† (succ m) zero ()
Jâ„• A Ï† (succ m) (succ n) e = Jâ„• (Î» m n â†’ A (succ m) (succ n))
                                 (Î» n â†’ Ï†(succ n))
                                 m n e

-- Of course we could have defined instead the weak version from the
-- strong one:

weak-Jâ„•'' : (A : (m n : â„•) â†’ Set) â†’ (âˆ€ n â†’ A n n) â†’  âˆ€ m n â†’ m â‰£ n â†’ A m n
weak-Jâ„•'' A = Jâ„• (Î» m n e â†’ A m n) 

-- Jâ„• can be regarded as an induction principle for equality on the
-- type â„•. Several properties of â‰£ can be proved using J without
-- reference to the inductive structure of the the type â„•, and often
-- its weak version suffices.

symâ„• : (x y : â„•) â†’ x â‰£ y â†’ y â‰£ x
symâ„• = weak-Jâ„• A Ï† 
 where
  A : (x y : â„•) â†’ Set
  A x y = y â‰£ x
  Ï† : (x : â„•) â†’ x â‰£ x
  Ï† = Reflâ„•

transâ„• : (x y z : â„•) â†’ x â‰£ y â†’ y â‰£ z â†’ x â‰£ z
transâ„• x y z r s =  transâ„•' x y r z s
 where
  transâ„•' : (x y : â„•) â†’ x â‰£ y â†’ (z : â„•) â†’ y â‰£ z â†’ x â‰£ z
  transâ„•' = weak-Jâ„• A Ï†
   where
    A : (x y : â„•) â†’ Set
    A x y = âˆ€(z : â„•) â†’ y â‰£ z â†’ x â‰£ z
    Ï† : (x : â„•) â†’ A x x 
    Ï† x z s = s

substâ„• : (P : â„• â†’ Set) â†’ (x y : â„•) â†’ x â‰£ y â†’ P x â†’ P y
substâ„• P = weak-Jâ„• A Ï†
 where
  A : (x y : â„•) â†’ Set
  A x y = P x â†’ P y
  Ï† : (x : â„•) â†’ A x x 
  Ï† x p = p

-- Transitivity can be proved using substitution:

sym-transâ„• : (x y z : â„•) â†’ x â‰£ y â†’ x â‰£ z â†’ y â‰£ z
sym-transâ„• x y z = rearrange z x y
 where
  rearrange : (z x y : â„•) â†’ x â‰£ y â†’ x â‰£ z â†’ y â‰£ z
  rearrange z = substâ„• (Î» x â†’ x â‰£ z)

transâ„•' : (x y z : â„•) â†’ x â‰£ y â†’ y â‰£ z â†’ x â‰£ z
transâ„•' x y z r s = sym-transâ„• y x z (symâ„• x y r) s

congâ„•â†’â„• : (f : â„• â†’ â„•) â†’ (x x' : â„•) â†’ x â‰£ x' â†’ f x â‰£ f x'
congâ„•â†’â„• f = weak-Jâ„• (Î» x x' â†’ f x â‰£ f x') (Î» x â†’ Reflâ„•(f x)) 

-- As another example, we show that addition is commutative:

zero-is-left-neutral : âˆ€ n â†’ zero + n â‰£ n 
zero-is-left-neutral zero = â‹†
zero-is-left-neutral (succ n) = IH  
             -- We need to inhabit the type (zero + succ n â‰£ succ n). 
             -- Expanding the definitions,
             --   (zero + succ n â‰£ succ n) = 
             --   (succ(zero + n) â‰£ succ n) = 
             --   (zero + n â‰£ n).
             -- Here "=" is definitional equality, silently applied by Agda.
 where
  IH : zero + n â‰£ n
  IH = zero-is-left-neutral n

-- Equivalently:

zero-is-left-neutral' : âˆ€ n â†’ zero + n â‰£ n
zero-is-left-neutral' = induction base step
 where
  base : ğŸ™
  base = â‹†
  step : âˆ€ n â†’ zero + n â‰£ n â†’ zero + n â‰£ n
  step n e = e

-- This with the following shows that, of course, it is equivalent to
-- define addition by induction on the first argument. The proof is by
-- induction on the second argument, following the definition of _+_.

addition-on-first : âˆ€ m n â†’ (succ m) + n â‰£ succ(m + n)
addition-on-first m zero = Reflâ„• m
addition-on-first m (succ n) = IH
 where
  IH : succ m + n â‰£ succ(m + n)
  IH = addition-on-first m n

-- Because the situation is symmetric, we can choose any of the two
-- arguments to perform the induction in the following theorem:

addition-commutative : âˆ€ m n â†’ m + n â‰£ n + m
addition-commutative zero n = zero-is-left-neutral n 
addition-commutative (succ m) n = lemma
 where
  IH : m + n â‰£ n + m
  IH = addition-commutative m n
  claim : succ(m + n) â‰£ succ(n + m)
  claim = congâ„•â†’â„• succ (m + n) (n + m) IH
  lemma : succ m + n â‰£ succ (n + m)
  lemma = transâ„• (succ m + n) (succ(m + n)) (succ (n + m)) 
                 (addition-on-first m n) claim


-- The above theorem "Jâ„•" motivates Martin-LÃ¶f's inductive definition
-- of the identity type for any type X:

data _â‰¡_ {X : Set} : X â†’ X â†’ Set where
 refl : {x : X} â†’ x â‰¡ x

-- Martin-LÃ¶f's notation is the following:

Id : (X : Set) â†’ X â†’ X â†’ Set
Id X x y  =  x â‰¡ y

Refl : (X : Set) â†’ (x : X) â†’ Id X x x
Refl X x = refl {X} {x}

-- The induction principle is as for equality on â„• defined earlier:

J : {X : Set} â†’ (A : (x x' : X) â†’ x â‰¡ x' â†’ Set)

   â†’ (âˆ€(x : X) â†’ A x x refl) â†’  âˆ€{x x' : X} â†’ âˆ€(r : x â‰¡ x') â†’ A x x' r

J A f {x} refl = f x

-- The propositional "axiom of function extensionality" is

Extensionality : Setâ‚
Extensionality = 
 âˆ€(X Y : Set) â†’ âˆ€(f g : X â†’ Y) â†’ (âˆ€(x : X) â†’ f x â‰¡ g x) â†’ f â‰¡ g

-- This is not provable or disprovable.
