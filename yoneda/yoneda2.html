<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>yoneda2</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Background">Martin Escardo, 8 May 2015. 

This is a follow-up of 
http://www.cs.bham.ac.uk/~mhe/yoneda/yoneda.html

You should read that first. 

We use pattern matching on identity types to define J only.  

This time we define Yoneda from J rather than from pattern matching.

I wondered whether if we chase the construction

  J ↦ Yoneda ↦ new-J

we get the same J definitionally. We don&#39;t. But we still get that the
new J satisfies its computation rule definitionally.

</a><a id="463" class="Markup">\begin{code}</a>

<a id="477" class="Symbol">{-#</a> <a id="481" class="Keyword">OPTIONS</a> <a id="489" class="Pragma">--without-K</a> <a id="501" class="Symbol">#-}</a>

<a id="U"></a><a id="506" href="yoneda2.html#506" class="Function">U</a> <a id="508" class="Symbol">=</a> <a id="510" class="PrimitiveType">Set</a>
<a id="U₁"></a><a id="514" href="yoneda2.html#514" class="Function">U₁</a> <a id="517" class="Symbol">=</a> <a id="519" class="PrimitiveType">Set₁</a>

<a id="525" class="Keyword">data</a> <a id="Id"></a><a id="530" href="yoneda2.html#530" class="Datatype">Id</a> <a id="533" class="Symbol">{</a><a id="534" href="yoneda2.html#534" class="Bound">X</a> <a id="536" class="Symbol">:</a> <a id="538" href="yoneda2.html#506" class="Function">U</a><a id="539" class="Symbol">}</a> <a id="541" class="Symbol">:</a> <a id="543" href="yoneda2.html#534" class="Bound">X</a> <a id="545" class="Symbol">→</a> <a id="547" href="yoneda2.html#534" class="Bound">X</a> <a id="549" class="Symbol">→</a> <a id="551" href="yoneda2.html#506" class="Function">U</a> <a id="553" class="Keyword">where</a>
 <a id="Id.refl"></a><a id="560" href="yoneda2.html#560" class="InductiveConstructor">refl</a> <a id="565" class="Symbol">:</a> <a id="567" class="Symbol">(</a><a id="568" href="yoneda2.html#568" class="Bound">x</a> <a id="570" class="Symbol">:</a> <a id="572" href="yoneda2.html#534" class="Bound">X</a><a id="573" class="Symbol">)</a> <a id="575" class="Symbol">→</a> <a id="577" href="yoneda2.html#530" class="Datatype">Id</a> <a id="580" href="yoneda2.html#568" class="Bound">x</a> <a id="582" href="yoneda2.html#568" class="Bound">x</a> 

<a id="_≡_"></a><a id="586" href="yoneda2.html#586" class="Function Operator">_≡_</a> <a id="590" class="Symbol">:</a> <a id="592" class="Symbol">{</a><a id="593" href="yoneda2.html#593" class="Bound">X</a> <a id="595" class="Symbol">:</a> <a id="597" href="yoneda2.html#506" class="Function">U</a><a id="598" class="Symbol">}</a> <a id="600" class="Symbol">→</a> <a id="602" href="yoneda2.html#593" class="Bound">X</a> <a id="604" class="Symbol">→</a> <a id="606" href="yoneda2.html#593" class="Bound">X</a> <a id="608" class="Symbol">→</a> <a id="610" href="yoneda2.html#506" class="Function">U</a>
<a id="612" href="yoneda2.html#612" class="Bound">x</a> <a id="614" href="yoneda2.html#586" class="Function Operator">≡</a> <a id="616" href="yoneda2.html#616" class="Bound">y</a> <a id="618" class="Symbol">=</a> <a id="620" href="yoneda2.html#530" class="Datatype">Id</a> <a id="623" href="yoneda2.html#612" class="Bound">x</a> <a id="625" href="yoneda2.html#616" class="Bound">y</a>

<a id="J"></a><a id="628" href="yoneda2.html#628" class="Function">J</a> <a id="630" class="Symbol">:</a> <a id="632" class="Symbol">(</a><a id="633" href="yoneda2.html#633" class="Bound">X</a> <a id="635" class="Symbol">:</a> <a id="637" href="yoneda2.html#506" class="Function">U</a><a id="638" class="Symbol">)</a> <a id="640" class="Symbol">(</a><a id="641" href="yoneda2.html#641" class="Bound">x</a> <a id="643" class="Symbol">:</a> <a id="645" href="yoneda2.html#633" class="Bound">X</a><a id="646" class="Symbol">)</a> <a id="648" class="Symbol">(</a><a id="649" href="yoneda2.html#649" class="Bound">B</a> <a id="651" class="Symbol">:</a> <a id="653" class="Symbol">(</a><a id="654" href="yoneda2.html#654" class="Bound">y</a> <a id="656" class="Symbol">:</a> <a id="658" href="yoneda2.html#633" class="Bound">X</a><a id="659" class="Symbol">)</a> <a id="661" class="Symbol">→</a> <a id="663" href="yoneda2.html#641" class="Bound">x</a> <a id="665" href="yoneda2.html#586" class="Function Operator">≡</a> <a id="667" href="yoneda2.html#654" class="Bound">y</a> <a id="669" class="Symbol">→</a> <a id="671" href="yoneda2.html#506" class="Function">U</a><a id="672" class="Symbol">)</a>
  <a id="676" class="Symbol">→</a> <a id="678" href="yoneda2.html#649" class="Bound">B</a> <a id="680" href="yoneda2.html#641" class="Bound">x</a> <a id="682" class="Symbol">(</a><a id="683" href="yoneda2.html#560" class="InductiveConstructor">refl</a> <a id="688" href="yoneda2.html#641" class="Bound">x</a><a id="689" class="Symbol">)</a> <a id="691" class="Symbol">→</a> <a id="693" class="Symbol">(</a><a id="694" href="yoneda2.html#694" class="Bound">y</a> <a id="696" class="Symbol">:</a> <a id="698" href="yoneda2.html#633" class="Bound">X</a><a id="699" class="Symbol">)</a> <a id="701" class="Symbol">(</a><a id="702" href="yoneda2.html#702" class="Bound">p</a> <a id="704" class="Symbol">:</a> <a id="706" href="yoneda2.html#641" class="Bound">x</a> <a id="708" href="yoneda2.html#586" class="Function Operator">≡</a> <a id="710" href="yoneda2.html#694" class="Bound">y</a><a id="711" class="Symbol">)</a> <a id="713" class="Symbol">→</a> <a id="715" href="yoneda2.html#649" class="Bound">B</a> <a id="717" href="yoneda2.html#694" class="Bound">y</a> <a id="719" href="yoneda2.html#702" class="Bound">p</a>
<a id="721" href="yoneda2.html#628" class="Function">J</a> <a id="723" href="yoneda2.html#723" class="Bound">X</a> <a id="725" href="yoneda2.html#725" class="Bound">x</a> <a id="727" href="yoneda2.html#727" class="Bound">B</a> <a id="729" href="yoneda2.html#729" class="Bound">b</a> <a id="731" class="DottedPattern Symbol">.</a><a id="732" href="yoneda2.html#725" class="DottedPattern Bound">x</a> <a id="734" class="Symbol">(</a><a id="735" href="yoneda2.html#560" class="InductiveConstructor">refl</a> <a id="740" class="DottedPattern Symbol">.</a><a id="741" href="yoneda2.html#725" class="DottedPattern Bound">x</a><a id="742" class="Symbol">)</a> <a id="744" class="Symbol">=</a> <a id="746" href="yoneda2.html#729" class="Bound">b</a>

<a id="PrShf"></a><a id="749" href="yoneda2.html#749" class="Function">PrShf</a> <a id="755" class="Symbol">:</a> <a id="757" href="yoneda2.html#506" class="Function">U</a> <a id="759" class="Symbol">→</a> <a id="761" href="yoneda2.html#514" class="Function">U₁</a>
<a id="764" href="yoneda2.html#749" class="Function">PrShf</a> <a id="770" href="yoneda2.html#770" class="Bound">X</a> <a id="772" class="Symbol">=</a> <a id="774" class="Symbol">(</a><a id="775" href="yoneda2.html#770" class="Bound">X</a> <a id="777" class="Symbol">→</a> <a id="779" href="yoneda2.html#506" class="Function">U</a><a id="780" class="Symbol">)</a>

<a id="Nat"></a><a id="783" href="yoneda2.html#783" class="Function">Nat</a> <a id="787" class="Symbol">:</a> <a id="789" class="Symbol">{</a><a id="790" href="yoneda2.html#790" class="Bound">X</a> <a id="792" class="Symbol">:</a> <a id="794" href="yoneda2.html#506" class="Function">U</a><a id="795" class="Symbol">}</a> <a id="797" class="Symbol">→</a> <a id="799" href="yoneda2.html#749" class="Function">PrShf</a> <a id="805" href="yoneda2.html#790" class="Bound">X</a> <a id="807" class="Symbol">→</a> <a id="809" href="yoneda2.html#749" class="Function">PrShf</a> <a id="815" href="yoneda2.html#790" class="Bound">X</a> <a id="817" class="Symbol">→</a> <a id="819" href="yoneda2.html#506" class="Function">U</a>
<a id="821" href="yoneda2.html#783" class="Function">Nat</a> <a id="825" class="Symbol">{</a><a id="826" href="yoneda2.html#826" class="Bound">X</a><a id="827" class="Symbol">}</a> <a id="829" href="yoneda2.html#829" class="Bound">A</a> <a id="831" href="yoneda2.html#831" class="Bound">B</a> <a id="833" class="Symbol">=</a> <a id="835" class="Symbol">{</a><a id="836" href="yoneda2.html#836" class="Bound">x</a> <a id="838" class="Symbol">:</a> <a id="840" href="yoneda2.html#826" class="Bound">X</a><a id="841" class="Symbol">}</a> <a id="843" class="Symbol">→</a> <a id="845" href="yoneda2.html#829" class="Bound">A</a> <a id="847" href="yoneda2.html#836" class="Bound">x</a> <a id="849" class="Symbol">→</a> <a id="851" href="yoneda2.html#831" class="Bound">B</a> <a id="853" href="yoneda2.html#836" class="Bound">x</a>

<a id="yoneda-elem"></a><a id="856" href="yoneda2.html#856" class="Function">yoneda-elem</a> <a id="868" class="Symbol">:</a> <a id="870" class="Symbol">{</a><a id="871" href="yoneda2.html#871" class="Bound">X</a> <a id="873" class="Symbol">:</a> <a id="875" href="yoneda2.html#506" class="Function">U</a><a id="876" class="Symbol">}</a> <a id="878" class="Symbol">{</a><a id="879" href="yoneda2.html#879" class="Bound">x</a> <a id="881" class="Symbol">:</a> <a id="883" href="yoneda2.html#871" class="Bound">X</a><a id="884" class="Symbol">}</a> <a id="886" class="Symbol">{</a><a id="887" href="yoneda2.html#887" class="Bound">A</a> <a id="889" class="Symbol">:</a> <a id="891" href="yoneda2.html#749" class="Function">PrShf</a> <a id="897" href="yoneda2.html#871" class="Bound">X</a><a id="898" class="Symbol">}</a>
            <a id="912" class="Symbol">→</a> <a id="914" href="yoneda2.html#783" class="Function">Nat</a> <a id="918" class="Symbol">(</a><a id="919" href="yoneda2.html#530" class="Datatype">Id</a> <a id="922" href="yoneda2.html#879" class="Bound">x</a><a id="923" class="Symbol">)</a> <a id="925" href="yoneda2.html#887" class="Bound">A</a> <a id="927" class="Symbol">→</a> <a id="929" href="yoneda2.html#887" class="Bound">A</a> <a id="931" href="yoneda2.html#879" class="Bound">x</a>
<a id="933" href="yoneda2.html#856" class="Function">yoneda-elem</a> <a id="945" class="Symbol">{</a><a id="946" href="yoneda2.html#946" class="Bound">X</a><a id="947" class="Symbol">}</a> <a id="949" class="Symbol">{</a><a id="950" href="yoneda2.html#950" class="Bound">x</a><a id="951" class="Symbol">}</a> <a id="953" href="yoneda2.html#953" class="Bound">η</a> <a id="955" class="Symbol">=</a> <a id="957" href="yoneda2.html#953" class="Bound">η</a><a id="958" class="Symbol">(</a><a id="959" href="yoneda2.html#560" class="InductiveConstructor">refl</a> <a id="964" href="yoneda2.html#950" class="Bound">x</a><a id="965" class="Symbol">)</a>

<a id="yoneda-nat"></a><a id="968" href="yoneda2.html#968" class="Function">yoneda-nat</a> <a id="979" class="Symbol">:</a> <a id="981" class="Symbol">{</a><a id="982" href="yoneda2.html#982" class="Bound">X</a> <a id="984" class="Symbol">:</a> <a id="986" href="yoneda2.html#506" class="Function">U</a><a id="987" class="Symbol">}</a> <a id="989" class="Symbol">{</a><a id="990" href="yoneda2.html#990" class="Bound">x</a> <a id="992" class="Symbol">:</a> <a id="994" href="yoneda2.html#982" class="Bound">X</a><a id="995" class="Symbol">}</a> <a id="997" class="Symbol">{</a><a id="998" href="yoneda2.html#998" class="Bound">A</a> <a id="1000" class="Symbol">:</a> <a id="1002" href="yoneda2.html#749" class="Function">PrShf</a> <a id="1008" href="yoneda2.html#982" class="Bound">X</a><a id="1009" class="Symbol">}</a> 
           <a id="1023" class="Symbol">→</a> <a id="1025" href="yoneda2.html#998" class="Bound">A</a> <a id="1027" href="yoneda2.html#990" class="Bound">x</a> <a id="1029" class="Symbol">→</a> <a id="1031" href="yoneda2.html#783" class="Function">Nat</a> <a id="1035" class="Symbol">(</a><a id="1036" href="yoneda2.html#530" class="Datatype">Id</a> <a id="1039" href="yoneda2.html#990" class="Bound">x</a><a id="1040" class="Symbol">)</a> <a id="1042" href="yoneda2.html#998" class="Bound">A</a> 
<a id="1045" href="yoneda2.html#968" class="Function">yoneda-nat</a> <a id="1056" class="Symbol">{</a><a id="1057" href="yoneda2.html#1057" class="Bound">X</a><a id="1058" class="Symbol">}</a> <a id="1060" class="Symbol">{</a><a id="1061" href="yoneda2.html#1061" class="Bound">x</a><a id="1062" class="Symbol">}</a> <a id="1064" class="Symbol">{</a><a id="1065" href="yoneda2.html#1065" class="Bound">A</a><a id="1066" class="Symbol">}</a> <a id="1068" href="yoneda2.html#1068" class="Bound">a</a> <a id="1070" class="Symbol">{</a><a id="1071" href="yoneda2.html#1071" class="Bound">y</a><a id="1072" class="Symbol">}</a> <a id="1074" class="Symbol">=</a> <a id="1076" href="yoneda2.html#628" class="Function">J</a> <a id="1078" href="yoneda2.html#1057" class="Bound">X</a> <a id="1080" href="yoneda2.html#1061" class="Bound">x</a> <a id="1082" class="Symbol">(λ</a> <a id="1085" href="yoneda2.html#1085" class="Bound">y</a> <a id="1087" href="yoneda2.html#1087" class="Bound">p</a> <a id="1089" class="Symbol">→</a> <a id="1091" href="yoneda2.html#1065" class="Bound">A</a> <a id="1093" href="yoneda2.html#1085" class="Bound">y</a><a id="1094" class="Symbol">)</a> <a id="1096" href="yoneda2.html#1068" class="Bound">a</a> <a id="1098" href="yoneda2.html#1071" class="Bound">y</a>

<a id="transport"></a><a id="1101" href="yoneda2.html#1101" class="Function">transport</a> <a id="1111" class="Symbol">:</a> <a id="1113" class="Symbol">{</a><a id="1114" href="yoneda2.html#1114" class="Bound">X</a> <a id="1116" class="Symbol">:</a> <a id="1118" href="yoneda2.html#506" class="Function">U</a><a id="1119" class="Symbol">}</a> <a id="1121" class="Symbol">(</a><a id="1122" href="yoneda2.html#1122" class="Bound">A</a> <a id="1124" class="Symbol">:</a> <a id="1126" href="yoneda2.html#1114" class="Bound">X</a> <a id="1128" class="Symbol">→</a> <a id="1130" href="yoneda2.html#506" class="Function">U</a><a id="1131" class="Symbol">)</a> <a id="1133" class="Symbol">{</a><a id="1134" href="yoneda2.html#1134" class="Bound">x</a> <a id="1136" href="yoneda2.html#1136" class="Bound">y</a> <a id="1138" class="Symbol">:</a> <a id="1140" href="yoneda2.html#1114" class="Bound">X</a><a id="1141" class="Symbol">}</a>
          <a id="1153" class="Symbol">→</a> <a id="1155" href="yoneda2.html#1134" class="Bound">x</a> <a id="1157" href="yoneda2.html#586" class="Function Operator">≡</a> <a id="1159" href="yoneda2.html#1136" class="Bound">y</a> <a id="1161" class="Symbol">→</a> <a id="1163" href="yoneda2.html#1122" class="Bound">A</a> <a id="1165" href="yoneda2.html#1134" class="Bound">x</a> <a id="1167" class="Symbol">→</a> <a id="1169" href="yoneda2.html#1122" class="Bound">A</a> <a id="1171" href="yoneda2.html#1136" class="Bound">y</a>
<a id="1173" href="yoneda2.html#1101" class="Function">transport</a> <a id="1183" class="Symbol">{</a><a id="1184" href="yoneda2.html#1184" class="Bound">X</a><a id="1185" class="Symbol">}</a> <a id="1187" href="yoneda2.html#1187" class="Bound">A</a> <a id="1189" class="Symbol">{</a><a id="1190" href="yoneda2.html#1190" class="Bound">x</a><a id="1191" class="Symbol">}</a> <a id="1193" href="yoneda2.html#1193" class="Bound">p</a> <a id="1195" href="yoneda2.html#1195" class="Bound">a</a> <a id="1197" class="Symbol">=</a> <a id="1199" href="yoneda2.html#968" class="Function">yoneda-nat</a> <a id="1210" class="Symbol">{</a><a id="1211" href="yoneda2.html#1184" class="Bound">X</a><a id="1212" class="Symbol">}</a> <a id="1214" class="Symbol">{</a><a id="1215" href="yoneda2.html#1190" class="Bound">x</a><a id="1216" class="Symbol">}</a> <a id="1218" class="Symbol">{</a><a id="1219" href="yoneda2.html#1187" class="Bound">A</a><a id="1220" class="Symbol">}</a> <a id="1222" href="yoneda2.html#1195" class="Bound">a</a> <a id="1224" href="yoneda2.html#1193" class="Bound">p</a>

<a id="yoneda-lemma"></a><a id="1227" href="yoneda2.html#1227" class="Function">yoneda-lemma</a> <a id="1240" class="Symbol">:</a> <a id="1242" class="Symbol">{</a><a id="1243" href="yoneda2.html#1243" class="Bound">X</a> <a id="1245" class="Symbol">:</a> <a id="1247" href="yoneda2.html#506" class="Function">U</a><a id="1248" class="Symbol">}</a> <a id="1250" class="Symbol">{</a><a id="1251" href="yoneda2.html#1251" class="Bound">x</a> <a id="1253" class="Symbol">:</a> <a id="1255" href="yoneda2.html#1243" class="Bound">X</a><a id="1256" class="Symbol">}</a> <a id="1258" class="Symbol">{</a><a id="1259" href="yoneda2.html#1259" class="Bound">A</a> <a id="1261" class="Symbol">:</a> <a id="1263" href="yoneda2.html#749" class="Function">PrShf</a> <a id="1269" href="yoneda2.html#1243" class="Bound">X</a><a id="1270" class="Symbol">}</a> <a id="1272" class="Symbol">(</a><a id="1273" href="yoneda2.html#1273" class="Bound">η</a> <a id="1275" class="Symbol">:</a> <a id="1277" href="yoneda2.html#783" class="Function">Nat</a> <a id="1281" class="Symbol">(</a><a id="1282" href="yoneda2.html#530" class="Datatype">Id</a> <a id="1285" href="yoneda2.html#1251" class="Bound">x</a><a id="1286" class="Symbol">)</a> <a id="1288" href="yoneda2.html#1259" class="Bound">A</a><a id="1289" class="Symbol">)</a> <a id="1291" class="Symbol">(</a><a id="1292" href="yoneda2.html#1292" class="Bound">y</a> <a id="1294" class="Symbol">:</a> <a id="1296" href="yoneda2.html#1243" class="Bound">X</a><a id="1297" class="Symbol">)</a> <a id="1299" class="Symbol">(</a><a id="1300" href="yoneda2.html#1300" class="Bound">p</a> <a id="1302" class="Symbol">:</a> <a id="1304" href="yoneda2.html#1251" class="Bound">x</a> <a id="1306" href="yoneda2.html#586" class="Function Operator">≡</a> <a id="1308" href="yoneda2.html#1292" class="Bound">y</a><a id="1309" class="Symbol">)</a> 
             <a id="1325" class="Symbol">→</a> <a id="1327" href="yoneda2.html#1101" class="Function">transport</a> <a id="1337" href="yoneda2.html#1259" class="Bound">A</a> <a id="1339" href="yoneda2.html#1300" class="Bound">p</a> <a id="1341" class="Symbol">(</a><a id="1342" href="yoneda2.html#856" class="Function">yoneda-elem</a> <a id="1354" href="yoneda2.html#1273" class="Bound">η</a><a id="1355" class="Symbol">)</a> <a id="1357" href="yoneda2.html#586" class="Function Operator">≡</a> <a id="1359" href="yoneda2.html#1273" class="Bound">η</a> <a id="1361" href="yoneda2.html#1300" class="Bound">p</a>
<a id="1363" href="yoneda2.html#1227" class="Function">yoneda-lemma</a> <a id="1376" class="Symbol">{</a><a id="1377" href="yoneda2.html#1377" class="Bound">X</a><a id="1378" class="Symbol">}</a> <a id="1380" class="Symbol">{</a><a id="1381" href="yoneda2.html#1381" class="Bound">x</a><a id="1382" class="Symbol">}</a> <a id="1384" class="Symbol">{</a><a id="1385" href="yoneda2.html#1385" class="Bound">A</a><a id="1386" class="Symbol">}</a> <a id="1388" href="yoneda2.html#1388" class="Bound">η</a> <a id="1390" class="Symbol">=</a> <a id="1392" href="yoneda2.html#628" class="Function">J</a> <a id="1394" href="yoneda2.html#1377" class="Bound">X</a> <a id="1396" href="yoneda2.html#1381" class="Bound">x</a> <a id="1398" class="Symbol">(λ</a> <a id="1401" href="yoneda2.html#1401" class="Bound">y</a> <a id="1403" href="yoneda2.html#1403" class="Bound">p</a> <a id="1405" class="Symbol">→</a> <a id="1407" href="yoneda2.html#1101" class="Function">transport</a> <a id="1417" href="yoneda2.html#1385" class="Bound">A</a> <a id="1419" href="yoneda2.html#1403" class="Bound">p</a> <a id="1421" class="Symbol">(</a><a id="1422" href="yoneda2.html#856" class="Function">yoneda-elem</a> <a id="1434" href="yoneda2.html#1388" class="Bound">η</a><a id="1435" class="Symbol">)</a> <a id="1437" href="yoneda2.html#586" class="Function Operator">≡</a> <a id="1439" href="yoneda2.html#1388" class="Bound">η</a> <a id="1441" href="yoneda2.html#1403" class="Bound">p</a><a id="1442" class="Symbol">)</a> <a id="1444" class="Symbol">(</a><a id="1445" href="yoneda2.html#560" class="InductiveConstructor">refl</a> <a id="1450" class="Symbol">(</a><a id="1451" href="yoneda2.html#856" class="Function">yoneda-elem</a> <a id="1463" href="yoneda2.html#1388" class="Bound">η</a><a id="1464" class="Symbol">))</a>

<a id="_•_"></a><a id="1468" href="yoneda2.html#1468" class="Function Operator">_•_</a> <a id="1472" class="Symbol">:</a> <a id="1474" class="Symbol">{</a><a id="1475" href="yoneda2.html#1475" class="Bound">X</a> <a id="1477" class="Symbol">:</a> <a id="1479" href="yoneda2.html#506" class="Function">U</a><a id="1480" class="Symbol">}</a> <a id="1482" class="Symbol">{</a><a id="1483" href="yoneda2.html#1483" class="Bound">x</a> <a id="1485" href="yoneda2.html#1485" class="Bound">y</a> <a id="1487" href="yoneda2.html#1487" class="Bound">z</a> <a id="1489" class="Symbol">:</a> <a id="1491" href="yoneda2.html#1475" class="Bound">X</a><a id="1492" class="Symbol">}</a> <a id="1494" class="Symbol">→</a> <a id="1496" href="yoneda2.html#1483" class="Bound">x</a> <a id="1498" href="yoneda2.html#586" class="Function Operator">≡</a> <a id="1500" href="yoneda2.html#1485" class="Bound">y</a> <a id="1502" class="Symbol">→</a> <a id="1504" href="yoneda2.html#1485" class="Bound">y</a> <a id="1506" href="yoneda2.html#586" class="Function Operator">≡</a> <a id="1508" href="yoneda2.html#1487" class="Bound">z</a> <a id="1510" class="Symbol">→</a> <a id="1512" href="yoneda2.html#1483" class="Bound">x</a> <a id="1514" href="yoneda2.html#586" class="Function Operator">≡</a> <a id="1516" href="yoneda2.html#1487" class="Bound">z</a>
<a id="1518" href="yoneda2.html#1518" class="Bound">p</a> <a id="1520" href="yoneda2.html#1468" class="Function Operator">•</a> <a id="1522" href="yoneda2.html#1522" class="Bound">q</a> <a id="1524" class="Symbol">=</a> <a id="1526" href="yoneda2.html#1101" class="Function">transport</a> <a id="1536" class="Symbol">(</a><a id="1537" href="yoneda2.html#530" class="Datatype">Id</a> <a id="1540" class="Symbol">_)</a> <a id="1543" href="yoneda2.html#1522" class="Bound">q</a> <a id="1545" href="yoneda2.html#1518" class="Bound">p</a> 

<a id="Id⁻¹"></a><a id="1549" href="yoneda2.html#1549" class="Function">Id⁻¹</a>  <a id="1555" class="Symbol">:</a> <a id="1557" class="Symbol">{</a><a id="1558" href="yoneda2.html#1558" class="Bound">X</a> <a id="1560" class="Symbol">:</a> <a id="1562" href="yoneda2.html#506" class="Function">U</a><a id="1563" class="Symbol">}</a> <a id="1565" class="Symbol">→</a> <a id="1567" href="yoneda2.html#1558" class="Bound">X</a> <a id="1569" class="Symbol">→</a> <a id="1571" href="yoneda2.html#1558" class="Bound">X</a> <a id="1573" class="Symbol">→</a> <a id="1575" href="yoneda2.html#506" class="Function">U</a>
<a id="1577" href="yoneda2.html#1549" class="Function">Id⁻¹</a> <a id="1582" href="yoneda2.html#1582" class="Bound">x</a> <a id="1584" href="yoneda2.html#1584" class="Bound">y</a> <a id="1586" class="Symbol">=</a> <a id="1588" href="yoneda2.html#530" class="Datatype">Id</a> <a id="1591" href="yoneda2.html#1584" class="Bound">y</a> <a id="1593" href="yoneda2.html#1582" class="Bound">x</a>

<a id="_⁻¹"></a><a id="1596" href="yoneda2.html#1596" class="Function Operator">_⁻¹</a> <a id="1600" class="Symbol">:</a> <a id="1602" class="Symbol">{</a><a id="1603" href="yoneda2.html#1603" class="Bound">X</a> <a id="1605" class="Symbol">:</a> <a id="1607" href="yoneda2.html#506" class="Function">U</a><a id="1608" class="Symbol">}</a> <a id="1610" class="Symbol">{</a><a id="1611" href="yoneda2.html#1611" class="Bound">x</a> <a id="1613" href="yoneda2.html#1613" class="Bound">y</a> <a id="1615" class="Symbol">:</a> <a id="1617" href="yoneda2.html#1603" class="Bound">X</a><a id="1618" class="Symbol">}</a> <a id="1620" class="Symbol">→</a> <a id="1622" href="yoneda2.html#1611" class="Bound">x</a> <a id="1624" href="yoneda2.html#586" class="Function Operator">≡</a> <a id="1626" href="yoneda2.html#1613" class="Bound">y</a> <a id="1628" class="Symbol">→</a> <a id="1630" href="yoneda2.html#1613" class="Bound">y</a> <a id="1632" href="yoneda2.html#586" class="Function Operator">≡</a> <a id="1634" href="yoneda2.html#1611" class="Bound">x</a>
<a id="1636" href="yoneda2.html#1636" class="Bound">p</a> <a id="1638" href="yoneda2.html#1596" class="Function Operator">⁻¹</a> <a id="1641" class="Symbol">=</a> <a id="1643" href="yoneda2.html#1101" class="Function">transport</a>  <a id="1654" class="Symbol">(</a><a id="1655" href="yoneda2.html#1549" class="Function">Id⁻¹</a> <a id="1660" class="Symbol">_)</a> <a id="1663" href="yoneda2.html#1636" class="Bound">p</a> <a id="1665" class="Symbol">(</a><a id="1666" href="yoneda2.html#560" class="InductiveConstructor">refl</a> <a id="1671" class="Symbol">_)</a>

<a id="constPrShf"></a><a id="1675" href="yoneda2.html#1675" class="Function">constPrShf</a> <a id="1686" class="Symbol">:</a> <a id="1688" class="Symbol">{</a><a id="1689" href="yoneda2.html#1689" class="Bound">X</a> <a id="1691" class="Symbol">:</a> <a id="1693" href="yoneda2.html#506" class="Function">U</a><a id="1694" class="Symbol">}</a> <a id="1696" class="Symbol">→</a> <a id="1698" href="yoneda2.html#506" class="Function">U</a> <a id="1700" class="Symbol">→</a> <a id="1702" href="yoneda2.html#749" class="Function">PrShf</a> <a id="1708" href="yoneda2.html#1689" class="Bound">X</a> 
<a id="1711" href="yoneda2.html#1675" class="Function">constPrShf</a> <a id="1722" href="yoneda2.html#1722" class="Bound">B</a> <a id="1724" class="Symbol">_</a> <a id="1726" class="Symbol">=</a> <a id="1728" href="yoneda2.html#1722" class="Bound">B</a>

<a id="yoneda-const"></a><a id="1731" href="yoneda2.html#1731" class="Function">yoneda-const</a> <a id="1744" class="Symbol">:</a> <a id="1746" class="Symbol">{</a><a id="1747" href="yoneda2.html#1747" class="Bound">X</a> <a id="1749" href="yoneda2.html#1749" class="Bound">B</a> <a id="1751" class="Symbol">:</a> <a id="1753" href="yoneda2.html#506" class="Function">U</a><a id="1754" class="Symbol">}</a> <a id="1756" class="Symbol">{</a><a id="1757" href="yoneda2.html#1757" class="Bound">x</a> <a id="1759" class="Symbol">:</a> <a id="1761" href="yoneda2.html#1747" class="Bound">X</a><a id="1762" class="Symbol">}</a> <a id="1764" class="Symbol">(</a><a id="1765" href="yoneda2.html#1765" class="Bound">η</a> <a id="1767" class="Symbol">:</a> <a id="1769" href="yoneda2.html#783" class="Function">Nat</a> <a id="1773" class="Symbol">(</a><a id="1774" href="yoneda2.html#530" class="Datatype">Id</a> <a id="1777" href="yoneda2.html#1757" class="Bound">x</a><a id="1778" class="Symbol">)</a> <a id="1780" class="Symbol">(</a><a id="1781" href="yoneda2.html#1675" class="Function">constPrShf</a> <a id="1792" href="yoneda2.html#1749" class="Bound">B</a><a id="1793" class="Symbol">))</a> <a id="1796" class="Symbol">(</a><a id="1797" href="yoneda2.html#1797" class="Bound">y</a> <a id="1799" class="Symbol">:</a> <a id="1801" href="yoneda2.html#1747" class="Bound">X</a><a id="1802" class="Symbol">)</a> <a id="1804" class="Symbol">(</a><a id="1805" href="yoneda2.html#1805" class="Bound">p</a> <a id="1807" class="Symbol">:</a> <a id="1809" href="yoneda2.html#1757" class="Bound">x</a> <a id="1811" href="yoneda2.html#586" class="Function Operator">≡</a> <a id="1813" href="yoneda2.html#1797" class="Bound">y</a><a id="1814" class="Symbol">)</a> 
            <a id="1829" class="Symbol">→</a> <a id="1831" href="yoneda2.html#856" class="Function">yoneda-elem</a> <a id="1843" href="yoneda2.html#1765" class="Bound">η</a> <a id="1845" href="yoneda2.html#586" class="Function Operator">≡</a> <a id="1847" href="yoneda2.html#1765" class="Bound">η</a> <a id="1849" href="yoneda2.html#1805" class="Bound">p</a> 
<a id="1852" href="yoneda2.html#1731" class="Function">yoneda-const</a> <a id="1865" class="Symbol">{</a><a id="1866" href="yoneda2.html#1866" class="Bound">X</a><a id="1867" class="Symbol">}</a> <a id="1869" class="Symbol">{</a><a id="1870" href="yoneda2.html#1870" class="Bound">B</a><a id="1871" class="Symbol">}</a> <a id="1873" class="Symbol">{</a><a id="1874" href="yoneda2.html#1874" class="Bound">x</a><a id="1875" class="Symbol">}</a> <a id="1877" href="yoneda2.html#1877" class="Bound">η</a> <a id="1879" href="yoneda2.html#1879" class="Bound">y</a> <a id="1881" href="yoneda2.html#1881" class="Bound">p</a>  <a id="1884" class="Symbol">=</a> <a id="1886" href="yoneda2.html#1987" class="Function">v</a> <a id="1888" href="yoneda2.html#1596" class="Function Operator">⁻¹</a> <a id="1891" href="yoneda2.html#1468" class="Function Operator">•</a> <a id="1893" href="yoneda2.html#1905" class="Function">u</a>
 <a id="1896" class="Keyword">where</a>
   <a id="1905" href="yoneda2.html#1905" class="Function">u</a> <a id="1907" class="Symbol">:</a> <a id="1909" href="yoneda2.html#1101" class="Function">transport</a> <a id="1919" class="Symbol">(</a><a id="1920" href="yoneda2.html#1675" class="Function">constPrShf</a> <a id="1931" href="yoneda2.html#1870" class="Bound">B</a><a id="1932" class="Symbol">)</a> <a id="1934" href="yoneda2.html#1881" class="Bound">p</a> <a id="1936" class="Symbol">(</a><a id="1937" href="yoneda2.html#856" class="Function">yoneda-elem</a> <a id="1949" href="yoneda2.html#1877" class="Bound">η</a><a id="1950" class="Symbol">)</a> <a id="1952" href="yoneda2.html#586" class="Function Operator">≡</a> <a id="1954" href="yoneda2.html#1877" class="Bound">η</a> <a id="1956" href="yoneda2.html#1881" class="Bound">p</a>
   <a id="1961" href="yoneda2.html#1905" class="Function">u</a> <a id="1963" class="Symbol">=</a> <a id="1965" href="yoneda2.html#1227" class="Function">yoneda-lemma</a> <a id="1978" href="yoneda2.html#1877" class="Bound">η</a> <a id="1980" href="yoneda2.html#1879" class="Bound">y</a> <a id="1982" href="yoneda2.html#1881" class="Bound">p</a>
   <a id="1987" href="yoneda2.html#1987" class="Function">v</a> <a id="1989" class="Symbol">:</a> <a id="1991" href="yoneda2.html#1101" class="Function">transport</a> <a id="2001" class="Symbol">(</a><a id="2002" href="yoneda2.html#1675" class="Function">constPrShf</a> <a id="2013" href="yoneda2.html#1870" class="Bound">B</a><a id="2014" class="Symbol">)</a> <a id="2016" href="yoneda2.html#1881" class="Bound">p</a> <a id="2018" class="Symbol">(</a><a id="2019" href="yoneda2.html#856" class="Function">yoneda-elem</a> <a id="2031" href="yoneda2.html#1877" class="Bound">η</a><a id="2032" class="Symbol">)</a> <a id="2034" href="yoneda2.html#586" class="Function Operator">≡</a> <a id="2036" href="yoneda2.html#856" class="Function">yoneda-elem</a> <a id="2048" href="yoneda2.html#1877" class="Bound">η</a>
   <a id="2053" href="yoneda2.html#1987" class="Function">v</a> <a id="2055" class="Symbol">=</a> <a id="2057" href="yoneda2.html#1227" class="Function">yoneda-lemma</a> <a id="2070" class="Symbol">((λ</a> <a id="2074" href="yoneda2.html#2074" class="Bound">p</a> <a id="2076" class="Symbol">→</a> <a id="2078" href="yoneda2.html#856" class="Function">yoneda-elem</a> <a id="2090" href="yoneda2.html#1877" class="Bound">η</a><a id="2091" class="Symbol">))</a> <a id="2094" href="yoneda2.html#1879" class="Bound">y</a> <a id="2096" href="yoneda2.html#1881" class="Bound">p</a>

<a id="2099" class="Markup">\end{code}</a><a id="2109" class="Background">

This time we work with an alternative definition of Σ that has better
definitional &quot;eta&quot; properties. However, this still doesn&#39;t help to get
new-J definitionally equal to our starting J.

</a><a id="2299" class="Markup">\begin{code} </a>

<a id="2314" class="Keyword">record</a> <a id="Σ"></a><a id="2321" href="yoneda2.html#2321" class="Record">Σ</a> <a id="2323" class="Symbol">{</a><a id="2324" href="yoneda2.html#2324" class="Bound">X</a> <a id="2326" class="Symbol">:</a> <a id="2328" href="yoneda2.html#506" class="Function">U</a><a id="2329" class="Symbol">}</a> <a id="2331" class="Symbol">(</a><a id="2332" href="yoneda2.html#2332" class="Bound">A</a> <a id="2334" class="Symbol">:</a> <a id="2336" href="yoneda2.html#2324" class="Bound">X</a> <a id="2338" class="Symbol">→</a> <a id="2340" href="yoneda2.html#506" class="Function">U</a><a id="2341" class="Symbol">)</a> <a id="2343" class="Symbol">:</a> <a id="2345" href="yoneda2.html#506" class="Function">U</a> <a id="2347" class="Keyword">where</a>
  <a id="2355" class="Keyword">constructor</a> <a id="_,_"></a><a id="2367" href="yoneda2.html#2367" class="InductiveConstructor Operator">_,_</a>
  <a id="2373" class="Keyword">field</a>
   <a id="Σ.π₀"></a><a id="2382" href="yoneda2.html#2382" class="Field">π₀</a> <a id="2385" class="Symbol">:</a> <a id="2387" href="yoneda2.html#2324" class="Bound">X</a>
   <a id="Σ.π₁"></a><a id="2392" href="yoneda2.html#2392" class="Field">π₁</a> <a id="2395" class="Symbol">:</a> <a id="2397" href="yoneda2.html#2332" class="Bound">A</a> <a id="2399" href="yoneda2.html#2382" class="Field">π₀</a>

<a id="2403" class="Keyword">open</a> <a id="2408" href="yoneda2.html#2321" class="Module">Σ</a> <a id="2410" class="Keyword">public</a>

<a id="paths-from"></a><a id="2418" href="yoneda2.html#2418" class="Function">paths-from</a> <a id="2429" class="Symbol">:</a> <a id="2431" class="Symbol">{</a><a id="2432" href="yoneda2.html#2432" class="Bound">X</a> <a id="2434" class="Symbol">:</a> <a id="2436" href="yoneda2.html#506" class="Function">U</a><a id="2437" class="Symbol">}</a> <a id="2439" class="Symbol">→</a> <a id="2441" href="yoneda2.html#2432" class="Bound">X</a> <a id="2443" class="Symbol">→</a> <a id="2445" href="yoneda2.html#506" class="Function">U</a>
<a id="2447" href="yoneda2.html#2418" class="Function">paths-from</a> <a id="2458" class="Symbol">{</a><a id="2459" href="yoneda2.html#2459" class="Bound">X</a><a id="2460" class="Symbol">}</a> <a id="2462" href="yoneda2.html#2462" class="Bound">x</a> <a id="2464" class="Symbol">=</a> <a id="2466" href="yoneda2.html#2321" class="Record">Σ</a> <a id="2468" class="Symbol">\(</a><a id="2470" href="yoneda2.html#2470" class="Bound">y</a> <a id="2472" class="Symbol">:</a> <a id="2474" href="yoneda2.html#2459" class="Bound">X</a><a id="2475" class="Symbol">)</a> <a id="2477" class="Symbol">→</a> <a id="2479" href="yoneda2.html#2462" class="Bound">x</a> <a id="2481" href="yoneda2.html#586" class="Function Operator">≡</a> <a id="2483" href="yoneda2.html#2470" class="Bound">y</a>

<a id="singletons-contractible"></a><a id="2486" href="yoneda2.html#2486" class="Function">singletons-contractible</a> <a id="2510" class="Symbol">:</a> <a id="2512" class="Symbol">{</a><a id="2513" href="yoneda2.html#2513" class="Bound">X</a> <a id="2515" class="Symbol">:</a> <a id="2517" href="yoneda2.html#506" class="Function">U</a><a id="2518" class="Symbol">}</a> <a id="2520" class="Symbol">{</a><a id="2521" href="yoneda2.html#2521" class="Bound">x</a> <a id="2523" class="Symbol">:</a> <a id="2525" href="yoneda2.html#2513" class="Bound">X</a><a id="2526" class="Symbol">}</a> <a id="2528" class="Symbol">(</a><a id="2529" href="yoneda2.html#2529" class="Bound">w</a> <a id="2531" class="Symbol">:</a> <a id="2533" href="yoneda2.html#2418" class="Function">paths-from</a> <a id="2544" href="yoneda2.html#2521" class="Bound">x</a><a id="2545" class="Symbol">)</a> <a id="2547" class="Symbol">→</a> <a id="2549" class="Symbol">(</a><a id="2550" href="yoneda2.html#2521" class="Bound">x</a> <a id="2552" href="yoneda2.html#2367" class="InductiveConstructor Operator">,</a> <a id="2554" href="yoneda2.html#560" class="InductiveConstructor">refl</a> <a id="2559" href="yoneda2.html#2521" class="Bound">x</a><a id="2560" class="Symbol">)</a> <a id="2562" href="yoneda2.html#586" class="Function Operator">≡</a> <a id="2564" href="yoneda2.html#2529" class="Bound">w</a>
<a id="2566" href="yoneda2.html#2486" class="Function">singletons-contractible</a> <a id="2590" class="Symbol">{</a><a id="2591" href="yoneda2.html#2591" class="Bound">X</a><a id="2592" class="Symbol">}</a> <a id="2594" class="Symbol">{</a><a id="2595" href="yoneda2.html#2595" class="Bound">x</a><a id="2596" class="Symbol">}</a> <a id="2598" class="Symbol">(</a><a id="2599" href="yoneda2.html#2599" class="Bound">y</a> <a id="2601" href="yoneda2.html#2367" class="InductiveConstructor Operator">,</a> <a id="2603" href="yoneda2.html#2603" class="Bound">p</a><a id="2604" class="Symbol">)</a> <a id="2606" class="Symbol">=</a> <a id="2608" href="yoneda2.html#1731" class="Function">yoneda-const</a> <a id="2621" href="yoneda2.html#2636" class="Function">η</a> <a id="2623" href="yoneda2.html#2599" class="Bound">y</a> <a id="2625" href="yoneda2.html#2603" class="Bound">p</a>
 <a id="2628" class="Keyword">where</a>
  <a id="2636" href="yoneda2.html#2636" class="Function">η</a> <a id="2638" class="Symbol">:</a> <a id="2640" href="yoneda2.html#783" class="Function">Nat</a> <a id="2644" class="Symbol">(</a><a id="2645" href="yoneda2.html#530" class="Datatype">Id</a> <a id="2648" href="yoneda2.html#2595" class="Bound">x</a><a id="2649" class="Symbol">)</a> <a id="2651" class="Symbol">(</a><a id="2652" href="yoneda2.html#1675" class="Function">constPrShf</a><a id="2662" class="Symbol">(</a><a id="2663" href="yoneda2.html#2418" class="Function">paths-from</a> <a id="2674" href="yoneda2.html#2595" class="Bound">x</a><a id="2675" class="Symbol">))</a>
  <a id="2680" href="yoneda2.html#2636" class="Function">η</a> <a id="2682" class="Symbol">{</a><a id="2683" href="yoneda2.html#2683" class="Bound">y</a><a id="2684" class="Symbol">}</a> <a id="2686" href="yoneda2.html#2686" class="Bound">p</a> <a id="2688" class="Symbol">=</a> <a id="2690" class="Symbol">(</a><a id="2691" href="yoneda2.html#2683" class="Bound">y</a> <a id="2693" href="yoneda2.html#2367" class="InductiveConstructor Operator">,</a> <a id="2695" href="yoneda2.html#2686" class="Bound">p</a><a id="2696" class="Symbol">)</a>

<a id="new-J&#39;"></a><a id="2699" href="yoneda2.html#2699" class="Function">new-J&#39;</a> <a id="2706" class="Symbol">:</a> <a id="2708" class="Symbol">(</a><a id="2709" href="yoneda2.html#2709" class="Bound">X</a> <a id="2711" class="Symbol">:</a> <a id="2713" href="yoneda2.html#506" class="Function">U</a><a id="2714" class="Symbol">)</a> <a id="2716" class="Symbol">(</a><a id="2717" href="yoneda2.html#2717" class="Bound">x</a> <a id="2719" class="Symbol">:</a> <a id="2721" href="yoneda2.html#2709" class="Bound">X</a><a id="2722" class="Symbol">)</a> <a id="2724" class="Symbol">(</a><a id="2725" href="yoneda2.html#2725" class="Bound">B&#39;</a> <a id="2728" class="Symbol">:</a> <a id="2730" href="yoneda2.html#2418" class="Function">paths-from</a> <a id="2741" href="yoneda2.html#2717" class="Bound">x</a> <a id="2743" class="Symbol">→</a> <a id="2745" href="yoneda2.html#506" class="Function">U</a><a id="2746" class="Symbol">)</a>
   <a id="2751" class="Symbol">→</a> <a id="2753" href="yoneda2.html#2725" class="Bound">B&#39;</a> <a id="2756" class="Symbol">(</a><a id="2757" href="yoneda2.html#2717" class="Bound">x</a> <a id="2759" href="yoneda2.html#2367" class="InductiveConstructor Operator">,</a> <a id="2761" href="yoneda2.html#560" class="InductiveConstructor">refl</a> <a id="2766" href="yoneda2.html#2717" class="Bound">x</a><a id="2767" class="Symbol">)</a> <a id="2769" class="Symbol">→</a> <a id="2771" class="Symbol">∀</a> <a id="2773" href="yoneda2.html#2773" class="Bound">w</a> <a id="2775" class="Symbol">→</a> <a id="2777" href="yoneda2.html#2725" class="Bound">B&#39;</a> <a id="2780" href="yoneda2.html#2773" class="Bound">w</a>
<a id="2782" href="yoneda2.html#2699" class="Function">new-J&#39;</a> <a id="2789" href="yoneda2.html#2789" class="Bound">X</a> <a id="2791" href="yoneda2.html#2791" class="Bound">x</a> <a id="2793" href="yoneda2.html#2793" class="Bound">B&#39;</a> <a id="2796" href="yoneda2.html#2796" class="Bound">b</a> <a id="2798" href="yoneda2.html#2798" class="Bound">w</a> <a id="2800" class="Symbol">=</a> <a id="2802" href="yoneda2.html#1101" class="Function">transport</a> <a id="2812" href="yoneda2.html#2793" class="Bound">B&#39;</a> <a id="2815" class="Symbol">(</a><a id="2816" href="yoneda2.html#2486" class="Function">singletons-contractible</a> <a id="2840" href="yoneda2.html#2798" class="Bound">w</a><a id="2841" class="Symbol">)</a> <a id="2843" href="yoneda2.html#2796" class="Bound">b</a>

<a id="uncurry"></a><a id="2846" href="yoneda2.html#2846" class="Function">uncurry</a> <a id="2854" class="Symbol">:</a> <a id="2856" class="Symbol">{</a><a id="2857" href="yoneda2.html#2857" class="Bound">X</a> <a id="2859" class="Symbol">:</a> <a id="2861" href="yoneda2.html#506" class="Function">U</a><a id="2862" class="Symbol">}{</a><a id="2864" href="yoneda2.html#2864" class="Bound">x</a> <a id="2866" class="Symbol">:</a> <a id="2868" href="yoneda2.html#2857" class="Bound">X</a><a id="2869" class="Symbol">}</a> <a id="2871" class="Symbol">→</a> <a id="2873" class="Symbol">((</a><a id="2875" href="yoneda2.html#2875" class="Bound">y</a> <a id="2877" class="Symbol">:</a> <a id="2879" href="yoneda2.html#2857" class="Bound">X</a><a id="2880" class="Symbol">)</a> <a id="2882" class="Symbol">→</a> <a id="2884" href="yoneda2.html#2864" class="Bound">x</a> <a id="2886" href="yoneda2.html#586" class="Function Operator">≡</a> <a id="2888" href="yoneda2.html#2875" class="Bound">y</a> <a id="2890" class="Symbol">→</a> <a id="2892" href="yoneda2.html#506" class="Function">U</a><a id="2893" class="Symbol">)</a> <a id="2895" class="Symbol">→</a> <a id="2897" class="Symbol">(</a><a id="2898" href="yoneda2.html#2418" class="Function">paths-from</a> <a id="2909" href="yoneda2.html#2864" class="Bound">x</a> <a id="2911" class="Symbol">→</a> <a id="2913" href="yoneda2.html#506" class="Function">U</a><a id="2914" class="Symbol">)</a>
<a id="2916" href="yoneda2.html#2846" class="Function">uncurry</a> <a id="2924" href="yoneda2.html#2924" class="Bound">B</a> <a id="2926" class="Symbol">(</a><a id="2927" href="yoneda2.html#2927" class="Bound">y</a> <a id="2929" href="yoneda2.html#2367" class="InductiveConstructor Operator">,</a> <a id="2931" href="yoneda2.html#2931" class="Bound">p</a><a id="2932" class="Symbol">)</a> <a id="2934" class="Symbol">=</a> <a id="2936" href="yoneda2.html#2924" class="Bound">B</a> <a id="2938" href="yoneda2.html#2927" class="Bound">y</a> <a id="2940" href="yoneda2.html#2931" class="Bound">p</a>

<a id="new-J"></a><a id="2943" href="yoneda2.html#2943" class="Function">new-J</a> <a id="2949" class="Symbol">:</a> <a id="2951" class="Symbol">(</a><a id="2952" href="yoneda2.html#2952" class="Bound">X</a> <a id="2954" class="Symbol">:</a> <a id="2956" href="yoneda2.html#506" class="Function">U</a><a id="2957" class="Symbol">)</a> <a id="2959" class="Symbol">(</a><a id="2960" href="yoneda2.html#2960" class="Bound">x</a> <a id="2962" class="Symbol">:</a> <a id="2964" href="yoneda2.html#2952" class="Bound">X</a><a id="2965" class="Symbol">)</a> <a id="2967" class="Symbol">(</a><a id="2968" href="yoneda2.html#2968" class="Bound">B</a> <a id="2970" class="Symbol">:</a> <a id="2972" class="Symbol">(</a><a id="2973" href="yoneda2.html#2973" class="Bound">y</a> <a id="2975" class="Symbol">:</a> <a id="2977" href="yoneda2.html#2952" class="Bound">X</a><a id="2978" class="Symbol">)</a> <a id="2980" class="Symbol">→</a> <a id="2982" href="yoneda2.html#2960" class="Bound">x</a> <a id="2984" href="yoneda2.html#586" class="Function Operator">≡</a> <a id="2986" href="yoneda2.html#2973" class="Bound">y</a> <a id="2988" class="Symbol">→</a> <a id="2990" href="yoneda2.html#506" class="Function">U</a><a id="2991" class="Symbol">)</a>
   <a id="2996" class="Symbol">→</a> <a id="2998" href="yoneda2.html#2968" class="Bound">B</a> <a id="3000" href="yoneda2.html#2960" class="Bound">x</a> <a id="3002" class="Symbol">(</a><a id="3003" href="yoneda2.html#560" class="InductiveConstructor">refl</a> <a id="3008" href="yoneda2.html#2960" class="Bound">x</a><a id="3009" class="Symbol">)</a> <a id="3011" class="Symbol">→</a> <a id="3013" class="Symbol">(</a><a id="3014" href="yoneda2.html#3014" class="Bound">y</a> <a id="3016" class="Symbol">:</a> <a id="3018" href="yoneda2.html#2952" class="Bound">X</a><a id="3019" class="Symbol">)</a> <a id="3021" class="Symbol">(</a><a id="3022" href="yoneda2.html#3022" class="Bound">p</a> <a id="3024" class="Symbol">:</a> <a id="3026" href="yoneda2.html#2960" class="Bound">x</a> <a id="3028" href="yoneda2.html#586" class="Function Operator">≡</a> <a id="3030" href="yoneda2.html#3014" class="Bound">y</a><a id="3031" class="Symbol">)</a> <a id="3033" class="Symbol">→</a> <a id="3035" href="yoneda2.html#2968" class="Bound">B</a> <a id="3037" href="yoneda2.html#3014" class="Bound">y</a> <a id="3039" href="yoneda2.html#3022" class="Bound">p</a>
<a id="3041" href="yoneda2.html#2943" class="Function">new-J</a> <a id="3047" href="yoneda2.html#3047" class="Bound">X</a> <a id="3049" href="yoneda2.html#3049" class="Bound">x</a> <a id="3051" href="yoneda2.html#3051" class="Bound">B</a> <a id="3053" href="yoneda2.html#3053" class="Bound">b</a> <a id="3055" href="yoneda2.html#3055" class="Bound">y</a> <a id="3057" href="yoneda2.html#3057" class="Bound">p</a> <a id="3059" class="Symbol">=</a> <a id="3061" href="yoneda2.html#2699" class="Function">new-J&#39;</a> <a id="3068" href="yoneda2.html#3047" class="Bound">X</a> <a id="3070" href="yoneda2.html#3049" class="Bound">x</a> <a id="3072" class="Symbol">(</a><a id="3073" href="yoneda2.html#2846" class="Function">uncurry</a> <a id="3081" href="yoneda2.html#3051" class="Bound">B</a><a id="3082" class="Symbol">)</a> <a id="3084" href="yoneda2.html#3053" class="Bound">b</a> <a id="3086" class="Symbol">(</a><a id="3087" href="yoneda2.html#3055" class="Bound">y</a> <a id="3089" href="yoneda2.html#2367" class="InductiveConstructor Operator">,</a> <a id="3091" href="yoneda2.html#3057" class="Bound">p</a><a id="3092" class="Symbol">)</a>

<a id="new-J-computation"></a><a id="3095" href="yoneda2.html#3095" class="Function">new-J-computation</a> <a id="3113" class="Symbol">:</a> <a id="3115" class="Symbol">{</a><a id="3116" href="yoneda2.html#3116" class="Bound">X</a> <a id="3118" class="Symbol">:</a> <a id="3120" href="yoneda2.html#506" class="Function">U</a><a id="3121" class="Symbol">}</a> <a id="3123" class="Symbol">{</a><a id="3124" href="yoneda2.html#3124" class="Bound">x</a> <a id="3126" class="Symbol">:</a> <a id="3128" href="yoneda2.html#3116" class="Bound">X</a><a id="3129" class="Symbol">}</a> <a id="3131" class="Symbol">{</a><a id="3132" href="yoneda2.html#3132" class="Bound">B</a> <a id="3134" class="Symbol">:</a> <a id="3136" class="Symbol">(</a><a id="3137" href="yoneda2.html#3137" class="Bound">y</a> <a id="3139" class="Symbol">:</a> <a id="3141" href="yoneda2.html#3116" class="Bound">X</a><a id="3142" class="Symbol">)</a> <a id="3144" class="Symbol">→</a> <a id="3146" href="yoneda2.html#3124" class="Bound">x</a> <a id="3148" href="yoneda2.html#586" class="Function Operator">≡</a> <a id="3150" href="yoneda2.html#3137" class="Bound">y</a> <a id="3152" class="Symbol">→</a> <a id="3154" href="yoneda2.html#506" class="Function">U</a><a id="3155" class="Symbol">}</a> <a id="3157" class="Symbol">(</a><a id="3158" href="yoneda2.html#3158" class="Bound">b</a> <a id="3160" class="Symbol">:</a> <a id="3162" href="yoneda2.html#3132" class="Bound">B</a> <a id="3164" href="yoneda2.html#3124" class="Bound">x</a> <a id="3166" class="Symbol">(</a><a id="3167" href="yoneda2.html#560" class="InductiveConstructor">refl</a> <a id="3172" href="yoneda2.html#3124" class="Bound">x</a><a id="3173" class="Symbol">))</a> 
                  <a id="3195" class="Symbol">→</a> <a id="3197" href="yoneda2.html#2943" class="Function">new-J</a> <a id="3203" href="yoneda2.html#3116" class="Bound">X</a> <a id="3205" href="yoneda2.html#3124" class="Bound">x</a> <a id="3207" href="yoneda2.html#3132" class="Bound">B</a> <a id="3209" href="yoneda2.html#3158" class="Bound">b</a> <a id="3211" href="yoneda2.html#3124" class="Bound">x</a> <a id="3213" class="Symbol">(</a><a id="3214" href="yoneda2.html#560" class="InductiveConstructor">refl</a> <a id="3219" href="yoneda2.html#3124" class="Bound">x</a><a id="3220" class="Symbol">)</a> <a id="3222" href="yoneda2.html#586" class="Function Operator">≡</a> <a id="3224" href="yoneda2.html#3158" class="Bound">b</a>
<a id="3226" href="yoneda2.html#3095" class="Function">new-J-computation</a> <a id="3244" class="Symbol">=</a> <a id="3246" href="yoneda2.html#560" class="InductiveConstructor">refl</a>

<a id="3252" class="Markup">\end{code}</a><a id="3262" class="Background">

Agda reports the following normal form for new-J, which is
definitionally different from J:

λ X x B b y p →
  J (Σ (λ y₁ → Id x y₁)) (x , refl x) (λ y₁ p₁ → B (π₀ y₁) (π₁ y₁)) b
  (y , p)
  (J (Σ (λ y₁ → Id x y₁))
   (J X x (λ y₁ p₁ → Σ (λ y₂ → Id x y₂)) (x , refl x) y p)
   (λ y₁ p₁ → Id (x , refl x) y₁)
   (J (Σ (λ y₁ → Id x y₁))
    (J X x (λ y₁ p₁ → Σ (λ y₂ → Id x y₂)) (x , refl x) y p)
    (λ y₁ p₁ →
       Id y₁ (J X x (λ y₂ p₂ → Σ (λ y₃ → Id x y₃)) (x , refl x) y p))
    (refl (J X x (λ y₁ p₁ → Σ (λ y₂ → Id x y₂)) (x , refl x) y p))
    (x , refl x)
    (J X x
     (λ y₁ p₁ →
        Id (J X x (λ y₂ p₂ → Σ (λ y₃ → Id x y₃)) (x , refl x) y₁ p₁)
        (x , refl x))
     (refl (x , refl x)) y p))
   (y , p)
   (J X x
    (λ y₁ p₁ →
       Id (J X x (λ y₂ p₂ → Σ (λ y₃ → Id x y₃)) (x , refl x) y₁ p₁)
       (y₁ , p₁))
    (refl (x , refl x)) y p))


</a><a id="4131" class="Markup">\begin{code}</a>

<a id="4145" class="Keyword">infix</a> <a id="4151" class="Number">1</a> <a id="4153" href="yoneda2.html#586" class="Function Operator">_≡_</a> 
<a id="4158" class="Keyword">infixl</a> <a id="4165" class="Number">0</a> <a id="4167" href="yoneda2.html#1468" class="Function Operator">_•_</a>

<a id="4172" class="Markup">\end{code}</a><a id="4182" class="Background">
</a></pre></body></html>