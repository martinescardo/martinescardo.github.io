<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Taboos.WLPO</title><link rel="stylesheet" href="Agda.css"><script type="text/javascript" src="highlight-hover.js"></script></head><body><pre class="Agda"><a id="1" class="Background">Martin Escardo 2012.

The Weak Limited Principle of Omniscience (only somebody called Bishop
could have called it that), or WLPO for short, says that every
infinity binary sequence is either constantly 1 or it isn&#39;t.

This is equivalent to saying that every decreasing infinity binary
sequence os either constantly one or not.

The type ℕ∞ of decreasing binary sequences is defined in the module
GenericConvergentSequence. The constantly 1 sequence is called ∞.

WLPO is independent of type theory: it holds in the model of classical
sets, and it fails in recursive models, because it amounts to a
solution of the Halting Problem. But we want to keep it undecided, for
the sake of being compatible with classical mathematics, following the
wishes of Bishop, and perhaps upsetting those of Brouwer who was happy
to accept continuity principles that falsify WLPO. In the words of
Aczel, WLPO is a taboo.  More generally, anything that implies a taboo
is a taboo, and any taboo is undecided. Taboos are boundary
propositions: they are classically true, recursively false, and
constructively, well, taboos!

</a><a id="1105" class="Markup">\begin{code}</a>

<a id="1119" class="Symbol">{-#</a> <a id="1123" class="Keyword">OPTIONS</a> <a id="1131" class="Pragma">--safe</a> <a id="1138" class="Pragma">--without-K</a> <a id="1150" class="Symbol">#-}</a>

<a id="1155" class="Keyword">module</a> <a id="1162" href="Taboos.WLPO.html" class="Module">Taboos.WLPO</a> <a id="1174" class="Keyword">where</a>

<a id="1181" class="Keyword">open</a> <a id="1186" class="Keyword">import</a> <a id="1193" href="MLTT.Spartan.html" class="Module">MLTT.Spartan</a>
<a id="1206" class="Keyword">open</a> <a id="1211" class="Keyword">import</a> <a id="1218" href="CoNaturals.Type.html" class="Module">CoNaturals.Type</a>
<a id="1234" class="Keyword">open</a> <a id="1239" class="Keyword">import</a> <a id="1246" href="UF.DiscreteAndSeparated.html" class="Module">UF.DiscreteAndSeparated</a>
<a id="1270" class="Keyword">open</a> <a id="1275" class="Keyword">import</a> <a id="1282" href="UF.FunExt.html" class="Module">UF.FunExt</a>
<a id="1292" class="Keyword">open</a> <a id="1297" class="Keyword">import</a> <a id="1304" href="NotionsOfDecidability.Decidable.html" class="Module">NotionsOfDecidability.Decidable</a>

<a id="WLPO"></a><a id="1337" href="Taboos.WLPO.html#1337" class="Function">WLPO</a> <a id="1342" class="Symbol">:</a> <a id="1344" href="Agda.Primitive.html#915" class="Primitive">𝓤₀</a> <a id="1347" href="MLTT.Universes.html#3976" class="Function Operator">̇</a>
<a id="1349" href="Taboos.WLPO.html#1337" class="Function">WLPO</a> <a id="1354" class="Symbol">=</a> <a id="1356" class="Symbol">(</a><a id="1357" href="Taboos.WLPO.html#1357" class="Bound">u</a> <a id="1359" class="Symbol">:</a> <a id="1361" href="CoNaturals.GenericConvergentSequence.html#1332" class="Function">ℕ∞</a><a id="1363" class="Symbol">)</a> <a id="1365" class="Symbol">→</a> <a id="1367" class="Symbol">(</a><a id="1368" href="Taboos.WLPO.html#1357" class="Bound">u</a> <a id="1370" href="MLTT.Id.html#193" class="Datatype Operator">＝</a> <a id="1372" href="CoNaturals.GenericConvergentSequence.html#6051" class="Function">∞</a><a id="1373" class="Symbol">)</a> <a id="1375" href="MLTT.Plus-Type.html#120" class="Datatype Operator">+</a> <a id="1377" class="Symbol">(</a><a id="1378" href="Taboos.WLPO.html#1357" class="Bound">u</a> <a id="1380" href="MLTT.Negation.html#635" class="Function Operator">≠</a> <a id="1382" href="CoNaturals.GenericConvergentSequence.html#6051" class="Function">∞</a><a id="1383" class="Symbol">)</a>

<a id="1386" class="Markup">\end{code}</a><a id="1396" class="Background">

If ℕ∞ is discrete, i.e. has decidable equality, then WLPO follows:

</a><a id="1466" class="Markup">\begin{code}</a>

<a id="ℕ∞-discrete-gives-WLPO"></a><a id="1480" href="Taboos.WLPO.html#1480" class="Function">ℕ∞-discrete-gives-WLPO</a> <a id="1503" class="Symbol">:</a> <a id="1505" href="UF.DiscreteAndSeparated.html#1853" class="Function">is-discrete</a> <a id="1517" href="CoNaturals.GenericConvergentSequence.html#1332" class="Function">ℕ∞</a> <a id="1520" class="Symbol">→</a> <a id="1522" href="Taboos.WLPO.html#1337" class="Function">WLPO</a>
<a id="1527" href="Taboos.WLPO.html#1480" class="Function">ℕ∞-discrete-gives-WLPO</a> <a id="1550" href="Taboos.WLPO.html#1550" class="Bound">d</a> <a id="1552" href="Taboos.WLPO.html#1552" class="Bound">u</a> <a id="1554" class="Symbol">=</a> <a id="1556" href="Taboos.WLPO.html#1550" class="Bound">d</a> <a id="1558" href="Taboos.WLPO.html#1552" class="Bound">u</a> <a id="1560" href="CoNaturals.GenericConvergentSequence.html#6051" class="Function">∞</a>

<a id="1563" class="Markup">\end{code}</a><a id="1573" class="Background">

Added 12 September 2018.

Conversely, assuming function extensionality, WLPO implies that ℕ∞ is
discrete. The proof uses a codistance (or closeness) function
c : ℕ∞ → ℕ∞ → ℕ∞ such that c u v ＝ ∞ ↔ u ＝ v.

</a><a id="1780" class="Markup">\begin{code}</a>

<a id="WLPO-gives-ℕ∞-discrete"></a><a id="1794" href="Taboos.WLPO.html#1794" class="Function">WLPO-gives-ℕ∞-discrete</a> <a id="1817" class="Symbol">:</a> <a id="1819" href="UF.FunExt.html#995" class="Function">FunExt</a> <a id="1826" class="Symbol">→</a> <a id="1828" href="Taboos.WLPO.html#1337" class="Function">WLPO</a> <a id="1833" class="Symbol">→</a> <a id="1835" href="UF.DiscreteAndSeparated.html#1853" class="Function">is-discrete</a> <a id="1847" href="CoNaturals.GenericConvergentSequence.html#1332" class="Function">ℕ∞</a>
<a id="1850" href="Taboos.WLPO.html#1794" class="Function">WLPO-gives-ℕ∞-discrete</a> <a id="1873" href="Taboos.WLPO.html#1873" class="Bound">fe</a> <a id="1876" href="Taboos.WLPO.html#1876" class="Bound">wlpo</a> <a id="1881" href="Taboos.WLPO.html#1881" class="Bound">u</a> <a id="1883" href="Taboos.WLPO.html#1883" class="Bound">v</a> <a id="1885" class="Symbol">=</a>
 <a id="1888" href="MLTT.Plus.html#583" class="Function">Cases</a> <a id="1894" class="Symbol">(</a><a id="1895" href="Taboos.WLPO.html#1876" class="Bound">wlpo</a> <a id="1900" class="Symbol">(</a><a id="1901" href="TWA.Closeness.html#8783" class="Function">ℕ∞-closeness</a> <a id="1914" href="Taboos.WLPO.html#1881" class="Bound">u</a> <a id="1916" href="Taboos.WLPO.html#1883" class="Bound">v</a><a id="1917" class="Symbol">))</a>
  <a id="1922" class="Symbol">(λ</a> <a id="1925" class="Symbol">(</a><a id="1926" href="Taboos.WLPO.html#1926" class="Bound">p</a> <a id="1928" class="Symbol">:</a> <a id="1930" href="TWA.Closeness.html#8783" class="Function">ℕ∞-closeness</a> <a id="1943" href="Taboos.WLPO.html#1881" class="Bound">u</a> <a id="1945" href="Taboos.WLPO.html#1883" class="Bound">v</a> <a id="1947" href="MLTT.Id.html#193" class="Datatype Operator">＝</a> <a id="1949" href="CoNaturals.GenericConvergentSequence.html#6051" class="Function">∞</a><a id="1950" class="Symbol">)</a>
        <a id="1960" class="Symbol">→</a> <a id="1962" href="MLTT.Plus-Type.html#169" class="InductiveConstructor">inl</a> <a id="1966" class="Symbol">(</a><a id="1967" href="TWA.Closeness.html#9148" class="Function">ℕ∞-infinitely-close-are-equal</a> <a id="1997" href="Taboos.WLPO.html#1881" class="Bound">u</a> <a id="1999" href="Taboos.WLPO.html#1883" class="Bound">v</a> <a id="2001" href="Taboos.WLPO.html#1926" class="Bound">p</a><a id="2002" class="Symbol">))</a>
  <a id="2007" class="Symbol">(λ</a> <a id="2010" class="Symbol">(</a><a id="2011" href="Taboos.WLPO.html#2011" class="Bound">n</a> <a id="2013" class="Symbol">:</a> <a id="2015" href="TWA.Closeness.html#8783" class="Function">ℕ∞-closeness</a> <a id="2028" href="Taboos.WLPO.html#1881" class="Bound">u</a> <a id="2030" href="Taboos.WLPO.html#1883" class="Bound">v</a> <a id="2032" href="MLTT.Negation.html#635" class="Function Operator">≠</a> <a id="2034" href="CoNaturals.GenericConvergentSequence.html#6051" class="Function">∞</a><a id="2035" class="Symbol">)</a>
        <a id="2045" class="Symbol">→</a> <a id="2047" href="MLTT.Plus-Type.html#186" class="InductiveConstructor">inr</a> <a id="2051" class="Symbol">(</a><a id="2052" href="MLTT.Negation.html#1240" class="Function">contrapositive</a> <a id="2067" class="Symbol">(λ</a> <a id="2070" class="Symbol">(</a><a id="2071" href="Taboos.WLPO.html#2071" class="Bound">q</a> <a id="2073" class="Symbol">:</a> <a id="2075" href="Taboos.WLPO.html#1881" class="Bound">u</a> <a id="2077" href="MLTT.Id.html#193" class="Datatype Operator">＝</a> <a id="2079" href="Taboos.WLPO.html#1883" class="Bound">v</a><a id="2080" class="Symbol">)</a>
                                    <a id="2118" class="Symbol">→</a> <a id="2120" href="TWA.Closeness.html#8999" class="Function">ℕ∞-equal-are-infinitely-close</a> <a id="2150" href="Taboos.WLPO.html#1881" class="Bound">u</a> <a id="2152" href="Taboos.WLPO.html#1883" class="Bound">v</a> <a id="2154" href="Taboos.WLPO.html#2071" class="Bound">q</a><a id="2155" class="Symbol">)</a> <a id="2157" href="Taboos.WLPO.html#2011" class="Bound">n</a><a id="2158" class="Symbol">))</a>
 <a id="2162" class="Keyword">where</a>
  <a id="2170" class="Keyword">open</a> <a id="2175" class="Keyword">import</a> <a id="2182" href="TWA.Closeness.html" class="Module">TWA.Closeness</a> <a id="2196" href="Taboos.WLPO.html#1873" class="Bound">fe</a>

<a id="2200" class="Markup">\end{code}</a><a id="2210" class="Background">

More discussion about WLPO is included in the modules
TheTopologyOfTheUniverse and FailureOfTotalSeparatedness, among
others.

Notice that weak excluded middle implies WLPO.

</a><a id="2387" class="Markup">\begin{code}</a>

<a id="2401" class="Keyword">open</a> <a id="2406" class="Keyword">import</a> <a id="2413" href="UF.ClassicalLogic.html" class="Module">UF.ClassicalLogic</a>

<a id="WEM-gives-WLPO"></a><a id="2432" href="Taboos.WLPO.html#2432" class="Function">WEM-gives-WLPO</a> <a id="2447" class="Symbol">:</a> <a id="2449" href="UF.FunExt.html#956" class="Function">funext₀</a> <a id="2457" class="Symbol">→</a> <a id="2459" href="UF.ClassicalLogic.html#2150" class="Function">WEM</a> <a id="2463" href="Agda.Primitive.html#915" class="Primitive">𝓤₀</a> <a id="2466" class="Symbol">→</a> <a id="2468" href="Taboos.WLPO.html#1337" class="Function">WLPO</a>
<a id="2473" href="Taboos.WLPO.html#2432" class="Function">WEM-gives-WLPO</a> <a id="2488" href="Taboos.WLPO.html#2488" class="Bound">fe</a> <a id="2491" href="Taboos.WLPO.html#2491" class="Bound">wem</a> <a id="2495" href="Taboos.WLPO.html#2495" class="Bound">u</a> <a id="2497" class="Symbol">=</a> <a id="2499" href="MLTT.Plus.html#583" class="Function">Cases</a> <a id="2505" class="Symbol">(</a><a id="2506" href="Taboos.WLPO.html#2491" class="Bound">wem</a> <a id="2510" class="Symbol">(</a><a id="2511" href="Taboos.WLPO.html#2495" class="Bound">u</a> <a id="2513" href="MLTT.Id.html#193" class="Datatype Operator">＝</a> <a id="2515" href="CoNaturals.GenericConvergentSequence.html#6051" class="Function">∞</a><a id="2516" class="Symbol">))</a>
                           <a id="2546" class="Symbol">(λ</a> <a id="2549" class="Symbol">(</a><a id="2550" href="Taboos.WLPO.html#2550" class="Bound">p</a> <a id="2552" class="Symbol">:</a> <a id="2554" class="Symbol">(</a><a id="2555" href="Taboos.WLPO.html#2495" class="Bound">u</a> <a id="2557" href="MLTT.Negation.html#635" class="Function Operator">≠</a> <a id="2559" href="CoNaturals.GenericConvergentSequence.html#6051" class="Function">∞</a><a id="2560" class="Symbol">))</a>
                                 <a id="2596" class="Symbol">→</a> <a id="2598" href="MLTT.Plus-Type.html#186" class="InductiveConstructor">inr</a> <a id="2602" href="Taboos.WLPO.html#2550" class="Bound">p</a><a id="2603" class="Symbol">)</a>
                           <a id="2632" class="Symbol">(λ</a> <a id="2635" class="Symbol">(</a><a id="2636" href="Taboos.WLPO.html#2636" class="Bound">ν</a> <a id="2638" class="Symbol">:</a> <a id="2640" href="MLTT.Negation.html#278" class="Function Operator">¬</a> <a id="2642" class="Symbol">(</a><a id="2643" href="Taboos.WLPO.html#2495" class="Bound">u</a> <a id="2645" href="MLTT.Negation.html#635" class="Function Operator">≠</a> <a id="2647" href="CoNaturals.GenericConvergentSequence.html#6051" class="Function">∞</a><a id="2648" class="Symbol">))</a>
                                 <a id="2684" class="Symbol">→</a> <a id="2686" href="MLTT.Plus-Type.html#169" class="InductiveConstructor">inl</a> <a id="2690" class="Symbol">(</a><a id="2691" href="CoNaturals.GenericConvergentSequence.html#4079" class="Function">ℕ∞-is-¬¬-separated</a> <a id="2710" href="Taboos.WLPO.html#2488" class="Bound">fe</a> <a id="2713" href="Taboos.WLPO.html#2495" class="Bound">u</a> <a id="2715" href="CoNaturals.GenericConvergentSequence.html#6051" class="Function">∞</a> <a id="2717" href="Taboos.WLPO.html#2636" class="Bound">ν</a><a id="2718" class="Symbol">))</a>
<a id="2721" class="Markup">\end{code}</a><a id="2731" class="Background">

Added 15th November 2023.

</a><a id="2760" class="Markup">\begin{code}</a>

<a id="2774" class="Keyword">open</a> <a id="2779" class="Keyword">import</a> <a id="2786" href="UF.Base.html" class="Module">UF.Base</a>

<a id="WLPO-traditional"></a><a id="2795" href="Taboos.WLPO.html#2795" class="Function">WLPO-traditional</a> <a id="2812" class="Symbol">:</a> <a id="2814" href="Agda.Primitive.html#915" class="Primitive">𝓤₀</a> <a id="2817" href="MLTT.Universes.html#3976" class="Function Operator">̇</a>
<a id="2819" href="Taboos.WLPO.html#2795" class="Function">WLPO-traditional</a> <a id="2836" class="Symbol">=</a> <a id="2838" class="Symbol">(</a><a id="2839" href="Taboos.WLPO.html#2839" class="Bound">α</a> <a id="2841" class="Symbol">:</a> <a id="2843" href="MLTT.Natural-Numbers-Type.html#96" class="Datatype">ℕ</a> <a id="2845" class="Symbol">→</a> <a id="2847" href="MLTT.Two.html#124" class="Datatype">𝟚</a><a id="2848" class="Symbol">)</a> <a id="2850" class="Symbol">→</a> <a id="2852" href="MLTT.Negation.html#584" class="Function">is-decidable</a> <a id="2865" class="Symbol">((</a><a id="2867" href="Taboos.WLPO.html#2867" class="Bound">n</a> <a id="2869" class="Symbol">:</a> <a id="2871" href="MLTT.Natural-Numbers-Type.html#96" class="Datatype">ℕ</a><a id="2872" class="Symbol">)</a> <a id="2874" class="Symbol">→</a> <a id="2876" href="Taboos.WLPO.html#2839" class="Bound">α</a> <a id="2878" href="Taboos.WLPO.html#2867" class="Bound">n</a> <a id="2880" href="MLTT.Id.html#193" class="Datatype Operator">＝</a> <a id="2882" href="MLTT.Two.html#147" class="InductiveConstructor">₁</a><a id="2883" class="Symbol">)</a>

<a id="2886" class="Keyword">open</a> <a id="2891" class="Keyword">import</a> <a id="2898" href="MLTT.Two-Properties.html" class="Module">MLTT.Two-Properties</a>

<a id="WLPO-gives-WLPO-traditional"></a><a id="2919" href="Taboos.WLPO.html#2919" class="Function">WLPO-gives-WLPO-traditional</a> <a id="2947" class="Symbol">:</a> <a id="2949" href="UF.FunExt.html#850" class="Function">funext</a> <a id="2956" href="Agda.Primitive.html#915" class="Primitive">𝓤₀</a> <a id="2959" href="Agda.Primitive.html#915" class="Primitive">𝓤₀</a> <a id="2962" class="Symbol">→</a> <a id="2964" href="Taboos.WLPO.html#1337" class="Function">WLPO</a> <a id="2969" class="Symbol">→</a> <a id="2971" href="Taboos.WLPO.html#2795" class="Function">WLPO-traditional</a>
<a id="2988" href="Taboos.WLPO.html#2919" class="Function">WLPO-gives-WLPO-traditional</a> <a id="3016" href="Taboos.WLPO.html#3016" class="Bound">fe</a> <a id="3019" href="Taboos.WLPO.html#3019" class="Bound">wlpo</a> <a id="3024" href="Taboos.WLPO.html#3024" class="Bound">α</a> <a id="3026" class="Symbol">=</a> <a id="3028" href="Taboos.WLPO.html#4019" class="Function">IV</a>
 <a id="3032" class="Keyword">where</a>
  <a id="3040" href="Taboos.WLPO.html#3040" class="Function">I</a> <a id="3042" class="Symbol">:</a> <a id="3044" class="Symbol">(</a><a id="3045" href="CoNaturals.GenericConvergentSequence.html#2883" class="Function">ℕ→𝟚-to-ℕ∞</a> <a id="3055" href="Taboos.WLPO.html#3024" class="Bound">α</a> <a id="3057" href="MLTT.Id.html#193" class="Datatype Operator">＝</a> <a id="3059" href="CoNaturals.GenericConvergentSequence.html#6051" class="Function">∞</a><a id="3060" class="Symbol">)</a> <a id="3062" href="MLTT.Plus-Type.html#120" class="Datatype Operator">+</a> <a id="3064" class="Symbol">(</a><a id="3065" href="CoNaturals.GenericConvergentSequence.html#2883" class="Function">ℕ→𝟚-to-ℕ∞</a> <a id="3075" href="Taboos.WLPO.html#3024" class="Bound">α</a> <a id="3077" href="MLTT.Negation.html#635" class="Function Operator">≠</a> <a id="3079" href="CoNaturals.GenericConvergentSequence.html#6051" class="Function">∞</a><a id="3080" class="Symbol">)</a>
  <a id="3084" href="Taboos.WLPO.html#3040" class="Function">I</a> <a id="3086" class="Symbol">=</a> <a id="3088" href="Taboos.WLPO.html#3019" class="Bound">wlpo</a> <a id="3093" class="Symbol">(</a><a id="3094" href="CoNaturals.GenericConvergentSequence.html#2883" class="Function">ℕ→𝟚-to-ℕ∞</a> <a id="3104" href="Taboos.WLPO.html#3024" class="Bound">α</a><a id="3105" class="Symbol">)</a>

  <a id="3110" href="Taboos.WLPO.html#3110" class="Function">II</a> <a id="3113" class="Symbol">:</a>  <a id="3116" href="CoNaturals.GenericConvergentSequence.html#2883" class="Function">ℕ→𝟚-to-ℕ∞</a> <a id="3126" href="Taboos.WLPO.html#3024" class="Bound">α</a> <a id="3128" href="MLTT.Id.html#193" class="Datatype Operator">＝</a> <a id="3130" href="CoNaturals.GenericConvergentSequence.html#6051" class="Function">∞</a> <a id="3132" class="Symbol">→</a> <a id="3134" class="Symbol">(</a><a id="3135" href="Taboos.WLPO.html#3135" class="Bound">n</a> <a id="3137" class="Symbol">:</a> <a id="3139" href="MLTT.Natural-Numbers-Type.html#96" class="Datatype">ℕ</a><a id="3140" class="Symbol">)</a> <a id="3142" class="Symbol">→</a> <a id="3144" href="Taboos.WLPO.html#3024" class="Bound">α</a> <a id="3146" href="Taboos.WLPO.html#3135" class="Bound">n</a> <a id="3148" href="MLTT.Id.html#193" class="Datatype Operator">＝</a> <a id="3150" href="MLTT.Two.html#147" class="InductiveConstructor">₁</a>
  <a id="3154" href="Taboos.WLPO.html#3110" class="Function">II</a> <a id="3157" href="Taboos.WLPO.html#3157" class="Bound">p</a> <a id="3159" href="Taboos.WLPO.html#3159" class="Bound">n</a> <a id="3161" class="Symbol">=</a> <a id="3163" href="Taboos.WLPO.html#3319" class="Function">II₂</a>
   <a id="3170" class="Keyword">where</a>
    <a id="3180" href="Taboos.WLPO.html#3180" class="Function">II₀</a> <a id="3184" class="Symbol">:</a> <a id="3186" href="CoNaturals.GenericConvergentSequence.html#1380" class="Function">ℕ∞-to-ℕ→𝟚</a> <a id="3196" class="Symbol">(</a><a id="3197" href="CoNaturals.GenericConvergentSequence.html#2883" class="Function">ℕ→𝟚-to-ℕ∞</a> <a id="3207" href="Taboos.WLPO.html#3024" class="Bound">α</a><a id="3208" class="Symbol">)</a> <a id="3210" href="MLTT.Id.html#193" class="Datatype Operator">＝</a> <a id="3212" href="CoNaturals.GenericConvergentSequence.html#1380" class="Function">ℕ∞-to-ℕ→𝟚</a> <a id="3222" href="CoNaturals.GenericConvergentSequence.html#6051" class="Function">∞</a>
    <a id="3228" href="Taboos.WLPO.html#3180" class="Function">II₀</a> <a id="3232" class="Symbol">=</a> <a id="3234" href="MLTT.Id.html#1136" class="Function">ap</a> <a id="3237" href="CoNaturals.GenericConvergentSequence.html#1380" class="Function">ℕ∞-to-ℕ→𝟚</a> <a id="3247" href="Taboos.WLPO.html#3157" class="Bound">p</a>

    <a id="3254" href="Taboos.WLPO.html#3254" class="Function">II₁</a> <a id="3258" class="Symbol">:</a> <a id="3260" href="CoNaturals.GenericConvergentSequence.html#1771" class="Function">force-decreasing</a> <a id="3277" href="Taboos.WLPO.html#3024" class="Bound">α</a> <a id="3279" href="Taboos.WLPO.html#3159" class="Bound">n</a> <a id="3281" href="MLTT.Id.html#193" class="Datatype Operator">＝</a> <a id="3283" href="MLTT.Two.html#147" class="InductiveConstructor">₁</a>
    <a id="3289" href="Taboos.WLPO.html#3254" class="Function">II₁</a> <a id="3293" class="Symbol">=</a> <a id="3295" href="MLTT.Id.html#1136" class="Function">ap</a> <a id="3298" class="Symbol">(λ</a> <a id="3301" href="Taboos.WLPO.html#3301" class="Bound">-</a> <a id="3303" class="Symbol">→</a> <a id="3305" href="Taboos.WLPO.html#3301" class="Bound">-</a> <a id="3307" href="Taboos.WLPO.html#3159" class="Bound">n</a><a id="3308" class="Symbol">)</a> <a id="3310" href="Taboos.WLPO.html#3180" class="Function">II₀</a>

    <a id="3319" href="Taboos.WLPO.html#3319" class="Function">II₂</a> <a id="3323" class="Symbol">:</a> <a id="3325" href="Taboos.WLPO.html#3024" class="Bound">α</a> <a id="3327" href="Taboos.WLPO.html#3159" class="Bound">n</a> <a id="3329" href="MLTT.Id.html#193" class="Datatype Operator">＝</a> <a id="3331" href="MLTT.Two.html#147" class="InductiveConstructor">₁</a>
    <a id="3337" href="Taboos.WLPO.html#3319" class="Function">II₂</a> <a id="3341" class="Symbol">=</a> <a id="3343" href="MLTT.Two-Properties.html#5151" class="Function">≤₂-criterion-converse</a> <a id="3365" class="Symbol">(</a><a id="3366" href="CoNaturals.GenericConvergentSequence.html#3302" class="Function">force-decreasing-is-smaller</a> <a id="3394" href="Taboos.WLPO.html#3024" class="Bound">α</a> <a id="3396" href="Taboos.WLPO.html#3159" class="Bound">n</a><a id="3397" class="Symbol">)</a> <a id="3399" href="Taboos.WLPO.html#3254" class="Function">II₁</a>

  <a id="3406" href="Taboos.WLPO.html#3406" class="Function">III</a> <a id="3410" class="Symbol">:</a> <a id="3412" class="Symbol">((</a><a id="3414" href="Taboos.WLPO.html#3414" class="Bound">n</a> <a id="3416" class="Symbol">:</a> <a id="3418" href="MLTT.Natural-Numbers-Type.html#96" class="Datatype">ℕ</a><a id="3419" class="Symbol">)</a> <a id="3421" class="Symbol">→</a> <a id="3423" href="Taboos.WLPO.html#3024" class="Bound">α</a> <a id="3425" href="Taboos.WLPO.html#3414" class="Bound">n</a> <a id="3427" href="MLTT.Id.html#193" class="Datatype Operator">＝</a> <a id="3429" href="MLTT.Two.html#147" class="InductiveConstructor">₁</a><a id="3430" class="Symbol">)</a> <a id="3432" class="Symbol">→</a> <a id="3434" href="CoNaturals.GenericConvergentSequence.html#2883" class="Function">ℕ→𝟚-to-ℕ∞</a> <a id="3444" href="Taboos.WLPO.html#3024" class="Bound">α</a> <a id="3446" href="MLTT.Id.html#193" class="Datatype Operator">＝</a> <a id="3448" href="CoNaturals.GenericConvergentSequence.html#6051" class="Function">∞</a>
  <a id="3452" href="Taboos.WLPO.html#3406" class="Function">III</a> <a id="3456" href="Taboos.WLPO.html#3456" class="Bound">ϕ</a> <a id="3458" class="Symbol">=</a> <a id="3460" href="CoNaturals.GenericConvergentSequence.html#1521" class="Function">ℕ∞-to-ℕ→𝟚-lc</a> <a id="3473" href="Taboos.WLPO.html#3016" class="Bound">fe</a> <a id="3476" class="Symbol">(</a><a id="3477" href="UF.FunExt.html#1242" class="Function">dfunext</a> <a id="3485" href="Taboos.WLPO.html#3016" class="Bound">fe</a> <a id="3488" href="Taboos.WLPO.html#3733" class="Function">III₁</a><a id="3492" class="Symbol">)</a>
   <a id="3497" class="Keyword">where</a>
    <a id="3507" href="Taboos.WLPO.html#3507" class="Function">III₀</a> <a id="3512" class="Symbol">:</a> <a id="3514" class="Symbol">(</a><a id="3515" href="Taboos.WLPO.html#3515" class="Bound">n</a> <a id="3517" class="Symbol">:</a> <a id="3519" href="MLTT.Natural-Numbers-Type.html#96" class="Datatype">ℕ</a><a id="3520" class="Symbol">)</a> <a id="3522" class="Symbol">→</a> <a id="3524" href="CoNaturals.GenericConvergentSequence.html#1771" class="Function">force-decreasing</a> <a id="3541" href="Taboos.WLPO.html#3024" class="Bound">α</a> <a id="3543" href="Taboos.WLPO.html#3515" class="Bound">n</a> <a id="3545" href="MLTT.Id.html#193" class="Datatype Operator">＝</a> <a id="3547" href="Taboos.WLPO.html#3024" class="Bound">α</a> <a id="3549" href="Taboos.WLPO.html#3515" class="Bound">n</a>
    <a id="3555" href="Taboos.WLPO.html#3507" class="Function">III₀</a> <a id="3560" class="Symbol">=</a> <a id="3562" href="CoNaturals.GenericConvergentSequence.html#2257" class="Function">force-decreasing-unchanged</a> <a id="3589" href="Taboos.WLPO.html#3024" class="Bound">α</a>
            <a id="3603" class="Symbol">(λ</a> <a id="3606" href="Taboos.WLPO.html#3606" class="Bound">i</a> <a id="3608" class="Symbol">→</a> <a id="3610" href="UF.Base.html#1753" class="Function">transport₂</a> <a id="3621" href="MLTT.Two-Properties.html#4271" class="Function Operator">_≤₂_</a>
                    <a id="3646" class="Symbol">((</a><a id="3648" href="Taboos.WLPO.html#3456" class="Bound">ϕ</a> <a id="3650" class="Symbol">(</a><a id="3651" href="MLTT.Natural-Numbers-Type.html#123" class="InductiveConstructor">succ</a> <a id="3656" href="Taboos.WLPO.html#3606" class="Bound">i</a><a id="3657" class="Symbol">))</a><a id="3659" href="MLTT.Id.html#1055" class="Function Operator">⁻¹</a><a id="3661" class="Symbol">)</a>
                    <a id="3683" class="Symbol">((</a><a id="3685" href="Taboos.WLPO.html#3456" class="Bound">ϕ</a> <a id="3687" href="Taboos.WLPO.html#3606" class="Bound">i</a><a id="3688" class="Symbol">)</a><a id="3689" href="MLTT.Id.html#1055" class="Function Operator">⁻¹</a><a id="3691" class="Symbol">)</a>
                    <a id="3713" class="Symbol">(</a><a id="3714" href="MLTT.Two-Properties.html#6429" class="Function">≤₂-refl</a> <a id="3722" class="Symbol">{</a><a id="3723" href="MLTT.Two.html#147" class="InductiveConstructor">₁</a><a id="3724" class="Symbol">}))</a>

    <a id="3733" href="Taboos.WLPO.html#3733" class="Function">III₁</a> <a id="3738" class="Symbol">:</a> <a id="3740" href="CoNaturals.GenericConvergentSequence.html#1380" class="Function">ℕ∞-to-ℕ→𝟚</a> <a id="3750" class="Symbol">(</a><a id="3751" href="CoNaturals.GenericConvergentSequence.html#2883" class="Function">ℕ→𝟚-to-ℕ∞</a> <a id="3761" href="Taboos.WLPO.html#3024" class="Bound">α</a><a id="3762" class="Symbol">)</a> <a id="3764" href="MLTT.Id.html#1425" class="Function Operator">∼</a> <a id="3766" href="CoNaturals.GenericConvergentSequence.html#1380" class="Function">ℕ∞-to-ℕ→𝟚</a> <a id="3776" href="CoNaturals.GenericConvergentSequence.html#6051" class="Function">∞</a>
    <a id="3782" href="Taboos.WLPO.html#3733" class="Function">III₁</a> <a id="3787" href="Taboos.WLPO.html#3787" class="Bound">n</a> <a id="3789" class="Symbol">=</a> <a id="3791" href="CoNaturals.GenericConvergentSequence.html#1380" class="Function">ℕ∞-to-ℕ→𝟚</a> <a id="3801" class="Symbol">(</a><a id="3802" href="CoNaturals.GenericConvergentSequence.html#2883" class="Function">ℕ→𝟚-to-ℕ∞</a> <a id="3812" href="Taboos.WLPO.html#3024" class="Bound">α</a><a id="3813" class="Symbol">)</a> <a id="3815" href="Taboos.WLPO.html#3787" class="Bound">n</a> <a id="3817" href="MLTT.Id.html#2239" class="Function Operator">＝⟨</a> <a id="3820" href="MLTT.Identity-Type.html#157" class="InductiveConstructor">refl</a> <a id="3825" href="MLTT.Id.html#2239" class="Function Operator">⟩</a>
             <a id="3840" href="CoNaturals.GenericConvergentSequence.html#1771" class="Function">force-decreasing</a> <a id="3857" href="Taboos.WLPO.html#3024" class="Bound">α</a> <a id="3859" href="Taboos.WLPO.html#3787" class="Bound">n</a>      <a id="3866" href="MLTT.Id.html#2239" class="Function Operator">＝⟨</a> <a id="3869" href="Taboos.WLPO.html#3507" class="Function">III₀</a> <a id="3874" href="Taboos.WLPO.html#3787" class="Bound">n</a> <a id="3876" href="MLTT.Id.html#2239" class="Function Operator">⟩</a>
             <a id="3891" href="Taboos.WLPO.html#3024" class="Bound">α</a> <a id="3893" href="Taboos.WLPO.html#3787" class="Bound">n</a>                       <a id="3917" href="MLTT.Id.html#2239" class="Function Operator">＝⟨</a> <a id="3920" href="Taboos.WLPO.html#3456" class="Bound">ϕ</a> <a id="3922" href="Taboos.WLPO.html#3787" class="Bound">n</a> <a id="3924" href="MLTT.Id.html#2239" class="Function Operator">⟩</a>
             <a id="3939" href="MLTT.Two.html#147" class="InductiveConstructor">₁</a>                         <a id="3965" href="MLTT.Id.html#2239" class="Function Operator">＝⟨</a> <a id="3968" href="MLTT.Identity-Type.html#157" class="InductiveConstructor">refl</a> <a id="3973" href="MLTT.Id.html#2239" class="Function Operator">⟩</a>
             <a id="3988" href="CoNaturals.GenericConvergentSequence.html#1380" class="Function">ℕ∞-to-ℕ→𝟚</a> <a id="3998" href="CoNaturals.GenericConvergentSequence.html#6051" class="Function">∞</a> <a id="4000" href="Taboos.WLPO.html#3787" class="Bound">n</a>             <a id="4014" href="MLTT.Id.html#2321" class="Function Operator">∎</a>

  <a id="4019" href="Taboos.WLPO.html#4019" class="Function">IV</a> <a id="4022" class="Symbol">:</a> <a id="4024" href="MLTT.Negation.html#584" class="Function">is-decidable</a> <a id="4037" class="Symbol">((</a><a id="4039" href="Taboos.WLPO.html#4039" class="Bound">n</a> <a id="4041" class="Symbol">:</a> <a id="4043" href="MLTT.Natural-Numbers-Type.html#96" class="Datatype">ℕ</a><a id="4044" class="Symbol">)</a> <a id="4046" class="Symbol">→</a> <a id="4048" href="Taboos.WLPO.html#3024" class="Bound">α</a> <a id="4050" href="Taboos.WLPO.html#4039" class="Bound">n</a> <a id="4052" href="MLTT.Id.html#193" class="Datatype Operator">＝</a> <a id="4054" href="MLTT.Two.html#147" class="InductiveConstructor">₁</a><a id="4055" class="Symbol">)</a>
  <a id="4059" href="Taboos.WLPO.html#4019" class="Function">IV</a> <a id="4062" class="Symbol">=</a> <a id="4064" href="NotionsOfDecidability.Decidable.html#410" class="Function">map-decidable</a> <a id="4078" href="Taboos.WLPO.html#3110" class="Function">II</a> <a id="4081" href="Taboos.WLPO.html#3406" class="Function">III</a> <a id="4085" href="Taboos.WLPO.html#3040" class="Function">I</a>

<a id="WLPO-traditional-gives-WLPO"></a><a id="4088" href="Taboos.WLPO.html#4088" class="Function">WLPO-traditional-gives-WLPO</a> <a id="4116" class="Symbol">:</a> <a id="4118" href="UF.FunExt.html#956" class="Function">funext₀</a> <a id="4126" class="Symbol">→</a> <a id="4128" href="Taboos.WLPO.html#2795" class="Function">WLPO-traditional</a> <a id="4145" class="Symbol">→</a> <a id="4147" href="Taboos.WLPO.html#1337" class="Function">WLPO</a>
<a id="4152" href="Taboos.WLPO.html#4088" class="Function">WLPO-traditional-gives-WLPO</a> <a id="4180" href="Taboos.WLPO.html#4180" class="Bound">fe</a> <a id="4183" href="Taboos.WLPO.html#4183" class="Bound">wlpot</a> <a id="4189" href="Taboos.WLPO.html#4189" class="Bound">u</a> <a id="4191" class="Symbol">=</a> <a id="4193" href="Taboos.WLPO.html#4452" class="Function">IV</a>
 <a id="4197" class="Keyword">where</a>
  <a id="4205" href="Taboos.WLPO.html#4205" class="Function">I</a> <a id="4207" class="Symbol">:</a> <a id="4209" href="MLTT.Negation.html#584" class="Function">is-decidable</a> <a id="4222" class="Symbol">((</a><a id="4224" href="Taboos.WLPO.html#4224" class="Bound">n</a> <a id="4226" class="Symbol">:</a> <a id="4228" href="MLTT.Natural-Numbers-Type.html#96" class="Datatype">ℕ</a><a id="4229" class="Symbol">)</a> <a id="4231" class="Symbol">→</a> <a id="4233" href="CoNaturals.GenericConvergentSequence.html#1380" class="Function">ℕ∞-to-ℕ→𝟚</a> <a id="4243" href="Taboos.WLPO.html#4189" class="Bound">u</a> <a id="4245" href="Taboos.WLPO.html#4224" class="Bound">n</a> <a id="4247" href="MLTT.Id.html#193" class="Datatype Operator">＝</a> <a id="4249" href="MLTT.Two.html#147" class="InductiveConstructor">₁</a><a id="4250" class="Symbol">)</a>
  <a id="4254" href="Taboos.WLPO.html#4205" class="Function">I</a> <a id="4256" class="Symbol">=</a> <a id="4258" href="Taboos.WLPO.html#4183" class="Bound">wlpot</a> <a id="4264" class="Symbol">(</a><a id="4265" href="CoNaturals.GenericConvergentSequence.html#1380" class="Function">ℕ∞-to-ℕ→𝟚</a> <a id="4275" href="Taboos.WLPO.html#4189" class="Bound">u</a><a id="4276" class="Symbol">)</a>

  <a id="4281" href="Taboos.WLPO.html#4281" class="Function">II</a> <a id="4284" class="Symbol">:</a> <a id="4286" class="Symbol">((</a><a id="4288" href="Taboos.WLPO.html#4288" class="Bound">n</a> <a id="4290" class="Symbol">:</a> <a id="4292" href="MLTT.Natural-Numbers-Type.html#96" class="Datatype">ℕ</a><a id="4293" class="Symbol">)</a> <a id="4295" class="Symbol">→</a> <a id="4297" href="CoNaturals.GenericConvergentSequence.html#1380" class="Function">ℕ∞-to-ℕ→𝟚</a> <a id="4307" href="Taboos.WLPO.html#4189" class="Bound">u</a> <a id="4309" href="Taboos.WLPO.html#4288" class="Bound">n</a> <a id="4311" href="MLTT.Id.html#193" class="Datatype Operator">＝</a> <a id="4313" href="MLTT.Two.html#147" class="InductiveConstructor">₁</a><a id="4314" class="Symbol">)</a> <a id="4316" class="Symbol">→</a> <a id="4318" href="Taboos.WLPO.html#4189" class="Bound">u</a> <a id="4320" href="MLTT.Id.html#193" class="Datatype Operator">＝</a> <a id="4322" href="CoNaturals.GenericConvergentSequence.html#6051" class="Function">∞</a>
  <a id="4326" href="Taboos.WLPO.html#4281" class="Function">II</a> <a id="4329" href="Taboos.WLPO.html#4329" class="Bound">ϕ</a> <a id="4331" class="Symbol">=</a> <a id="4333" href="CoNaturals.GenericConvergentSequence.html#1521" class="Function">ℕ∞-to-ℕ→𝟚-lc</a> <a id="4346" href="Taboos.WLPO.html#4180" class="Bound">fe</a> <a id="4349" class="Symbol">(</a><a id="4350" href="UF.FunExt.html#1242" class="Function">dfunext</a> <a id="4358" href="Taboos.WLPO.html#4180" class="Bound">fe</a> <a id="4361" href="Taboos.WLPO.html#4329" class="Bound">ϕ</a><a id="4362" class="Symbol">)</a>

  <a id="4367" href="Taboos.WLPO.html#4367" class="Function">III</a> <a id="4371" class="Symbol">:</a>  <a id="4374" href="Taboos.WLPO.html#4189" class="Bound">u</a> <a id="4376" href="MLTT.Id.html#193" class="Datatype Operator">＝</a> <a id="4378" href="CoNaturals.GenericConvergentSequence.html#6051" class="Function">∞</a> <a id="4380" class="Symbol">→</a> <a id="4382" class="Symbol">(</a><a id="4383" href="Taboos.WLPO.html#4383" class="Bound">n</a> <a id="4385" class="Symbol">:</a> <a id="4387" href="MLTT.Natural-Numbers-Type.html#96" class="Datatype">ℕ</a><a id="4388" class="Symbol">)</a> <a id="4390" class="Symbol">→</a> <a id="4392" href="CoNaturals.GenericConvergentSequence.html#1380" class="Function">ℕ∞-to-ℕ→𝟚</a> <a id="4402" href="Taboos.WLPO.html#4189" class="Bound">u</a> <a id="4404" href="Taboos.WLPO.html#4383" class="Bound">n</a> <a id="4406" href="MLTT.Id.html#193" class="Datatype Operator">＝</a> <a id="4408" href="MLTT.Two.html#147" class="InductiveConstructor">₁</a>
  <a id="4412" href="Taboos.WLPO.html#4367" class="Function">III</a> <a id="4416" href="Taboos.WLPO.html#4416" class="Bound">e</a> <a id="4418" href="Taboos.WLPO.html#4418" class="Bound">n</a> <a id="4420" class="Symbol">=</a> <a id="4422" href="MLTT.Id.html#1136" class="Function">ap</a> <a id="4425" class="Symbol">(λ</a> <a id="4428" href="Taboos.WLPO.html#4428" class="Bound">-</a> <a id="4430" class="Symbol">→</a> <a id="4432" href="CoNaturals.GenericConvergentSequence.html#1380" class="Function">ℕ∞-to-ℕ→𝟚</a> <a id="4442" href="Taboos.WLPO.html#4428" class="Bound">-</a> <a id="4444" href="Taboos.WLPO.html#4418" class="Bound">n</a><a id="4445" class="Symbol">)</a> <a id="4447" href="Taboos.WLPO.html#4416" class="Bound">e</a>

  <a id="4452" href="Taboos.WLPO.html#4452" class="Function">IV</a> <a id="4455" class="Symbol">:</a> <a id="4457" class="Symbol">(</a><a id="4458" href="Taboos.WLPO.html#4189" class="Bound">u</a> <a id="4460" href="MLTT.Id.html#193" class="Datatype Operator">＝</a> <a id="4462" href="CoNaturals.GenericConvergentSequence.html#6051" class="Function">∞</a><a id="4463" class="Symbol">)</a> <a id="4465" href="MLTT.Plus-Type.html#120" class="Datatype Operator">+</a> <a id="4467" class="Symbol">(</a><a id="4468" href="Taboos.WLPO.html#4189" class="Bound">u</a> <a id="4470" href="MLTT.Negation.html#635" class="Function Operator">≠</a> <a id="4472" href="CoNaturals.GenericConvergentSequence.html#6051" class="Function">∞</a><a id="4473" class="Symbol">)</a>
  <a id="4477" href="Taboos.WLPO.html#4452" class="Function">IV</a> <a id="4480" class="Symbol">=</a> <a id="4482" href="NotionsOfDecidability.Decidable.html#410" class="Function">map-decidable</a> <a id="4496" href="Taboos.WLPO.html#4281" class="Function">II</a> <a id="4499" href="Taboos.WLPO.html#4367" class="Function">III</a> <a id="4503" href="Taboos.WLPO.html#4205" class="Function">I</a>

<a id="4506" class="Markup">\end{code}</a><a id="4516" class="Background">

Added 9th September 2024. WLPO amounts to saying that the constancy of
a binary sequence is decidable.

</a><a id="4622" class="Markup">\begin{code}</a>

<a id="WLPO-variation"></a><a id="4636" href="Taboos.WLPO.html#4636" class="Function">WLPO-variation</a> <a id="4651" class="Symbol">:</a> <a id="4653" href="Agda.Primitive.html#915" class="Primitive">𝓤₀</a> <a id="4656" href="MLTT.Universes.html#3976" class="Function Operator">̇</a>
<a id="4658" href="Taboos.WLPO.html#4636" class="Function">WLPO-variation</a> <a id="4673" class="Symbol">=</a> <a id="4675" class="Symbol">(</a><a id="4676" href="Taboos.WLPO.html#4676" class="Bound">α</a> <a id="4678" class="Symbol">:</a> <a id="4680" href="MLTT.Natural-Numbers-Type.html#96" class="Datatype">ℕ</a> <a id="4682" class="Symbol">→</a> <a id="4684" href="MLTT.Two.html#124" class="Datatype">𝟚</a><a id="4685" class="Symbol">)</a> <a id="4687" class="Symbol">→</a> <a id="4689" href="MLTT.Negation.html#584" class="Function">is-decidable</a> <a id="4702" class="Symbol">((</a><a id="4704" href="Taboos.WLPO.html#4704" class="Bound">n</a> <a id="4706" class="Symbol">:</a> <a id="4708" href="MLTT.Natural-Numbers-Type.html#96" class="Datatype">ℕ</a><a id="4709" class="Symbol">)</a> <a id="4711" class="Symbol">→</a> <a id="4713" href="Taboos.WLPO.html#4676" class="Bound">α</a> <a id="4715" href="Taboos.WLPO.html#4704" class="Bound">n</a> <a id="4717" href="MLTT.Id.html#193" class="Datatype Operator">＝</a> <a id="4719" href="Taboos.WLPO.html#4676" class="Bound">α</a> <a id="4721" class="Number">0</a><a id="4722" class="Symbol">)</a>

<a id="WLPO-variation-gives-WLPO-traditional"></a><a id="4725" href="Taboos.WLPO.html#4725" class="Function">WLPO-variation-gives-WLPO-traditional</a>
 <a id="4764" class="Symbol">:</a> <a id="4766" href="Taboos.WLPO.html#4636" class="Function">WLPO-variation</a>
 <a id="4782" class="Symbol">→</a> <a id="4784" href="Taboos.WLPO.html#2795" class="Function">WLPO-traditional</a>
<a id="4801" href="Taboos.WLPO.html#4725" class="Function">WLPO-variation-gives-WLPO-traditional</a> <a id="4839" href="Taboos.WLPO.html#4839" class="Bound">wlpov</a> <a id="4845" href="Taboos.WLPO.html#4845" class="Bound">α</a>
 <a id="4848" class="Symbol">=</a> <a id="4850" href="MLTT.Two-Properties.html#534" class="Function">𝟚-equality-cases</a> <a id="4867" href="Taboos.WLPO.html#4881" class="Function">I</a> <a id="4869" href="Taboos.WLPO.html#5106" class="Function">II</a>
 <a id="4873" class="Keyword">where</a>
  <a id="4881" href="Taboos.WLPO.html#4881" class="Function">I</a> <a id="4883" class="Symbol">:</a> <a id="4885" href="Taboos.WLPO.html#4845" class="Bound">α</a> <a id="4887" class="Number">0</a> <a id="4889" href="MLTT.Id.html#193" class="Datatype Operator">＝</a> <a id="4891" href="MLTT.Two.html#140" class="InductiveConstructor">₀</a> <a id="4893" class="Symbol">→</a> <a id="4895" class="Symbol">((</a><a id="4897" href="Taboos.WLPO.html#4897" class="Bound">n</a> <a id="4899" class="Symbol">:</a> <a id="4901" href="MLTT.Natural-Numbers-Type.html#96" class="Datatype">ℕ</a><a id="4902" class="Symbol">)</a> <a id="4904" class="Symbol">→</a> <a id="4906" href="Taboos.WLPO.html#4845" class="Bound">α</a> <a id="4908" href="Taboos.WLPO.html#4897" class="Bound">n</a> <a id="4910" href="MLTT.Id.html#193" class="Datatype Operator">＝</a> <a id="4912" href="MLTT.Two.html#147" class="InductiveConstructor">₁</a><a id="4913" class="Symbol">)</a> <a id="4915" href="MLTT.Plus-Type.html#120" class="Datatype Operator">+</a> <a id="4917" href="MLTT.Negation.html#278" class="Function Operator">¬</a> <a id="4919" class="Symbol">((</a><a id="4921" href="Taboos.WLPO.html#4921" class="Bound">n</a> <a id="4923" class="Symbol">:</a> <a id="4925" href="MLTT.Natural-Numbers-Type.html#96" class="Datatype">ℕ</a><a id="4926" class="Symbol">)</a> <a id="4928" class="Symbol">→</a> <a id="4930" href="Taboos.WLPO.html#4845" class="Bound">α</a> <a id="4932" href="Taboos.WLPO.html#4921" class="Bound">n</a> <a id="4934" href="MLTT.Id.html#193" class="Datatype Operator">＝</a> <a id="4936" href="MLTT.Two.html#147" class="InductiveConstructor">₁</a><a id="4937" class="Symbol">)</a>
  <a id="4941" href="Taboos.WLPO.html#4881" class="Function">I</a> <a id="4943" href="Taboos.WLPO.html#4943" class="Bound">p</a> <a id="4945" class="Symbol">=</a> <a id="4947" href="MLTT.Plus-Type.html#186" class="InductiveConstructor">inr</a> <a id="4951" class="Symbol">(λ</a> <a id="4954" class="Symbol">(</a><a id="4955" href="Taboos.WLPO.html#4955" class="Bound">ϕ</a> <a id="4957" class="Symbol">:</a> <a id="4959" class="Symbol">(</a><a id="4960" href="Taboos.WLPO.html#4960" class="Bound">n</a> <a id="4962" class="Symbol">:</a> <a id="4964" href="MLTT.Natural-Numbers-Type.html#96" class="Datatype">ℕ</a><a id="4965" class="Symbol">)</a> <a id="4967" class="Symbol">→</a> <a id="4969" href="Taboos.WLPO.html#4845" class="Bound">α</a> <a id="4971" href="Taboos.WLPO.html#4960" class="Bound">n</a> <a id="4973" href="MLTT.Id.html#193" class="Datatype Operator">＝</a> <a id="4975" href="MLTT.Two.html#147" class="InductiveConstructor">₁</a><a id="4976" class="Symbol">)</a>
               <a id="4993" class="Symbol">→</a> <a id="4995" href="MLTT.Two-Properties.html#2184" class="Function">zero-is-not-one</a>
                  <a id="5029" class="Symbol">(</a><a id="5030" href="MLTT.Two.html#140" class="InductiveConstructor">₀</a>   <a id="5034" href="MLTT.Id.html#2239" class="Function Operator">＝⟨</a> <a id="5037" href="Taboos.WLPO.html#4943" class="Bound">p</a> <a id="5039" href="MLTT.Id.html#1055" class="Function Operator">⁻¹</a> <a id="5042" href="MLTT.Id.html#2239" class="Function Operator">⟩</a>
                   <a id="5063" href="Taboos.WLPO.html#4845" class="Bound">α</a> <a id="5065" class="Number">0</a> <a id="5067" href="MLTT.Id.html#2239" class="Function Operator">＝⟨</a> <a id="5070" href="Taboos.WLPO.html#4955" class="Bound">ϕ</a> <a id="5072" class="Number">0</a> <a id="5074" href="MLTT.Id.html#2239" class="Function Operator">⟩</a>
                   <a id="5095" href="MLTT.Two.html#147" class="InductiveConstructor">₁</a>   <a id="5099" href="MLTT.Id.html#2321" class="Function Operator">∎</a><a id="5100" class="Symbol">))</a>

  <a id="5106" href="Taboos.WLPO.html#5106" class="Function">II</a> <a id="5109" class="Symbol">:</a> <a id="5111" href="Taboos.WLPO.html#4845" class="Bound">α</a> <a id="5113" class="Number">0</a> <a id="5115" href="MLTT.Id.html#193" class="Datatype Operator">＝</a> <a id="5117" href="MLTT.Two.html#147" class="InductiveConstructor">₁</a> <a id="5119" class="Symbol">→</a> <a id="5121" class="Symbol">((</a><a id="5123" href="Taboos.WLPO.html#5123" class="Bound">n</a> <a id="5125" class="Symbol">:</a> <a id="5127" href="MLTT.Natural-Numbers-Type.html#96" class="Datatype">ℕ</a><a id="5128" class="Symbol">)</a> <a id="5130" class="Symbol">→</a> <a id="5132" href="Taboos.WLPO.html#4845" class="Bound">α</a> <a id="5134" href="Taboos.WLPO.html#5123" class="Bound">n</a> <a id="5136" href="MLTT.Id.html#193" class="Datatype Operator">＝</a> <a id="5138" href="MLTT.Two.html#147" class="InductiveConstructor">₁</a><a id="5139" class="Symbol">)</a> <a id="5141" href="MLTT.Plus-Type.html#120" class="Datatype Operator">+</a> <a id="5143" href="MLTT.Negation.html#278" class="Function Operator">¬</a> <a id="5145" class="Symbol">((</a><a id="5147" href="Taboos.WLPO.html#5147" class="Bound">n</a> <a id="5149" class="Symbol">:</a> <a id="5151" href="MLTT.Natural-Numbers-Type.html#96" class="Datatype">ℕ</a><a id="5152" class="Symbol">)</a> <a id="5154" class="Symbol">→</a> <a id="5156" href="Taboos.WLPO.html#4845" class="Bound">α</a> <a id="5158" href="Taboos.WLPO.html#5147" class="Bound">n</a> <a id="5160" href="MLTT.Id.html#193" class="Datatype Operator">＝</a> <a id="5162" href="MLTT.Two.html#147" class="InductiveConstructor">₁</a><a id="5163" class="Symbol">)</a>
  <a id="5167" href="Taboos.WLPO.html#5106" class="Function">II</a> <a id="5170" href="Taboos.WLPO.html#5170" class="Bound">p</a> <a id="5172" class="Symbol">=</a> <a id="5174" href="NotionsOfDecidability.Decidable.html#410" class="Function">map-decidable</a>
          <a id="5198" class="Symbol">(λ</a> <a id="5201" class="Symbol">(</a><a id="5202" href="Taboos.WLPO.html#5202" class="Bound">ϕ</a> <a id="5204" class="Symbol">:</a> <a id="5206" class="Symbol">(</a><a id="5207" href="Taboos.WLPO.html#5207" class="Bound">n</a> <a id="5209" class="Symbol">:</a> <a id="5211" href="MLTT.Natural-Numbers-Type.html#96" class="Datatype">ℕ</a><a id="5212" class="Symbol">)</a> <a id="5214" class="Symbol">→</a> <a id="5216" href="Taboos.WLPO.html#4845" class="Bound">α</a> <a id="5218" href="Taboos.WLPO.html#5207" class="Bound">n</a> <a id="5220" href="MLTT.Id.html#193" class="Datatype Operator">＝</a> <a id="5222" href="Taboos.WLPO.html#4845" class="Bound">α</a> <a id="5224" class="Number">0</a><a id="5225" class="Symbol">)</a> <a id="5227" class="Symbol">(</a><a id="5228" href="Taboos.WLPO.html#5228" class="Bound">n</a> <a id="5230" class="Symbol">:</a> <a id="5232" href="MLTT.Natural-Numbers-Type.html#96" class="Datatype">ℕ</a><a id="5233" class="Symbol">)</a>
             <a id="5248" class="Symbol">→</a> <a id="5250" href="Taboos.WLPO.html#4845" class="Bound">α</a> <a id="5252" href="Taboos.WLPO.html#5228" class="Bound">n</a> <a id="5254" href="MLTT.Id.html#2239" class="Function Operator">＝⟨</a> <a id="5257" href="Taboos.WLPO.html#5202" class="Bound">ϕ</a> <a id="5259" href="Taboos.WLPO.html#5228" class="Bound">n</a> <a id="5261" href="MLTT.Id.html#2239" class="Function Operator">⟩</a>
               <a id="5278" href="Taboos.WLPO.html#4845" class="Bound">α</a> <a id="5280" class="Number">0</a> <a id="5282" href="MLTT.Id.html#2239" class="Function Operator">＝⟨</a> <a id="5285" href="Taboos.WLPO.html#5170" class="Bound">p</a> <a id="5287" href="MLTT.Id.html#2239" class="Function Operator">⟩</a>
               <a id="5304" href="MLTT.Two.html#147" class="InductiveConstructor">₁</a>   <a id="5308" href="MLTT.Id.html#2321" class="Function Operator">∎</a><a id="5309" class="Symbol">)</a>
          <a id="5321" class="Symbol">(λ</a> <a id="5324" class="Symbol">(</a><a id="5325" href="Taboos.WLPO.html#5325" class="Bound">γ</a> <a id="5327" class="Symbol">:</a> <a id="5329" class="Symbol">(</a><a id="5330" href="Taboos.WLPO.html#5330" class="Bound">n</a> <a id="5332" class="Symbol">:</a> <a id="5334" href="MLTT.Natural-Numbers-Type.html#96" class="Datatype">ℕ</a><a id="5335" class="Symbol">)</a> <a id="5337" class="Symbol">→</a> <a id="5339" href="Taboos.WLPO.html#4845" class="Bound">α</a> <a id="5341" href="Taboos.WLPO.html#5330" class="Bound">n</a> <a id="5343" href="MLTT.Id.html#193" class="Datatype Operator">＝</a> <a id="5345" href="MLTT.Two.html#147" class="InductiveConstructor">₁</a><a id="5346" class="Symbol">)</a> <a id="5348" class="Symbol">(</a><a id="5349" href="Taboos.WLPO.html#5349" class="Bound">n</a> <a id="5351" class="Symbol">:</a> <a id="5353" href="MLTT.Natural-Numbers-Type.html#96" class="Datatype">ℕ</a><a id="5354" class="Symbol">)</a>
             <a id="5369" class="Symbol">→</a> <a id="5371" href="Taboos.WLPO.html#4845" class="Bound">α</a> <a id="5373" href="Taboos.WLPO.html#5349" class="Bound">n</a> <a id="5375" href="MLTT.Id.html#2239" class="Function Operator">＝⟨</a> <a id="5378" href="Taboos.WLPO.html#5325" class="Bound">γ</a> <a id="5380" href="Taboos.WLPO.html#5349" class="Bound">n</a> <a id="5382" href="MLTT.Id.html#2239" class="Function Operator">⟩</a>
               <a id="5399" href="MLTT.Two.html#147" class="InductiveConstructor">₁</a>   <a id="5403" href="MLTT.Id.html#2239" class="Function Operator">＝⟨</a> <a id="5406" href="Taboos.WLPO.html#5170" class="Bound">p</a> <a id="5408" href="MLTT.Id.html#1055" class="Function Operator">⁻¹</a> <a id="5411" href="MLTT.Id.html#2239" class="Function Operator">⟩</a>
               <a id="5428" href="Taboos.WLPO.html#4845" class="Bound">α</a> <a id="5430" class="Number">0</a> <a id="5432" href="MLTT.Id.html#2321" class="Function Operator">∎</a><a id="5433" class="Symbol">)</a>
          <a id="5445" class="Symbol">(</a><a id="5446" href="Taboos.WLPO.html#4839" class="Bound">wlpov</a> <a id="5452" href="Taboos.WLPO.html#4845" class="Bound">α</a><a id="5453" class="Symbol">)</a>

<a id="5456" class="Markup">\end{code}</a><a id="5466" class="Background">

TODO. The converse.
</a></pre></body></html>