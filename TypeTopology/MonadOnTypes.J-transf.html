<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>MonadOnTypes.J-transf</title><link rel="stylesheet" href="Agda.css"><script type="text/javascript" src="highlight-hover.js"></script></head><body><pre class="Agda"><a id="1" class="Background">Martin Escardo, Paulo Oliva, 2023

A J monad transformer that give a monad T and a type R produces a new
monad JT X := (X → T R) → T X.

</a><a id="138" class="Markup">\begin{code}</a>

<a id="152" class="Symbol">{-#</a> <a id="156" class="Keyword">OPTIONS</a> <a id="164" class="Pragma">--safe</a> <a id="171" class="Pragma">--without-K</a> <a id="183" class="Symbol">#-}</a>

<a id="188" class="Keyword">open</a> <a id="193" class="Keyword">import</a> <a id="200" href="MLTT.Spartan.html" class="Module">MLTT.Spartan</a> <a id="213" class="Keyword">hiding</a> <a id="220" class="Symbol">(</a><a id="221" href="MLTT.Id.html#438" class="Function">J</a><a id="222" class="Symbol">)</a>

<a id="225" class="Keyword">module</a> <a id="232" href="MonadOnTypes.J-transf.html" class="Module">MonadOnTypes.J-transf</a> <a id="254" class="Keyword">where</a>

<a id="261" class="Keyword">open</a> <a id="266" class="Keyword">import</a> <a id="273" href="UF.FunExt.html" class="Module">UF.FunExt</a>
<a id="283" class="Keyword">open</a> <a id="288" class="Keyword">import</a> <a id="295" href="MonadOnTypes.Monad.html" class="Module">MonadOnTypes.Monad</a>

<a id="𝕁-transf"></a><a id="315" href="MonadOnTypes.J-transf.html#315" class="Function">𝕁-transf</a> <a id="324" class="Symbol">:</a> <a id="326" href="UF.FunExt.html#1047" class="Function">Fun-Ext</a> <a id="334" class="Symbol">→</a> <a id="336" href="MonadOnTypes.Monad.html#263" class="Record">Monad</a> <a id="342" class="Symbol">→</a> <a id="344" href="Notation.General.html#372" class="Function">Type</a> <a id="349" class="Symbol">→</a> <a id="351" href="MonadOnTypes.Monad.html#263" class="Record">Monad</a>
<a id="357" href="MonadOnTypes.J-transf.html#315" class="Function">𝕁-transf</a> <a id="366" href="MonadOnTypes.J-transf.html#366" class="Bound">fe</a> <a id="369" href="MonadOnTypes.J-transf.html#369" class="Bound">𝓣</a> <a id="371" href="MonadOnTypes.J-transf.html#371" class="Bound">R</a> <a id="373" class="Symbol">=</a> <a id="375" href="MonadOnTypes.Monad.html#298" class="InductiveConstructor">monad</a> <a id="381" href="MonadOnTypes.J-transf.html#441" class="Function">JT</a> <a id="384" href="MonadOnTypes.J-transf.html#484" class="Function">ηᴶᵀ</a> <a id="388" href="MonadOnTypes.J-transf.html#535" class="Function">extᴶᵀ</a> <a id="394" href="MonadOnTypes.J-transf.html#648" class="Function">extᴶᵀ-η</a> <a id="402" href="MonadOnTypes.J-transf.html#908" class="Function">unitᴶᵀ</a> <a id="409" href="MonadOnTypes.J-transf.html#1035" class="Function">assocᴶᵀ</a>
 <a id="418" class="Keyword">where</a>
 <a id="425" href="MonadOnTypes.J-transf.html#425" class="Function">T</a> <a id="427" class="Symbol">=</a> <a id="429" href="MonadOnTypes.Monad.html#313" class="Field">functor</a> <a id="437" href="MonadOnTypes.J-transf.html#369" class="Bound">𝓣</a>

 <a id="441" href="MonadOnTypes.J-transf.html#441" class="Function">JT</a> <a id="444" class="Symbol">:</a> <a id="446" href="Notation.General.html#372" class="Function">Type</a> <a id="451" class="Symbol">→</a> <a id="453" href="Notation.General.html#372" class="Function">Type</a>
 <a id="459" href="MonadOnTypes.J-transf.html#441" class="Function">JT</a> <a id="462" href="MonadOnTypes.J-transf.html#462" class="Bound">X</a> <a id="464" class="Symbol">=</a> <a id="466" class="Symbol">(</a><a id="467" href="MonadOnTypes.J-transf.html#462" class="Bound">X</a> <a id="469" class="Symbol">→</a> <a id="471" href="MonadOnTypes.J-transf.html#425" class="Function">T</a> <a id="473" href="MonadOnTypes.J-transf.html#371" class="Bound">R</a><a id="474" class="Symbol">)</a> <a id="476" class="Symbol">→</a> <a id="478" href="MonadOnTypes.J-transf.html#425" class="Function">T</a> <a id="480" href="MonadOnTypes.J-transf.html#462" class="Bound">X</a>

 <a id="484" href="MonadOnTypes.J-transf.html#484" class="Function">ηᴶᵀ</a> <a id="488" class="Symbol">:</a> <a id="490" class="Symbol">{</a><a id="491" href="MonadOnTypes.J-transf.html#491" class="Bound">X</a> <a id="493" class="Symbol">:</a> <a id="495" href="Notation.General.html#372" class="Function">Type</a><a id="499" class="Symbol">}</a> <a id="501" class="Symbol">→</a> <a id="503" href="MonadOnTypes.J-transf.html#491" class="Bound">X</a> <a id="505" class="Symbol">→</a> <a id="507" href="MonadOnTypes.J-transf.html#441" class="Function">JT</a> <a id="510" href="MonadOnTypes.J-transf.html#491" class="Bound">X</a>
 <a id="513" href="MonadOnTypes.J-transf.html#484" class="Function">ηᴶᵀ</a> <a id="517" class="Symbol">=</a> <a id="519" class="Symbol">λ</a> <a id="521" href="MonadOnTypes.J-transf.html#521" class="Bound">x</a> <a id="523" href="MonadOnTypes.J-transf.html#523" class="Bound">p</a> <a id="525" class="Symbol">→</a> <a id="527" href="MonadOnTypes.Monad.html#337" class="Field">η</a> <a id="529" href="MonadOnTypes.J-transf.html#369" class="Bound">𝓣</a> <a id="531" href="MonadOnTypes.J-transf.html#521" class="Bound">x</a>

 <a id="535" href="MonadOnTypes.J-transf.html#535" class="Function">extᴶᵀ</a> <a id="541" class="Symbol">:</a> <a id="543" class="Symbol">{</a><a id="544" href="MonadOnTypes.J-transf.html#544" class="Bound">X</a> <a id="546" href="MonadOnTypes.J-transf.html#546" class="Bound">Y</a> <a id="548" class="Symbol">:</a> <a id="550" href="Notation.General.html#372" class="Function">Type</a><a id="554" class="Symbol">}</a> <a id="556" class="Symbol">→</a> <a id="558" class="Symbol">(</a><a id="559" href="MonadOnTypes.J-transf.html#544" class="Bound">X</a> <a id="561" class="Symbol">→</a> <a id="563" href="MonadOnTypes.J-transf.html#441" class="Function">JT</a> <a id="566" href="MonadOnTypes.J-transf.html#546" class="Bound">Y</a><a id="567" class="Symbol">)</a> <a id="569" class="Symbol">→</a> <a id="571" href="MonadOnTypes.J-transf.html#441" class="Function">JT</a> <a id="574" href="MonadOnTypes.J-transf.html#544" class="Bound">X</a> <a id="576" class="Symbol">→</a> <a id="578" href="MonadOnTypes.J-transf.html#441" class="Function">JT</a> <a id="581" href="MonadOnTypes.J-transf.html#546" class="Bound">Y</a>
 <a id="584" href="MonadOnTypes.J-transf.html#535" class="Function">extᴶᵀ</a> <a id="590" href="MonadOnTypes.J-transf.html#590" class="Bound">f</a> <a id="592" href="MonadOnTypes.J-transf.html#592" class="Bound">ε</a> <a id="594" href="MonadOnTypes.J-transf.html#594" class="Bound">p</a> <a id="596" class="Symbol">=</a> <a id="598" href="MonadOnTypes.Monad.html#376" class="Field">ext</a> <a id="602" href="MonadOnTypes.J-transf.html#369" class="Bound">𝓣</a> <a id="604" class="Symbol">(λ</a> <a id="607" href="MonadOnTypes.J-transf.html#607" class="Bound">x</a> <a id="609" class="Symbol">→</a> <a id="611" href="MonadOnTypes.J-transf.html#590" class="Bound">f</a> <a id="613" href="MonadOnTypes.J-transf.html#607" class="Bound">x</a> <a id="615" href="MonadOnTypes.J-transf.html#594" class="Bound">p</a><a id="616" class="Symbol">)</a> <a id="618" class="Symbol">(</a><a id="619" href="MonadOnTypes.J-transf.html#592" class="Bound">ε</a> <a id="621" class="Symbol">(λ</a> <a id="624" href="MonadOnTypes.J-transf.html#624" class="Bound">x</a> <a id="626" class="Symbol">→</a> <a id="628" href="MonadOnTypes.Monad.html#376" class="Field">ext</a> <a id="632" href="MonadOnTypes.J-transf.html#369" class="Bound">𝓣</a> <a id="634" href="MonadOnTypes.J-transf.html#594" class="Bound">p</a> <a id="636" class="Symbol">(</a><a id="637" href="MonadOnTypes.J-transf.html#590" class="Bound">f</a> <a id="639" href="MonadOnTypes.J-transf.html#624" class="Bound">x</a> <a id="641" href="MonadOnTypes.J-transf.html#594" class="Bound">p</a><a id="642" class="Symbol">)))</a>

 <a id="648" href="MonadOnTypes.J-transf.html#648" class="Function">extᴶᵀ-η</a> <a id="656" class="Symbol">:</a> <a id="658" class="Symbol">{</a><a id="659" href="MonadOnTypes.J-transf.html#659" class="Bound">X</a> <a id="661" class="Symbol">:</a> <a id="663" href="Notation.General.html#372" class="Function">Type</a><a id="667" class="Symbol">}</a> <a id="669" class="Symbol">→</a> <a id="671" href="MonadOnTypes.J-transf.html#535" class="Function">extᴶᵀ</a> <a id="677" class="Symbol">(</a><a id="678" href="MonadOnTypes.J-transf.html#484" class="Function">ηᴶᵀ</a> <a id="682" class="Symbol">{</a><a id="683" href="MonadOnTypes.J-transf.html#659" class="Bound">X</a><a id="684" class="Symbol">})</a> <a id="687" href="MLTT.Id.html#1891" class="Function Operator">∼</a> <a id="689" href="MLTT.Pi.html#478" class="Function">𝑖𝑑</a> <a id="692" class="Symbol">(</a><a id="693" href="MonadOnTypes.J-transf.html#441" class="Function">JT</a> <a id="696" href="MonadOnTypes.J-transf.html#659" class="Bound">X</a><a id="697" class="Symbol">)</a>
 <a id="700" href="MonadOnTypes.J-transf.html#648" class="Function">extᴶᵀ-η</a> <a id="708" href="MonadOnTypes.J-transf.html#708" class="Bound">ε</a> <a id="710" class="Symbol">=</a> <a id="712" href="UF.FunExt.html#1242" class="Function">dfunext</a> <a id="720" href="MonadOnTypes.J-transf.html#366" class="Bound">fe</a> <a id="723" class="Symbol">λ</a> <a id="725" href="MonadOnTypes.J-transf.html#725" class="Bound">p</a> <a id="727" class="Symbol">→</a>
  <a id="731" href="MonadOnTypes.Monad.html#376" class="Field">ext</a> <a id="735" href="MonadOnTypes.J-transf.html#369" class="Bound">𝓣</a> <a id="737" class="Symbol">(</a><a id="738" href="MonadOnTypes.Monad.html#337" class="Field">η</a> <a id="740" href="MonadOnTypes.J-transf.html#369" class="Bound">𝓣</a><a id="741" class="Symbol">)</a> <a id="743" class="Symbol">(</a><a id="744" href="MonadOnTypes.J-transf.html#708" class="Bound">ε</a> <a id="746" class="Symbol">(λ</a> <a id="749" href="MonadOnTypes.J-transf.html#749" class="Bound">x</a> <a id="751" class="Symbol">→</a> <a id="753" href="MonadOnTypes.Monad.html#376" class="Field">ext</a> <a id="757" href="MonadOnTypes.J-transf.html#369" class="Bound">𝓣</a> <a id="759" href="MonadOnTypes.J-transf.html#725" class="Bound">p</a> <a id="761" class="Symbol">(</a><a id="762" href="MonadOnTypes.Monad.html#337" class="Field">η</a> <a id="764" href="MonadOnTypes.J-transf.html#369" class="Bound">𝓣</a> <a id="766" href="MonadOnTypes.J-transf.html#749" class="Bound">x</a><a id="767" class="Symbol">)))</a> <a id="771" href="MLTT.Id.html#2705" class="Function Operator">＝⟨</a> <a id="774" href="MonadOnTypes.Monad.html#443" class="Field">ext-η</a> <a id="780" href="MonadOnTypes.J-transf.html#369" class="Bound">𝓣</a> <a id="782" class="Symbol">_</a> <a id="784" href="MLTT.Id.html#2705" class="Function Operator">⟩</a>
  <a id="788" href="MonadOnTypes.J-transf.html#708" class="Bound">ε</a> <a id="790" class="Symbol">(λ</a> <a id="793" href="MonadOnTypes.J-transf.html#793" class="Bound">x</a> <a id="795" class="Symbol">→</a> <a id="797" href="MonadOnTypes.Monad.html#376" class="Field">ext</a> <a id="801" href="MonadOnTypes.J-transf.html#369" class="Bound">𝓣</a> <a id="803" href="MonadOnTypes.J-transf.html#725" class="Bound">p</a> <a id="805" class="Symbol">(</a><a id="806" href="MonadOnTypes.Monad.html#337" class="Field">η</a> <a id="808" href="MonadOnTypes.J-transf.html#369" class="Bound">𝓣</a> <a id="810" href="MonadOnTypes.J-transf.html#793" class="Bound">x</a><a id="811" class="Symbol">))</a>               <a id="828" href="MLTT.Id.html#2705" class="Function Operator">＝⟨</a> <a id="831" href="MLTT.Id.html#1136" class="Function">ap</a> <a id="834" href="MonadOnTypes.J-transf.html#708" class="Bound">ε</a> <a id="836" class="Symbol">(</a><a id="837" href="UF.FunExt.html#1242" class="Function">dfunext</a> <a id="845" href="MonadOnTypes.J-transf.html#366" class="Bound">fe</a> <a id="848" class="Symbol">(</a><a id="849" href="MonadOnTypes.Monad.html#507" class="Field">unit</a> <a id="854" href="MonadOnTypes.J-transf.html#369" class="Bound">𝓣</a> <a id="856" class="Symbol">_))</a> <a id="860" href="MLTT.Id.html#2705" class="Function Operator">⟩</a>
  <a id="864" href="MonadOnTypes.J-transf.html#708" class="Bound">ε</a> <a id="866" href="MonadOnTypes.J-transf.html#725" class="Bound">p</a>                                     <a id="904" href="MLTT.Id.html#2787" class="Function Operator">∎</a>

 <a id="908" href="MonadOnTypes.J-transf.html#908" class="Function">unitᴶᵀ</a> <a id="915" class="Symbol">:</a> <a id="917" class="Symbol">{</a><a id="918" href="MonadOnTypes.J-transf.html#918" class="Bound">X</a> <a id="920" href="MonadOnTypes.J-transf.html#920" class="Bound">Y</a> <a id="922" class="Symbol">:</a> <a id="924" href="Notation.General.html#372" class="Function">Type</a><a id="928" class="Symbol">}</a> <a id="930" class="Symbol">(</a><a id="931" href="MonadOnTypes.J-transf.html#931" class="Bound">f</a> <a id="933" class="Symbol">:</a> <a id="935" href="MonadOnTypes.J-transf.html#918" class="Bound">X</a> <a id="937" class="Symbol">→</a> <a id="939" href="MonadOnTypes.J-transf.html#441" class="Function">JT</a> <a id="942" href="MonadOnTypes.J-transf.html#920" class="Bound">Y</a><a id="943" class="Symbol">)</a> <a id="945" class="Symbol">(</a><a id="946" href="MonadOnTypes.J-transf.html#946" class="Bound">x</a> <a id="948" class="Symbol">:</a> <a id="950" href="MonadOnTypes.J-transf.html#918" class="Bound">X</a><a id="951" class="Symbol">)</a> <a id="953" class="Symbol">→</a> <a id="955" href="MonadOnTypes.J-transf.html#535" class="Function">extᴶᵀ</a> <a id="961" href="MonadOnTypes.J-transf.html#931" class="Bound">f</a> <a id="963" class="Symbol">(</a><a id="964" href="MonadOnTypes.J-transf.html#484" class="Function">ηᴶᵀ</a> <a id="968" href="MonadOnTypes.J-transf.html#946" class="Bound">x</a><a id="969" class="Symbol">)</a> <a id="971" href="MLTT.Id.html#193" class="Datatype Operator">＝</a> <a id="973" href="MonadOnTypes.J-transf.html#931" class="Bound">f</a> <a id="975" href="MonadOnTypes.J-transf.html#946" class="Bound">x</a>
 <a id="978" href="MonadOnTypes.J-transf.html#908" class="Function">unitᴶᵀ</a> <a id="985" href="MonadOnTypes.J-transf.html#985" class="Bound">f</a> <a id="987" href="MonadOnTypes.J-transf.html#987" class="Bound">x</a> <a id="989" class="Symbol">=</a> <a id="991" href="UF.FunExt.html#1242" class="Function">dfunext</a> <a id="999" href="MonadOnTypes.J-transf.html#366" class="Bound">fe</a> <a id="1002" class="Symbol">(λ</a> <a id="1005" href="MonadOnTypes.J-transf.html#1005" class="Bound">p</a> <a id="1007" class="Symbol">→</a> <a id="1009" href="MonadOnTypes.Monad.html#507" class="Field">unit</a> <a id="1014" href="MonadOnTypes.J-transf.html#369" class="Bound">𝓣</a> <a id="1016" class="Symbol">(λ</a> <a id="1019" href="MonadOnTypes.J-transf.html#1019" class="Bound">x</a> <a id="1021" class="Symbol">→</a> <a id="1023" href="MonadOnTypes.J-transf.html#985" class="Bound">f</a> <a id="1025" href="MonadOnTypes.J-transf.html#1019" class="Bound">x</a> <a id="1027" href="MonadOnTypes.J-transf.html#1005" class="Bound">p</a><a id="1028" class="Symbol">)</a> <a id="1030" href="MonadOnTypes.J-transf.html#987" class="Bound">x</a><a id="1031" class="Symbol">)</a>

 <a id="1035" href="MonadOnTypes.J-transf.html#1035" class="Function">assocᴶᵀ</a> <a id="1043" class="Symbol">:</a> <a id="1045" class="Symbol">{</a><a id="1046" href="MonadOnTypes.J-transf.html#1046" class="Bound">X</a> <a id="1048" href="MonadOnTypes.J-transf.html#1048" class="Bound">Y</a> <a id="1050" href="MonadOnTypes.J-transf.html#1050" class="Bound">Z</a> <a id="1052" class="Symbol">:</a> <a id="1054" href="Notation.General.html#372" class="Function">Type</a><a id="1058" class="Symbol">}</a> <a id="1060" class="Symbol">(</a><a id="1061" href="MonadOnTypes.J-transf.html#1061" class="Bound">g</a> <a id="1063" class="Symbol">:</a> <a id="1065" href="MonadOnTypes.J-transf.html#1048" class="Bound">Y</a> <a id="1067" class="Symbol">→</a> <a id="1069" href="MonadOnTypes.J-transf.html#441" class="Function">JT</a> <a id="1072" href="MonadOnTypes.J-transf.html#1050" class="Bound">Z</a><a id="1073" class="Symbol">)</a> <a id="1075" class="Symbol">(</a><a id="1076" href="MonadOnTypes.J-transf.html#1076" class="Bound">f</a> <a id="1078" class="Symbol">:</a> <a id="1080" href="MonadOnTypes.J-transf.html#1046" class="Bound">X</a> <a id="1082" class="Symbol">→</a> <a id="1084" href="MonadOnTypes.J-transf.html#441" class="Function">JT</a> <a id="1087" href="MonadOnTypes.J-transf.html#1048" class="Bound">Y</a><a id="1088" class="Symbol">)</a> <a id="1090" class="Symbol">(</a><a id="1091" href="MonadOnTypes.J-transf.html#1091" class="Bound">ε</a> <a id="1093" class="Symbol">:</a> <a id="1095" href="MonadOnTypes.J-transf.html#441" class="Function">JT</a> <a id="1098" href="MonadOnTypes.J-transf.html#1046" class="Bound">X</a><a id="1099" class="Symbol">)</a>
        <a id="1109" class="Symbol">→</a> <a id="1111" href="MonadOnTypes.J-transf.html#535" class="Function">extᴶᵀ</a> <a id="1117" class="Symbol">(λ</a> <a id="1120" href="MonadOnTypes.J-transf.html#1120" class="Bound">x</a> <a id="1122" class="Symbol">→</a> <a id="1124" href="MonadOnTypes.J-transf.html#535" class="Function">extᴶᵀ</a> <a id="1130" href="MonadOnTypes.J-transf.html#1061" class="Bound">g</a> <a id="1132" class="Symbol">(</a><a id="1133" href="MonadOnTypes.J-transf.html#1076" class="Bound">f</a> <a id="1135" href="MonadOnTypes.J-transf.html#1120" class="Bound">x</a><a id="1136" class="Symbol">))</a> <a id="1139" href="MonadOnTypes.J-transf.html#1091" class="Bound">ε</a> <a id="1141" href="MLTT.Id.html#193" class="Datatype Operator">＝</a> <a id="1143" href="MonadOnTypes.J-transf.html#535" class="Function">extᴶᵀ</a> <a id="1149" href="MonadOnTypes.J-transf.html#1061" class="Bound">g</a> <a id="1151" class="Symbol">(</a><a id="1152" href="MonadOnTypes.J-transf.html#535" class="Function">extᴶᵀ</a> <a id="1158" href="MonadOnTypes.J-transf.html#1076" class="Bound">f</a> <a id="1160" href="MonadOnTypes.J-transf.html#1091" class="Bound">ε</a><a id="1161" class="Symbol">)</a>
 <a id="1164" href="MonadOnTypes.J-transf.html#1035" class="Function">assocᴶᵀ</a> <a id="1172" href="MonadOnTypes.J-transf.html#1172" class="Bound">g</a> <a id="1174" href="MonadOnTypes.J-transf.html#1174" class="Bound">f</a> <a id="1176" href="MonadOnTypes.J-transf.html#1176" class="Bound">ε</a> <a id="1178" class="Symbol">=</a> <a id="1180" href="UF.FunExt.html#1242" class="Function">dfunext</a> <a id="1188" href="MonadOnTypes.J-transf.html#366" class="Bound">fe</a> <a id="1191" href="MonadOnTypes.J-transf.html#1204" class="Function">γ</a>
  <a id="1195" class="Keyword">where</a>
   <a id="1204" href="MonadOnTypes.J-transf.html#1204" class="Function">γ</a> <a id="1206" class="Symbol">:</a> <a id="1208" class="Symbol">∀</a> <a id="1210" href="MonadOnTypes.J-transf.html#1210" class="Bound">p</a> <a id="1212" class="Symbol">→</a> <a id="1214" href="MonadOnTypes.J-transf.html#535" class="Function">extᴶᵀ</a> <a id="1220" class="Symbol">(λ</a> <a id="1223" href="MonadOnTypes.J-transf.html#1223" class="Bound">x</a> <a id="1225" class="Symbol">→</a> <a id="1227" href="MonadOnTypes.J-transf.html#535" class="Function">extᴶᵀ</a> <a id="1233" href="MonadOnTypes.J-transf.html#1172" class="Bound">g</a> <a id="1235" class="Symbol">(</a><a id="1236" href="MonadOnTypes.J-transf.html#1174" class="Bound">f</a> <a id="1238" href="MonadOnTypes.J-transf.html#1223" class="Bound">x</a><a id="1239" class="Symbol">))</a> <a id="1242" href="MonadOnTypes.J-transf.html#1176" class="Bound">ε</a> <a id="1244" href="MonadOnTypes.J-transf.html#1210" class="Bound">p</a> <a id="1246" href="MLTT.Id.html#193" class="Datatype Operator">＝</a> <a id="1248" href="MonadOnTypes.J-transf.html#535" class="Function">extᴶᵀ</a> <a id="1254" href="MonadOnTypes.J-transf.html#1172" class="Bound">g</a> <a id="1256" class="Symbol">(</a><a id="1257" href="MonadOnTypes.J-transf.html#535" class="Function">extᴶᵀ</a> <a id="1263" href="MonadOnTypes.J-transf.html#1174" class="Bound">f</a> <a id="1265" href="MonadOnTypes.J-transf.html#1176" class="Bound">ε</a><a id="1266" class="Symbol">)</a> <a id="1268" href="MonadOnTypes.J-transf.html#1210" class="Bound">p</a>
   <a id="1273" href="MonadOnTypes.J-transf.html#1204" class="Function">γ</a> <a id="1275" href="MonadOnTypes.J-transf.html#1275" class="Bound">p</a> <a id="1277" class="Symbol">=</a>
    <a id="1283" href="MonadOnTypes.J-transf.html#535" class="Function">extᴶᵀ</a> <a id="1289" class="Symbol">(λ</a> <a id="1292" href="MonadOnTypes.J-transf.html#1292" class="Bound">x</a> <a id="1294" class="Symbol">→</a> <a id="1296" href="MonadOnTypes.J-transf.html#535" class="Function">extᴶᵀ</a> <a id="1302" href="MonadOnTypes.J-transf.html#1172" class="Bound">g</a> <a id="1304" class="Symbol">(</a><a id="1305" href="MonadOnTypes.J-transf.html#1174" class="Bound">f</a> <a id="1307" href="MonadOnTypes.J-transf.html#1292" class="Bound">x</a><a id="1308" class="Symbol">))</a> <a id="1311" href="MonadOnTypes.J-transf.html#1176" class="Bound">ε</a> <a id="1313" href="MonadOnTypes.J-transf.html#1275" class="Bound">p</a>                 <a id="1331" href="MLTT.Id.html#2705" class="Function Operator">＝⟨</a> <a id="1334" href="MLTT.Identity-Type.html#157" class="InductiveConstructor">refl</a> <a id="1339" href="MLTT.Id.html#2705" class="Function Operator">⟩</a>
    <a id="1345" href="MonadOnTypes.J-transf.html#1591" class="Function">𝕖</a> <a id="1347" class="Symbol">(λ</a> <a id="1350" href="MonadOnTypes.J-transf.html#1350" class="Bound">x</a> <a id="1352" class="Symbol">→</a> <a id="1354" href="MonadOnTypes.J-transf.html#1591" class="Function">𝕖</a> <a id="1356" href="MonadOnTypes.J-transf.html#1607" class="Function">𝕘</a> <a id="1358" class="Symbol">(</a><a id="1359" href="MonadOnTypes.J-transf.html#1629" class="Function">𝕗</a> <a id="1361" href="MonadOnTypes.J-transf.html#1350" class="Bound">x</a><a id="1362" class="Symbol">))</a> <a id="1365" class="Symbol">(</a><a id="1366" href="MonadOnTypes.J-transf.html#1176" class="Bound">ε</a> <a id="1368" class="Symbol">(λ</a> <a id="1371" href="MonadOnTypes.J-transf.html#1371" class="Bound">x</a> <a id="1373" class="Symbol">→</a> <a id="1375" href="MonadOnTypes.J-transf.html#1591" class="Function">𝕖</a> <a id="1377" href="MonadOnTypes.J-transf.html#1275" class="Bound">p</a> <a id="1379" class="Symbol">(</a><a id="1380" href="MonadOnTypes.J-transf.html#1591" class="Function">𝕖</a> <a id="1382" href="MonadOnTypes.J-transf.html#1607" class="Function">𝕘</a> <a id="1384" class="Symbol">(</a><a id="1385" href="MonadOnTypes.J-transf.html#1629" class="Function">𝕗</a> <a id="1387" href="MonadOnTypes.J-transf.html#1371" class="Bound">x</a><a id="1388" class="Symbol">))))</a> <a id="1393" href="MLTT.Id.html#2705" class="Function Operator">＝⟨</a> <a id="1396" href="MonadOnTypes.Monad.html#578" class="Field">assoc</a> <a id="1402" href="MonadOnTypes.J-transf.html#369" class="Bound">𝓣</a> <a id="1404" class="Symbol">_</a> <a id="1406" class="Symbol">_</a> <a id="1408" class="Symbol">_</a> <a id="1410" href="MLTT.Id.html#2705" class="Function Operator">⟩</a>
    <a id="1416" href="MonadOnTypes.J-transf.html#1591" class="Function">𝕖</a> <a id="1418" href="MonadOnTypes.J-transf.html#1607" class="Function">𝕘</a> <a id="1420" class="Symbol">(</a><a id="1421" href="MonadOnTypes.J-transf.html#1591" class="Function">𝕖</a> <a id="1423" href="MonadOnTypes.J-transf.html#1629" class="Function">𝕗</a> <a id="1425" class="Symbol">(</a><a id="1426" href="MonadOnTypes.J-transf.html#1176" class="Bound">ε</a> <a id="1428" class="Symbol">(λ</a> <a id="1431" href="MonadOnTypes.J-transf.html#1431" class="Bound">x</a> <a id="1433" class="Symbol">→</a> <a id="1435" href="MonadOnTypes.J-transf.html#1591" class="Function">𝕖</a> <a id="1437" href="MonadOnTypes.J-transf.html#1275" class="Bound">p</a> <a id="1439" class="Symbol">(</a><a id="1440" href="MonadOnTypes.J-transf.html#1591" class="Function">𝕖</a> <a id="1442" href="MonadOnTypes.J-transf.html#1607" class="Function">𝕘</a> <a id="1444" class="Symbol">(</a><a id="1445" href="MonadOnTypes.J-transf.html#1629" class="Function">𝕗</a> <a id="1447" href="MonadOnTypes.J-transf.html#1431" class="Bound">x</a><a id="1448" class="Symbol">)))))</a>           <a id="1464" href="MLTT.Id.html#2705" class="Function Operator">＝⟨</a> <a id="1467" href="MonadOnTypes.J-transf.html#1667" class="Function">again-by-assoc</a> <a id="1482" href="MLTT.Id.html#2705" class="Function Operator">⟩</a>
    <a id="1488" href="MonadOnTypes.J-transf.html#1591" class="Function">𝕖</a> <a id="1490" href="MonadOnTypes.J-transf.html#1607" class="Function">𝕘</a> <a id="1492" class="Symbol">(</a><a id="1493" href="MonadOnTypes.J-transf.html#1591" class="Function">𝕖</a> <a id="1495" href="MonadOnTypes.J-transf.html#1629" class="Function">𝕗</a> <a id="1497" class="Symbol">(</a><a id="1498" href="MonadOnTypes.J-transf.html#1176" class="Bound">ε</a> <a id="1500" class="Symbol">(λ</a> <a id="1503" href="MonadOnTypes.J-transf.html#1503" class="Bound">x</a> <a id="1505" class="Symbol">→</a> <a id="1507" href="MonadOnTypes.J-transf.html#1591" class="Function">𝕖</a> <a id="1509" class="Symbol">(λ</a> <a id="1512" href="MonadOnTypes.J-transf.html#1512" class="Bound">y</a> <a id="1514" class="Symbol">→</a> <a id="1516" href="MonadOnTypes.J-transf.html#1591" class="Function">𝕖</a> <a id="1518" href="MonadOnTypes.J-transf.html#1275" class="Bound">p</a> <a id="1520" class="Symbol">(</a><a id="1521" href="MonadOnTypes.J-transf.html#1607" class="Function">𝕘</a> <a id="1523" href="MonadOnTypes.J-transf.html#1512" class="Bound">y</a><a id="1524" class="Symbol">))</a> <a id="1527" class="Symbol">(</a><a id="1528" href="MonadOnTypes.J-transf.html#1629" class="Function">𝕗</a> <a id="1530" href="MonadOnTypes.J-transf.html#1503" class="Bound">x</a><a id="1531" class="Symbol">))))</a> <a id="1536" href="MLTT.Id.html#2705" class="Function Operator">＝⟨</a> <a id="1539" href="MLTT.Identity-Type.html#157" class="InductiveConstructor">refl</a> <a id="1544" href="MLTT.Id.html#2705" class="Function Operator">⟩</a>
    <a id="1550" href="MonadOnTypes.J-transf.html#535" class="Function">extᴶᵀ</a> <a id="1556" href="MonadOnTypes.J-transf.html#1172" class="Bound">g</a> <a id="1558" class="Symbol">(</a><a id="1559" href="MonadOnTypes.J-transf.html#535" class="Function">extᴶᵀ</a> <a id="1565" href="MonadOnTypes.J-transf.html#1174" class="Bound">f</a> <a id="1567" href="MonadOnTypes.J-transf.html#1176" class="Bound">ε</a><a id="1568" class="Symbol">)</a> <a id="1570" href="MonadOnTypes.J-transf.html#1275" class="Bound">p</a> <a id="1572" href="MLTT.Id.html#2787" class="Function Operator">∎</a>
     <a id="1579" class="Keyword">where</a>
      <a id="1591" href="MonadOnTypes.J-transf.html#1591" class="Function">𝕖</a> <a id="1593" class="Symbol">=</a> <a id="1595" href="MonadOnTypes.Monad.html#376" class="Field">ext</a> <a id="1599" href="MonadOnTypes.J-transf.html#369" class="Bound">𝓣</a>
      <a id="1607" href="MonadOnTypes.J-transf.html#1607" class="Function">𝕘</a> <a id="1609" class="Symbol">=</a> <a id="1611" class="Symbol">λ</a> <a id="1613" href="MonadOnTypes.J-transf.html#1613" class="Bound">y</a> <a id="1615" class="Symbol">→</a> <a id="1617" href="MonadOnTypes.J-transf.html#1172" class="Bound">g</a> <a id="1619" href="MonadOnTypes.J-transf.html#1613" class="Bound">y</a> <a id="1621" href="MonadOnTypes.J-transf.html#1275" class="Bound">p</a>
      <a id="1629" href="MonadOnTypes.J-transf.html#1629" class="Function">𝕗</a> <a id="1631" class="Symbol">=</a> <a id="1633" class="Symbol">λ</a> <a id="1635" href="MonadOnTypes.J-transf.html#1635" class="Bound">x</a> <a id="1637" class="Symbol">→</a> <a id="1639" href="MonadOnTypes.J-transf.html#1174" class="Bound">f</a> <a id="1641" href="MonadOnTypes.J-transf.html#1635" class="Bound">x</a> <a id="1643" class="Symbol">(λ</a> <a id="1646" href="MonadOnTypes.J-transf.html#1646" class="Bound">y</a> <a id="1648" class="Symbol">→</a> <a id="1650" href="MonadOnTypes.J-transf.html#1591" class="Function">𝕖</a> <a id="1652" href="MonadOnTypes.J-transf.html#1275" class="Bound">p</a> <a id="1654" class="Symbol">(</a><a id="1655" href="MonadOnTypes.J-transf.html#1607" class="Function">𝕘</a> <a id="1657" href="MonadOnTypes.J-transf.html#1646" class="Bound">y</a><a id="1658" class="Symbol">))</a>
      <a id="1667" href="MonadOnTypes.J-transf.html#1667" class="Function">again-by-assoc</a> <a id="1682" class="Symbol">=</a> <a id="1684" href="MLTT.Id.html#1136" class="Function">ap</a> <a id="1687" class="Symbol">(λ</a> <a id="1690" href="MonadOnTypes.J-transf.html#1690" class="Bound">-</a> <a id="1692" class="Symbol">→</a> <a id="1694" href="MonadOnTypes.J-transf.html#1591" class="Function">𝕖</a> <a id="1696" href="MonadOnTypes.J-transf.html#1607" class="Function">𝕘</a> <a id="1698" class="Symbol">(</a><a id="1699" href="MonadOnTypes.J-transf.html#1591" class="Function">𝕖</a> <a id="1701" href="MonadOnTypes.J-transf.html#1629" class="Function">𝕗</a> <a id="1703" class="Symbol">(</a><a id="1704" href="MonadOnTypes.J-transf.html#1176" class="Bound">ε</a> <a id="1706" href="MonadOnTypes.J-transf.html#1690" class="Bound">-</a><a id="1707" class="Symbol">)))</a>
                          <a id="1737" class="Symbol">(</a><a id="1738" href="UF.FunExt.html#1242" class="Function">dfunext</a> <a id="1746" href="MonadOnTypes.J-transf.html#366" class="Bound">fe</a> <a id="1749" class="Symbol">(λ</a> <a id="1752" href="MonadOnTypes.J-transf.html#1752" class="Bound">x</a> <a id="1754" class="Symbol">→</a> <a id="1756" class="Symbol">(</a><a id="1757" href="MonadOnTypes.Monad.html#578" class="Field">assoc</a> <a id="1763" href="MonadOnTypes.J-transf.html#369" class="Bound">𝓣</a> <a id="1765" class="Symbol">_</a> <a id="1767" class="Symbol">_</a> <a id="1769" class="Symbol">_)</a><a id="1771" href="MLTT.Id.html#1055" class="Function Operator">⁻¹</a><a id="1773" class="Symbol">))</a>

<a id="𝕁&#39;"></a><a id="1777" href="MonadOnTypes.J-transf.html#1777" class="Function">𝕁&#39;</a> <a id="1780" class="Symbol">:</a> <a id="1782" href="UF.FunExt.html#1047" class="Function">Fun-Ext</a> <a id="1790" class="Symbol">→</a> <a id="1792" href="Notation.General.html#372" class="Function">Type</a> <a id="1797" class="Symbol">→</a> <a id="1799" href="MonadOnTypes.Monad.html#263" class="Record">Monad</a>
<a id="1805" href="MonadOnTypes.J-transf.html#1777" class="Function">𝕁&#39;</a> <a id="1808" href="MonadOnTypes.J-transf.html#1808" class="Bound">fe</a> <a id="1811" class="Symbol">=</a> <a id="1813" href="MonadOnTypes.J-transf.html#315" class="Function">𝕁-transf</a> <a id="1822" href="MonadOnTypes.J-transf.html#1808" class="Bound">fe</a> <a id="1825" href="MonadOnTypes.Monad.html#4686" class="Function">𝕀𝕕</a>

<a id="1829" class="Keyword">module</a> <a id="JT-definitions"></a><a id="1836" href="MonadOnTypes.J-transf.html#1836" class="Module">JT-definitions</a>
        <a id="1859" class="Symbol">(</a><a id="1860" href="MonadOnTypes.J-transf.html#1860" class="Bound">𝓣</a> <a id="1862" class="Symbol">:</a> <a id="1864" href="MonadOnTypes.Monad.html#263" class="Record">Monad</a><a id="1869" class="Symbol">)</a>
        <a id="1879" class="Symbol">(</a><a id="1880" href="MonadOnTypes.J-transf.html#1880" class="Bound">R</a> <a id="1882" class="Symbol">:</a> <a id="1884" href="Notation.General.html#372" class="Function">Type</a><a id="1888" class="Symbol">)</a>
        <a id="1898" class="Symbol">(</a><a id="1899" href="MonadOnTypes.J-transf.html#1899" class="Bound">fe</a> <a id="1902" class="Symbol">:</a> <a id="1904" href="UF.FunExt.html#1047" class="Function">Fun-Ext</a><a id="1911" class="Symbol">)</a>
       <a id="1920" class="Keyword">where</a>

 <a id="1928" class="Keyword">open</a> <a id="1933" class="Keyword">import</a> <a id="1940" href="MonadOnTypes.K.html" class="Module">MonadOnTypes.K</a>

 <a id="1957" class="Keyword">open</a> <a id="1962" href="MonadOnTypes.Monad.html#5092" class="Module">T-definitions</a> <a id="1976" href="MonadOnTypes.J-transf.html#1860" class="Bound">𝓣</a>
 <a id="1979" class="Keyword">open</a> <a id="1984" href="MonadOnTypes.K.html#438" class="Module">K-definitions</a> <a id="1998" href="MonadOnTypes.J-transf.html#1880" class="Bound">R</a>

 <a id="JT-definitions.𝕁𝕋"></a><a id="2002" href="MonadOnTypes.J-transf.html#2002" class="Function">𝕁𝕋</a> <a id="2005" class="Symbol">:</a> <a id="2007" href="MonadOnTypes.Monad.html#263" class="Record">Monad</a>
 <a id="2014" href="MonadOnTypes.J-transf.html#2002" class="Function">𝕁𝕋</a> <a id="2017" class="Symbol">=</a> <a id="2019" href="MonadOnTypes.J-transf.html#315" class="Function">𝕁-transf</a> <a id="2028" href="MonadOnTypes.J-transf.html#1899" class="Bound">fe</a> <a id="2031" href="MonadOnTypes.J-transf.html#1860" class="Bound">𝓣</a> <a id="2033" href="MonadOnTypes.J-transf.html#1880" class="Bound">R</a>

 <a id="JT-definitions.JT"></a><a id="2037" href="MonadOnTypes.J-transf.html#2037" class="Function">JT</a> <a id="2040" class="Symbol">:</a> <a id="2042" href="Notation.General.html#372" class="Function">Type</a> <a id="2047" class="Symbol">→</a> <a id="2049" href="Notation.General.html#372" class="Function">Type</a>
 <a id="2055" href="MonadOnTypes.J-transf.html#2037" class="Function">JT</a> <a id="2058" class="Symbol">=</a> <a id="2060" href="MonadOnTypes.Monad.html#313" class="Field">functor</a> <a id="2068" href="MonadOnTypes.J-transf.html#2002" class="Function">𝕁𝕋</a>

 <a id="JT-definitions.KT"></a><a id="2073" href="MonadOnTypes.J-transf.html#2073" class="Function">KT</a> <a id="2076" class="Symbol">:</a> <a id="2078" href="Notation.General.html#372" class="Function">Type</a> <a id="2083" class="Symbol">→</a> <a id="2085" href="Notation.General.html#372" class="Function">Type</a>
 <a id="2091" href="MonadOnTypes.J-transf.html#2073" class="Function">KT</a> <a id="2094" href="MonadOnTypes.J-transf.html#2094" class="Bound">X</a> <a id="2096" class="Symbol">=</a> <a id="2098" class="Symbol">(</a><a id="2099" href="MonadOnTypes.J-transf.html#2094" class="Bound">X</a> <a id="2101" class="Symbol">→</a> <a id="2103" href="MonadOnTypes.Monad.html#5126" class="Function">T</a> <a id="2105" href="MonadOnTypes.J-transf.html#1880" class="Bound">R</a><a id="2106" class="Symbol">)</a> <a id="2108" class="Symbol">→</a> <a id="2110" href="MonadOnTypes.J-transf.html#1880" class="Bound">R</a>

 <a id="JT-definitions.ηᴶᵀ"></a><a id="2114" href="MonadOnTypes.J-transf.html#2114" class="Function">ηᴶᵀ</a> <a id="2118" class="Symbol">:</a> <a id="2120" class="Symbol">{</a><a id="2121" href="MonadOnTypes.J-transf.html#2121" class="Bound">X</a> <a id="2123" class="Symbol">:</a> <a id="2125" href="Notation.General.html#372" class="Function">Type</a><a id="2129" class="Symbol">}</a> <a id="2131" class="Symbol">→</a> <a id="2133" href="MonadOnTypes.J-transf.html#2121" class="Bound">X</a> <a id="2135" class="Symbol">→</a> <a id="2137" href="MonadOnTypes.J-transf.html#2037" class="Function">JT</a> <a id="2140" href="MonadOnTypes.J-transf.html#2121" class="Bound">X</a>
 <a id="2143" href="MonadOnTypes.J-transf.html#2114" class="Function">ηᴶᵀ</a> <a id="2147" class="Symbol">=</a> <a id="2149" href="MonadOnTypes.Monad.html#337" class="Field">η</a> <a id="2151" href="MonadOnTypes.J-transf.html#2002" class="Function">𝕁𝕋</a>

 <a id="JT-definitions.extᴶᵀ"></a><a id="2156" href="MonadOnTypes.J-transf.html#2156" class="Function">extᴶᵀ</a> <a id="2162" class="Symbol">:</a> <a id="2164" class="Symbol">{</a><a id="2165" href="MonadOnTypes.J-transf.html#2165" class="Bound">X</a> <a id="2167" href="MonadOnTypes.J-transf.html#2167" class="Bound">Y</a> <a id="2169" class="Symbol">:</a> <a id="2171" href="Notation.General.html#372" class="Function">Type</a><a id="2175" class="Symbol">}</a> <a id="2177" class="Symbol">→</a> <a id="2179" class="Symbol">(</a><a id="2180" href="MonadOnTypes.J-transf.html#2165" class="Bound">X</a> <a id="2182" class="Symbol">→</a> <a id="2184" href="MonadOnTypes.J-transf.html#2037" class="Function">JT</a> <a id="2187" href="MonadOnTypes.J-transf.html#2167" class="Bound">Y</a><a id="2188" class="Symbol">)</a> <a id="2190" class="Symbol">→</a> <a id="2192" href="MonadOnTypes.J-transf.html#2037" class="Function">JT</a> <a id="2195" href="MonadOnTypes.J-transf.html#2165" class="Bound">X</a> <a id="2197" class="Symbol">→</a> <a id="2199" href="MonadOnTypes.J-transf.html#2037" class="Function">JT</a> <a id="2202" href="MonadOnTypes.J-transf.html#2167" class="Bound">Y</a>
 <a id="2205" href="MonadOnTypes.J-transf.html#2156" class="Function">extᴶᵀ</a> <a id="2211" class="Symbol">=</a> <a id="2213" href="MonadOnTypes.Monad.html#376" class="Field">ext</a> <a id="2217" href="MonadOnTypes.J-transf.html#2002" class="Function">𝕁𝕋</a>

 <a id="JT-definitions.mapᴶᵀ"></a><a id="2222" href="MonadOnTypes.J-transf.html#2222" class="Function">mapᴶᵀ</a> <a id="2228" class="Symbol">:</a> <a id="2230" class="Symbol">{</a><a id="2231" href="MonadOnTypes.J-transf.html#2231" class="Bound">X</a> <a id="2233" href="MonadOnTypes.J-transf.html#2233" class="Bound">Y</a> <a id="2235" class="Symbol">:</a> <a id="2237" href="Notation.General.html#372" class="Function">Type</a><a id="2241" class="Symbol">}</a> <a id="2243" class="Symbol">→</a> <a id="2245" class="Symbol">(</a><a id="2246" href="MonadOnTypes.J-transf.html#2231" class="Bound">X</a> <a id="2248" class="Symbol">→</a> <a id="2250" href="MonadOnTypes.J-transf.html#2233" class="Bound">Y</a><a id="2251" class="Symbol">)</a> <a id="2253" class="Symbol">→</a> <a id="2255" href="MonadOnTypes.J-transf.html#2037" class="Function">JT</a> <a id="2258" href="MonadOnTypes.J-transf.html#2231" class="Bound">X</a> <a id="2260" class="Symbol">→</a> <a id="2262" href="MonadOnTypes.J-transf.html#2037" class="Function">JT</a> <a id="2265" href="MonadOnTypes.J-transf.html#2233" class="Bound">Y</a>
 <a id="2268" href="MonadOnTypes.J-transf.html#2222" class="Function">mapᴶᵀ</a> <a id="2274" class="Symbol">=</a> <a id="2276" href="MonadOnTypes.Monad.html#713" class="Function">map</a> <a id="2280" href="MonadOnTypes.J-transf.html#2002" class="Function">𝕁𝕋</a>

 <a id="JT-definitions._⊗ᴶᵀ_"></a><a id="2285" href="MonadOnTypes.J-transf.html#2285" class="Function Operator">_⊗ᴶᵀ_</a> <a id="2291" class="Symbol">:</a> <a id="2293" class="Symbol">{</a><a id="2294" href="MonadOnTypes.J-transf.html#2294" class="Bound">X</a> <a id="2296" class="Symbol">:</a> <a id="2298" href="Notation.General.html#372" class="Function">Type</a><a id="2302" class="Symbol">}</a> <a id="2304" class="Symbol">{</a><a id="2305" href="MonadOnTypes.J-transf.html#2305" class="Bound">Y</a> <a id="2307" class="Symbol">:</a> <a id="2309" href="MonadOnTypes.J-transf.html#2294" class="Bound">X</a> <a id="2311" class="Symbol">→</a> <a id="2313" href="Notation.General.html#372" class="Function">Type</a><a id="2317" class="Symbol">}</a>
       <a id="2326" class="Symbol">→</a> <a id="2328" href="MonadOnTypes.J-transf.html#2037" class="Function">JT</a> <a id="2331" href="MonadOnTypes.J-transf.html#2294" class="Bound">X</a>
       <a id="2340" class="Symbol">→</a> <a id="2342" class="Symbol">((</a><a id="2344" href="MonadOnTypes.J-transf.html#2344" class="Bound">x</a> <a id="2346" class="Symbol">:</a> <a id="2348" href="MonadOnTypes.J-transf.html#2294" class="Bound">X</a><a id="2349" class="Symbol">)</a> <a id="2351" class="Symbol">→</a> <a id="2353" href="MonadOnTypes.J-transf.html#2037" class="Function">JT</a> <a id="2356" class="Symbol">(</a><a id="2357" href="MonadOnTypes.J-transf.html#2305" class="Bound">Y</a> <a id="2359" href="MonadOnTypes.J-transf.html#2344" class="Bound">x</a><a id="2360" class="Symbol">))</a>
       <a id="2370" class="Symbol">→</a> <a id="2372" href="MonadOnTypes.J-transf.html#2037" class="Function">JT</a> <a id="2375" class="Symbol">(</a><a id="2376" href="MLTT.Sigma.html#423" class="Function">Σ</a> <a id="2378" href="MonadOnTypes.J-transf.html#2378" class="Bound">x</a> <a id="2380" href="MLTT.Sigma.html#423" class="Function">꞉</a> <a id="2382" href="MonadOnTypes.J-transf.html#2294" class="Bound">X</a> <a id="2384" href="MLTT.Sigma.html#423" class="Function">,</a> <a id="2386" href="MonadOnTypes.J-transf.html#2305" class="Bound">Y</a> <a id="2388" href="MonadOnTypes.J-transf.html#2378" class="Bound">x</a><a id="2389" class="Symbol">)</a>
 <a id="2392" href="MonadOnTypes.J-transf.html#2285" class="Function Operator">_⊗ᴶᵀ_</a> <a id="2398" class="Symbol">=</a> <a id="2400" href="MonadOnTypes.Monad.html#4097" class="Function Operator">_⊗_</a> <a id="2404" href="MonadOnTypes.J-transf.html#2002" class="Function">𝕁𝕋</a>

<a id="2408" class="Keyword">module</a> <a id="JT-algebra-definitions"></a><a id="2415" href="MonadOnTypes.J-transf.html#2415" class="Module">JT-algebra-definitions</a>
        <a id="2446" class="Symbol">(</a><a id="2447" href="MonadOnTypes.J-transf.html#2447" class="Bound">𝓣</a> <a id="2449" class="Symbol">:</a> <a id="2451" href="MonadOnTypes.Monad.html#263" class="Record">Monad</a><a id="2456" class="Symbol">)</a>
        <a id="2466" class="Symbol">(</a><a id="2467" href="MonadOnTypes.J-transf.html#2467" class="Bound">R</a> <a id="2469" class="Symbol">:</a> <a id="2471" href="Notation.General.html#372" class="Function">Type</a><a id="2475" class="Symbol">)</a>
        <a id="2485" class="Symbol">(</a><a id="2486" href="MonadOnTypes.J-transf.html#2486" class="Bound">𝓐</a> <a id="2488" class="Symbol">:</a> <a id="2490" href="MonadOnTypes.Monad.html#9160" class="Record">Algebra</a> <a id="2498" href="MonadOnTypes.J-transf.html#2447" class="Bound">𝓣</a> <a id="2500" href="MonadOnTypes.J-transf.html#2467" class="Bound">R</a><a id="2501" class="Symbol">)</a>
        <a id="2511" class="Symbol">(</a><a id="2512" href="MonadOnTypes.J-transf.html#2512" class="Bound">fe</a> <a id="2515" class="Symbol">:</a> <a id="2517" href="UF.FunExt.html#1047" class="Function">Fun-Ext</a><a id="2524" class="Symbol">)</a>
       <a id="2533" class="Keyword">where</a>

 <a id="2541" class="Keyword">open</a> <a id="2546" class="Keyword">import</a> <a id="2553" href="MonadOnTypes.K.html" class="Module">MonadOnTypes.K</a>

 <a id="2570" class="Keyword">open</a> <a id="2575" href="MonadOnTypes.Monad.html#5092" class="Module">T-definitions</a> <a id="2589" href="MonadOnTypes.J-transf.html#2447" class="Bound">𝓣</a>
 <a id="2592" class="Keyword">open</a> <a id="2597" href="MonadOnTypes.K.html#438" class="Module">K-definitions</a> <a id="2611" href="MonadOnTypes.J-transf.html#2467" class="Bound">R</a>
 <a id="2614" class="Keyword">open</a> <a id="2619" href="MonadOnTypes.J-transf.html#1836" class="Module">JT-definitions</a> <a id="2634" href="MonadOnTypes.J-transf.html#2447" class="Bound">𝓣</a> <a id="2636" href="MonadOnTypes.J-transf.html#2467" class="Bound">R</a> <a id="2638" href="MonadOnTypes.J-transf.html#2512" class="Bound">fe</a>
 <a id="2642" class="Keyword">open</a> <a id="2647" href="MonadOnTypes.Monad.html#14643" class="Module">α-definitions</a> <a id="2661" href="MonadOnTypes.J-transf.html#2447" class="Bound">𝓣</a> <a id="2663" href="MonadOnTypes.J-transf.html#2467" class="Bound">R</a> <a id="2665" href="MonadOnTypes.J-transf.html#2486" class="Bound">𝓐</a>

 <a id="JT-algebra-definitions.α-overlineᵀ"></a><a id="2669" href="MonadOnTypes.J-transf.html#2669" class="Function">α-overlineᵀ</a> <a id="2681" class="Symbol">:</a> <a id="2683" class="Symbol">{</a><a id="2684" href="MonadOnTypes.J-transf.html#2684" class="Bound">X</a> <a id="2686" class="Symbol">:</a> <a id="2688" href="Notation.General.html#372" class="Function">Type</a><a id="2692" class="Symbol">}</a> <a id="2694" class="Symbol">→</a> <a id="2696" href="MonadOnTypes.J-transf.html#2037" class="Function">JT</a> <a id="2699" href="MonadOnTypes.J-transf.html#2684" class="Bound">X</a> <a id="2701" class="Symbol">→</a> <a id="2703" href="MonadOnTypes.J-transf.html#2073" class="Function">KT</a> <a id="2706" href="MonadOnTypes.J-transf.html#2684" class="Bound">X</a>
 <a id="2709" href="MonadOnTypes.J-transf.html#2669" class="Function">α-overlineᵀ</a> <a id="2721" href="MonadOnTypes.J-transf.html#2721" class="Bound">ε</a> <a id="2723" class="Symbol">=</a> <a id="2725" class="Symbol">λ</a> <a id="2727" href="MonadOnTypes.J-transf.html#2727" class="Bound">p</a> <a id="2729" class="Symbol">→</a> <a id="2731" href="MonadOnTypes.Monad.html#14760" class="Function">α</a> <a id="2733" class="Symbol">(</a><a id="2734" href="MonadOnTypes.Monad.html#5197" class="Function">extᵀ</a> <a id="2739" href="MonadOnTypes.J-transf.html#2727" class="Bound">p</a> <a id="2741" class="Symbol">(</a><a id="2742" href="MonadOnTypes.J-transf.html#2721" class="Bound">ε</a> <a id="2744" href="MonadOnTypes.J-transf.html#2727" class="Bound">p</a><a id="2745" class="Symbol">))</a>

 <a id="JT-algebra-definitions._α-attainsᵀ_"></a><a id="2750" href="MonadOnTypes.J-transf.html#2750" class="Function Operator">_α-attainsᵀ_</a> <a id="2763" class="Symbol">:</a> <a id="2765" class="Symbol">{</a><a id="2766" href="MonadOnTypes.J-transf.html#2766" class="Bound">X</a> <a id="2768" class="Symbol">:</a> <a id="2770" href="Notation.General.html#372" class="Function">Type</a><a id="2774" class="Symbol">}</a> <a id="2776" class="Symbol">→</a> <a id="2778" href="MonadOnTypes.J-transf.html#2037" class="Function">JT</a> <a id="2781" href="MonadOnTypes.J-transf.html#2766" class="Bound">X</a> <a id="2783" class="Symbol">→</a> <a id="2785" href="MonadOnTypes.K.html#471" class="Function">K</a> <a id="2787" href="MonadOnTypes.J-transf.html#2766" class="Bound">X</a> <a id="2789" class="Symbol">→</a> <a id="2791" href="Notation.General.html#372" class="Function">Type</a>
 <a id="2797" href="MonadOnTypes.J-transf.html#2750" class="Function Operator">_α-attainsᵀ_</a> <a id="2810" class="Symbol">{</a><a id="2811" href="MonadOnTypes.J-transf.html#2811" class="Bound">X</a><a id="2812" class="Symbol">}</a> <a id="2814" href="MonadOnTypes.J-transf.html#2814" class="Bound">ε</a> <a id="2816" href="MonadOnTypes.J-transf.html#2816" class="Bound">ϕ</a> <a id="2818" class="Symbol">=</a> <a id="2820" class="Symbol">(</a><a id="2821" href="MonadOnTypes.J-transf.html#2821" class="Bound">p</a> <a id="2823" class="Symbol">:</a> <a id="2825" href="MonadOnTypes.J-transf.html#2811" class="Bound">X</a> <a id="2827" class="Symbol">→</a> <a id="2829" href="MonadOnTypes.Monad.html#5126" class="Function">T</a> <a id="2831" href="MonadOnTypes.J-transf.html#2467" class="Bound">R</a><a id="2832" class="Symbol">)</a> <a id="2834" class="Symbol">→</a> <a id="2836" href="MonadOnTypes.J-transf.html#2669" class="Function">α-overlineᵀ</a> <a id="2848" href="MonadOnTypes.J-transf.html#2814" class="Bound">ε</a> <a id="2850" href="MonadOnTypes.J-transf.html#2821" class="Bound">p</a> <a id="2852" href="MLTT.Id.html#193" class="Datatype Operator">＝</a> <a id="2854" href="MonadOnTypes.J-transf.html#2816" class="Bound">ϕ</a> <a id="2856" class="Symbol">(</a><a id="2857" href="MonadOnTypes.Monad.html#14760" class="Function">α</a> <a id="2859" href="MLTT.Pi.html#513" class="Function Operator">∘</a> <a id="2861" href="MonadOnTypes.J-transf.html#2821" class="Bound">p</a><a id="2862" class="Symbol">)</a>

<a id="2865" class="Markup">\end{code}</a><a id="2875" class="Background">

Is the following variation of α-overlineᵀ useful?

</a><a id="2928" class="Markup">\begin{code}</a>

 <a id="JT-algebra-definitions.-α-overlineᵀ"></a><a id="2943" href="MonadOnTypes.J-transf.html#2943" class="Function">-α-overlineᵀ</a> <a id="2956" class="Symbol">:</a> <a id="2958" class="Symbol">{</a><a id="2959" href="MonadOnTypes.J-transf.html#2959" class="Bound">X</a> <a id="2961" class="Symbol">:</a> <a id="2963" href="Notation.General.html#372" class="Function">Type</a><a id="2967" class="Symbol">}</a> <a id="2969" class="Symbol">→</a> <a id="2971" href="MonadOnTypes.J-transf.html#2037" class="Function">JT</a> <a id="2974" href="MonadOnTypes.J-transf.html#2959" class="Bound">X</a> <a id="2976" class="Symbol">→</a> <a id="2978" href="MonadOnTypes.K.html#471" class="Function">K</a> <a id="2980" href="MonadOnTypes.J-transf.html#2959" class="Bound">X</a>
 <a id="2983" href="MonadOnTypes.J-transf.html#2943" class="Function">-α-overlineᵀ</a> <a id="2996" href="MonadOnTypes.J-transf.html#2996" class="Bound">ε</a> <a id="2998" class="Symbol">=</a> <a id="3000" class="Symbol">λ</a> <a id="3002" href="MonadOnTypes.J-transf.html#3002" class="Bound">p</a> <a id="3004" class="Symbol">→</a> <a id="3006" href="MonadOnTypes.Monad.html#14760" class="Function">α</a> <a id="3008" class="Symbol">(</a><a id="3009" href="MonadOnTypes.Monad.html#5197" class="Function">extᵀ</a> <a id="3014" class="Symbol">(</a><a id="3015" href="MonadOnTypes.Monad.html#5159" class="Function">ηᵀ</a> <a id="3018" href="MLTT.Pi.html#513" class="Function Operator">∘</a> <a id="3020" href="MonadOnTypes.J-transf.html#3002" class="Bound">p</a><a id="3021" class="Symbol">)</a> <a id="3023" class="Symbol">(</a><a id="3024" href="MonadOnTypes.J-transf.html#2996" class="Bound">ε</a> <a id="3026" class="Symbol">(</a><a id="3027" href="MonadOnTypes.Monad.html#5159" class="Function">ηᵀ</a> <a id="3030" href="MLTT.Pi.html#513" class="Function Operator">∘</a> <a id="3032" href="MonadOnTypes.J-transf.html#3002" class="Bound">p</a><a id="3033" class="Symbol">)))</a>

<a id="3038" class="Markup">\end{code}</a><a id="3048" class="Background">
</a></pre></body></html>