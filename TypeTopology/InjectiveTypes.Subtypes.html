<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>InjectiveTypes.Subtypes</title><link rel="stylesheet" href="Agda.css"><script type="text/javascript" src="highlight-hover.js"></script></head><body><pre class="Agda"><a id="1" class="Background">Martin Escardo, 21st October 2024

A necessary and sufficient condition for the injectivity of a subtype
of an injective type.

Modified by Martin Escardo and Tom de Jong 31st October 2024 to
improve the universe levels.

</a><a id="223" class="Markup">\begin{code}</a>

<a id="237" class="Symbol">{-#</a> <a id="241" class="Keyword">OPTIONS</a> <a id="249" class="Pragma">--safe</a> <a id="256" class="Pragma">--without-K</a> <a id="268" class="Symbol">#-}</a>

<a id="273" class="Keyword">open</a> <a id="278" class="Keyword">import</a> <a id="285" href="UF.FunExt.html" class="Module">UF.FunExt</a>

<a id="296" class="Keyword">module</a> <a id="303" href="InjectiveTypes.Subtypes.html" class="Module">InjectiveTypes.Subtypes</a>
        <a id="335" class="Symbol">(</a><a id="336" href="InjectiveTypes.Subtypes.html#336" class="Bound">fe</a> <a id="339" class="Symbol">:</a> <a id="341" href="UF.FunExt.html#995" class="Function">FunExt</a><a id="347" class="Symbol">)</a>
       <a id="356" class="Keyword">where</a>

<a id="363" class="Keyword">open</a> <a id="368" class="Keyword">import</a> <a id="375" href="InjectiveTypes.Blackboard.html" class="Module">InjectiveTypes.Blackboard</a> <a id="401" href="InjectiveTypes.Subtypes.html#336" class="Bound">fe</a>
<a id="404" class="Keyword">open</a> <a id="409" class="Keyword">import</a> <a id="416" href="InjectiveTypes.OverSmallMaps.html" class="Module">InjectiveTypes.OverSmallMaps</a> <a id="445" href="InjectiveTypes.Subtypes.html#336" class="Bound">fe</a>
<a id="448" class="Keyword">open</a> <a id="453" class="Keyword">import</a> <a id="460" href="MLTT.Spartan.html" class="Module">MLTT.Spartan</a>
<a id="473" class="Keyword">open</a> <a id="478" class="Keyword">import</a> <a id="485" href="UF.Embeddings.html" class="Module">UF.Embeddings</a>
<a id="499" class="Keyword">open</a> <a id="504" class="Keyword">import</a> <a id="511" href="UF.Retracts.html" class="Module">UF.Retracts</a>
<a id="523" class="Keyword">open</a> <a id="528" class="Keyword">import</a> <a id="535" href="UF.Size.html" class="Module">UF.Size</a>
<a id="543" class="Keyword">open</a> <a id="548" class="Keyword">import</a> <a id="555" href="UF.Subsingletons.html" class="Module">UF.Subsingletons</a>

<a id="573" class="Keyword">module</a> <a id="580" href="InjectiveTypes.Subtypes.html#580" class="Module">_</a> <a id="582" class="Symbol">(</a><a id="583" href="InjectiveTypes.Subtypes.html#583" class="Bound">D</a> <a id="585" class="Symbol">:</a> <a id="587" href="MLTT.Universes.html#3834" class="Generalizable">𝓤</a> <a id="589" href="MLTT.Universes.html#3977" class="Function Operator">̇</a> <a id="591" class="Symbol">)</a>
         <a id="602" class="Symbol">(</a><a id="603" href="InjectiveTypes.Subtypes.html#603" class="Bound">P</a> <a id="605" class="Symbol">:</a> <a id="607" href="InjectiveTypes.Subtypes.html#583" class="Bound">D</a> <a id="609" class="Symbol">→</a> <a id="611" href="MLTT.Universes.html#3836" class="Generalizable">𝓥</a> <a id="613" href="MLTT.Universes.html#3977" class="Function Operator">̇</a> <a id="615" class="Symbol">)</a>
         <a id="626" class="Symbol">(</a><a id="627" href="InjectiveTypes.Subtypes.html#627" class="Bound">P-is-prop-valued</a> <a id="644" class="Symbol">:</a> <a id="646" class="Symbol">(</a><a id="647" href="InjectiveTypes.Subtypes.html#647" class="Bound">d</a> <a id="649" class="Symbol">:</a> <a id="651" href="InjectiveTypes.Subtypes.html#583" class="Bound">D</a><a id="652" class="Symbol">)</a> <a id="654" class="Symbol">→</a> <a id="656" href="UF.Subsingletons.html#469" class="Function">is-prop</a> <a id="664" class="Symbol">(</a><a id="665" href="InjectiveTypes.Subtypes.html#603" class="Bound">P</a> <a id="667" href="InjectiveTypes.Subtypes.html#647" class="Bound">d</a><a id="668" class="Symbol">))</a>
       <a id="678" class="Keyword">where</a>

 <a id="686" class="Keyword">private</a>
  <a id="696" href="InjectiveTypes.Subtypes.html#696" class="Function">s</a> <a id="698" class="Symbol">:</a> <a id="700" href="MLTT.Sigma-Type.html#116" class="Record">Σ</a> <a id="702" href="InjectiveTypes.Subtypes.html#603" class="Bound">P</a> <a id="704" class="Symbol">→</a> <a id="706" href="InjectiveTypes.Subtypes.html#583" class="Bound">D</a>
  <a id="710" href="InjectiveTypes.Subtypes.html#696" class="Function">s</a> <a id="712" class="Symbol">=</a> <a id="714" href="MLTT.Sigma-Type.html#198" class="Field">pr₁</a>

 <a id="720" href="InjectiveTypes.Subtypes.html#720" class="Function">necessary-condition-for-injectivity-of-subtype</a>
  <a id="769" class="Symbol">:</a> <a id="771" href="InjectiveTypes.Blackboard.html#15851" class="Function">ainjective-type</a> <a id="787" class="Symbol">(</a><a id="788" href="MLTT.Sigma-Type.html#116" class="Record">Σ</a> <a id="790" href="InjectiveTypes.Subtypes.html#603" class="Bound">P</a><a id="791" class="Symbol">)</a> <a id="793" class="Symbol">(</a><a id="794" href="InjectiveTypes.Subtypes.html#611" class="Bound">𝓥</a> <a id="796" href="Agda.Primitive.html#961" class="Primitive Operator">⊔</a> <a id="798" href="MLTT.Universes.html#3838" class="Generalizable">𝓦</a><a id="799" class="Symbol">)</a> <a id="801" href="MLTT.Universes.html#3840" class="Generalizable">𝓣</a>
  <a id="805" class="Symbol">→</a> <a id="807" href="MLTT.Sigma.html#423" class="Function">Σ</a> <a id="809" href="InjectiveTypes.Subtypes.html#809" class="Bound">f</a> <a id="811" href="MLTT.Sigma.html#423" class="Function">꞉</a> <a id="813" class="Symbol">(</a><a id="814" href="InjectiveTypes.Subtypes.html#583" class="Bound">D</a> <a id="816" class="Symbol">→</a> <a id="818" href="InjectiveTypes.Subtypes.html#583" class="Bound">D</a><a id="819" class="Symbol">)</a> <a id="821" href="MLTT.Sigma.html#423" class="Function">,</a> <a id="823" class="Symbol">((</a><a id="825" href="InjectiveTypes.Subtypes.html#825" class="Bound">d</a> <a id="827" class="Symbol">:</a> <a id="829" href="InjectiveTypes.Subtypes.html#583" class="Bound">D</a><a id="830" class="Symbol">)</a> <a id="832" class="Symbol">→</a> <a id="834" href="InjectiveTypes.Subtypes.html#603" class="Bound">P</a> <a id="836" class="Symbol">(</a><a id="837" href="InjectiveTypes.Subtypes.html#809" class="Bound">f</a> <a id="839" href="InjectiveTypes.Subtypes.html#825" class="Bound">d</a><a id="840" class="Symbol">))</a> <a id="843" href="MLTT.Sigma.html#558" class="Function Operator">×</a> <a id="845" class="Symbol">((</a><a id="847" href="InjectiveTypes.Subtypes.html#847" class="Bound">d</a> <a id="849" class="Symbol">:</a> <a id="851" href="InjectiveTypes.Subtypes.html#583" class="Bound">D</a><a id="852" class="Symbol">)</a> <a id="854" class="Symbol">→</a> <a id="856" href="InjectiveTypes.Subtypes.html#603" class="Bound">P</a> <a id="858" href="InjectiveTypes.Subtypes.html#847" class="Bound">d</a> <a id="860" class="Symbol">→</a> <a id="862" href="InjectiveTypes.Subtypes.html#809" class="Bound">f</a> <a id="864" href="InjectiveTypes.Subtypes.html#847" class="Bound">d</a> <a id="866" href="MLTT.Id.html#193" class="Datatype Operator">＝</a> <a id="868" href="InjectiveTypes.Subtypes.html#847" class="Bound">d</a><a id="869" class="Symbol">)</a>
 <a id="872" href="InjectiveTypes.Subtypes.html#720" class="Function">necessary-condition-for-injectivity-of-subtype</a> <a id="919" class="Symbol">{</a><a id="920" href="InjectiveTypes.Subtypes.html#920" class="Bound">𝓦</a><a id="921" class="Symbol">}</a> <a id="923" class="Symbol">{</a><a id="924" href="InjectiveTypes.Subtypes.html#924" class="Bound">𝓣</a><a id="925" class="Symbol">}</a> <a id="927" href="InjectiveTypes.Subtypes.html#927" class="Bound">Σ-ainj</a> <a id="934" class="Symbol">=</a> <a id="936" href="InjectiveTypes.Subtypes.html#1246" class="Function">f</a> <a id="938" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="940" href="InjectiveTypes.Subtypes.html#1273" class="Function">g</a> <a id="942" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="944" href="InjectiveTypes.Subtypes.html#1475" class="Function">h</a>
  <a id="948" class="Keyword">where</a>
   <a id="957" href="InjectiveTypes.Subtypes.html#957" class="Function">ρ</a> <a id="959" class="Symbol">:</a> <a id="961" href="UF.Retracts.html#1303" class="Function Operator">retract</a> <a id="969" href="MLTT.Sigma-Type.html#116" class="Record">Σ</a> <a id="971" href="InjectiveTypes.Subtypes.html#603" class="Bound">P</a> <a id="973" href="UF.Retracts.html#1303" class="Function Operator">of</a> <a id="976" href="InjectiveTypes.Subtypes.html#583" class="Bound">D</a>
   <a id="981" href="InjectiveTypes.Subtypes.html#957" class="Function">ρ</a> <a id="983" class="Symbol">=</a> <a id="985" href="InjectiveTypes.OverSmallMaps.html#2897" class="Function">embedding-retract&#39;</a> <a id="1004" href="InjectiveTypes.Subtypes.html#920" class="Bound">𝓦</a>
        <a id="1014" class="Symbol">(</a><a id="1015" href="MLTT.Sigma-Type.html#116" class="Record">Σ</a> <a id="1017" href="InjectiveTypes.Subtypes.html#603" class="Bound">P</a><a id="1018" class="Symbol">)</a>
        <a id="1028" href="InjectiveTypes.Subtypes.html#583" class="Bound">D</a>
        <a id="1038" href="InjectiveTypes.Subtypes.html#696" class="Function">s</a>
        <a id="1048" class="Symbol">(</a><a id="1049" href="UF.Embeddings.html#8652" class="Function">pr₁-is-embedding</a> <a id="1066" href="InjectiveTypes.Subtypes.html#627" class="Bound">P-is-prop-valued</a><a id="1082" class="Symbol">)</a>
        <a id="1092" href="UF.Size.html#22233" class="Function">pr₁-is-small-map</a>
        <a id="1117" href="InjectiveTypes.Subtypes.html#927" class="Bound">Σ-ainj</a>

   <a id="1128" href="InjectiveTypes.Subtypes.html#1128" class="Function">r</a> <a id="1130" class="Symbol">:</a> <a id="1132" href="InjectiveTypes.Subtypes.html#583" class="Bound">D</a> <a id="1134" class="Symbol">→</a> <a id="1136" href="MLTT.Sigma-Type.html#116" class="Record">Σ</a> <a id="1138" href="InjectiveTypes.Subtypes.html#603" class="Bound">P</a>
   <a id="1143" href="InjectiveTypes.Subtypes.html#1128" class="Function">r</a> <a id="1145" class="Symbol">=</a> <a id="1147" href="UF.Retracts.html#1552" class="Function">retraction</a> <a id="1158" href="InjectiveTypes.Subtypes.html#957" class="Function">ρ</a>

   <a id="1164" href="InjectiveTypes.Subtypes.html#1164" class="Function">_</a> <a id="1166" class="Symbol">:</a> <a id="1168" href="InjectiveTypes.Subtypes.html#696" class="Function">s</a> <a id="1170" href="MLTT.Id.html#193" class="Datatype Operator">＝</a> <a id="1172" href="UF.Retracts.html#1643" class="Function">section</a> <a id="1180" href="InjectiveTypes.Subtypes.html#957" class="Function">ρ</a>
   <a id="1185" class="Symbol">_</a> <a id="1187" class="Symbol">=</a> <a id="1189" href="MLTT.Identity-Type.html#157" class="InductiveConstructor">refl</a>

   <a id="1198" href="InjectiveTypes.Subtypes.html#1198" class="Function">rs</a> <a id="1201" class="Symbol">:</a> <a id="1203" href="InjectiveTypes.Subtypes.html#1128" class="Function">r</a> <a id="1205" href="MLTT.Pi.html#513" class="Function Operator">∘</a> <a id="1207" href="InjectiveTypes.Subtypes.html#696" class="Function">s</a> <a id="1209" href="MLTT.Id.html#1891" class="Function Operator">∼</a> <a id="1211" href="MLTT.Pi.html#444" class="Function">id</a>
   <a id="1217" href="InjectiveTypes.Subtypes.html#1198" class="Function">rs</a> <a id="1220" class="Symbol">=</a> <a id="1222" href="UF.Retracts.html#2157" class="Function">retract-condition</a> <a id="1240" href="InjectiveTypes.Subtypes.html#957" class="Function">ρ</a>

   <a id="1246" href="InjectiveTypes.Subtypes.html#1246" class="Function">f</a> <a id="1248" class="Symbol">:</a> <a id="1250" href="InjectiveTypes.Subtypes.html#583" class="Bound">D</a> <a id="1252" class="Symbol">→</a> <a id="1254" href="InjectiveTypes.Subtypes.html#583" class="Bound">D</a>
   <a id="1259" href="InjectiveTypes.Subtypes.html#1246" class="Function">f</a> <a id="1261" class="Symbol">=</a> <a id="1263" href="InjectiveTypes.Subtypes.html#696" class="Function">s</a> <a id="1265" href="MLTT.Pi.html#513" class="Function Operator">∘</a> <a id="1267" href="InjectiveTypes.Subtypes.html#1128" class="Function">r</a>

   <a id="1273" href="InjectiveTypes.Subtypes.html#1273" class="Function">g</a> <a id="1275" class="Symbol">:</a> <a id="1277" class="Symbol">(</a><a id="1278" href="InjectiveTypes.Subtypes.html#1278" class="Bound">d</a> <a id="1280" class="Symbol">:</a> <a id="1282" href="InjectiveTypes.Subtypes.html#583" class="Bound">D</a><a id="1283" class="Symbol">)</a> <a id="1285" class="Symbol">→</a> <a id="1287" href="InjectiveTypes.Subtypes.html#603" class="Bound">P</a> <a id="1289" class="Symbol">(</a><a id="1290" href="InjectiveTypes.Subtypes.html#1246" class="Function">f</a> <a id="1292" href="InjectiveTypes.Subtypes.html#1278" class="Bound">d</a><a id="1293" class="Symbol">)</a>
   <a id="1298" href="InjectiveTypes.Subtypes.html#1273" class="Function">g</a> <a id="1300" href="InjectiveTypes.Subtypes.html#1300" class="Bound">d</a> <a id="1302" class="Symbol">=</a> <a id="1304" href="MLTT.Sigma-Type.html#209" class="Field">pr₂</a> <a id="1308" class="Symbol">(</a><a id="1309" href="InjectiveTypes.Subtypes.html#1128" class="Function">r</a> <a id="1311" href="InjectiveTypes.Subtypes.html#1300" class="Bound">d</a><a id="1312" class="Symbol">)</a>

   <a id="1318" href="InjectiveTypes.Subtypes.html#1318" class="Function">fg</a> <a id="1321" class="Symbol">:</a> <a id="1323" class="Symbol">(</a><a id="1324" href="InjectiveTypes.Subtypes.html#1324" class="Bound">d</a> <a id="1326" class="Symbol">:</a> <a id="1328" href="InjectiveTypes.Subtypes.html#583" class="Bound">D</a><a id="1329" class="Symbol">)</a> <a id="1331" class="Symbol">(</a><a id="1332" href="InjectiveTypes.Subtypes.html#1332" class="Bound">p</a> <a id="1334" class="Symbol">:</a> <a id="1336" href="InjectiveTypes.Subtypes.html#603" class="Bound">P</a> <a id="1338" href="InjectiveTypes.Subtypes.html#1324" class="Bound">d</a><a id="1339" class="Symbol">)</a> <a id="1341" class="Symbol">→</a> <a id="1343" class="Symbol">(</a><a id="1344" href="InjectiveTypes.Subtypes.html#1246" class="Function">f</a> <a id="1346" href="InjectiveTypes.Subtypes.html#1324" class="Bound">d</a> <a id="1348" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="1350" href="InjectiveTypes.Subtypes.html#1273" class="Function">g</a> <a id="1352" href="InjectiveTypes.Subtypes.html#1324" class="Bound">d</a><a id="1353" class="Symbol">)</a> <a id="1355" href="MLTT.Id.html#193" class="Datatype Operator">＝</a> <a id="1357" class="Symbol">(</a><a id="1358" href="InjectiveTypes.Subtypes.html#1324" class="Bound">d</a> <a id="1360" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="1362" href="InjectiveTypes.Subtypes.html#1332" class="Bound">p</a><a id="1363" class="Symbol">)</a>
   <a id="1368" href="InjectiveTypes.Subtypes.html#1318" class="Function">fg</a> <a id="1371" href="InjectiveTypes.Subtypes.html#1371" class="Bound">d</a> <a id="1373" href="InjectiveTypes.Subtypes.html#1373" class="Bound">p</a> <a id="1375" class="Symbol">=</a> <a id="1377" href="InjectiveTypes.Subtypes.html#1246" class="Function">f</a> <a id="1379" href="InjectiveTypes.Subtypes.html#1371" class="Bound">d</a> <a id="1381" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="1383" href="InjectiveTypes.Subtypes.html#1273" class="Function">g</a> <a id="1385" href="InjectiveTypes.Subtypes.html#1371" class="Bound">d</a>     <a id="1391" href="MLTT.Id.html#2705" class="Function Operator">＝⟨</a> <a id="1394" href="MLTT.Identity-Type.html#157" class="InductiveConstructor">refl</a> <a id="1399" href="MLTT.Id.html#2705" class="Function Operator">⟩</a>
            <a id="1413" href="InjectiveTypes.Subtypes.html#1128" class="Function">r</a> <a id="1415" class="Symbol">(</a><a id="1416" href="InjectiveTypes.Subtypes.html#696" class="Function">s</a> <a id="1418" class="Symbol">(</a><a id="1419" href="InjectiveTypes.Subtypes.html#1371" class="Bound">d</a> <a id="1421" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="1423" href="InjectiveTypes.Subtypes.html#1373" class="Bound">p</a><a id="1424" class="Symbol">))</a> <a id="1427" href="MLTT.Id.html#2705" class="Function Operator">＝⟨</a> <a id="1430" href="InjectiveTypes.Subtypes.html#1198" class="Function">rs</a> <a id="1433" class="Symbol">(</a><a id="1434" href="InjectiveTypes.Subtypes.html#1371" class="Bound">d</a> <a id="1436" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="1438" href="InjectiveTypes.Subtypes.html#1373" class="Bound">p</a><a id="1439" class="Symbol">)</a> <a id="1441" href="MLTT.Id.html#2705" class="Function Operator">⟩</a>
            <a id="1455" class="Symbol">(</a><a id="1456" href="InjectiveTypes.Subtypes.html#1371" class="Bound">d</a> <a id="1458" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="1460" href="InjectiveTypes.Subtypes.html#1373" class="Bound">p</a><a id="1461" class="Symbol">)</a>       <a id="1469" href="MLTT.Id.html#2787" class="Function Operator">∎</a>

   <a id="1475" href="InjectiveTypes.Subtypes.html#1475" class="Function">h</a> <a id="1477" class="Symbol">:</a> <a id="1479" class="Symbol">(</a><a id="1480" href="InjectiveTypes.Subtypes.html#1480" class="Bound">d</a> <a id="1482" class="Symbol">:</a> <a id="1484" href="InjectiveTypes.Subtypes.html#583" class="Bound">D</a><a id="1485" class="Symbol">)</a> <a id="1487" class="Symbol">→</a> <a id="1489" href="InjectiveTypes.Subtypes.html#603" class="Bound">P</a> <a id="1491" href="InjectiveTypes.Subtypes.html#1480" class="Bound">d</a> <a id="1493" class="Symbol">→</a> <a id="1495" href="InjectiveTypes.Subtypes.html#1246" class="Function">f</a> <a id="1497" href="InjectiveTypes.Subtypes.html#1480" class="Bound">d</a> <a id="1499" href="MLTT.Id.html#193" class="Datatype Operator">＝</a> <a id="1501" href="InjectiveTypes.Subtypes.html#1480" class="Bound">d</a>
   <a id="1506" href="InjectiveTypes.Subtypes.html#1475" class="Function">h</a> <a id="1508" href="InjectiveTypes.Subtypes.html#1508" class="Bound">d</a> <a id="1510" href="InjectiveTypes.Subtypes.html#1510" class="Bound">p</a> <a id="1512" class="Symbol">=</a> <a id="1514" href="MLTT.Id.html#1136" class="Function">ap</a> <a id="1517" href="InjectiveTypes.Subtypes.html#696" class="Function">s</a> <a id="1519" class="Symbol">(</a><a id="1520" href="InjectiveTypes.Subtypes.html#1318" class="Function">fg</a> <a id="1523" href="InjectiveTypes.Subtypes.html#1508" class="Bound">d</a> <a id="1525" href="InjectiveTypes.Subtypes.html#1510" class="Bound">p</a><a id="1526" class="Symbol">)</a>

 <a id="1530" href="InjectiveTypes.Subtypes.html#1530" class="Function">sufficient-condition-for-injectivity-of-subtype</a>
  <a id="1580" class="Symbol">:</a> <a id="1582" href="InjectiveTypes.Blackboard.html#15851" class="Function">ainjective-type</a> <a id="1598" href="InjectiveTypes.Subtypes.html#583" class="Bound">D</a> <a id="1600" href="MLTT.Universes.html#3838" class="Generalizable">𝓦</a> <a id="1602" href="MLTT.Universes.html#3840" class="Generalizable">𝓣</a>
  <a id="1606" class="Symbol">→</a>  <a id="1609" class="Symbol">(</a><a id="1610" href="MLTT.Sigma.html#423" class="Function">Σ</a> <a id="1612" href="InjectiveTypes.Subtypes.html#1612" class="Bound">f</a> <a id="1614" href="MLTT.Sigma.html#423" class="Function">꞉</a> <a id="1616" class="Symbol">(</a><a id="1617" href="InjectiveTypes.Subtypes.html#583" class="Bound">D</a> <a id="1619" class="Symbol">→</a> <a id="1621" href="InjectiveTypes.Subtypes.html#583" class="Bound">D</a><a id="1622" class="Symbol">)</a> <a id="1624" href="MLTT.Sigma.html#423" class="Function">,</a> <a id="1626" class="Symbol">((</a><a id="1628" href="InjectiveTypes.Subtypes.html#1628" class="Bound">d</a> <a id="1630" class="Symbol">:</a> <a id="1632" href="InjectiveTypes.Subtypes.html#583" class="Bound">D</a><a id="1633" class="Symbol">)</a> <a id="1635" class="Symbol">→</a> <a id="1637" href="InjectiveTypes.Subtypes.html#603" class="Bound">P</a> <a id="1639" class="Symbol">(</a><a id="1640" href="InjectiveTypes.Subtypes.html#1612" class="Bound">f</a> <a id="1642" href="InjectiveTypes.Subtypes.html#1628" class="Bound">d</a><a id="1643" class="Symbol">))</a> <a id="1646" href="MLTT.Sigma.html#558" class="Function Operator">×</a> <a id="1648" class="Symbol">((</a><a id="1650" href="InjectiveTypes.Subtypes.html#1650" class="Bound">d</a> <a id="1652" class="Symbol">:</a> <a id="1654" href="InjectiveTypes.Subtypes.html#583" class="Bound">D</a><a id="1655" class="Symbol">)</a> <a id="1657" class="Symbol">→</a> <a id="1659" href="InjectiveTypes.Subtypes.html#603" class="Bound">P</a> <a id="1661" href="InjectiveTypes.Subtypes.html#1650" class="Bound">d</a> <a id="1663" class="Symbol">→</a> <a id="1665" href="InjectiveTypes.Subtypes.html#1612" class="Bound">f</a> <a id="1667" href="InjectiveTypes.Subtypes.html#1650" class="Bound">d</a> <a id="1669" href="MLTT.Id.html#193" class="Datatype Operator">＝</a> <a id="1671" href="InjectiveTypes.Subtypes.html#1650" class="Bound">d</a><a id="1672" class="Symbol">))</a>
  <a id="1677" class="Symbol">→</a> <a id="1679" href="InjectiveTypes.Blackboard.html#15851" class="Function">ainjective-type</a> <a id="1695" class="Symbol">(</a><a id="1696" href="MLTT.Sigma-Type.html#116" class="Record">Σ</a> <a id="1698" href="InjectiveTypes.Subtypes.html#603" class="Bound">P</a><a id="1699" class="Symbol">)</a> <a id="1701" href="MLTT.Universes.html#3838" class="Generalizable">𝓦</a> <a id="1703" href="MLTT.Universes.html#3840" class="Generalizable">𝓣</a>
 <a id="1706" href="InjectiveTypes.Subtypes.html#1530" class="Function">sufficient-condition-for-injectivity-of-subtype</a> <a id="1754" href="InjectiveTypes.Subtypes.html#1754" class="Bound">D-ainj</a> <a id="1761" class="Symbol">(</a><a id="1762" href="InjectiveTypes.Subtypes.html#1762" class="Bound">f</a> <a id="1764" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="1766" href="InjectiveTypes.Subtypes.html#1766" class="Bound">g</a> <a id="1768" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="1770" href="InjectiveTypes.Subtypes.html#1770" class="Bound">h</a><a id="1771" class="Symbol">)</a>
  <a id="1775" class="Symbol">=</a> <a id="1777" href="InjectiveTypes.Blackboard.html#17384" class="Function">retract-of-ainjective</a> <a id="1799" class="Symbol">(</a><a id="1800" href="MLTT.Sigma-Type.html#116" class="Record">Σ</a> <a id="1802" href="InjectiveTypes.Subtypes.html#603" class="Bound">P</a><a id="1803" class="Symbol">)</a> <a id="1805" href="InjectiveTypes.Subtypes.html#583" class="Bound">D</a> <a id="1807" href="InjectiveTypes.Subtypes.html#1754" class="Bound">D-ainj</a> <a id="1814" class="Symbol">(</a><a id="1815" href="InjectiveTypes.Subtypes.html#1838" class="Function">r</a> <a id="1817" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="1819" href="InjectiveTypes.Subtypes.html#696" class="Function">s</a> <a id="1821" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="1823" href="InjectiveTypes.Subtypes.html#1873" class="Function">rs</a><a id="1825" class="Symbol">)</a>
  <a id="1829" class="Keyword">where</a>
   <a id="1838" href="InjectiveTypes.Subtypes.html#1838" class="Function">r</a> <a id="1840" class="Symbol">:</a> <a id="1842" href="InjectiveTypes.Subtypes.html#583" class="Bound">D</a> <a id="1844" class="Symbol">→</a> <a id="1846" href="MLTT.Sigma-Type.html#116" class="Record">Σ</a> <a id="1848" href="InjectiveTypes.Subtypes.html#603" class="Bound">P</a>
   <a id="1853" href="InjectiveTypes.Subtypes.html#1838" class="Function">r</a> <a id="1855" href="InjectiveTypes.Subtypes.html#1855" class="Bound">d</a> <a id="1857" class="Symbol">=</a> <a id="1859" href="InjectiveTypes.Subtypes.html#1762" class="Bound">f</a> <a id="1861" href="InjectiveTypes.Subtypes.html#1855" class="Bound">d</a> <a id="1863" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="1865" href="InjectiveTypes.Subtypes.html#1766" class="Bound">g</a> <a id="1867" href="InjectiveTypes.Subtypes.html#1855" class="Bound">d</a>

   <a id="1873" href="InjectiveTypes.Subtypes.html#1873" class="Function">rs</a> <a id="1876" class="Symbol">:</a> <a id="1878" href="InjectiveTypes.Subtypes.html#1838" class="Function">r</a> <a id="1880" href="MLTT.Pi.html#513" class="Function Operator">∘</a> <a id="1882" href="InjectiveTypes.Subtypes.html#696" class="Function">s</a> <a id="1884" href="MLTT.Id.html#1891" class="Function Operator">∼</a> <a id="1886" href="MLTT.Pi.html#444" class="Function">id</a>
   <a id="1892" href="InjectiveTypes.Subtypes.html#1873" class="Function">rs</a> <a id="1895" class="Symbol">(</a><a id="1896" href="InjectiveTypes.Subtypes.html#1896" class="Bound">d</a> <a id="1898" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="1900" href="InjectiveTypes.Subtypes.html#1900" class="Bound">p</a><a id="1901" class="Symbol">)</a> <a id="1903" class="Symbol">=</a> <a id="1905" href="InjectiveTypes.Subtypes.html#1838" class="Function">r</a> <a id="1907" class="Symbol">(</a><a id="1908" href="InjectiveTypes.Subtypes.html#696" class="Function">s</a> <a id="1910" class="Symbol">(</a><a id="1911" href="InjectiveTypes.Subtypes.html#1896" class="Bound">d</a> <a id="1913" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="1915" href="InjectiveTypes.Subtypes.html#1900" class="Bound">p</a><a id="1916" class="Symbol">))</a> <a id="1919" href="MLTT.Id.html#2705" class="Function Operator">＝⟨</a> <a id="1922" href="MLTT.Identity-Type.html#157" class="InductiveConstructor">refl</a> <a id="1927" href="MLTT.Id.html#2705" class="Function Operator">⟩</a>
                <a id="1945" href="InjectiveTypes.Subtypes.html#1838" class="Function">r</a> <a id="1947" href="InjectiveTypes.Subtypes.html#1896" class="Bound">d</a>           <a id="1959" href="MLTT.Id.html#2705" class="Function Operator">＝⟨</a> <a id="1962" href="MLTT.Identity-Type.html#157" class="InductiveConstructor">refl</a> <a id="1967" href="MLTT.Id.html#2705" class="Function Operator">⟩</a>
                <a id="1985" href="InjectiveTypes.Subtypes.html#1762" class="Bound">f</a> <a id="1987" href="InjectiveTypes.Subtypes.html#1896" class="Bound">d</a> <a id="1989" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="1991" href="InjectiveTypes.Subtypes.html#1766" class="Bound">g</a> <a id="1993" href="InjectiveTypes.Subtypes.html#1896" class="Bound">d</a>     <a id="1999" href="MLTT.Id.html#2705" class="Function Operator">＝⟨</a> <a id="2002" href="UF.Subsingletons.html#4964" class="Function">to-subtype-＝</a> <a id="2015" href="InjectiveTypes.Subtypes.html#627" class="Bound">P-is-prop-valued</a> <a id="2032" class="Symbol">(</a><a id="2033" href="InjectiveTypes.Subtypes.html#1770" class="Bound">h</a> <a id="2035" href="InjectiveTypes.Subtypes.html#1896" class="Bound">d</a> <a id="2037" href="InjectiveTypes.Subtypes.html#1900" class="Bound">p</a><a id="2038" class="Symbol">)</a> <a id="2040" href="MLTT.Id.html#2705" class="Function Operator">⟩</a>
                <a id="2058" href="InjectiveTypes.Subtypes.html#1896" class="Bound">d</a> <a id="2060" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="2062" href="InjectiveTypes.Subtypes.html#1900" class="Bound">p</a>         <a id="2072" href="MLTT.Id.html#2787" class="Function Operator">∎</a>

<a id="2075" class="Markup">\end{code}</a><a id="2085" class="Background">

By composing the necessary and sufficient conditions, we get the
following resizing theorem as a corollary.

</a><a id="2196" class="Markup">\begin{code}</a>

 <a id="2211" href="InjectiveTypes.Subtypes.html#2211" class="Function">subtype-injectivity-resizing</a>
  <a id="2242" class="Symbol">:</a> <a id="2244" class="Symbol">({</a><a id="2246" href="InjectiveTypes.Subtypes.html#2246" class="Bound">𝓦</a> <a id="2248" href="InjectiveTypes.Subtypes.html#2248" class="Bound">𝓣</a> <a id="2250" href="InjectiveTypes.Subtypes.html#2250" class="Bound">𝓣&#39;</a><a id="2252" class="Symbol">}</a> <a id="2254" href="InjectiveTypes.Subtypes.html#2254" class="Bound">𝓥&#39;</a> <a id="2257" class="Symbol">:</a> <a id="2259" href="Agda.Primitive.html#742" class="Postulate">Universe</a><a id="2267" class="Symbol">)</a>
  <a id="2271" class="Symbol">→</a> <a id="2273" href="InjectiveTypes.Blackboard.html#15851" class="Function">ainjective-type</a> <a id="2289" href="InjectiveTypes.Subtypes.html#583" class="Bound">D</a> <a id="2291" href="InjectiveTypes.Subtypes.html#2246" class="Bound">𝓦</a> <a id="2293" href="InjectiveTypes.Subtypes.html#2248" class="Bound">𝓣</a>
  <a id="2297" class="Symbol">→</a> <a id="2299" href="InjectiveTypes.Blackboard.html#15851" class="Function">ainjective-type</a> <a id="2315" class="Symbol">(</a><a id="2316" href="MLTT.Sigma-Type.html#116" class="Record">Σ</a> <a id="2318" href="InjectiveTypes.Subtypes.html#603" class="Bound">P</a><a id="2319" class="Symbol">)</a> <a id="2321" class="Symbol">(</a><a id="2322" href="InjectiveTypes.Subtypes.html#611" class="Bound">𝓥</a> <a id="2324" href="Agda.Primitive.html#961" class="Primitive Operator">⊔</a> <a id="2326" href="InjectiveTypes.Subtypes.html#2254" class="Bound">𝓥&#39;</a><a id="2328" class="Symbol">)</a> <a id="2330" href="InjectiveTypes.Subtypes.html#2250" class="Bound">𝓣&#39;</a>
  <a id="2335" class="Symbol">→</a> <a id="2337" href="InjectiveTypes.Blackboard.html#15851" class="Function">ainjective-type</a> <a id="2353" class="Symbol">(</a><a id="2354" href="MLTT.Sigma-Type.html#116" class="Record">Σ</a> <a id="2356" href="InjectiveTypes.Subtypes.html#603" class="Bound">P</a><a id="2357" class="Symbol">)</a> <a id="2359" href="InjectiveTypes.Subtypes.html#2246" class="Bound">𝓦</a> <a id="2361" href="InjectiveTypes.Subtypes.html#2248" class="Bound">𝓣</a>
 <a id="2364" href="InjectiveTypes.Subtypes.html#2211" class="Function">subtype-injectivity-resizing</a> <a id="2393" href="InjectiveTypes.Subtypes.html#2393" class="Bound">𝓥&#39;</a> <a id="2396" href="InjectiveTypes.Subtypes.html#2396" class="Bound">D-ainj</a> <a id="2403" href="InjectiveTypes.Subtypes.html#2403" class="Bound">Σ-ainj</a>
  <a id="2412" class="Symbol">=</a> <a id="2414" href="InjectiveTypes.Subtypes.html#1530" class="Function">sufficient-condition-for-injectivity-of-subtype</a> <a id="2462" href="InjectiveTypes.Subtypes.html#2396" class="Bound">D-ainj</a>
     <a id="2474" class="Symbol">(</a><a id="2475" href="InjectiveTypes.Subtypes.html#720" class="Function">necessary-condition-for-injectivity-of-subtype</a> <a id="2522" class="Symbol">{</a><a id="2523" href="InjectiveTypes.Subtypes.html#2393" class="Bound">𝓥&#39;</a><a id="2525" class="Symbol">}</a> <a id="2527" href="InjectiveTypes.Subtypes.html#2403" class="Bound">Σ-ainj</a><a id="2533" class="Symbol">)</a>

<a id="2536" class="Markup">\end{code}</a><a id="2546" class="Background">

The following choice of universes makes the condition truly
sufficient and necessary.

</a><a id="2635" class="Markup">\begin{code}</a>

<a id="2649" class="Keyword">module</a> <a id="2656" href="InjectiveTypes.Subtypes.html#2656" class="Module">_</a> <a id="2658" class="Symbol">(</a><a id="2659" href="InjectiveTypes.Subtypes.html#2659" class="Bound">D</a> <a id="2661" class="Symbol">:</a> <a id="2663" href="MLTT.Universes.html#3834" class="Generalizable">𝓤</a> <a id="2665" href="MLTT.Universes.html#3977" class="Function Operator">̇</a> <a id="2667" class="Symbol">)</a>
         <a id="2678" class="Symbol">(</a><a id="2679" href="InjectiveTypes.Subtypes.html#2679" class="Bound">P</a> <a id="2681" class="Symbol">:</a> <a id="2683" href="InjectiveTypes.Subtypes.html#2659" class="Bound">D</a> <a id="2685" class="Symbol">→</a> <a id="2687" href="MLTT.Universes.html#3836" class="Generalizable">𝓥</a> <a id="2689" href="MLTT.Universes.html#3977" class="Function Operator">̇</a> <a id="2691" class="Symbol">)</a>
         <a id="2702" class="Symbol">(</a><a id="2703" href="InjectiveTypes.Subtypes.html#2703" class="Bound">P-is-prop-valued</a> <a id="2720" class="Symbol">:</a> <a id="2722" class="Symbol">(</a><a id="2723" href="InjectiveTypes.Subtypes.html#2723" class="Bound">d</a> <a id="2725" class="Symbol">:</a> <a id="2727" href="InjectiveTypes.Subtypes.html#2659" class="Bound">D</a><a id="2728" class="Symbol">)</a> <a id="2730" class="Symbol">→</a> <a id="2732" href="UF.Subsingletons.html#469" class="Function">is-prop</a> <a id="2740" class="Symbol">(</a><a id="2741" href="InjectiveTypes.Subtypes.html#2679" class="Bound">P</a> <a id="2743" href="InjectiveTypes.Subtypes.html#2723" class="Bound">d</a><a id="2744" class="Symbol">))</a>
         <a id="2756" class="Symbol">(</a><a id="2757" href="InjectiveTypes.Subtypes.html#2757" class="Bound">D-ainj</a> <a id="2764" class="Symbol">:</a> <a id="2766" href="InjectiveTypes.Blackboard.html#15851" class="Function">ainjective-type</a> <a id="2782" href="InjectiveTypes.Subtypes.html#2659" class="Bound">D</a> <a id="2784" class="Symbol">(</a><a id="2785" href="MLTT.Universes.html#3836" class="Generalizable">𝓥</a> <a id="2787" href="Agda.Primitive.html#961" class="Primitive Operator">⊔</a> <a id="2789" href="MLTT.Universes.html#3838" class="Generalizable">𝓦</a><a id="2790" class="Symbol">)</a> <a id="2792" href="MLTT.Universes.html#3840" class="Generalizable">𝓣</a><a id="2793" class="Symbol">)</a>
       <a id="2802" class="Keyword">where</a>

 <a id="2810" href="InjectiveTypes.Subtypes.html#2810" class="Function">necessary-and-sufficient-condition-for-injectivity-of-subtype</a>
  <a id="2874" class="Symbol">:</a> <a id="2876" href="InjectiveTypes.Blackboard.html#15851" class="Function">ainjective-type</a> <a id="2892" class="Symbol">(</a><a id="2893" href="MLTT.Sigma-Type.html#116" class="Record">Σ</a> <a id="2895" href="InjectiveTypes.Subtypes.html#2679" class="Bound">P</a><a id="2896" class="Symbol">)</a> <a id="2898" class="Symbol">(</a><a id="2899" href="InjectiveTypes.Subtypes.html#2687" class="Bound">𝓥</a> <a id="2901" href="Agda.Primitive.html#961" class="Primitive Operator">⊔</a> <a id="2903" href="InjectiveTypes.Subtypes.html#2789" class="Bound">𝓦</a><a id="2904" class="Symbol">)</a> <a id="2906" href="InjectiveTypes.Subtypes.html#2792" class="Bound">𝓣</a>
  <a id="2910" href="Notation.General.html#2587" class="Function Operator">↔</a> <a id="2912" class="Symbol">(</a><a id="2913" href="MLTT.Sigma.html#423" class="Function">Σ</a> <a id="2915" href="InjectiveTypes.Subtypes.html#2915" class="Bound">f</a> <a id="2917" href="MLTT.Sigma.html#423" class="Function">꞉</a> <a id="2919" class="Symbol">(</a><a id="2920" href="InjectiveTypes.Subtypes.html#2659" class="Bound">D</a> <a id="2922" class="Symbol">→</a> <a id="2924" href="InjectiveTypes.Subtypes.html#2659" class="Bound">D</a><a id="2925" class="Symbol">)</a> <a id="2927" href="MLTT.Sigma.html#423" class="Function">,</a> <a id="2929" class="Symbol">((</a><a id="2931" href="InjectiveTypes.Subtypes.html#2931" class="Bound">d</a> <a id="2933" class="Symbol">:</a> <a id="2935" href="InjectiveTypes.Subtypes.html#2659" class="Bound">D</a><a id="2936" class="Symbol">)</a> <a id="2938" class="Symbol">→</a> <a id="2940" href="InjectiveTypes.Subtypes.html#2679" class="Bound">P</a> <a id="2942" class="Symbol">(</a><a id="2943" href="InjectiveTypes.Subtypes.html#2915" class="Bound">f</a> <a id="2945" href="InjectiveTypes.Subtypes.html#2931" class="Bound">d</a><a id="2946" class="Symbol">))</a> <a id="2949" href="MLTT.Sigma.html#558" class="Function Operator">×</a> <a id="2951" class="Symbol">((</a><a id="2953" href="InjectiveTypes.Subtypes.html#2953" class="Bound">d</a> <a id="2955" class="Symbol">:</a> <a id="2957" href="InjectiveTypes.Subtypes.html#2659" class="Bound">D</a><a id="2958" class="Symbol">)</a> <a id="2960" class="Symbol">→</a> <a id="2962" href="InjectiveTypes.Subtypes.html#2679" class="Bound">P</a> <a id="2964" href="InjectiveTypes.Subtypes.html#2953" class="Bound">d</a> <a id="2966" class="Symbol">→</a> <a id="2968" href="InjectiveTypes.Subtypes.html#2915" class="Bound">f</a> <a id="2970" href="InjectiveTypes.Subtypes.html#2953" class="Bound">d</a> <a id="2972" href="MLTT.Id.html#193" class="Datatype Operator">＝</a> <a id="2974" href="InjectiveTypes.Subtypes.html#2953" class="Bound">d</a><a id="2975" class="Symbol">))</a>
 <a id="2979" href="InjectiveTypes.Subtypes.html#2810" class="Function">necessary-and-sufficient-condition-for-injectivity-of-subtype</a>
  <a id="3043" class="Symbol">=</a> <a id="3045" href="InjectiveTypes.Subtypes.html#720" class="Function">necessary-condition-for-injectivity-of-subtype</a> <a id="3092" href="InjectiveTypes.Subtypes.html#2659" class="Bound">D</a> <a id="3094" href="InjectiveTypes.Subtypes.html#2679" class="Bound">P</a> <a id="3096" href="InjectiveTypes.Subtypes.html#2703" class="Bound">P-is-prop-valued</a> <a id="3113" class="Symbol">{</a><a id="3114" href="InjectiveTypes.Subtypes.html#2789" class="Bound">𝓦</a><a id="3115" class="Symbol">}</a> <a id="3117" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a>
    <a id="3123" href="InjectiveTypes.Subtypes.html#1530" class="Function">sufficient-condition-for-injectivity-of-subtype</a> <a id="3171" href="InjectiveTypes.Subtypes.html#2659" class="Bound">D</a> <a id="3173" href="InjectiveTypes.Subtypes.html#2679" class="Bound">P</a> <a id="3175" href="InjectiveTypes.Subtypes.html#2703" class="Bound">P-is-prop-valued</a> <a id="3192" href="InjectiveTypes.Subtypes.html#2757" class="Bound">D-ainj</a>

<a id="3200" class="Markup">\end{code}</a><a id="3210" class="Background">

Because there are no small injective types unless Ω¬¬-resizing holds,
the following particular case is of interest.

</a><a id="3329" class="Markup">\begin{code}</a>

<a id="3343" class="Keyword">module</a> <a id="3350" href="InjectiveTypes.Subtypes.html#3350" class="Module">_</a> <a id="3352" class="Symbol">(</a><a id="3353" href="InjectiveTypes.Subtypes.html#3353" class="Bound">D</a> <a id="3355" class="Symbol">:</a> <a id="3357" href="MLTT.Universes.html#3834" class="Generalizable">𝓤</a> <a id="3359" href="Agda.Primitive.html#931" class="Primitive Operator">⁺</a> <a id="3361" href="MLTT.Universes.html#3977" class="Function Operator">̇</a> <a id="3363" class="Symbol">)</a>
         <a id="3374" class="Symbol">(</a><a id="3375" href="InjectiveTypes.Subtypes.html#3375" class="Bound">P</a> <a id="3377" class="Symbol">:</a> <a id="3379" href="InjectiveTypes.Subtypes.html#3353" class="Bound">D</a> <a id="3381" class="Symbol">→</a> <a id="3383" href="MLTT.Universes.html#3834" class="Generalizable">𝓤</a> <a id="3385" href="MLTT.Universes.html#3977" class="Function Operator">̇</a> <a id="3387" class="Symbol">)</a>
         <a id="3398" class="Symbol">(</a><a id="3399" href="InjectiveTypes.Subtypes.html#3399" class="Bound">P-is-prop-valued</a> <a id="3416" class="Symbol">:</a> <a id="3418" class="Symbol">(</a><a id="3419" href="InjectiveTypes.Subtypes.html#3419" class="Bound">d</a> <a id="3421" class="Symbol">:</a> <a id="3423" href="InjectiveTypes.Subtypes.html#3353" class="Bound">D</a><a id="3424" class="Symbol">)</a> <a id="3426" class="Symbol">→</a> <a id="3428" href="UF.Subsingletons.html#469" class="Function">is-prop</a> <a id="3436" class="Symbol">(</a><a id="3437" href="InjectiveTypes.Subtypes.html#3375" class="Bound">P</a> <a id="3439" href="InjectiveTypes.Subtypes.html#3419" class="Bound">d</a><a id="3440" class="Symbol">))</a>
         <a id="3452" class="Symbol">(</a><a id="3453" href="InjectiveTypes.Subtypes.html#3453" class="Bound">D-ainj</a> <a id="3460" class="Symbol">:</a> <a id="3462" href="InjectiveTypes.Blackboard.html#15851" class="Function">ainjective-type</a> <a id="3478" href="InjectiveTypes.Subtypes.html#3353" class="Bound">D</a> <a id="3480" href="MLTT.Universes.html#3834" class="Generalizable">𝓤</a> <a id="3482" href="MLTT.Universes.html#3834" class="Generalizable">𝓤</a><a id="3483" class="Symbol">)</a>
       <a id="3492" class="Keyword">where</a>

 <a id="3500" href="InjectiveTypes.Subtypes.html#3500" class="Function">necessary-and-sufficient-condition-for-injectivity-of-subtype-single-universe</a>
  <a id="3580" class="Symbol">:</a> <a id="3582" href="InjectiveTypes.Blackboard.html#15851" class="Function">ainjective-type</a> <a id="3598" class="Symbol">(</a><a id="3599" href="MLTT.Sigma-Type.html#116" class="Record">Σ</a> <a id="3601" href="InjectiveTypes.Subtypes.html#3375" class="Bound">P</a><a id="3602" class="Symbol">)</a> <a id="3604" href="InjectiveTypes.Subtypes.html#3357" class="Bound">𝓤</a> <a id="3606" href="InjectiveTypes.Subtypes.html#3357" class="Bound">𝓤</a>
  <a id="3610" href="Notation.General.html#2587" class="Function Operator">↔</a> <a id="3612" class="Symbol">(</a><a id="3613" href="MLTT.Sigma.html#423" class="Function">Σ</a> <a id="3615" href="InjectiveTypes.Subtypes.html#3615" class="Bound">f</a> <a id="3617" href="MLTT.Sigma.html#423" class="Function">꞉</a> <a id="3619" class="Symbol">(</a><a id="3620" href="InjectiveTypes.Subtypes.html#3353" class="Bound">D</a> <a id="3622" class="Symbol">→</a> <a id="3624" href="InjectiveTypes.Subtypes.html#3353" class="Bound">D</a><a id="3625" class="Symbol">)</a> <a id="3627" href="MLTT.Sigma.html#423" class="Function">,</a> <a id="3629" class="Symbol">((</a><a id="3631" href="InjectiveTypes.Subtypes.html#3631" class="Bound">d</a> <a id="3633" class="Symbol">:</a> <a id="3635" href="InjectiveTypes.Subtypes.html#3353" class="Bound">D</a><a id="3636" class="Symbol">)</a> <a id="3638" class="Symbol">→</a> <a id="3640" href="InjectiveTypes.Subtypes.html#3375" class="Bound">P</a> <a id="3642" class="Symbol">(</a><a id="3643" href="InjectiveTypes.Subtypes.html#3615" class="Bound">f</a> <a id="3645" href="InjectiveTypes.Subtypes.html#3631" class="Bound">d</a><a id="3646" class="Symbol">))</a> <a id="3649" href="MLTT.Sigma.html#558" class="Function Operator">×</a> <a id="3651" class="Symbol">((</a><a id="3653" href="InjectiveTypes.Subtypes.html#3653" class="Bound">d</a> <a id="3655" class="Symbol">:</a> <a id="3657" href="InjectiveTypes.Subtypes.html#3353" class="Bound">D</a><a id="3658" class="Symbol">)</a> <a id="3660" class="Symbol">→</a> <a id="3662" href="InjectiveTypes.Subtypes.html#3375" class="Bound">P</a> <a id="3664" href="InjectiveTypes.Subtypes.html#3653" class="Bound">d</a> <a id="3666" class="Symbol">→</a> <a id="3668" href="InjectiveTypes.Subtypes.html#3615" class="Bound">f</a> <a id="3670" href="InjectiveTypes.Subtypes.html#3653" class="Bound">d</a> <a id="3672" href="MLTT.Id.html#193" class="Datatype Operator">＝</a> <a id="3674" href="InjectiveTypes.Subtypes.html#3653" class="Bound">d</a><a id="3675" class="Symbol">))</a>
 <a id="3679" href="InjectiveTypes.Subtypes.html#3500" class="Function">necessary-and-sufficient-condition-for-injectivity-of-subtype-single-universe</a>
  <a id="3759" class="Symbol">=</a> <a id="3761" href="InjectiveTypes.Subtypes.html#2810" class="Function">necessary-and-sufficient-condition-for-injectivity-of-subtype</a>
     <a id="3828" class="Symbol">{</a><a id="3829" href="InjectiveTypes.Subtypes.html#3357" class="Bound">𝓤</a> <a id="3831" href="Agda.Primitive.html#931" class="Primitive Operator">⁺</a><a id="3832" class="Symbol">}</a> <a id="3834" class="Symbol">{</a><a id="3835" href="InjectiveTypes.Subtypes.html#3357" class="Bound">𝓤</a><a id="3836" class="Symbol">}</a> <a id="3838" class="Symbol">{</a><a id="3839" href="InjectiveTypes.Subtypes.html#3357" class="Bound">𝓤</a><a id="3840" class="Symbol">}</a> <a id="3842" class="Symbol">{</a><a id="3843" href="InjectiveTypes.Subtypes.html#3357" class="Bound">𝓤</a><a id="3844" class="Symbol">}</a>
     <a id="3851" href="InjectiveTypes.Subtypes.html#3353" class="Bound">D</a>
     <a id="3858" href="InjectiveTypes.Subtypes.html#3375" class="Bound">P</a>
     <a id="3865" href="InjectiveTypes.Subtypes.html#3399" class="Bound">P-is-prop-valued</a>
     <a id="3887" href="InjectiveTypes.Subtypes.html#3453" class="Bound">D-ainj</a>

<a id="3895" class="Markup">\end{code}</a><a id="3905" class="Background">

TODO. Can the above logical equivalences be made into type
equivalences?
</a></pre></body></html>