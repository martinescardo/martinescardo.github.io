<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>TypeTopology.CantorMinusPoint</title><link rel="stylesheet" href="Agda.css"><script type="text/javascript" src="highlight-hover.js"></script></head><body><pre class="Agda"><a id="1" class="Background">Martin Escardo, 11th December 2023.

We implement the isomorphism described at https://math.stackexchange.com/a/486093 .

Namely that the Cantor space (ℕ → 𝟚) with a removed point is
isomorphic to the product ℕ × (ℕ → 𝟚).

Because the Cantor space is homogeneous, meaning that for every two
points α and β there is an automorphism that maps α to β, it suffices
to consider a particular point of the Cantor space, as in the above
link, which is what we also do here.

To make the proof given in the above link constructive, we remove the
point by considering the subtype of all points *apart* from this
point, rather than all points *different* from this point.

</a><a id="663" class="Markup">\begin{code}</a>

<a id="677" class="Symbol">{-#</a> <a id="681" class="Keyword">OPTIONS</a> <a id="689" class="Pragma">--safe</a> <a id="696" class="Pragma">--without-K</a> <a id="708" class="Symbol">#-}</a>

<a id="713" class="Keyword">open</a> <a id="718" class="Keyword">import</a> <a id="725" href="MLTT.Spartan.html" class="Module">MLTT.Spartan</a>
<a id="738" class="Keyword">open</a> <a id="743" class="Keyword">import</a> <a id="750" href="MLTT.Two-Properties.html" class="Module">MLTT.Two-Properties</a>
<a id="770" class="Keyword">open</a> <a id="775" class="Keyword">import</a> <a id="782" href="Naturals.Order.html" class="Module">Naturals.Order</a>
<a id="797" class="Keyword">open</a> <a id="802" class="Keyword">import</a> <a id="809" href="Notation.Order.html" class="Module">Notation.Order</a>
<a id="824" class="Keyword">open</a> <a id="829" class="Keyword">import</a> <a id="836" href="TypeTopology.Cantor.html" class="Module">TypeTopology.Cantor</a>
<a id="856" class="Keyword">open</a> <a id="861" class="Keyword">import</a> <a id="868" href="UF.Base.html" class="Module">UF.Base</a>
<a id="876" class="Keyword">open</a> <a id="881" class="Keyword">import</a> <a id="888" href="UF.Equiv.html" class="Module">UF.Equiv</a>
<a id="897" class="Keyword">open</a> <a id="902" class="Keyword">import</a> <a id="909" href="UF.FunExt.html" class="Module">UF.FunExt</a>
<a id="919" class="Keyword">open</a> <a id="924" class="Keyword">import</a> <a id="931" href="UF.Subsingletons.html" class="Module">UF.Subsingletons</a>

<a id="949" class="Keyword">module</a> <a id="956" href="TypeTopology.CantorMinusPoint.html" class="Module">TypeTopology.CantorMinusPoint</a> <a id="986" class="Symbol">(</a><a id="987" href="TypeTopology.CantorMinusPoint.html#987" class="Bound">fe</a> <a id="990" class="Symbol">:</a> <a id="992" href="UF.FunExt.html#1047" class="Function">Fun-Ext</a><a id="999" class="Symbol">)</a> <a id="1001" class="Keyword">where</a>

<a id="1008" class="Markup">\end{code}</a><a id="1018" class="Background">

The function ϕ is defined so that ϕ n β is the binary sequence of
n-many ones followed by a zero and then β.

</a><a id="1130" class="Markup">\begin{code}</a>

<a id="ϕ"></a><a id="1144" href="TypeTopology.CantorMinusPoint.html#1144" class="Function">ϕ</a> <a id="1146" class="Symbol">:</a> <a id="1148" href="MLTT.Natural-Numbers-Type.html#96" class="Datatype">ℕ</a> <a id="1150" class="Symbol">→</a> <a id="1152" href="TypeTopology.Cantor.html#678" class="Function">𝟚ᴺ</a> <a id="1155" class="Symbol">→</a> <a id="1157" href="TypeTopology.Cantor.html#678" class="Function">𝟚ᴺ</a>
<a id="1160" href="TypeTopology.CantorMinusPoint.html#1144" class="Function">ϕ</a> <a id="1162" class="Number">0</a>        <a id="1171" href="TypeTopology.CantorMinusPoint.html#1171" class="Bound">β</a> <a id="1173" class="Symbol">=</a> <a id="1175" href="MLTT.Two.html#140" class="InductiveConstructor">₀</a> <a id="1177" href="TypeTopology.Cantor.html#1097" class="Function Operator">∷</a> <a id="1179" href="TypeTopology.CantorMinusPoint.html#1171" class="Bound">β</a>
<a id="1181" href="TypeTopology.CantorMinusPoint.html#1144" class="Function">ϕ</a> <a id="1183" class="Symbol">(</a><a id="1184" href="MLTT.Natural-Numbers-Type.html#123" class="InductiveConstructor">succ</a> <a id="1189" href="TypeTopology.CantorMinusPoint.html#1189" class="Bound">n</a><a id="1190" class="Symbol">)</a> <a id="1192" href="TypeTopology.CantorMinusPoint.html#1192" class="Bound">β</a> <a id="1194" class="Symbol">=</a> <a id="1196" href="MLTT.Two.html#147" class="InductiveConstructor">₁</a> <a id="1198" href="TypeTopology.Cantor.html#1097" class="Function Operator">∷</a> <a id="1200" href="TypeTopology.CantorMinusPoint.html#1144" class="Function">ϕ</a> <a id="1202" href="TypeTopology.CantorMinusPoint.html#1189" class="Bound">n</a> <a id="1204" href="TypeTopology.CantorMinusPoint.html#1192" class="Bound">β</a>

<a id="1207" class="Markup">\end{code}</a><a id="1217" class="Background">

We will need the following two properties of the map ϕ.

</a><a id="1276" class="Markup">\begin{code}</a>

<a id="ϕ-property-δ"></a><a id="1290" href="TypeTopology.CantorMinusPoint.html#1290" class="Function">ϕ-property-δ</a> <a id="1303" class="Symbol">:</a> <a id="1305" class="Symbol">(</a><a id="1306" href="TypeTopology.CantorMinusPoint.html#1306" class="Bound">β</a> <a id="1308" class="Symbol">:</a> <a id="1310" href="TypeTopology.Cantor.html#678" class="Function">𝟚ᴺ</a><a id="1312" class="Symbol">)</a> <a id="1314" class="Symbol">(</a><a id="1315" href="TypeTopology.CantorMinusPoint.html#1315" class="Bound">i</a> <a id="1317" class="Symbol">:</a> <a id="1319" href="MLTT.Natural-Numbers-Type.html#96" class="Datatype">ℕ</a><a id="1320" class="Symbol">)</a> <a id="1322" class="Symbol">→</a> <a id="1324" href="TypeTopology.CantorMinusPoint.html#1144" class="Function">ϕ</a> <a id="1326" href="TypeTopology.CantorMinusPoint.html#1315" class="Bound">i</a> <a id="1328" href="TypeTopology.CantorMinusPoint.html#1306" class="Bound">β</a> <a id="1330" href="TypeTopology.CantorMinusPoint.html#1315" class="Bound">i</a> <a id="1332" href="MLTT.Negation.html#635" class="Function Operator">≠</a> <a id="1334" href="MLTT.Two.html#147" class="InductiveConstructor">₁</a>
<a id="1336" href="TypeTopology.CantorMinusPoint.html#1290" class="Function">ϕ-property-δ</a> <a id="1349" href="TypeTopology.CantorMinusPoint.html#1349" class="Bound">β</a> <a id="1351" class="Number">0</a>        <a id="1360" class="Symbol">=</a> <a id="1362" href="MLTT.Two-Properties.html#2184" class="Function">zero-is-not-one</a>
<a id="1378" href="TypeTopology.CantorMinusPoint.html#1290" class="Function">ϕ-property-δ</a> <a id="1391" href="TypeTopology.CantorMinusPoint.html#1391" class="Bound">β</a> <a id="1393" class="Symbol">(</a><a id="1394" href="MLTT.Natural-Numbers-Type.html#123" class="InductiveConstructor">succ</a> <a id="1399" href="TypeTopology.CantorMinusPoint.html#1399" class="Bound">i</a><a id="1400" class="Symbol">)</a> <a id="1402" class="Symbol">=</a> <a id="1404" href="TypeTopology.CantorMinusPoint.html#1290" class="Function">ϕ-property-δ</a> <a id="1417" href="TypeTopology.CantorMinusPoint.html#1391" class="Bound">β</a> <a id="1419" href="TypeTopology.CantorMinusPoint.html#1399" class="Bound">i</a>

<a id="ϕ-property-μ"></a><a id="1422" href="TypeTopology.CantorMinusPoint.html#1422" class="Function">ϕ-property-μ</a> <a id="1435" class="Symbol">:</a> <a id="1437" class="Symbol">(</a><a id="1438" href="TypeTopology.CantorMinusPoint.html#1438" class="Bound">β</a> <a id="1440" class="Symbol">:</a> <a id="1442" href="TypeTopology.Cantor.html#678" class="Function">𝟚ᴺ</a><a id="1444" class="Symbol">)</a> <a id="1446" class="Symbol">(</a><a id="1447" href="TypeTopology.CantorMinusPoint.html#1447" class="Bound">n</a> <a id="1449" href="TypeTopology.CantorMinusPoint.html#1449" class="Bound">i</a> <a id="1451" class="Symbol">:</a> <a id="1453" href="MLTT.Natural-Numbers-Type.html#96" class="Datatype">ℕ</a><a id="1454" class="Symbol">)</a> <a id="1456" class="Symbol">→</a> <a id="1458" href="TypeTopology.CantorMinusPoint.html#1144" class="Function">ϕ</a> <a id="1460" href="TypeTopology.CantorMinusPoint.html#1447" class="Bound">n</a> <a id="1462" href="TypeTopology.CantorMinusPoint.html#1438" class="Bound">β</a> <a id="1464" href="TypeTopology.CantorMinusPoint.html#1449" class="Bound">i</a> <a id="1466" href="MLTT.Negation.html#635" class="Function Operator">≠</a> <a id="1468" href="MLTT.Two.html#147" class="InductiveConstructor">₁</a> <a id="1470" class="Symbol">→</a> <a id="1472" href="TypeTopology.CantorMinusPoint.html#1447" class="Bound">n</a> <a id="1474" href="Notation.Order.html#559" class="Field Operator">≤</a> <a id="1476" href="TypeTopology.CantorMinusPoint.html#1449" class="Bound">i</a>
<a id="1478" href="TypeTopology.CantorMinusPoint.html#1422" class="Function">ϕ-property-μ</a> <a id="1491" href="TypeTopology.CantorMinusPoint.html#1491" class="Bound">β</a> <a id="1493" class="Number">0</a>        <a id="1502" href="TypeTopology.CantorMinusPoint.html#1502" class="Bound">i</a>        <a id="1511" href="TypeTopology.CantorMinusPoint.html#1511" class="Bound">ν</a> <a id="1513" class="Symbol">=</a> <a id="1515" href="Naturals.Order.html#1789" class="Function">zero-least</a> <a id="1526" href="TypeTopology.CantorMinusPoint.html#1502" class="Bound">i</a>
<a id="1528" href="TypeTopology.CantorMinusPoint.html#1422" class="Function">ϕ-property-μ</a> <a id="1541" href="TypeTopology.CantorMinusPoint.html#1541" class="Bound">β</a> <a id="1543" class="Symbol">(</a><a id="1544" href="MLTT.Natural-Numbers-Type.html#123" class="InductiveConstructor">succ</a> <a id="1549" href="TypeTopology.CantorMinusPoint.html#1549" class="Bound">n</a><a id="1550" class="Symbol">)</a> <a id="1552" class="Number">0</a>        <a id="1561" href="TypeTopology.CantorMinusPoint.html#1561" class="Bound">ν</a> <a id="1563" class="Symbol">=</a> <a id="1565" href="TypeTopology.CantorMinusPoint.html#1561" class="Bound">ν</a> <a id="1567" href="MLTT.Identity-Type.html#157" class="InductiveConstructor">refl</a>
<a id="1572" href="TypeTopology.CantorMinusPoint.html#1422" class="Function">ϕ-property-μ</a> <a id="1585" href="TypeTopology.CantorMinusPoint.html#1585" class="Bound">β</a> <a id="1587" class="Symbol">(</a><a id="1588" href="MLTT.Natural-Numbers-Type.html#123" class="InductiveConstructor">succ</a> <a id="1593" href="TypeTopology.CantorMinusPoint.html#1593" class="Bound">n</a><a id="1594" class="Symbol">)</a> <a id="1596" class="Symbol">(</a><a id="1597" href="MLTT.Natural-Numbers-Type.html#123" class="InductiveConstructor">succ</a> <a id="1602" href="TypeTopology.CantorMinusPoint.html#1602" class="Bound">i</a><a id="1603" class="Symbol">)</a> <a id="1605" href="TypeTopology.CantorMinusPoint.html#1605" class="Bound">ν</a> <a id="1607" class="Symbol">=</a> <a id="1609" href="TypeTopology.CantorMinusPoint.html#1422" class="Function">ϕ-property-μ</a> <a id="1622" href="TypeTopology.CantorMinusPoint.html#1585" class="Bound">β</a> <a id="1624" href="TypeTopology.CantorMinusPoint.html#1593" class="Bound">n</a> <a id="1626" href="TypeTopology.CantorMinusPoint.html#1602" class="Bound">i</a> <a id="1628" href="TypeTopology.CantorMinusPoint.html#1605" class="Bound">ν</a>

<a id="1631" class="Markup">\end{code}</a><a id="1641" class="Background">

The function ψ is defined so that ψ n α removes n + 1 terms from the
beginning of the sequence α.

</a><a id="1742" class="Markup">\begin{code}</a>

<a id="ψ"></a><a id="1756" href="TypeTopology.CantorMinusPoint.html#1756" class="Function">ψ</a> <a id="1758" class="Symbol">:</a> <a id="1760" href="MLTT.Natural-Numbers-Type.html#96" class="Datatype">ℕ</a> <a id="1762" class="Symbol">→</a> <a id="1764" href="TypeTopology.Cantor.html#678" class="Function">𝟚ᴺ</a> <a id="1767" class="Symbol">→</a> <a id="1769" href="TypeTopology.Cantor.html#678" class="Function">𝟚ᴺ</a>
<a id="1772" href="TypeTopology.CantorMinusPoint.html#1756" class="Function">ψ</a> <a id="1774" class="Number">0</a>        <a id="1783" href="TypeTopology.CantorMinusPoint.html#1783" class="Bound">α</a> <a id="1785" class="Symbol">=</a> <a id="1787" href="TypeTopology.Cantor.html#997" class="Function">tail</a> <a id="1792" href="TypeTopology.CantorMinusPoint.html#1783" class="Bound">α</a>
<a id="1794" href="TypeTopology.CantorMinusPoint.html#1756" class="Function">ψ</a> <a id="1796" class="Symbol">(</a><a id="1797" href="MLTT.Natural-Numbers-Type.html#123" class="InductiveConstructor">succ</a> <a id="1802" href="TypeTopology.CantorMinusPoint.html#1802" class="Bound">n</a><a id="1803" class="Symbol">)</a> <a id="1805" href="TypeTopology.CantorMinusPoint.html#1805" class="Bound">α</a> <a id="1807" class="Symbol">=</a> <a id="1809" href="TypeTopology.CantorMinusPoint.html#1756" class="Function">ψ</a> <a id="1811" href="TypeTopology.CantorMinusPoint.html#1802" class="Bound">n</a> <a id="1813" class="Symbol">(</a><a id="1814" href="TypeTopology.Cantor.html#997" class="Function">tail</a> <a id="1819" href="TypeTopology.CantorMinusPoint.html#1805" class="Bound">α</a><a id="1820" class="Symbol">)</a>

<a id="1823" class="Markup">\end{code}</a><a id="1833" class="Background">

The function ψ n is a left inverse of the function ϕ n.

</a><a id="1892" class="Markup">\begin{code}</a>

<a id="ψϕ"></a><a id="1906" href="TypeTopology.CantorMinusPoint.html#1906" class="Function">ψϕ</a> <a id="1909" class="Symbol">:</a> <a id="1911" class="Symbol">(</a><a id="1912" href="TypeTopology.CantorMinusPoint.html#1912" class="Bound">n</a> <a id="1914" class="Symbol">:</a> <a id="1916" href="MLTT.Natural-Numbers-Type.html#96" class="Datatype">ℕ</a><a id="1917" class="Symbol">)</a> <a id="1919" class="Symbol">→</a> <a id="1921" href="TypeTopology.CantorMinusPoint.html#1756" class="Function">ψ</a> <a id="1923" href="TypeTopology.CantorMinusPoint.html#1912" class="Bound">n</a> <a id="1925" href="MLTT.Pi.html#513" class="Function Operator">∘</a> <a id="1927" href="TypeTopology.CantorMinusPoint.html#1144" class="Function">ϕ</a> <a id="1929" href="TypeTopology.CantorMinusPoint.html#1912" class="Bound">n</a> <a id="1931" href="MLTT.Id.html#1891" class="Function Operator">∼</a> <a id="1933" href="MLTT.Pi.html#444" class="Function">id</a>
<a id="1936" href="TypeTopology.CantorMinusPoint.html#1906" class="Function">ψϕ</a> <a id="1939" href="TypeTopology.CantorMinusPoint.html#1939" class="Bound">n</a> <a id="1941" href="TypeTopology.CantorMinusPoint.html#1941" class="Bound">α</a> <a id="1943" class="Symbol">=</a> <a id="1945" href="UF.FunExt.html#1242" class="Function">dfunext</a> <a id="1953" href="TypeTopology.CantorMinusPoint.html#987" class="Bound">fe</a> <a id="1956" class="Symbol">(</a><a id="1957" href="TypeTopology.CantorMinusPoint.html#1973" class="Function">h</a> <a id="1959" href="TypeTopology.CantorMinusPoint.html#1939" class="Bound">n</a> <a id="1961" href="TypeTopology.CantorMinusPoint.html#1941" class="Bound">α</a><a id="1962" class="Symbol">)</a>
 <a id="1965" class="Keyword">where</a>
  <a id="1973" href="TypeTopology.CantorMinusPoint.html#1973" class="Function">h</a> <a id="1975" class="Symbol">:</a> <a id="1977" class="Symbol">(</a><a id="1978" href="TypeTopology.CantorMinusPoint.html#1978" class="Bound">n</a> <a id="1980" class="Symbol">:</a> <a id="1982" href="MLTT.Natural-Numbers-Type.html#96" class="Datatype">ℕ</a><a id="1983" class="Symbol">)</a> <a id="1985" class="Symbol">(</a><a id="1986" href="TypeTopology.CantorMinusPoint.html#1986" class="Bound">α</a> <a id="1988" class="Symbol">:</a> <a id="1990" href="TypeTopology.Cantor.html#678" class="Function">𝟚ᴺ</a><a id="1992" class="Symbol">)</a> <a id="1994" class="Symbol">→</a> <a id="1996" href="TypeTopology.CantorMinusPoint.html#1756" class="Function">ψ</a> <a id="1998" href="TypeTopology.CantorMinusPoint.html#1978" class="Bound">n</a> <a id="2000" class="Symbol">(</a><a id="2001" href="TypeTopology.CantorMinusPoint.html#1144" class="Function">ϕ</a> <a id="2003" href="TypeTopology.CantorMinusPoint.html#1978" class="Bound">n</a> <a id="2005" href="TypeTopology.CantorMinusPoint.html#1986" class="Bound">α</a><a id="2006" class="Symbol">)</a> <a id="2008" href="MLTT.Id.html#1891" class="Function Operator">∼</a> <a id="2010" href="TypeTopology.CantorMinusPoint.html#1986" class="Bound">α</a>
  <a id="2014" href="TypeTopology.CantorMinusPoint.html#1973" class="Function">h</a> <a id="2016" class="Number">0</a>        <a id="2025" class="Symbol">=</a> <a id="2027" href="TypeTopology.Cantor.html#1449" class="Function">tail-cons&#39;</a> <a id="2038" href="MLTT.Two.html#140" class="InductiveConstructor">₀</a>
  <a id="2042" href="TypeTopology.CantorMinusPoint.html#1973" class="Function">h</a> <a id="2044" class="Symbol">(</a><a id="2045" href="MLTT.Natural-Numbers-Type.html#123" class="InductiveConstructor">succ</a> <a id="2050" href="TypeTopology.CantorMinusPoint.html#2050" class="Bound">n</a><a id="2051" class="Symbol">)</a> <a id="2053" class="Symbol">=</a> <a id="2055" href="TypeTopology.CantorMinusPoint.html#1973" class="Function">h</a> <a id="2057" href="TypeTopology.CantorMinusPoint.html#2050" class="Bound">n</a>

<a id="2060" class="Markup">\end{code}</a><a id="2070" class="Background">

But it is a right inverse only for sequences α apart 𝟏, in the following
sense, where the apartness relation is defined by

    α ♯ β = Σ n ꞉ ℕ , (α n ≠ β n)
                    × ((i : ℕ) → α i ≠ β i → n ≤ i)

in the module Cantor.

</a><a id="2306" class="Markup">\begin{code}</a>

<a id="ϕψ"></a><a id="2320" href="TypeTopology.CantorMinusPoint.html#2320" class="Function">ϕψ</a> <a id="2323" class="Symbol">:</a> <a id="2325" class="Symbol">(</a><a id="2326" href="TypeTopology.CantorMinusPoint.html#2326" class="Bound">α</a> <a id="2328" class="Symbol">:</a> <a id="2330" href="TypeTopology.Cantor.html#678" class="Function">𝟚ᴺ</a><a id="2332" class="Symbol">)</a>
     <a id="2339" class="Symbol">(</a><a id="2340" href="TypeTopology.CantorMinusPoint.html#2340" class="Bound">(</a><a id="2341" href="TypeTopology.CantorMinusPoint.html#2341" class="Bound">n</a> <a id="2343" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="2345" href="TypeTopology.CantorMinusPoint.html#2345" class="Bound">δ</a> <a id="2347" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="2349" href="TypeTopology.CantorMinusPoint.html#2349" class="Bound">μ</a><a id="2350" href="TypeTopology.CantorMinusPoint.html#2340" class="Bound">)</a> <a id="2352" class="Symbol">:</a> <a id="2354" href="TypeTopology.CantorMinusPoint.html#2326" class="Bound">α</a> <a id="2356" href="TypeTopology.Cantor.html#5157" class="Function Operator">♯</a> <a id="2358" href="TypeTopology.Cantor.html#901" class="Function">𝟏</a><a id="2359" class="Symbol">)</a>
   <a id="2364" class="Symbol">→</a> <a id="2366" href="TypeTopology.CantorMinusPoint.html#1144" class="Function">ϕ</a> <a id="2368" href="TypeTopology.CantorMinusPoint.html#2341" class="Bound">n</a> <a id="2370" class="Symbol">(</a><a id="2371" href="TypeTopology.CantorMinusPoint.html#1756" class="Function">ψ</a> <a id="2373" href="TypeTopology.CantorMinusPoint.html#2341" class="Bound">n</a> <a id="2375" href="TypeTopology.CantorMinusPoint.html#2326" class="Bound">α</a><a id="2376" class="Symbol">)</a> <a id="2378" href="MLTT.Id.html#193" class="Datatype Operator">＝</a> <a id="2380" href="TypeTopology.CantorMinusPoint.html#2326" class="Bound">α</a>
<a id="2382" href="TypeTopology.CantorMinusPoint.html#2320" class="Function">ϕψ</a> <a id="2385" href="TypeTopology.CantorMinusPoint.html#2385" class="Bound">α</a> <a id="2387" class="Symbol">(</a><a id="2388" href="TypeTopology.CantorMinusPoint.html#2388" class="Bound">n</a> <a id="2390" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="2392" href="TypeTopology.CantorMinusPoint.html#2392" class="Bound">δ</a> <a id="2394" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="2396" href="TypeTopology.CantorMinusPoint.html#2396" class="Bound">μ</a><a id="2397" class="Symbol">)</a> <a id="2399" class="Symbol">=</a> <a id="2401" href="UF.FunExt.html#1242" class="Function">dfunext</a> <a id="2409" href="TypeTopology.CantorMinusPoint.html#987" class="Bound">fe</a> <a id="2412" class="Symbol">(</a><a id="2413" href="TypeTopology.CantorMinusPoint.html#2433" class="Function">h</a> <a id="2415" href="TypeTopology.CantorMinusPoint.html#2388" class="Bound">n</a> <a id="2417" href="TypeTopology.CantorMinusPoint.html#2385" class="Bound">α</a> <a id="2419" href="TypeTopology.CantorMinusPoint.html#2392" class="Bound">δ</a> <a id="2421" href="TypeTopology.CantorMinusPoint.html#2396" class="Bound">μ</a><a id="2422" class="Symbol">)</a>
 <a id="2425" class="Keyword">where</a>
  <a id="2433" href="TypeTopology.CantorMinusPoint.html#2433" class="Function">h</a> <a id="2435" class="Symbol">:</a> <a id="2437" class="Symbol">(</a><a id="2438" href="TypeTopology.CantorMinusPoint.html#2438" class="Bound">n</a> <a id="2440" class="Symbol">:</a> <a id="2442" href="MLTT.Natural-Numbers-Type.html#96" class="Datatype">ℕ</a><a id="2443" class="Symbol">)</a> <a id="2445" class="Symbol">(</a><a id="2446" href="TypeTopology.CantorMinusPoint.html#2446" class="Bound">α</a> <a id="2448" class="Symbol">:</a> <a id="2450" href="TypeTopology.Cantor.html#678" class="Function">𝟚ᴺ</a><a id="2452" class="Symbol">)</a>
    <a id="2458" class="Symbol">→</a> <a id="2460" href="TypeTopology.CantorMinusPoint.html#2446" class="Bound">α</a> <a id="2462" href="TypeTopology.CantorMinusPoint.html#2438" class="Bound">n</a> <a id="2464" href="MLTT.Negation.html#635" class="Function Operator">≠</a> <a id="2466" href="MLTT.Two.html#147" class="InductiveConstructor">₁</a>
    <a id="2472" class="Symbol">→</a> <a id="2474" class="Symbol">((</a><a id="2476" href="TypeTopology.CantorMinusPoint.html#2476" class="Bound">i</a> <a id="2478" class="Symbol">:</a> <a id="2480" href="MLTT.Natural-Numbers-Type.html#96" class="Datatype">ℕ</a><a id="2481" class="Symbol">)</a> <a id="2483" class="Symbol">→</a> <a id="2485" href="TypeTopology.CantorMinusPoint.html#2446" class="Bound">α</a> <a id="2487" href="TypeTopology.CantorMinusPoint.html#2476" class="Bound">i</a> <a id="2489" href="MLTT.Negation.html#635" class="Function Operator">≠</a> <a id="2491" href="MLTT.Two.html#147" class="InductiveConstructor">₁</a> <a id="2493" class="Symbol">→</a> <a id="2495" href="TypeTopology.CantorMinusPoint.html#2438" class="Bound">n</a> <a id="2497" href="Notation.Order.html#559" class="Field Operator">≤</a> <a id="2499" href="TypeTopology.CantorMinusPoint.html#2476" class="Bound">i</a><a id="2500" class="Symbol">)</a>
    <a id="2506" class="Symbol">→</a> <a id="2508" href="TypeTopology.CantorMinusPoint.html#1144" class="Function">ϕ</a> <a id="2510" href="TypeTopology.CantorMinusPoint.html#2438" class="Bound">n</a> <a id="2512" class="Symbol">(</a><a id="2513" href="TypeTopology.CantorMinusPoint.html#1756" class="Function">ψ</a> <a id="2515" href="TypeTopology.CantorMinusPoint.html#2438" class="Bound">n</a> <a id="2517" href="TypeTopology.CantorMinusPoint.html#2446" class="Bound">α</a><a id="2518" class="Symbol">)</a> <a id="2520" href="MLTT.Id.html#1891" class="Function Operator">∼</a> <a id="2522" href="TypeTopology.CantorMinusPoint.html#2446" class="Bound">α</a>
  <a id="2526" href="TypeTopology.CantorMinusPoint.html#2433" class="Function">h</a> <a id="2528" class="Number">0</a> <a id="2530" href="TypeTopology.CantorMinusPoint.html#2530" class="Bound">α</a> <a id="2532" href="TypeTopology.CantorMinusPoint.html#2532" class="Bound">δ</a> <a id="2534" class="Symbol">_</a> <a id="2536" class="Symbol">=</a>
   <a id="2541" href="TypeTopology.CantorMinusPoint.html#1144" class="Function">ϕ</a> <a id="2543" class="Number">0</a> <a id="2545" class="Symbol">(</a><a id="2546" href="TypeTopology.CantorMinusPoint.html#1756" class="Function">ψ</a> <a id="2548" class="Number">0</a> <a id="2550" href="TypeTopology.CantorMinusPoint.html#2530" class="Bound">α</a><a id="2551" class="Symbol">)</a>     <a id="2557" href="MLTT.Id.html#2876" class="Function Operator">∼⟨</a> <a id="2560" href="MLTT.Id.html#1945" class="Function">∼-refl</a> <a id="2567" href="MLTT.Id.html#2876" class="Function Operator">⟩</a>
   <a id="2572" href="MLTT.Two.html#140" class="InductiveConstructor">₀</a> <a id="2574" href="TypeTopology.Cantor.html#1097" class="Function Operator">∷</a> <a id="2576" href="TypeTopology.Cantor.html#997" class="Function">tail</a> <a id="2581" href="TypeTopology.CantorMinusPoint.html#2530" class="Bound">α</a>      <a id="2588" href="MLTT.Id.html#2876" class="Function Operator">∼⟨</a> <a id="2591" href="MLTT.Id.html#2127" class="Function">∼-ap</a> <a id="2596" class="Symbol">(</a><a id="2597" href="TypeTopology.Cantor.html#1097" class="Function Operator">_∷</a> <a id="2600" href="TypeTopology.Cantor.html#997" class="Function">tail</a> <a id="2605" href="TypeTopology.CantorMinusPoint.html#2530" class="Bound">α</a><a id="2606" class="Symbol">)</a> <a id="2608" class="Symbol">((</a><a id="2610" href="MLTT.Two-Properties.html#2704" class="Function">different-from-₁-equal-₀</a> <a id="2635" href="TypeTopology.CantorMinusPoint.html#2532" class="Bound">δ</a><a id="2636" class="Symbol">)</a><a id="2637" href="MLTT.Id.html#1055" class="Function Operator">⁻¹</a><a id="2639" class="Symbol">)</a> <a id="2641" href="MLTT.Id.html#2876" class="Function Operator">⟩</a>
   <a id="2646" href="TypeTopology.Cantor.html#969" class="Function">head</a> <a id="2651" href="TypeTopology.CantorMinusPoint.html#2530" class="Bound">α</a> <a id="2653" href="TypeTopology.Cantor.html#1097" class="Function Operator">∷</a> <a id="2655" href="TypeTopology.Cantor.html#997" class="Function">tail</a> <a id="2660" href="TypeTopology.CantorMinusPoint.html#2530" class="Bound">α</a> <a id="2662" href="MLTT.Id.html#2876" class="Function Operator">∼⟨</a> <a id="2665" href="MLTT.Id.html#2068" class="Function">∼-sym</a> <a id="2671" class="Symbol">(</a><a id="2672" href="TypeTopology.Cantor.html#1526" class="Function">cons-head-tail</a> <a id="2687" href="TypeTopology.CantorMinusPoint.html#2530" class="Bound">α</a><a id="2688" class="Symbol">)</a> <a id="2690" href="MLTT.Id.html#2876" class="Function Operator">⟩</a>
   <a id="2695" href="TypeTopology.CantorMinusPoint.html#2530" class="Bound">α</a>               <a id="2711" href="MLTT.Id.html#2959" class="Function Operator">∼∎</a>
  <a id="2716" href="TypeTopology.CantorMinusPoint.html#2433" class="Function">h</a> <a id="2718" class="Symbol">(</a><a id="2719" href="MLTT.Natural-Numbers-Type.html#123" class="InductiveConstructor">succ</a> <a id="2724" href="TypeTopology.CantorMinusPoint.html#2724" class="Bound">n</a><a id="2725" class="Symbol">)</a> <a id="2727" href="TypeTopology.CantorMinusPoint.html#2727" class="Bound">α</a> <a id="2729" href="TypeTopology.CantorMinusPoint.html#2729" class="Bound">δ</a> <a id="2731" href="TypeTopology.CantorMinusPoint.html#2731" class="Bound">μ</a> <a id="2733" class="Symbol">=</a>
    <a id="2739" href="TypeTopology.CantorMinusPoint.html#1144" class="Function">ϕ</a> <a id="2741" class="Symbol">(</a><a id="2742" href="MLTT.Natural-Numbers-Type.html#123" class="InductiveConstructor">succ</a> <a id="2747" href="TypeTopology.CantorMinusPoint.html#2724" class="Bound">n</a><a id="2748" class="Symbol">)</a> <a id="2750" class="Symbol">(</a><a id="2751" href="TypeTopology.CantorMinusPoint.html#1756" class="Function">ψ</a> <a id="2753" class="Symbol">(</a><a id="2754" href="MLTT.Natural-Numbers-Type.html#123" class="InductiveConstructor">succ</a> <a id="2759" href="TypeTopology.CantorMinusPoint.html#2724" class="Bound">n</a><a id="2760" class="Symbol">)</a> <a id="2762" href="TypeTopology.CantorMinusPoint.html#2727" class="Bound">α</a><a id="2763" class="Symbol">)</a> <a id="2765" href="MLTT.Id.html#2876" class="Function Operator">∼⟨</a> <a id="2768" href="MLTT.Id.html#1945" class="Function">∼-refl</a> <a id="2775" href="MLTT.Id.html#2876" class="Function Operator">⟩</a>
    <a id="2781" href="MLTT.Two.html#147" class="InductiveConstructor">₁</a> <a id="2783" href="TypeTopology.Cantor.html#1097" class="Function Operator">∷</a> <a id="2785" href="TypeTopology.CantorMinusPoint.html#1144" class="Function">ϕ</a> <a id="2787" href="TypeTopology.CantorMinusPoint.html#2724" class="Bound">n</a> <a id="2789" class="Symbol">(</a><a id="2790" href="TypeTopology.CantorMinusPoint.html#1756" class="Function">ψ</a> <a id="2792" href="TypeTopology.CantorMinusPoint.html#2724" class="Bound">n</a> <a id="2794" class="Symbol">(</a><a id="2795" href="TypeTopology.Cantor.html#997" class="Function">tail</a> <a id="2800" href="TypeTopology.CantorMinusPoint.html#2727" class="Bound">α</a><a id="2801" class="Symbol">))</a>    <a id="2807" href="MLTT.Id.html#2876" class="Function Operator">∼⟨</a> <a id="2810" href="TypeTopology.Cantor.html#1127" class="Function">cons-∼</a> <a id="2817" class="Symbol">(</a><a id="2818" href="TypeTopology.CantorMinusPoint.html#2433" class="Function">h</a> <a id="2820" href="TypeTopology.CantorMinusPoint.html#2724" class="Bound">n</a> <a id="2822" class="Symbol">(</a><a id="2823" href="TypeTopology.Cantor.html#997" class="Function">tail</a> <a id="2828" href="TypeTopology.CantorMinusPoint.html#2727" class="Bound">α</a><a id="2829" class="Symbol">)</a> <a id="2831" href="TypeTopology.CantorMinusPoint.html#2729" class="Bound">δ</a> <a id="2833" class="Symbol">(</a><a id="2834" href="TypeTopology.CantorMinusPoint.html#2731" class="Bound">μ</a> <a id="2836" href="MLTT.Pi.html#513" class="Function Operator">∘</a> <a id="2838" href="MLTT.Natural-Numbers-Type.html#123" class="InductiveConstructor">succ</a><a id="2842" class="Symbol">))</a> <a id="2845" href="MLTT.Id.html#2876" class="Function Operator">⟩</a>
    <a id="2851" href="MLTT.Two.html#147" class="InductiveConstructor">₁</a> <a id="2853" href="TypeTopology.Cantor.html#1097" class="Function Operator">∷</a> <a id="2855" href="TypeTopology.Cantor.html#997" class="Function">tail</a> <a id="2860" href="TypeTopology.CantorMinusPoint.html#2727" class="Bound">α</a>                <a id="2877" href="MLTT.Id.html#2876" class="Function Operator">∼⟨</a> <a id="2880" href="TypeTopology.CantorMinusPoint.html#2995" class="Function">h₁</a> <a id="2883" href="MLTT.Id.html#2876" class="Function Operator">⟩</a>
    <a id="2889" href="TypeTopology.Cantor.html#969" class="Function">head</a> <a id="2894" href="TypeTopology.CantorMinusPoint.html#2727" class="Bound">α</a> <a id="2896" href="TypeTopology.Cantor.html#1097" class="Function Operator">∷</a> <a id="2898" href="TypeTopology.Cantor.html#997" class="Function">tail</a> <a id="2903" href="TypeTopology.CantorMinusPoint.html#2727" class="Bound">α</a>           <a id="2915" href="MLTT.Id.html#2876" class="Function Operator">∼⟨</a> <a id="2918" href="MLTT.Id.html#2068" class="Function">∼-sym</a> <a id="2924" class="Symbol">(</a><a id="2925" href="TypeTopology.Cantor.html#1526" class="Function">cons-head-tail</a> <a id="2940" href="TypeTopology.CantorMinusPoint.html#2727" class="Bound">α</a><a id="2941" class="Symbol">)</a> <a id="2943" href="MLTT.Id.html#2876" class="Function Operator">⟩</a>
    <a id="2949" href="TypeTopology.CantorMinusPoint.html#2727" class="Bound">α</a>                         <a id="2975" href="MLTT.Id.html#2959" class="Function Operator">∼∎</a>
     <a id="2983" class="Keyword">where</a>
      <a id="2995" href="TypeTopology.CantorMinusPoint.html#2995" class="Function">h₁</a> <a id="2998" class="Symbol">=</a> <a id="3000" href="TypeTopology.Cantor.html#1229" class="Function">∼-cons</a> <a id="3007" class="Symbol">((</a><a id="3009" href="TypeTopology.Cantor.html#8023" class="Function">♯-agreement</a> <a id="3021" href="TypeTopology.CantorMinusPoint.html#2727" class="Bound">α</a> <a id="3023" href="TypeTopology.Cantor.html#901" class="Function">𝟏</a> <a id="3025" class="Symbol">(</a><a id="3026" href="MLTT.Natural-Numbers-Type.html#123" class="InductiveConstructor">succ</a> <a id="3031" href="TypeTopology.CantorMinusPoint.html#2724" class="Bound">n</a> <a id="3033" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="3035" href="TypeTopology.CantorMinusPoint.html#2729" class="Bound">δ</a> <a id="3037" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="3039" href="TypeTopology.CantorMinusPoint.html#2731" class="Bound">μ</a><a id="3040" class="Symbol">)</a> <a id="3042" class="Number">0</a> <a id="3044" class="Symbol">(</a><a id="3045" href="Naturals.Order.html#1789" class="Function">zero-least</a> <a id="3056" href="TypeTopology.CantorMinusPoint.html#2724" class="Bound">n</a><a id="3057" class="Symbol">))</a><a id="3059" href="MLTT.Id.html#1055" class="Function Operator">⁻¹</a><a id="3061" class="Symbol">)</a>

<a id="3064" class="Markup">\end{code}</a><a id="3074" class="Background">

With the above we have all ingredients needed to characterize the
Cantor type with the point 𝟏 removed as the type ℕ × 𝟚ᴺ.

</a><a id="3200" class="Markup">\begin{code}</a>

<a id="Cantor-minus-𝟏-≃"></a><a id="3214" href="TypeTopology.CantorMinusPoint.html#3214" class="Function">Cantor-minus-𝟏-≃</a> <a id="3231" class="Symbol">:</a> <a id="3233" class="Symbol">(</a><a id="3234" href="MLTT.Sigma.html#423" class="Function">Σ</a> <a id="3236" href="TypeTopology.CantorMinusPoint.html#3236" class="Bound">α</a> <a id="3238" href="MLTT.Sigma.html#423" class="Function">꞉</a> <a id="3240" href="TypeTopology.Cantor.html#678" class="Function">𝟚ᴺ</a> <a id="3243" href="MLTT.Sigma.html#423" class="Function">,</a> <a id="3245" href="TypeTopology.CantorMinusPoint.html#3236" class="Bound">α</a> <a id="3247" href="TypeTopology.Cantor.html#5157" class="Function Operator">♯</a> <a id="3249" href="TypeTopology.Cantor.html#901" class="Function">𝟏</a><a id="3250" class="Symbol">)</a> <a id="3252" href="UF.Equiv.html#1318" class="Function Operator">≃</a> <a id="3254" class="Symbol">(</a><a id="3255" href="MLTT.Natural-Numbers-Type.html#96" class="Datatype">ℕ</a> <a id="3257" href="MLTT.Sigma.html#558" class="Function Operator">×</a> <a id="3259" href="TypeTopology.Cantor.html#678" class="Function">𝟚ᴺ</a><a id="3261" class="Symbol">)</a>
<a id="3263" href="TypeTopology.CantorMinusPoint.html#3214" class="Function">Cantor-minus-𝟏-≃</a> <a id="3280" class="Symbol">=</a> <a id="3282" href="UF.Equiv.html#7224" class="Function">qinveq</a> <a id="3289" href="TypeTopology.CantorMinusPoint.html#3344" class="Function">f</a> <a id="3291" class="Symbol">(</a><a id="3292" href="TypeTopology.CantorMinusPoint.html#3400" class="Function">g</a> <a id="3294" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="3296" href="TypeTopology.CantorMinusPoint.html#3488" class="Function">gf</a> <a id="3299" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="3301" href="TypeTopology.CantorMinusPoint.html#3576" class="Function">fg</a><a id="3303" class="Symbol">)</a>
 <a id="3306" class="Keyword">where</a>
  <a id="3314" href="TypeTopology.CantorMinusPoint.html#3314" class="Function">Cantor⁻</a> <a id="3322" class="Symbol">=</a> <a id="3324" href="MLTT.Sigma.html#423" class="Function">Σ</a> <a id="3326" href="TypeTopology.CantorMinusPoint.html#3326" class="Bound">α</a> <a id="3328" href="MLTT.Sigma.html#423" class="Function">꞉</a> <a id="3330" href="TypeTopology.Cantor.html#678" class="Function">𝟚ᴺ</a> <a id="3333" href="MLTT.Sigma.html#423" class="Function">,</a> <a id="3335" href="TypeTopology.CantorMinusPoint.html#3326" class="Bound">α</a> <a id="3337" href="TypeTopology.Cantor.html#5157" class="Function Operator">♯</a> <a id="3339" href="TypeTopology.Cantor.html#901" class="Function">𝟏</a>

  <a id="3344" href="TypeTopology.CantorMinusPoint.html#3344" class="Function">f</a> <a id="3346" class="Symbol">:</a> <a id="3348" href="TypeTopology.CantorMinusPoint.html#3314" class="Function">Cantor⁻</a> <a id="3356" class="Symbol">→</a> <a id="3358" href="MLTT.Natural-Numbers-Type.html#96" class="Datatype">ℕ</a> <a id="3360" href="MLTT.Sigma.html#558" class="Function Operator">×</a> <a id="3362" href="TypeTopology.Cantor.html#678" class="Function">𝟚ᴺ</a>
  <a id="3367" href="TypeTopology.CantorMinusPoint.html#3344" class="Function">f</a> <a id="3369" class="Symbol">(</a><a id="3370" href="TypeTopology.CantorMinusPoint.html#3370" class="Bound">α</a> <a id="3372" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="3374" href="TypeTopology.CantorMinusPoint.html#3374" class="Bound">i</a> <a id="3376" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="3378" href="TypeTopology.CantorMinusPoint.html#3378" class="Bound">δ</a> <a id="3380" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="3382" href="TypeTopology.CantorMinusPoint.html#3382" class="Bound">m</a><a id="3383" class="Symbol">)</a> <a id="3385" class="Symbol">=</a> <a id="3387" href="TypeTopology.CantorMinusPoint.html#3374" class="Bound">i</a> <a id="3389" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="3391" href="TypeTopology.CantorMinusPoint.html#1756" class="Function">ψ</a> <a id="3393" href="TypeTopology.CantorMinusPoint.html#3374" class="Bound">i</a> <a id="3395" href="TypeTopology.CantorMinusPoint.html#3370" class="Bound">α</a>

  <a id="3400" href="TypeTopology.CantorMinusPoint.html#3400" class="Function">g</a> <a id="3402" class="Symbol">:</a> <a id="3404" class="Symbol">(</a><a id="3405" href="MLTT.Natural-Numbers-Type.html#96" class="Datatype">ℕ</a> <a id="3407" href="MLTT.Sigma.html#558" class="Function Operator">×</a> <a id="3409" href="TypeTopology.Cantor.html#678" class="Function">𝟚ᴺ</a><a id="3411" class="Symbol">)</a> <a id="3413" class="Symbol">→</a> <a id="3415" href="TypeTopology.CantorMinusPoint.html#3314" class="Function">Cantor⁻</a>
  <a id="3425" href="TypeTopology.CantorMinusPoint.html#3400" class="Function">g</a> <a id="3427" class="Symbol">(</a><a id="3428" href="TypeTopology.CantorMinusPoint.html#3428" class="Bound">n</a> <a id="3430" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="3432" href="TypeTopology.CantorMinusPoint.html#3432" class="Bound">β</a><a id="3433" class="Symbol">)</a> <a id="3435" class="Symbol">=</a> <a id="3437" href="TypeTopology.CantorMinusPoint.html#1144" class="Function">ϕ</a> <a id="3439" href="TypeTopology.CantorMinusPoint.html#3428" class="Bound">n</a> <a id="3441" href="TypeTopology.CantorMinusPoint.html#3432" class="Bound">β</a> <a id="3443" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="3445" href="TypeTopology.CantorMinusPoint.html#3428" class="Bound">n</a> <a id="3447" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="3449" href="TypeTopology.CantorMinusPoint.html#1290" class="Function">ϕ-property-δ</a> <a id="3462" href="TypeTopology.CantorMinusPoint.html#3432" class="Bound">β</a> <a id="3464" href="TypeTopology.CantorMinusPoint.html#3428" class="Bound">n</a> <a id="3466" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="3468" href="TypeTopology.CantorMinusPoint.html#1422" class="Function">ϕ-property-μ</a> <a id="3481" href="TypeTopology.CantorMinusPoint.html#3432" class="Bound">β</a> <a id="3483" href="TypeTopology.CantorMinusPoint.html#3428" class="Bound">n</a>

  <a id="3488" href="TypeTopology.CantorMinusPoint.html#3488" class="Function">gf</a> <a id="3491" class="Symbol">:</a> <a id="3493" href="TypeTopology.CantorMinusPoint.html#3400" class="Function">g</a> <a id="3495" href="MLTT.Pi.html#513" class="Function Operator">∘</a> <a id="3497" href="TypeTopology.CantorMinusPoint.html#3344" class="Function">f</a> <a id="3499" href="MLTT.Id.html#1891" class="Function Operator">∼</a> <a id="3501" href="MLTT.Pi.html#444" class="Function">id</a>
  <a id="3506" href="TypeTopology.CantorMinusPoint.html#3488" class="Function">gf</a> <a id="3509" class="Symbol">(</a><a id="3510" href="TypeTopology.CantorMinusPoint.html#3510" class="Bound">α</a> <a id="3512" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="3514" href="TypeTopology.CantorMinusPoint.html#3514" class="Bound">a</a><a id="3515" class="Symbol">)</a> <a id="3517" class="Symbol">=</a> <a id="3519" href="UF.Subsingletons.html#4964" class="Function">to-subtype-＝</a> <a id="3532" class="Symbol">(λ</a> <a id="3535" href="TypeTopology.CantorMinusPoint.html#3535" class="Bound">α</a> <a id="3537" class="Symbol">→</a> <a id="3539" href="TypeTopology.Cantor.html#6124" class="Function">♯-is-prop-valued</a> <a id="3556" href="TypeTopology.CantorMinusPoint.html#987" class="Bound">fe</a> <a id="3559" href="TypeTopology.CantorMinusPoint.html#3535" class="Bound">α</a> <a id="3561" href="TypeTopology.Cantor.html#901" class="Function">𝟏</a><a id="3562" class="Symbol">)</a> <a id="3564" class="Symbol">(</a><a id="3565" href="TypeTopology.CantorMinusPoint.html#2320" class="Function">ϕψ</a> <a id="3568" href="TypeTopology.CantorMinusPoint.html#3510" class="Bound">α</a> <a id="3570" href="TypeTopology.CantorMinusPoint.html#3514" class="Bound">a</a><a id="3571" class="Symbol">)</a>

  <a id="3576" href="TypeTopology.CantorMinusPoint.html#3576" class="Function">fg</a> <a id="3579" class="Symbol">:</a> <a id="3581" href="TypeTopology.CantorMinusPoint.html#3344" class="Function">f</a> <a id="3583" href="MLTT.Pi.html#513" class="Function Operator">∘</a> <a id="3585" href="TypeTopology.CantorMinusPoint.html#3400" class="Function">g</a> <a id="3587" href="MLTT.Id.html#1891" class="Function Operator">∼</a> <a id="3589" href="MLTT.Pi.html#444" class="Function">id</a>
  <a id="3594" href="TypeTopology.CantorMinusPoint.html#3576" class="Function">fg</a> <a id="3597" class="Symbol">(</a><a id="3598" href="TypeTopology.CantorMinusPoint.html#3598" class="Bound">n</a> <a id="3600" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="3602" href="TypeTopology.CantorMinusPoint.html#3602" class="Bound">β</a><a id="3603" class="Symbol">)</a> <a id="3605" class="Symbol">=</a> <a id="3607" href="UF.Base.html#14933" class="Function">to-Σ-＝</a> <a id="3614" class="Symbol">(</a><a id="3615" href="MLTT.Identity-Type.html#157" class="InductiveConstructor">refl</a> <a id="3620" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="3622" href="TypeTopology.CantorMinusPoint.html#1906" class="Function">ψϕ</a> <a id="3625" href="TypeTopology.CantorMinusPoint.html#3598" class="Bound">n</a> <a id="3627" href="TypeTopology.CantorMinusPoint.html#3602" class="Bound">β</a><a id="3628" class="Symbol">)</a>

<a id="3631" class="Markup">\end{code}</a><a id="3641" class="Background">

And this is what we wanted to show. Notice how the prop-valuedness of
the apartness relation is crucial for the proof that this construction
works.

As discussed above, it doesn&#39;t matter which point we remove, because
the Cantor space is homogeneous, in the sense that for any two points
α and β there is an automorphism (in fact, an involution) that maps α
to β, as proved in the module Cantor.

TODO. Use this to conclude, as a corollary, that

 (Σ α ꞉ 𝟚ᴺ , α ♯ γ) ≃ (ℕ × 𝟚ᴺ)

for any point γ.
</a></pre></body></html>