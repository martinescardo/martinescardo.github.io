<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Games.Alternative</title><link rel="stylesheet" href="Agda.css"><script type="text/javascript" src="highlight-hover.js"></script></head><body><pre class="Agda"><a id="1" class="Background">Martin Escardo, Paulo Oliva, 9th July 2024.

Alternative, equivalent, inductive definition of Game, which may have
some practical advantages, such as more modular definitions of games.

</a><a id="187" class="Markup">\begin{code}</a>

<a id="201" class="Symbol">{-#</a> <a id="205" class="Keyword">OPTIONS</a> <a id="213" class="Pragma">--safe</a> <a id="220" class="Pragma">--without-K</a> <a id="232" class="Symbol">#-}</a>

<a id="237" class="Keyword">open</a> <a id="242" class="Keyword">import</a> <a id="249" href="MLTT.Spartan.html" class="Module">MLTT.Spartan</a> <a id="262" class="Keyword">hiding</a> <a id="269" class="Symbol">(</a><a id="270" href="MLTT.Id.html#438" class="Function">J</a><a id="271" class="Symbol">)</a>

<a id="274" class="Keyword">module</a> <a id="281" href="Games.Alternative.html" class="Module">Games.Alternative</a> <a id="299" class="Symbol">(</a><a id="300" href="Games.Alternative.html#300" class="Bound">R</a> <a id="302" class="Symbol">:</a> <a id="304" href="Notation.General.html#372" class="Function">Type</a><a id="308" class="Symbol">)</a> <a id="310" class="Keyword">where</a>

<a id="317" class="Keyword">open</a> <a id="322" class="Keyword">import</a> <a id="329" href="UF.Equiv.html" class="Module">UF.Equiv</a>
<a id="338" class="Keyword">open</a> <a id="343" class="Keyword">import</a> <a id="350" href="UF.FunExt.html" class="Module">UF.FunExt</a>

<a id="361" class="Keyword">open</a> <a id="366" class="Keyword">import</a> <a id="373" href="Games.TypeTrees.html" class="Module">Games.TypeTrees</a>
<a id="389" class="Keyword">open</a> <a id="394" class="Keyword">import</a> <a id="401" href="Games.FiniteHistoryDependent.html" class="Module">Games.FiniteHistoryDependent</a> <a id="430" href="Games.Alternative.html#300" class="Bound">R</a>
             <a id="445" class="Keyword">renaming</a> <a id="454" class="Symbol">(</a><a id="455" href="Games.FiniteHistoryDependent.html#3132" class="Record">Game</a> <a id="460" class="Symbol">to</a> <a id="463" class="Record">Game&#39;</a> <a id="469" class="Symbol">;</a>
                       <a id="494" href="Games.FiniteHistoryDependent.html#3164" class="InductiveConstructor">game</a> <a id="499" class="Symbol">to</a> <a id="502" class="InductiveConstructor">game&#39;</a><a id="507" class="Symbol">)</a>

<a id="510" class="Keyword">open</a> <a id="515" class="Keyword">import</a> <a id="522" href="MonadOnTypes.K.html" class="Module">MonadOnType</a><a id="533" href="MonadOnTypes.K.html" class="Deadcode Module">s.</a><a id="535" href="MonadOnTypes.K.html" class="Module">K</a>

<a id="538" class="Keyword">open</a> <a id="543" href="MonadOnTypes.K.html#438" class="Module">K-definitions</a> <a id="557" href="Games.Alternative.html#300" class="Bound">R</a>

<a id="560" class="Keyword">data</a> <a id="Game"></a><a id="565" href="Games.Alternative.html#565" class="Datatype">Game</a> <a id="570" class="Symbol">:</a> <a id="572" href="Notation.General.html#384" class="Function">Ty</a><a id="574" href="Notation.General.html#384" class="Deadcode Function">p</a><a id="575" href="Notation.General.html#384" class="Function">eâ‚</a> <a id="578" class="Keyword">where</a>
 <a id="Game.leaf"></a><a id="585" href="Games.Alternative.html#585" class="InductiveConstructor">leaf</a>   <a id="592" class="Symbol">:</a> <a id="594" href="Games.Alternative.html#300" class="Bound">R</a> <a id="596" class="Symbol">â†’</a> <a id="598" href="Games.Alternative.html#565" class="Datatype">Game</a>
 <a id="Game.branch"></a><a id="604" href="Games.Alternative.html#604" class="InductiveConstructor">branch</a> <a id="611" class="Symbol">:</a> <a id="613" class="Symbol">(</a><a id="614" href="Games.Alternative.html#614" class="Bound">X</a> <a id="616" class="Symbol">:</a> <a id="618" href="Notation.General.html#372" class="Function">Ty</a><a id="620" href="Notation.General.html#372" class="Deadcode Function">pe</a><a id="622" class="Symbol">)</a> <a id="624" class="Symbol">â†’</a> <a id="626" href="MonadOnTypes.K.html#471" class="Function">K</a> <a id="628" href="Games.Alternative.html#614" class="Bound">X</a> <a id="630" class="Symbol">â†’</a> <a id="632" class="Symbol">(</a><a id="633" href="Games.Alternative.html#614" class="Bound">X</a> <a id="635" class="Symbol">â†’</a> <a id="637" href="Games.Alternative.html#565" class="Datatype">Game</a><a id="641" class="Symbol">)</a> <a id="643" class="Symbol">â†’</a> <a id="645" href="Games.Alternative.html#565" class="Datatype">Game</a>

<a id="leaf&#39;"></a><a id="651" href="Games.Alternative.html#651" class="Function">leaf&#39;</a> <a id="657" class="Symbol">:</a> <a id="659" href="Games.Alternative.html#300" class="Bound">R</a> <a id="661" class="Symbol">â†’</a> <a id="663" href="Games.Alternative.html#463" class="Record">Game&#39;</a>
<a id="669" href="Games.Alternative.html#651" class="Function">leaf&#39;</a> <a id="675" href="Games.Alternative.html#675" class="Bound">r</a> <a id="677" class="Symbol">=</a> <a id="679" href="Games.Alternative.html#502" class="InductiveConstructor">game&#39;</a> <a id="685" href="Games.TypeTrees.html#737" class="InductiveConstructor">[]</a> <a id="688" class="Symbol">(Î»</a> <a id="691" href="Games.Alternative.html#691" class="Bound">âŸ¨âŸ©</a> <a id="694" class="Symbol">â†’</a> <a id="696" href="Games.Alternative.html#675" class="Bound">r</a><a id="697" class="Symbol">)</a> <a id="699" href="Games.TypeTrees.html#1274" class="InductiveConstructor">âŸ¨âŸ©</a>

<a id="branch&#39;"></a><a id="703" href="Games.Alternative.html#703" class="Function">branch&#39;</a> <a id="711" class="Symbol">:</a> <a id="713" class="Symbol">(</a><a id="714" href="Games.Alternative.html#714" class="Bound">X</a> <a id="716" class="Symbol">:</a> <a id="718" href="Notation.General.html#372" class="Function">Type</a><a id="722" class="Symbol">)</a> <a id="724" class="Symbol">â†’</a> <a id="726" href="MonadOnTypes.K.html#471" class="Function">K</a> <a id="728" href="Games.Alternative.html#714" class="Bound">X</a> <a id="730" class="Symbol">â†’</a> <a id="732" class="Symbol">(</a><a id="733" href="Games.Alternative.html#714" class="Bound">X</a> <a id="735" class="Symbol">â†’</a> <a id="737" href="Games.Alternative.html#463" class="Record">Game&#39;</a><a id="742" class="Symbol">)</a> <a id="744" class="Symbol">â†’</a> <a id="746" href="Games.Alternative.html#463" class="Record">Game&#39;</a>
<a id="752" href="Games.Alternative.html#703" class="Function">branch&#39;</a> <a id="760" href="Games.Alternative.html#760" class="Bound">X</a> <a id="762" href="Games.Alternative.html#762" class="Bound">Ï•</a> <a id="764" href="Games.Alternative.html#764" class="Bound">Gf</a> <a id="767" class="Symbol">=</a> <a id="769" href="Games.Alternative.html#502" class="InductiveConstructor">game&#39;</a> <a id="775" class="Symbol">(</a><a id="776" href="Games.Alternative.html#760" class="Bound">X</a> <a id="778" href="Games.TypeTrees.html#747" class="InductiveConstructor Operator">âˆ·</a> <a id="780" class="Symbol">(</a><a id="781" href="Games.FiniteHistoryDependent.html#3178" class="Field">game-tree</a> <a id="791" href="MLTT.Pi.html#513" class="Function Operator">âˆ˜</a> <a id="793" href="Games.Alternative.html#764" class="Bound">Gf</a><a id="795" class="Symbol">))</a>
                       <a id="821" class="Symbol">(Î»</a> <a id="824" class="Symbol">(</a><a id="825" href="Games.Alternative.html#825" class="Bound">x</a> <a id="827" href="Games.TypeTrees.html#1296" class="InductiveConstructor Operator">::</a> <a id="830" href="Games.Alternative.html#830" class="Bound">xs</a><a id="832" class="Symbol">)</a> <a id="834" class="Symbol">â†’</a> <a id="836" href="Games.FiniteHistoryDependent.html#3194" class="Field">payoff-function</a> <a id="852" class="Symbol">(</a><a id="853" href="Games.Alternative.html#764" class="Bound">Gf</a> <a id="856" href="Games.Alternative.html#825" class="Bound">x</a><a id="857" class="Symbol">)</a> <a id="859" href="Games.Alternative.html#830" class="Bound">xs</a><a id="861" class="Symbol">)</a>
                       <a id="886" class="Symbol">(</a><a id="887" href="Games.Alternative.html#762" class="Bound">Ï•</a> <a id="889" href="Games.TypeTrees.html#1296" class="InductiveConstructor Operator">::</a> <a id="892" class="Symbol">(</a><a id="893" href="Games.FiniteHistoryDependent.html#3234" class="Field">quantifier-tree</a> <a id="909" href="MLTT.Pi.html#513" class="Function Operator">âˆ˜</a> <a id="911" href="Games.Alternative.html#764" class="Bound">Gf</a><a id="913" class="Symbol">))</a>

<a id="from-Game"></a><a id="917" href="Games.Alternative.html#917" class="Function">from-Game</a> <a id="927" class="Symbol">:</a> <a id="929" href="Games.Alternative.html#565" class="Datatype">Game</a> <a id="934" class="Symbol">â†’</a> <a id="936" href="Games.Alternative.html#463" class="Record">Game&#39;</a>
<a id="942" href="Games.Alternative.html#917" class="Function">from-Game</a> <a id="952" class="Symbol">(</a><a id="953" href="Games.Alternative.html#585" class="InductiveConstructor">leaf</a> <a id="958" href="Games.Alternative.html#958" class="Bound">r</a><a id="959" class="Symbol">)</a>        <a id="968" class="Symbol">=</a> <a id="970" href="Games.Alternative.html#651" class="Function">leaf&#39;</a> <a id="976" href="Games.Alternative.html#958" class="Bound">r</a>
<a id="978" href="Games.Alternative.html#917" class="Function">from-Game</a> <a id="988" class="Symbol">(</a><a id="989" href="Games.Alternative.html#604" class="InductiveConstructor">branch</a> <a id="996" href="Games.Alternative.html#996" class="Bound">X</a> <a id="998" href="Games.Alternative.html#998" class="Bound">Ï•</a> <a id="1000" href="Games.Alternative.html#1000" class="Bound">Gf</a><a id="1002" class="Symbol">)</a> <a id="1004" class="Symbol">=</a> <a id="1006" href="Games.Alternative.html#703" class="Function">branch&#39;</a> <a id="1014" href="Games.Alternative.html#996" class="Bound">X</a> <a id="1016" href="Games.Alternative.html#998" class="Bound">Ï•</a> <a id="1018" class="Symbol">(</a><a id="1019" href="Games.Alternative.html#917" class="Function">from-Game</a> <a id="1029" href="MLTT.Pi.html#513" class="Function Operator">âˆ˜</a> <a id="1031" href="Games.Alternative.html#1000" class="Bound">Gf</a><a id="1033" class="Symbol">)</a>

<a id="1036" class="Markup">\end{code}</a><a id="1046" class="Background">

We need the curried version h of the conversion function to-Game
defined below to convince the termination checker that the following
recursion does terminate.

</a><a id="1209" class="Markup">\begin{code}</a>

<a id="to-Game"></a><a id="1223" href="Games.Alternative.html#1223" class="Function">to-Game</a> <a id="1231" class="Symbol">:</a> <a id="1233" href="Games.Alternative.html#463" class="Record">Game&#39;</a> <a id="1239" class="Symbol">â†’</a> <a id="1241" href="Games.Alternative.html#565" class="Datatype">Game</a>
<a id="1246" href="Games.Alternative.html#1223" class="Function">to-Game</a> <a id="1254" class="Symbol">(</a><a id="1255" href="Games.Alternative.html#502" class="InductiveConstructor">game&#39;</a> <a id="1261" href="Games.Alternative.html#1261" class="Bound">Xt</a> <a id="1264" href="Games.Alternative.html#1264" class="Bound">q</a> <a id="1266" href="Games.Alternative.html#1266" class="Bound">Ï•t</a><a id="1268" class="Symbol">)</a> <a id="1270" class="Symbol">=</a> <a id="1272" href="Games.Alternative.html#1291" class="Function">h</a> <a id="1274" href="Games.Alternative.html#1261" class="Bound">Xt</a> <a id="1277" href="Games.Alternative.html#1264" class="Bound">q</a> <a id="1279" href="Games.Alternative.html#1266" class="Bound">Ï•t</a>
 <a id="1283" class="Keyword">where</a>
  <a id="1291" href="Games.Alternative.html#1291" class="Function">h</a> <a id="1293" class="Symbol">:</a> <a id="1295" class="Symbol">(</a><a id="1296" href="Games.Alternative.html#1296" class="Bound">Xt</a> <a id="1299" class="Symbol">:</a> <a id="1301" href="Games.TypeTrees.html#719" class="Datatype">ğ‘»</a><a id="1302" class="Symbol">)</a> <a id="1304" class="Symbol">â†’</a> <a id="1306" class="Symbol">(</a><a id="1307" href="Games.TypeTrees.html#906" class="Function">Path</a> <a id="1312" href="Games.Alternative.html#1296" class="Bound">Xt</a> <a id="1315" class="Symbol">â†’</a> <a id="1317" href="Games.Alternative.html#300" class="Bound">R</a><a id="1318" class="Symbol">)</a> <a id="1320" class="Symbol">â†’</a> <a id="1322" href="Games.FiniteHistoryDependent.html#2364" class="Function">ğ“š</a> <a id="1324" href="Games.Alternative.html#1296" class="Bound">Xt</a> <a id="1327" class="Symbol">â†’</a> <a id="1329" href="Games.Alternative.html#565" class="Datatype">Game</a>
  <a id="1336" href="Games.Alternative.html#1291" class="Function">h</a> <a id="1338" href="Games.TypeTrees.html#737" class="InductiveConstructor">[]</a> <a id="1341" href="Games.Alternative.html#1341" class="Bound">q</a> <a id="1343" href="Games.Alternative.html#1343" class="Bound">Ï•t</a>              <a id="1359" class="Symbol">=</a> <a id="1361" href="Games.Alternative.html#585" class="InductiveConstructor">leaf</a> <a id="1366" class="Symbol">(</a><a id="1367" href="Games.Alternative.html#1341" class="Bound">q</a> <a id="1369" href="Games.TypeTrees.html#1274" class="InductiveConstructor">âŸ¨âŸ©</a><a id="1371" class="Symbol">)</a>
  <a id="1375" href="Games.Alternative.html#1291" class="Function">h</a> <a id="1377" class="Symbol">(</a><a id="1378" href="Games.Alternative.html#1378" class="Bound">X</a> <a id="1380" href="Games.TypeTrees.html#747" class="InductiveConstructor Operator">âˆ·</a> <a id="1382" href="Games.Alternative.html#1382" class="Bound">Xf</a><a id="1384" class="Symbol">)</a> <a id="1386" href="Games.Alternative.html#1386" class="Bound">q</a> <a id="1388" class="Symbol">(</a><a id="1389" href="Games.Alternative.html#1389" class="Bound">Ï•</a> <a id="1391" href="Games.TypeTrees.html#1296" class="InductiveConstructor Operator">::</a> <a id="1394" href="Games.Alternative.html#1394" class="Bound">Ï•f</a><a id="1396" class="Symbol">)</a> <a id="1398" class="Symbol">=</a> <a id="1400" href="Games.Alternative.html#604" class="InductiveConstructor">branch</a> <a id="1407" href="Games.Alternative.html#1378" class="Bound">X</a> <a id="1409" href="Games.Alternative.html#1389" class="Bound">Ï•</a> <a id="1411" class="Symbol">(Î»</a> <a id="1414" href="Games.Alternative.html#1414" class="Bound">x</a> <a id="1416" class="Symbol">â†’</a> <a id="1418" href="Games.Alternative.html#1291" class="Function">h</a> <a id="1420" class="Symbol">(</a><a id="1421" href="Games.Alternative.html#1382" class="Bound">Xf</a> <a id="1424" href="Games.Alternative.html#1414" class="Bound">x</a><a id="1425" class="Symbol">)</a> <a id="1427" class="Symbol">(</a><a id="1428" href="Games.TypeTrees.html#1622" class="Function">subpred</a> <a id="1436" href="Games.Alternative.html#1386" class="Bound">q</a> <a id="1438" href="Games.Alternative.html#1414" class="Bound">x</a><a id="1439" class="Symbol">)</a> <a id="1441" class="Symbol">(</a><a id="1442" href="Games.Alternative.html#1394" class="Bound">Ï•f</a> <a id="1445" href="Games.Alternative.html#1414" class="Bound">x</a><a id="1446" class="Symbol">))</a>

<a id="1450" class="Markup">\end{code}</a><a id="1460" class="Background">

The equations we would have liked to use to define the function
to-Game are the following:

</a><a id="1554" class="Markup">\begin{code}</a>

<a id="to-Game-base"></a><a id="1568" href="Games.Alternative.html#1568" class="Function">to-Game-base</a>
 <a id="1582" class="Symbol">:</a> <a id="1584" class="Symbol">(</a><a id="1585" href="Games.Alternative.html#1585" class="Bound">q</a> <a id="1587" class="Symbol">:</a> <a id="1589" href="Games.TypeTrees.html#906" class="Function">Path</a> <a id="1594" class="InductiveConstructor">[]</a> <a id="1597" class="Symbol">â†’</a> <a id="1599" href="Games.Alternative.html#300" class="Bound">R</a><a id="1600" class="Symbol">)</a>
 <a id="1603" class="Symbol">â†’</a> <a id="1605" href="Games.Alternative.html#1223" class="Function">to-Game</a> <a id="1613" class="Symbol">(</a><a id="1614" href="Games.Alternative.html#502" class="InductiveConstructor">game&#39;</a> <a id="1620" class="InductiveConstructor">[]</a> <a id="1623" href="Games.Alternative.html#1585" class="Bound">q</a> <a id="1625" href="Games.TypeTrees.html#1274" class="InductiveConstructor">âŸ¨âŸ©</a><a id="1627" class="Symbol">)</a> <a id="1629" href="MLTT.Id.html#193" class="Datatype Operator">ï¼</a> <a id="1631" href="Games.Alternative.html#585" class="InductiveConstructor">leaf</a> <a id="1636" class="Symbol">(</a><a id="1637" href="Games.Alternative.html#1585" class="Bound">q</a> <a id="1639" href="Games.TypeTrees.html#1274" class="InductiveConstructor">âŸ¨âŸ©</a><a id="1641" class="Symbol">)</a>
<a id="1643" href="Games.Alternative.html#1568" class="Function">to-Game-base</a> <a id="1656" href="Games.Alternative.html#1656" class="Bound">q</a> <a id="1658" class="Symbol">=</a> <a id="1660" href="MLTT.Identity-Type.html#157" class="InductiveConstructor">refl</a>

<a id="to-Game-step"></a><a id="1666" href="Games.Alternative.html#1666" class="Function">to-Game-step</a>
 <a id="1680" class="Symbol">:</a> <a id="1682" class="Symbol">(</a><a id="1683" href="Games.Alternative.html#1683" class="Bound">X</a> <a id="1685" class="Symbol">:</a> <a id="1687" href="Notation.General.html#372" class="Function">Type</a><a id="1691" class="Symbol">)</a> <a id="1693" class="Symbol">(</a><a id="1694" href="Games.Alternative.html#1694" class="Bound">Xf</a> <a id="1697" class="Symbol">:</a> <a id="1699" href="Games.Alternative.html#1683" class="Bound">X</a> <a id="1701" class="Symbol">â†’</a> <a id="1703" href="Games.TypeTrees.html#719" class="Datatype">ğ‘»</a><a id="1704" class="Symbol">)</a> <a id="1706" class="Symbol">(</a><a id="1707" href="Games.Alternative.html#1707" class="Bound">Ï•</a> <a id="1709" class="Symbol">:</a> <a id="1711" href="MonadOnTypes.K.html#471" class="Function">K</a> <a id="1713" href="Games.Alternative.html#1683" class="Bound">X</a><a id="1714" class="Symbol">)</a> <a id="1716" class="Symbol">(</a><a id="1717" href="Games.Alternative.html#1717" class="Bound">Ï•f</a> <a id="1720" class="Symbol">:</a> <a id="1722" class="Symbol">(</a><a id="1723" href="Games.Alternative.html#1723" class="Bound">x</a> <a id="1725" class="Symbol">:</a> <a id="1727" href="Games.Alternative.html#1683" class="Bound">X</a><a id="1728" class="Symbol">)</a> <a id="1730" class="Symbol">â†’</a> <a id="1732" href="Games.FiniteHistoryDependent.html#2364" class="Function">ğ“š</a> <a id="1734" class="Symbol">(</a><a id="1735" href="Games.Alternative.html#1694" class="Bound">Xf</a> <a id="1738" href="Games.Alternative.html#1723" class="Bound">x</a><a id="1739" class="Symbol">))</a> <a id="1742" class="Symbol">(</a><a id="1743" href="Games.Alternative.html#1743" class="Bound">q</a> <a id="1745" class="Symbol">:</a> <a id="1747" href="Games.TypeTrees.html#906" class="Function">Path</a> <a id="1752" class="Symbol">(</a><a id="1753" href="Games.Alternative.html#1683" class="Bound">X</a> <a id="1755" class="InductiveConstructor Operator">âˆ·</a> <a id="1757" href="Games.Alternative.html#1694" class="Bound">Xf</a><a id="1759" class="Symbol">)</a> <a id="1761" class="Symbol">â†’</a> <a id="1763" href="Games.Alternative.html#300" class="Bound">R</a><a id="1764" class="Symbol">)</a>
 <a id="1767" class="Symbol">â†’</a> <a id="1769" href="Games.Alternative.html#1223" class="Function">to-Game</a> <a id="1777" class="Symbol">(</a><a id="1778" href="Games.Alternative.html#502" class="InductiveConstructor">game&#39;</a> <a id="1784" class="Symbol">(</a><a id="1785" href="Games.Alternative.html#1683" class="Bound">X</a> <a id="1787" class="InductiveConstructor Operator">âˆ·</a> <a id="1789" href="Games.Alternative.html#1694" class="Bound">Xf</a><a id="1791" class="Symbol">)</a> <a id="1793" href="Games.Alternative.html#1743" class="Bound">q</a> <a id="1795" class="Symbol">(</a><a id="1796" href="Games.Alternative.html#1707" class="Bound">Ï•</a> <a id="1798" href="Games.TypeTrees.html#1296" class="InductiveConstructor Operator">::</a> <a id="1801" href="Games.Alternative.html#1717" class="Bound">Ï•f</a><a id="1803" class="Symbol">))</a>
 <a id="1807" href="MLTT.Id.html#193" class="Datatype Operator">ï¼</a> <a id="1809" href="Games.Alternative.html#604" class="InductiveConstructor">branch</a> <a id="1816" href="Games.Alternative.html#1683" class="Bound">X</a> <a id="1818" href="Games.Alternative.html#1707" class="Bound">Ï•</a> <a id="1820" class="Symbol">(Î»</a> <a id="1823" href="Games.Alternative.html#1823" class="Bound">x</a> <a id="1825" class="Symbol">â†’</a> <a id="1827" href="Games.Alternative.html#1223" class="Function">to-Game</a> <a id="1835" class="Symbol">(</a><a id="1836" href="Games.Alternative.html#502" class="InductiveConstructor">game&#39;</a> <a id="1842" class="Symbol">(</a><a id="1843" href="Games.Alternative.html#1694" class="Bound">Xf</a> <a id="1846" href="Games.Alternative.html#1823" class="Bound">x</a><a id="1847" class="Symbol">)</a> <a id="1849" class="Symbol">(</a><a id="1850" href="Games.TypeTrees.html#1622" class="Function">subpred</a> <a id="1858" href="Games.Alternative.html#1743" class="Bound">q</a> <a id="1860" href="Games.Alternative.html#1823" class="Bound">x</a><a id="1861" class="Symbol">)</a> <a id="1863" class="Symbol">(</a><a id="1864" href="Games.Alternative.html#1717" class="Bound">Ï•f</a> <a id="1867" href="Games.Alternative.html#1823" class="Bound">x</a><a id="1868" class="Symbol">)))</a>
<a id="1872" href="Games.Alternative.html#1666" class="Function">to-Game-step</a> <a id="1885" href="Games.Alternative.html#1885" class="Bound">X</a> <a id="1887" href="Games.Alternative.html#1887" class="Bound">Xf</a> <a id="1890" href="Games.Alternative.html#1890" class="Bound">Ï•</a> <a id="1892" href="Games.Alternative.html#1892" class="Bound">Ï•f</a> <a id="1895" href="Games.Alternative.html#1895" class="Bound">q</a> <a id="1897" class="Symbol">=</a> <a id="1899" href="MLTT.Identity-Type.html#157" class="InductiveConstructor">refl</a>

<a id="1905" class="Markup">\end{code}</a><a id="1915" class="Background">

We also have he following equivalent definitional equalities expressed
in terms of leaf&#39; and branch&#39;:

</a><a id="2020" class="Markup">\begin{code}</a>

<a id="to-Game-base&#39;"></a><a id="2034" href="Games.Alternative.html#2034" class="Function">to-Game-base&#39;</a> <a id="2048" class="Symbol">:</a> <a id="2050" class="Symbol">(</a><a id="2051" href="Games.Alternative.html#2051" class="Bound">r</a> <a id="2053" class="Symbol">:</a> <a id="2055" href="Games.Alternative.html#300" class="Bound">R</a><a id="2056" class="Symbol">)</a> <a id="2058" class="Symbol">â†’</a> <a id="2060" href="Games.Alternative.html#1223" class="Function">to-Game</a> <a id="2068" class="Symbol">(</a><a id="2069" href="Games.Alternative.html#651" class="Function">leaf&#39;</a> <a id="2075" href="Games.Alternative.html#2051" class="Bound">r</a><a id="2076" class="Symbol">)</a> <a id="2078" href="MLTT.Id.html#193" class="Datatype Operator">ï¼</a> <a id="2080" href="Games.Alternative.html#585" class="InductiveConstructor">leaf</a> <a id="2085" href="Games.Alternative.html#2051" class="Bound">r</a>
<a id="2087" href="Games.Alternative.html#2034" class="Function">to-Game-base&#39;</a> <a id="2101" href="Games.Alternative.html#2101" class="Bound">r</a> <a id="2103" class="Symbol">=</a> <a id="2105" href="MLTT.Identity-Type.html#157" class="InductiveConstructor">refl</a>

<a id="2111" class="Keyword">module</a> <a id="2118" href="Games.Alternative.html#2118" class="Module">_</a>
         <a id="2129" class="Symbol">(</a><a id="2130" href="Games.Alternative.html#2130" class="Bound">X</a> <a id="2132" class="Symbol">:</a> <a id="2134" href="Notation.General.html#372" class="Function">Type</a><a id="2138" class="Symbol">)</a>
         <a id="2149" class="Symbol">(</a><a id="2150" href="Games.Alternative.html#2150" class="Bound">Xf</a> <a id="2153" class="Symbol">:</a> <a id="2155" href="Games.Alternative.html#2130" class="Bound">X</a> <a id="2157" class="Symbol">â†’</a> <a id="2159" href="Games.TypeTrees.html#719" class="Datatype">ğ‘»</a><a id="2160" class="Symbol">)</a>
         <a id="2171" class="Symbol">(</a><a id="2172" href="Games.Alternative.html#2172" class="Bound">Ï•</a> <a id="2174" class="Symbol">:</a> <a id="2176" href="MonadOnTypes.K.html#471" class="Function">K</a> <a id="2178" href="Games.Alternative.html#2130" class="Bound">X</a><a id="2179" class="Symbol">)</a>
         <a id="2190" class="Symbol">(</a><a id="2191" href="Games.Alternative.html#2191" class="Bound">Ï•f</a> <a id="2194" class="Symbol">:</a> <a id="2196" class="Symbol">(</a><a id="2197" href="Games.Alternative.html#2197" class="Bound">x</a> <a id="2199" class="Symbol">:</a> <a id="2201" href="Games.Alternative.html#2130" class="Bound">X</a><a id="2202" class="Symbol">)</a> <a id="2204" class="Symbol">â†’</a> <a id="2206" href="Games.FiniteHistoryDependent.html#2364" class="Function">ğ“š</a> <a id="2208" class="Symbol">(</a><a id="2209" href="Games.Alternative.html#2150" class="Bound">Xf</a> <a id="2212" href="Games.Alternative.html#2197" class="Bound">x</a><a id="2213" class="Symbol">))</a>
         <a id="2225" class="Symbol">(</a><a id="2226" href="Games.Alternative.html#2226" class="Bound">q</a> <a id="2228" class="Symbol">:</a> <a id="2230" href="Games.TypeTrees.html#906" class="Function">Path</a> <a id="2235" class="Symbol">(</a><a id="2236" href="Games.Alternative.html#2130" class="Bound">X</a> <a id="2238" href="Games.TypeTrees.html#747" class="InductiveConstructor Operator">âˆ·</a> <a id="2240" href="Games.Alternative.html#2150" class="Bound">Xf</a><a id="2242" class="Symbol">)</a> <a id="2244" class="Symbol">â†’</a> <a id="2246" href="Games.Alternative.html#300" class="Bound">R</a><a id="2247" class="Symbol">)</a>
       <a id="2256" class="Keyword">where</a>

 <a id="2264" href="Games.Alternative.html#2264" class="Function">subgame</a> <a id="2272" class="Symbol">:</a> <a id="2274" href="Games.Alternative.html#2130" class="Bound">X</a> <a id="2276" class="Symbol">â†’</a> <a id="2278" href="Games.Alternative.html#463" class="Record">Game&#39;</a>
 <a id="2285" href="Games.Alternative.html#2264" class="Function">subgame</a> <a id="2293" href="Games.Alternative.html#2293" class="Bound">x</a> <a id="2295" class="Symbol">=</a> <a id="2297" href="Games.Alternative.html#502" class="InductiveConstructor">game&#39;</a> <a id="2303" class="Symbol">(</a><a id="2304" href="Games.Alternative.html#2150" class="Bound">Xf</a> <a id="2307" href="Games.Alternative.html#2293" class="Bound">x</a><a id="2308" class="Symbol">)</a> <a id="2310" class="Symbol">(</a><a id="2311" href="Games.TypeTrees.html#1622" class="Function">subpred</a> <a id="2319" href="Games.Alternative.html#2226" class="Bound">q</a> <a id="2321" href="Games.Alternative.html#2293" class="Bound">x</a><a id="2322" class="Symbol">)</a> <a id="2324" class="Symbol">(</a><a id="2325" href="Games.Alternative.html#2191" class="Bound">Ï•f</a> <a id="2328" href="Games.Alternative.html#2293" class="Bound">x</a><a id="2329" class="Symbol">)</a>

 <a id="2333" href="Games.Alternative.html#2333" class="Function">to-Game-step&#39;</a> <a id="2347" class="Symbol">:</a> <a id="2349" href="Games.Alternative.html#1223" class="Function">to-Game</a> <a id="2357" class="Symbol">(</a><a id="2358" href="Games.Alternative.html#703" class="Function">branch&#39;</a> <a id="2366" href="Games.Alternative.html#2130" class="Bound">X</a> <a id="2368" href="Games.Alternative.html#2172" class="Bound">Ï•</a> <a id="2370" href="Games.Alternative.html#2264" class="Function">subgame</a><a id="2377" class="Symbol">)</a> <a id="2379" href="MLTT.Id.html#193" class="Datatype Operator">ï¼</a> <a id="2381" href="Games.Alternative.html#604" class="InductiveConstructor">branch</a> <a id="2388" href="Games.Alternative.html#2130" class="Bound">X</a> <a id="2390" href="Games.Alternative.html#2172" class="Bound">Ï•</a> <a id="2392" class="Symbol">(</a><a id="2393" href="Games.Alternative.html#1223" class="Function">to-Game</a> <a id="2401" href="MLTT.Pi.html#513" class="Function Operator">âˆ˜</a> <a id="2403" href="Games.Alternative.html#2264" class="Function">subgame</a><a id="2410" class="Symbol">)</a>
 <a id="2413" href="Games.Alternative.html#2333" class="Function">to-Game-step&#39;</a> <a id="2427" class="Symbol">=</a> <a id="2429" href="MLTT.Identity-Type.html#157" class="InductiveConstructor">refl</a>

<a id="2435" class="Markup">\end{code}</a><a id="2445" class="Background">

The above conversion functions are mutually inverse the types Game and
Game&#39; are equivalence, assuming function extensionality.

</a><a id="2576" class="Markup">\begin{code}</a>

<a id="2590" class="Keyword">module</a> <a id="2597" href="Games.Alternative.html#2597" class="Module">_</a> <a id="2599" class="Symbol">(</a><a id="2600" href="Games.Alternative.html#2600" class="Bound">fe</a> <a id="2603" class="Symbol">:</a> <a id="2605" href="UF.FunExt.html#1047" class="Function">Fun-Ext</a><a id="2612" class="Symbol">)</a> <a id="2614" class="Keyword">where</a>

 <a id="2622" href="Games.Alternative.html#2622" class="Function">from-to-Game</a> <a id="2635" class="Symbol">:</a> <a id="2637" href="Games.Alternative.html#917" class="Function">from-Game</a> <a id="2647" href="MLTT.Pi.html#513" class="Function Operator">âˆ˜</a> <a id="2649" href="Games.Alternative.html#1223" class="Function">to-Game</a> <a id="2657" href="MLTT.Id.html#1891" class="Function Operator">âˆ¼</a> <a id="2659" href="MLTT.Pi.html#444" class="Function">id</a>
 <a id="2663" href="Games.Alternative.html#2622" class="Function">from-to-Game</a> <a id="2676" class="Symbol">(</a><a id="2677" href="Games.Alternative.html#502" class="InductiveConstructor">game&#39;</a> <a id="2683" href="Games.Alternative.html#2683" class="Bound">Xt</a> <a id="2686" href="Games.Alternative.html#2686" class="Bound">q</a> <a id="2688" href="Games.Alternative.html#2688" class="Bound">Ï•t</a><a id="2690" class="Symbol">)</a> <a id="2692" class="Symbol">=</a> <a id="2694" href="Games.Alternative.html#2715" class="Function">h</a> <a id="2696" href="Games.Alternative.html#2683" class="Bound">Xt</a> <a id="2699" href="Games.Alternative.html#2686" class="Bound">q</a> <a id="2701" href="Games.Alternative.html#2688" class="Bound">Ï•t</a>
  <a id="2706" class="Keyword">where</a>
   <a id="2715" href="Games.Alternative.html#2715" class="Function">h</a> <a id="2717" class="Symbol">:</a> <a id="2719" class="Symbol">(</a><a id="2720" href="Games.Alternative.html#2720" class="Bound">Xt</a> <a id="2723" class="Symbol">:</a> <a id="2725" href="Games.TypeTrees.html#719" class="Datatype">ğ‘»</a><a id="2726" class="Symbol">)</a>
       <a id="2735" class="Symbol">(</a><a id="2736" href="Games.Alternative.html#2736" class="Bound">q</a> <a id="2738" class="Symbol">:</a> <a id="2740" href="Games.TypeTrees.html#906" class="Function">Path</a> <a id="2745" href="Games.Alternative.html#2720" class="Bound">Xt</a> <a id="2748" class="Symbol">â†’</a> <a id="2750" href="Games.Alternative.html#300" class="Bound">R</a><a id="2751" class="Symbol">)</a>
       <a id="2760" class="Symbol">(</a><a id="2761" href="Games.Alternative.html#2761" class="Bound">Ï•t</a> <a id="2764" class="Symbol">:</a> <a id="2766" href="Games.FiniteHistoryDependent.html#2364" class="Function">ğ“š</a> <a id="2768" href="Games.Alternative.html#2720" class="Bound">Xt</a><a id="2770" class="Symbol">)</a>
     <a id="2777" class="Symbol">â†’</a> <a id="2779" href="Games.Alternative.html#917" class="Function">from-Game</a> <a id="2789" class="Symbol">(</a><a id="2790" href="Games.Alternative.html#1223" class="Function">to-Game</a> <a id="2798" class="Symbol">(</a><a id="2799" href="Games.Alternative.html#502" class="InductiveConstructor">game&#39;</a> <a id="2805" href="Games.Alternative.html#2720" class="Bound">Xt</a> <a id="2808" href="Games.Alternative.html#2736" class="Bound">q</a> <a id="2810" href="Games.Alternative.html#2761" class="Bound">Ï•t</a><a id="2812" class="Symbol">))</a> <a id="2815" href="MLTT.Id.html#193" class="Datatype Operator">ï¼</a> <a id="2817" href="Games.Alternative.html#502" class="InductiveConstructor">game&#39;</a> <a id="2823" href="Games.Alternative.html#2720" class="Bound">Xt</a> <a id="2826" href="Games.Alternative.html#2736" class="Bound">q</a> <a id="2828" href="Games.Alternative.html#2761" class="Bound">Ï•t</a>
   <a id="2834" href="Games.Alternative.html#2715" class="Function">h</a> <a id="2836" href="Games.TypeTrees.html#737" class="InductiveConstructor">[]</a>       <a id="2845" href="Games.Alternative.html#2845" class="Bound">q</a> <a id="2847" href="Games.TypeTrees.html#1274" class="InductiveConstructor">âŸ¨âŸ©</a>       <a id="2856" class="Symbol">=</a> <a id="2858" href="MLTT.Identity-Type.html#157" class="InductiveConstructor">refl</a>
   <a id="2866" href="Games.Alternative.html#2715" class="Function">h</a> <a id="2868" class="Symbol">(</a><a id="2869" href="Games.Alternative.html#2869" class="Bound">X</a> <a id="2871" href="Games.TypeTrees.html#747" class="InductiveConstructor Operator">âˆ·</a> <a id="2873" href="Games.Alternative.html#2873" class="Bound">Xf</a><a id="2875" class="Symbol">)</a> <a id="2877" href="Games.Alternative.html#2877" class="Bound">q</a> <a id="2879" class="Symbol">(</a><a id="2880" href="Games.Alternative.html#2880" class="Bound">Ï•</a> <a id="2882" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="2884" href="Games.Alternative.html#2884" class="Bound">Ï•f</a><a id="2886" class="Symbol">)</a> <a id="2888" class="Symbol">=</a>
    <a id="2894" href="Games.Alternative.html#917" class="Function">from-Game</a> <a id="2904" class="Symbol">(</a><a id="2905" href="Games.Alternative.html#1223" class="Function">to-Game</a> <a id="2913" class="Symbol">(</a><a id="2914" href="Games.Alternative.html#502" class="InductiveConstructor">game&#39;</a> <a id="2920" class="Symbol">(</a><a id="2921" href="Games.Alternative.html#2869" class="Bound">X</a> <a id="2923" href="Games.TypeTrees.html#747" class="InductiveConstructor Operator">âˆ·</a> <a id="2925" href="Games.Alternative.html#2873" class="Bound">Xf</a><a id="2927" class="Symbol">)</a> <a id="2929" href="Games.Alternative.html#2877" class="Bound">q</a> <a id="2931" class="Symbol">(</a><a id="2932" href="Games.Alternative.html#2880" class="Bound">Ï•</a> <a id="2934" href="Games.TypeTrees.html#1296" class="InductiveConstructor Operator">::</a> <a id="2937" href="Games.Alternative.html#2884" class="Bound">Ï•f</a><a id="2939" class="Symbol">)))</a> <a id="2943" href="MLTT.Id.html#2705" class="Function Operator">ï¼âŸ¨</a> <a id="2946" href="MLTT.Identity-Type.html#157" class="InductiveConstructor">refl</a> <a id="2951" href="MLTT.Id.html#2705" class="Function Operator">âŸ©</a>
    <a id="2957" href="Games.Alternative.html#917" class="Function">from-Game</a> <a id="2967" class="Symbol">(</a><a id="2968" href="Games.Alternative.html#604" class="InductiveConstructor">branch</a> <a id="2975" href="Games.Alternative.html#2869" class="Bound">X</a> <a id="2977" href="Games.Alternative.html#2880" class="Bound">Ï•</a> <a id="2979" class="Symbol">(</a><a id="2980" href="Games.Alternative.html#1223" class="Function">to-Game</a> <a id="2988" href="MLTT.Pi.html#513" class="Function Operator">âˆ˜</a> <a id="2990" href="Games.Alternative.html#3209" class="Function">Gf</a><a id="2992" class="Symbol">))</a>            <a id="3006" href="MLTT.Id.html#2705" class="Function Operator">ï¼âŸ¨</a> <a id="3009" href="MLTT.Identity-Type.html#157" class="InductiveConstructor">refl</a> <a id="3014" href="MLTT.Id.html#2705" class="Function Operator">âŸ©</a>
    <a id="3020" href="Games.Alternative.html#703" class="Function">branch&#39;</a> <a id="3028" href="Games.Alternative.html#2869" class="Bound">X</a> <a id="3030" href="Games.Alternative.html#2880" class="Bound">Ï•</a> <a id="3032" href="Games.Alternative.html#3212" class="Function">Hf</a>                                   <a id="3069" href="MLTT.Id.html#2705" class="Function Operator">ï¼âŸ¨</a> <a id="3072" href="Games.Alternative.html#3367" class="Function">I</a> <a id="3074" href="MLTT.Id.html#2705" class="Function Operator">âŸ©</a>
    <a id="3080" href="Games.Alternative.html#703" class="Function">branch&#39;</a> <a id="3088" href="Games.Alternative.html#2869" class="Bound">X</a> <a id="3090" href="Games.Alternative.html#2880" class="Bound">Ï•</a> <a id="3092" href="Games.Alternative.html#3209" class="Function">Gf</a>                                   <a id="3129" href="MLTT.Id.html#2705" class="Function Operator">ï¼âŸ¨</a> <a id="3132" href="MLTT.Identity-Type.html#157" class="InductiveConstructor">refl</a> <a id="3137" href="MLTT.Id.html#2705" class="Function Operator">âŸ©</a>
    <a id="3143" href="Games.Alternative.html#502" class="InductiveConstructor">game&#39;</a> <a id="3149" class="Symbol">(</a><a id="3150" href="Games.Alternative.html#2869" class="Bound">X</a> <a id="3152" href="Games.TypeTrees.html#747" class="InductiveConstructor Operator">âˆ·</a> <a id="3154" href="Games.Alternative.html#2873" class="Bound">Xf</a><a id="3156" class="Symbol">)</a> <a id="3158" href="Games.Alternative.html#2877" class="Bound">q</a> <a id="3160" class="Symbol">(</a><a id="3161" href="Games.Alternative.html#2880" class="Bound">Ï•</a> <a id="3163" href="Games.TypeTrees.html#1296" class="InductiveConstructor Operator">::</a> <a id="3166" href="Games.Alternative.html#2884" class="Bound">Ï•f</a><a id="3168" class="Symbol">)</a>                       <a id="3192" href="MLTT.Id.html#2787" class="Function Operator">âˆ</a>
    <a id="3198" class="Keyword">where</a>
     <a id="3209" href="Games.Alternative.html#3209" class="Function">Gf</a> <a id="3212" href="Games.Alternative.html#3212" class="Function">Hf</a> <a id="3215" class="Symbol">:</a> <a id="3217" href="Games.Alternative.html#2869" class="Bound">X</a> <a id="3219" class="Symbol">â†’</a> <a id="3221" href="Games.Alternative.html#463" class="Record">Game&#39;</a>
     <a id="3232" href="Games.Alternative.html#3209" class="Function">Gf</a> <a id="3235" href="Games.Alternative.html#3235" class="Bound">x</a> <a id="3237" class="Symbol">=</a> <a id="3239" href="Games.Alternative.html#2264" class="Function">subgame</a> <a id="3247" href="Games.Alternative.html#2869" class="Bound">X</a> <a id="3249" href="Games.Alternative.html#2873" class="Bound">Xf</a> <a id="3252" href="Games.Alternative.html#2880" class="Bound">Ï•</a> <a id="3254" href="Games.Alternative.html#2884" class="Bound">Ï•f</a> <a id="3257" href="Games.Alternative.html#2877" class="Bound">q</a> <a id="3259" href="Games.Alternative.html#3235" class="Bound">x</a>
     <a id="3266" href="Games.Alternative.html#3212" class="Function">Hf</a> <a id="3269" href="Games.Alternative.html#3269" class="Bound">x</a> <a id="3271" class="Symbol">=</a> <a id="3273" href="Games.Alternative.html#917" class="Function">from-Game</a> <a id="3283" class="Symbol">(</a><a id="3284" href="Games.Alternative.html#1223" class="Function">to-Game</a> <a id="3292" class="Symbol">(</a><a id="3293" href="Games.Alternative.html#3209" class="Function">Gf</a> <a id="3296" href="Games.Alternative.html#3269" class="Bound">x</a><a id="3297" class="Symbol">))</a>

     <a id="3306" href="Games.Alternative.html#3306" class="Function">IH</a> <a id="3309" class="Symbol">:</a> <a id="3311" href="Games.Alternative.html#3212" class="Function">Hf</a> <a id="3314" href="MLTT.Id.html#1891" class="Function Operator">âˆ¼</a> <a id="3316" href="Games.Alternative.html#3209" class="Function">Gf</a>
     <a id="3324" href="Games.Alternative.html#3306" class="Function">IH</a> <a id="3327" href="Games.Alternative.html#3327" class="Bound">x</a> <a id="3329" class="Symbol">=</a> <a id="3331" href="Games.Alternative.html#2715" class="Function">h</a> <a id="3333" class="Symbol">(</a><a id="3334" href="Games.Alternative.html#2873" class="Bound">Xf</a> <a id="3337" href="Games.Alternative.html#3327" class="Bound">x</a><a id="3338" class="Symbol">)</a> <a id="3340" class="Symbol">(</a><a id="3341" href="Games.TypeTrees.html#1622" class="Function">subpred</a> <a id="3349" href="Games.Alternative.html#2877" class="Bound">q</a> <a id="3351" href="Games.Alternative.html#3327" class="Bound">x</a><a id="3352" class="Symbol">)</a> <a id="3354" class="Symbol">(</a><a id="3355" href="Games.Alternative.html#2884" class="Bound">Ï•f</a> <a id="3358" href="Games.Alternative.html#3327" class="Bound">x</a><a id="3359" class="Symbol">)</a>

     <a id="3367" href="Games.Alternative.html#3367" class="Function">I</a> <a id="3369" class="Symbol">:</a> <a id="3371" href="Games.Alternative.html#703" class="Function">branch&#39;</a> <a id="3379" href="Games.Alternative.html#2869" class="Bound">X</a> <a id="3381" href="Games.Alternative.html#2880" class="Bound">Ï•</a> <a id="3383" href="Games.Alternative.html#3212" class="Function">Hf</a> <a id="3386" href="MLTT.Id.html#193" class="Datatype Operator">ï¼</a> <a id="3388" href="Games.Alternative.html#703" class="Function">branch&#39;</a> <a id="3396" href="Games.Alternative.html#2869" class="Bound">X</a> <a id="3398" href="Games.Alternative.html#2880" class="Bound">Ï•</a> <a id="3400" href="Games.Alternative.html#3209" class="Function">Gf</a>
     <a id="3408" href="Games.Alternative.html#3367" class="Function">I</a> <a id="3410" class="Symbol">=</a> <a id="3412" href="MLTT.Id.html#1136" class="Function">ap</a> <a id="3415" class="Symbol">(</a><a id="3416" href="Games.Alternative.html#703" class="Function">branch&#39;</a> <a id="3424" href="Games.Alternative.html#2869" class="Bound">X</a> <a id="3426" href="Games.Alternative.html#2880" class="Bound">Ï•</a><a id="3427" class="Symbol">)</a> <a id="3429" class="Symbol">(</a><a id="3430" href="UF.FunExt.html#1242" class="Function">dfunext</a> <a id="3438" href="Games.Alternative.html#2600" class="Bound">fe</a> <a id="3441" href="Games.Alternative.html#3306" class="Function">IH</a><a id="3443" class="Symbol">)</a>

 <a id="3447" href="Games.Alternative.html#3447" class="Function">to-from-Game</a> <a id="3460" class="Symbol">:</a> <a id="3462" href="Games.Alternative.html#1223" class="Function">to-Game</a> <a id="3470" href="MLTT.Pi.html#513" class="Function Operator">âˆ˜</a> <a id="3472" href="Games.Alternative.html#917" class="Function">from-Game</a> <a id="3482" href="MLTT.Id.html#1891" class="Function Operator">âˆ¼</a> <a id="3484" href="MLTT.Pi.html#444" class="Function">id</a>
 <a id="3488" href="Games.Alternative.html#3447" class="Function">to-from-Game</a> <a id="3501" class="Symbol">(</a><a id="3502" href="Games.Alternative.html#585" class="InductiveConstructor">leaf</a> <a id="3507" href="Games.Alternative.html#3507" class="Bound">x</a><a id="3508" class="Symbol">)</a>        <a id="3517" class="Symbol">=</a> <a id="3519" href="MLTT.Identity-Type.html#157" class="InductiveConstructor">refl</a>
 <a id="3525" href="Games.Alternative.html#3447" class="Function">to-from-Game</a> <a id="3538" class="Symbol">(</a><a id="3539" href="Games.Alternative.html#604" class="InductiveConstructor">branch</a> <a id="3546" href="Games.Alternative.html#3546" class="Bound">X</a> <a id="3548" href="Games.Alternative.html#3548" class="Bound">Ï•</a> <a id="3550" href="Games.Alternative.html#3550" class="Bound">Gf</a><a id="3552" class="Symbol">)</a> <a id="3554" class="Symbol">=</a>
  <a id="3558" href="Games.Alternative.html#1223" class="Function">to-Game</a> <a id="3566" class="Symbol">(</a><a id="3567" href="Games.Alternative.html#917" class="Function">from-Game</a> <a id="3577" class="Symbol">(</a><a id="3578" href="Games.Alternative.html#604" class="InductiveConstructor">branch</a> <a id="3585" href="Games.Alternative.html#3546" class="Bound">X</a> <a id="3587" href="Games.Alternative.html#3548" class="Bound">Ï•</a> <a id="3589" href="Games.Alternative.html#3550" class="Bound">Gf</a><a id="3591" class="Symbol">))</a>    <a id="3597" href="MLTT.Id.html#2705" class="Function Operator">ï¼âŸ¨</a> <a id="3600" href="MLTT.Identity-Type.html#157" class="InductiveConstructor">refl</a> <a id="3605" href="MLTT.Id.html#2705" class="Function Operator">âŸ©</a>
  <a id="3609" href="Games.Alternative.html#1223" class="Function">to-Game</a> <a id="3617" class="Symbol">(</a><a id="3618" href="Games.Alternative.html#703" class="Function">branch&#39;</a> <a id="3626" href="Games.Alternative.html#3546" class="Bound">X</a> <a id="3628" href="Games.Alternative.html#3548" class="Bound">Ï•</a> <a id="3630" class="Symbol">(</a><a id="3631" href="Games.Alternative.html#917" class="Function">from-Game</a> <a id="3641" href="MLTT.Pi.html#513" class="Function Operator">âˆ˜</a> <a id="3643" href="Games.Alternative.html#3550" class="Bound">Gf</a><a id="3645" class="Symbol">))</a> <a id="3648" href="MLTT.Id.html#2705" class="Function Operator">ï¼âŸ¨</a> <a id="3651" href="MLTT.Identity-Type.html#157" class="InductiveConstructor">refl</a> <a id="3656" href="MLTT.Id.html#2705" class="Function Operator">âŸ©</a>
  <a id="3660" href="Games.Alternative.html#604" class="InductiveConstructor">branch</a> <a id="3667" href="Games.Alternative.html#3546" class="Bound">X</a> <a id="3669" href="Games.Alternative.html#3548" class="Bound">Ï•</a> <a id="3671" class="Symbol">(</a><a id="3672" href="Games.Alternative.html#1223" class="Function">to-Game</a> <a id="3680" href="MLTT.Pi.html#513" class="Function Operator">âˆ˜</a> <a id="3682" href="Games.Alternative.html#917" class="Function">from-Game</a> <a id="3692" href="MLTT.Pi.html#513" class="Function Operator">âˆ˜</a> <a id="3694" href="Games.Alternative.html#3550" class="Bound">Gf</a><a id="3696" class="Symbol">)</a>  <a id="3699" href="MLTT.Id.html#2705" class="Function Operator">ï¼âŸ¨</a> <a id="3702" href="Games.Alternative.html#3760" class="Function">I</a> <a id="3704" href="MLTT.Id.html#2705" class="Function Operator">âŸ©</a>
  <a id="3708" href="Games.Alternative.html#604" class="InductiveConstructor">branch</a> <a id="3715" href="Games.Alternative.html#3546" class="Bound">X</a> <a id="3717" href="Games.Alternative.html#3548" class="Bound">Ï•</a> <a id="3719" href="Games.Alternative.html#3550" class="Bound">Gf</a>                          <a id="3747" href="MLTT.Id.html#2787" class="Function Operator">âˆ</a>
  <a id="3751" class="Keyword">where</a>
   <a id="3760" href="Games.Alternative.html#3760" class="Function">I</a> <a id="3762" class="Symbol">=</a> <a id="3764" href="MLTT.Id.html#1136" class="Function">ap</a> <a id="3767" class="Symbol">(</a><a id="3768" href="Games.Alternative.html#604" class="InductiveConstructor">branch</a> <a id="3775" href="Games.Alternative.html#3546" class="Bound">X</a> <a id="3777" href="Games.Alternative.html#3548" class="Bound">Ï•</a><a id="3778" class="Symbol">)</a> <a id="3780" class="Symbol">(</a><a id="3781" href="UF.FunExt.html#1242" class="Function">dfunext</a> <a id="3789" href="Games.Alternative.html#2600" class="Bound">fe</a> <a id="3792" class="Symbol">(</a><a id="3793" href="Games.Alternative.html#3447" class="Function">to-from-Game</a> <a id="3806" href="MLTT.Pi.html#513" class="Function Operator">âˆ˜</a> <a id="3808" href="Games.Alternative.html#3550" class="Bound">Gf</a><a id="3810" class="Symbol">))</a>

 <a id="3815" href="Games.Alternative.html#3815" class="Function">Game-is-equiv-to-Game&#39;</a> <a id="3838" class="Symbol">:</a> <a id="3840" href="Games.Alternative.html#565" class="Datatype">Game</a> <a id="3845" href="UF.Equiv.html#1318" class="Function Operator">â‰ƒ</a> <a id="3847" href="Games.Alternative.html#463" class="Record">Game&#39;</a>
 <a id="3854" href="Games.Alternative.html#3815" class="Function">Game-is-equiv-to-Game&#39;</a> <a id="3877" class="Symbol">=</a> <a id="3879" href="UF.Equiv.html#7224" class="Function">qinveq</a> <a id="3886" href="Games.Alternative.html#917" class="Function">from-Game</a> <a id="3896" class="Symbol">(</a><a id="3897" href="Games.Alternative.html#1223" class="Function">to-Game</a> <a id="3905" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="3907" href="Games.Alternative.html#3447" class="Function">to-from-Game</a> <a id="3920" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="3922" href="Games.Alternative.html#2622" class="Function">from-to-Game</a><a id="3934" class="Symbol">)</a>

<a id="3937" class="Markup">\end{code}</a><a id="3947" class="Background">
</a></pre></body></html>