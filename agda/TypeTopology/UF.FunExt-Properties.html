<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>UF.FunExt-Properties</title><link rel="stylesheet" href="Agda.css"><script type="text/javascript" src="highlight-hover.js"></script></head><body><pre class="Agda"><a id="1" class="Background">Martin Escardo, 19th May 2018.

Properties of function extensionality.

</a><a id="73" class="Markup">\begin{code}</a>

<a id="87" class="Symbol">{-#</a> <a id="91" class="Keyword">OPTIONS</a> <a id="99" class="Pragma">--safe</a> <a id="106" class="Pragma">--without-K</a> <a id="118" class="Symbol">#-}</a>

<a id="123" class="Keyword">module</a> <a id="130" href="UF.FunExt-Properties.html" class="Module">UF.FunExt-Properties</a> <a id="151" class="Keyword">where</a>

<a id="158" class="Keyword">open</a> <a id="163" class="Keyword">import</a> <a id="170" href="MLTT.Spartan.html" class="Module">MLTT.Spartan</a>
<a id="183" class="Keyword">open</a> <a id="188" class="Keyword">import</a> <a id="195" href="UF.Base.html" class="Module">UF.Base</a>
<a id="203" class="Keyword">open</a> <a id="208" class="Keyword">import</a> <a id="215" href="UF.FunExt.html" class="Module">UF.FunExt</a>
<a id="225" class="Keyword">open</a> <a id="230" class="Keyword">import</a> <a id="237" href="UF.Equiv.html" class="Module">UF.Equiv</a>
<a id="246" class="Keyword">open</a> <a id="251" class="Keyword">import</a> <a id="258" href="UF.Equiv-FunExt.html" class="Module">UF.Equiv-FunExt</a>
<a id="274" class="Keyword">open</a> <a id="279" class="Keyword">import</a> <a id="286" href="UF.Yoneda.html" class="Module">UF.Yoneda</a>
<a id="296" class="Keyword">open</a> <a id="301" class="Keyword">import</a> <a id="308" href="UF.Subsingletons.html" class="Module">UF.Subsingletons</a>
<a id="325" class="Keyword">open</a> <a id="330" class="Keyword">import</a> <a id="337" href="UF.Retracts.html" class="Module">UF.Retracts</a>

<a id="350" class="Markup">\end{code}</a><a id="360" class="Background">

Vladimir Voevodsky proved in Coq that naive function extensionality
(any two pointwise equal non-dependent functions are equal) implies
function extensionality (happly is an equivalence, for dependent
functions):

  https://github.com/vladimirias/Foundations/blob/master/Generalities/uu0.v

Here is an Agda version, with explicit indication of the universe levels.

</a><a id="728" class="Markup">\begin{code}</a>

<a id="naive-funext-gives-funext&#39;"></a><a id="742" href="UF.FunExt-Properties.html#742" class="Function">naive-funext-gives-funext&#39;</a> <a id="769" class="Symbol">:</a> <a id="771" href="UF.FunExt.html#640" class="Function">naive-funext</a> <a id="784" href="MLTT.Universes.html#3834" class="Generalizable">𝓤</a> <a id="786" class="Symbol">(</a><a id="787" href="MLTT.Universes.html#3834" class="Generalizable">𝓤</a> <a id="789" href="Agda.Primitive.html#961" class="Primitive Operator">⊔</a> <a id="791" href="MLTT.Universes.html#3836" class="Generalizable">𝓥</a><a id="792" class="Symbol">)</a> <a id="794" class="Symbol">→</a> <a id="796" href="UF.FunExt.html#640" class="Function">naive-funext</a> <a id="809" href="MLTT.Universes.html#3834" class="Generalizable">𝓤</a> <a id="811" href="MLTT.Universes.html#3834" class="Generalizable">𝓤</a> <a id="813" class="Symbol">→</a> <a id="815" href="UF.FunExt.html#850" class="Function">funext</a> <a id="822" href="MLTT.Universes.html#3834" class="Generalizable">𝓤</a> <a id="824" href="MLTT.Universes.html#3836" class="Generalizable">𝓥</a>
<a id="826" href="UF.FunExt-Properties.html#742" class="Function">naive-funext-gives-funext&#39;</a> <a id="853" class="Symbol">{</a><a id="854" href="UF.FunExt-Properties.html#854" class="Bound">𝓤</a><a id="855" class="Symbol">}</a> <a id="857" class="Symbol">{</a><a id="858" href="UF.FunExt-Properties.html#858" class="Bound">𝓥</a><a id="859" class="Symbol">}</a> <a id="861" href="UF.FunExt-Properties.html#861" class="Bound">nfe</a> <a id="865" href="UF.FunExt-Properties.html#865" class="Bound">nfe&#39;</a> <a id="870" class="Symbol">=</a> <a id="872" href="UF.Yoneda.html#17253" class="Function">funext-via-singletons</a> <a id="894" href="UF.FunExt-Properties.html#905" class="Function">γ</a>
 <a id="897" class="Keyword">where</a>
  <a id="905" href="UF.FunExt-Properties.html#905" class="Function">γ</a> <a id="907" class="Symbol">:</a> <a id="909" class="Symbol">(</a><a id="910" href="UF.FunExt-Properties.html#910" class="Bound">X</a> <a id="912" class="Symbol">:</a> <a id="914" href="UF.FunExt-Properties.html#854" class="Bound">𝓤</a> <a id="916" href="MLTT.Universes.html#3977" class="Function Operator">̇</a> <a id="918" class="Symbol">)</a> <a id="920" class="Symbol">(</a><a id="921" href="UF.FunExt-Properties.html#921" class="Bound">A</a> <a id="923" class="Symbol">:</a> <a id="925" href="UF.FunExt-Properties.html#910" class="Bound">X</a> <a id="927" class="Symbol">→</a> <a id="929" href="UF.FunExt-Properties.html#858" class="Bound">𝓥</a> <a id="931" href="MLTT.Universes.html#3977" class="Function Operator">̇</a> <a id="933" class="Symbol">)</a>
    <a id="939" class="Symbol">→</a> <a id="941" class="Symbol">((</a><a id="943" href="UF.FunExt-Properties.html#943" class="Bound">x</a> <a id="945" class="Symbol">:</a> <a id="947" href="UF.FunExt-Properties.html#910" class="Bound">X</a><a id="948" class="Symbol">)</a> <a id="950" class="Symbol">→</a> <a id="952" href="UF.Subsingletons.html#1565" class="Function">is-singleton</a> <a id="965" class="Symbol">(</a><a id="966" href="UF.FunExt-Properties.html#921" class="Bound">A</a> <a id="968" href="UF.FunExt-Properties.html#943" class="Bound">x</a><a id="969" class="Symbol">))</a>
    <a id="976" class="Symbol">→</a> <a id="978" href="UF.Subsingletons.html#1565" class="Function">is-singleton</a> <a id="991" class="Symbol">(</a><a id="992" href="MLTT.Pi.html#164" class="Function">Π</a> <a id="994" href="UF.FunExt-Properties.html#921" class="Bound">A</a><a id="995" class="Symbol">)</a>
  <a id="999" href="UF.FunExt-Properties.html#905" class="Function">γ</a> <a id="1001" href="UF.FunExt-Properties.html#1001" class="Bound">X</a> <a id="1003" href="UF.FunExt-Properties.html#1003" class="Bound">A</a> <a id="1005" href="UF.FunExt-Properties.html#1005" class="Bound">φ</a> <a id="1007" class="Symbol">=</a> <a id="1009" href="UF.FunExt-Properties.html#1550" class="Function">δ</a>
   <a id="1014" class="Keyword">where</a>
    <a id="1024" href="UF.FunExt-Properties.html#1024" class="Function">f</a> <a id="1026" class="Symbol">:</a> <a id="1028" href="MLTT.Sigma-Type.html#116" class="Record">Σ</a> <a id="1030" href="UF.FunExt-Properties.html#1003" class="Bound">A</a> <a id="1032" class="Symbol">→</a> <a id="1034" href="UF.FunExt-Properties.html#1001" class="Bound">X</a>
    <a id="1040" href="UF.FunExt-Properties.html#1024" class="Function">f</a> <a id="1042" class="Symbol">=</a> <a id="1044" href="MLTT.Sigma-Type.html#198" class="Field">pr₁</a>

    <a id="1053" href="UF.FunExt-Properties.html#1053" class="Function">f-is-equiv</a> <a id="1064" class="Symbol">:</a> <a id="1066" href="UF.Equiv.html#402" class="Function">is-equiv</a> <a id="1075" href="UF.FunExt-Properties.html#1024" class="Function">f</a>
    <a id="1081" href="UF.FunExt-Properties.html#1053" class="Function">f-is-equiv</a> <a id="1092" class="Symbol">=</a> <a id="1094" href="UF.Equiv.html#20566" class="Function">pr₁-is-equiv</a> <a id="1107" href="UF.FunExt-Properties.html#1001" class="Bound">X</a> <a id="1109" href="UF.FunExt-Properties.html#1003" class="Bound">A</a> <a id="1111" href="UF.FunExt-Properties.html#1005" class="Bound">φ</a>

    <a id="1118" href="UF.FunExt-Properties.html#1118" class="Function">g</a> <a id="1120" class="Symbol">:</a> <a id="1122" class="Symbol">(</a><a id="1123" href="UF.FunExt-Properties.html#1001" class="Bound">X</a> <a id="1125" class="Symbol">→</a> <a id="1127" href="MLTT.Sigma-Type.html#116" class="Record">Σ</a> <a id="1129" href="UF.FunExt-Properties.html#1003" class="Bound">A</a><a id="1130" class="Symbol">)</a> <a id="1132" class="Symbol">→</a> <a id="1134" class="Symbol">(</a><a id="1135" href="UF.FunExt-Properties.html#1001" class="Bound">X</a> <a id="1137" class="Symbol">→</a> <a id="1139" href="UF.FunExt-Properties.html#1001" class="Bound">X</a><a id="1140" class="Symbol">)</a>
    <a id="1146" href="UF.FunExt-Properties.html#1118" class="Function">g</a> <a id="1148" href="UF.FunExt-Properties.html#1148" class="Bound">h</a> <a id="1150" class="Symbol">=</a> <a id="1152" href="UF.FunExt-Properties.html#1024" class="Function">f</a> <a id="1154" href="MLTT.Pi.html#513" class="Function Operator">∘</a> <a id="1156" href="UF.FunExt-Properties.html#1148" class="Bound">h</a>

    <a id="1163" href="UF.FunExt-Properties.html#1163" class="Function">g-is-equiv</a> <a id="1174" class="Symbol">:</a> <a id="1176" href="UF.Equiv.html#402" class="Function">is-equiv</a> <a id="1185" href="UF.FunExt-Properties.html#1118" class="Function">g</a>
    <a id="1191" href="UF.FunExt-Properties.html#1163" class="Function">g-is-equiv</a> <a id="1202" class="Symbol">=</a> <a id="1204" href="UF.Equiv-FunExt.html#1862" class="Function">equiv-post</a> <a id="1215" href="UF.FunExt-Properties.html#861" class="Bound">nfe</a> <a id="1219" href="UF.FunExt-Properties.html#865" class="Bound">nfe&#39;</a> <a id="1224" href="UF.FunExt-Properties.html#1024" class="Function">f</a> <a id="1226" href="UF.FunExt-Properties.html#1053" class="Function">f-is-equiv</a>

    <a id="1242" href="UF.FunExt-Properties.html#1242" class="Function">e</a> <a id="1244" class="Symbol">:</a> <a id="1246" href="UF.Subsingletons.html#8927" class="Function">∃!</a> <a id="1249" href="UF.FunExt-Properties.html#1249" class="Bound">h</a> <a id="1251" href="UF.Subsingletons.html#8927" class="Function">꞉</a> <a id="1253" class="Symbol">(</a><a id="1254" href="UF.FunExt-Properties.html#1001" class="Bound">X</a> <a id="1256" class="Symbol">→</a> <a id="1258" href="MLTT.Sigma-Type.html#116" class="Record">Σ</a> <a id="1260" href="UF.FunExt-Properties.html#1003" class="Bound">A</a><a id="1261" class="Symbol">)</a> <a id="1263" href="UF.Subsingletons.html#8927" class="Function">,</a> <a id="1265" href="UF.FunExt-Properties.html#1024" class="Function">f</a> <a id="1267" href="MLTT.Pi.html#513" class="Function Operator">∘</a> <a id="1269" href="UF.FunExt-Properties.html#1249" class="Bound">h</a> <a id="1271" href="MLTT.Id.html#193" class="Datatype Operator">＝</a> <a id="1273" href="MLTT.Pi.html#444" class="Function">id</a>
    <a id="1280" href="UF.FunExt-Properties.html#1242" class="Function">e</a> <a id="1282" class="Symbol">=</a> <a id="1284" href="UF.Equiv.html#17598" class="Function">equivs-are-vv-equivs</a> <a id="1305" href="UF.FunExt-Properties.html#1118" class="Function">g</a> <a id="1307" href="UF.FunExt-Properties.html#1163" class="Function">g-is-equiv</a> <a id="1318" href="MLTT.Pi.html#444" class="Function">id</a>

    <a id="1326" href="UF.FunExt-Properties.html#1326" class="Function">r</a> <a id="1328" class="Symbol">:</a> <a id="1330" class="Symbol">(</a><a id="1331" href="MLTT.Sigma.html#423" class="Function">Σ</a> <a id="1333" href="UF.FunExt-Properties.html#1333" class="Bound">h</a> <a id="1335" href="MLTT.Sigma.html#423" class="Function">꞉</a> <a id="1337" class="Symbol">(</a><a id="1338" href="UF.FunExt-Properties.html#1001" class="Bound">X</a> <a id="1340" class="Symbol">→</a> <a id="1342" href="MLTT.Sigma-Type.html#116" class="Record">Σ</a> <a id="1344" href="UF.FunExt-Properties.html#1003" class="Bound">A</a><a id="1345" class="Symbol">)</a> <a id="1347" href="MLTT.Sigma.html#423" class="Function">,</a> <a id="1349" href="UF.FunExt-Properties.html#1024" class="Function">f</a> <a id="1351" href="MLTT.Pi.html#513" class="Function Operator">∘</a> <a id="1353" href="UF.FunExt-Properties.html#1333" class="Bound">h</a> <a id="1355" href="MLTT.Id.html#193" class="Datatype Operator">＝</a> <a id="1357" href="MLTT.Pi.html#444" class="Function">id</a><a id="1359" class="Symbol">)</a> <a id="1361" class="Symbol">→</a> <a id="1363" href="MLTT.Pi.html#164" class="Function">Π</a> <a id="1365" href="UF.FunExt-Properties.html#1003" class="Bound">A</a>
    <a id="1371" href="UF.FunExt-Properties.html#1326" class="Function">r</a> <a id="1373" class="Symbol">(</a><a id="1374" href="UF.FunExt-Properties.html#1374" class="Bound">h</a> <a id="1376" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="1378" href="UF.FunExt-Properties.html#1378" class="Bound">p</a><a id="1379" class="Symbol">)</a> <a id="1381" href="UF.FunExt-Properties.html#1381" class="Bound">x</a> <a id="1383" class="Symbol">=</a> <a id="1385" href="MLTT.Id.html#735" class="Function">transport</a> <a id="1395" href="UF.FunExt-Properties.html#1003" class="Bound">A</a> <a id="1397" class="Symbol">(</a><a id="1398" href="UF.Base.html#10654" class="Function">happly</a> <a id="1405" href="UF.FunExt-Properties.html#1378" class="Bound">p</a> <a id="1407" href="UF.FunExt-Properties.html#1381" class="Bound">x</a><a id="1408" class="Symbol">)</a> <a id="1410" class="Symbol">(</a><a id="1411" href="MLTT.Sigma-Type.html#209" class="Field">pr₂</a> <a id="1415" class="Symbol">(</a><a id="1416" href="UF.FunExt-Properties.html#1374" class="Bound">h</a> <a id="1418" href="UF.FunExt-Properties.html#1381" class="Bound">x</a><a id="1419" class="Symbol">))</a>

    <a id="1427" href="UF.FunExt-Properties.html#1427" class="Function">s</a> <a id="1429" class="Symbol">:</a> <a id="1431" href="MLTT.Pi.html#164" class="Function">Π</a> <a id="1433" href="UF.FunExt-Properties.html#1003" class="Bound">A</a> <a id="1435" class="Symbol">→</a> <a id="1437" class="Symbol">(</a><a id="1438" href="MLTT.Sigma.html#423" class="Function">Σ</a> <a id="1440" href="UF.FunExt-Properties.html#1440" class="Bound">h</a> <a id="1442" href="MLTT.Sigma.html#423" class="Function">꞉</a> <a id="1444" class="Symbol">(</a><a id="1445" href="UF.FunExt-Properties.html#1001" class="Bound">X</a> <a id="1447" class="Symbol">→</a> <a id="1449" href="MLTT.Sigma-Type.html#116" class="Record">Σ</a> <a id="1451" href="UF.FunExt-Properties.html#1003" class="Bound">A</a><a id="1452" class="Symbol">)</a> <a id="1454" href="MLTT.Sigma.html#423" class="Function">,</a> <a id="1456" href="UF.FunExt-Properties.html#1024" class="Function">f</a> <a id="1458" href="MLTT.Pi.html#513" class="Function Operator">∘</a> <a id="1460" href="UF.FunExt-Properties.html#1440" class="Bound">h</a> <a id="1462" href="MLTT.Id.html#193" class="Datatype Operator">＝</a> <a id="1464" href="MLTT.Pi.html#444" class="Function">id</a><a id="1466" class="Symbol">)</a>
    <a id="1472" href="UF.FunExt-Properties.html#1427" class="Function">s</a> <a id="1474" href="UF.FunExt-Properties.html#1474" class="Bound">φ</a> <a id="1476" class="Symbol">=</a> <a id="1478" class="Symbol">(λ</a> <a id="1481" href="UF.FunExt-Properties.html#1481" class="Bound">x</a> <a id="1483" class="Symbol">→</a> <a id="1485" href="UF.FunExt-Properties.html#1481" class="Bound">x</a> <a id="1487" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="1489" href="UF.FunExt-Properties.html#1474" class="Bound">φ</a> <a id="1491" href="UF.FunExt-Properties.html#1481" class="Bound">x</a><a id="1492" class="Symbol">)</a> <a id="1494" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="1496" href="MLTT.Identity-Type.html#157" class="InductiveConstructor">refl</a>

    <a id="1506" href="UF.FunExt-Properties.html#1506" class="Function">rs</a> <a id="1509" class="Symbol">:</a> <a id="1511" class="Symbol">∀</a> <a id="1513" href="UF.FunExt-Properties.html#1513" class="Bound">φ</a> <a id="1515" class="Symbol">→</a> <a id="1517" href="UF.FunExt-Properties.html#1326" class="Function">r</a> <a id="1519" class="Symbol">(</a><a id="1520" href="UF.FunExt-Properties.html#1427" class="Function">s</a> <a id="1522" href="UF.FunExt-Properties.html#1513" class="Bound">φ</a><a id="1523" class="Symbol">)</a> <a id="1525" href="MLTT.Id.html#193" class="Datatype Operator">＝</a> <a id="1527" href="UF.FunExt-Properties.html#1513" class="Bound">φ</a>
    <a id="1533" href="UF.FunExt-Properties.html#1506" class="Function">rs</a> <a id="1536" href="UF.FunExt-Properties.html#1536" class="Bound">φ</a> <a id="1538" class="Symbol">=</a> <a id="1540" href="MLTT.Identity-Type.html#157" class="InductiveConstructor">refl</a>

    <a id="1550" href="UF.FunExt-Properties.html#1550" class="Function">δ</a> <a id="1552" class="Symbol">:</a> <a id="1554" href="UF.Subsingletons.html#1565" class="Function">is-singleton</a> <a id="1567" class="Symbol">(</a><a id="1568" href="MLTT.Pi.html#164" class="Function">Π</a> <a id="1570" href="UF.FunExt-Properties.html#1003" class="Bound">A</a><a id="1571" class="Symbol">)</a>
    <a id="1577" href="UF.FunExt-Properties.html#1550" class="Function">δ</a> <a id="1579" class="Symbol">=</a> <a id="1581" href="UF.Retracts.html#2463" class="Function">retract-of-singleton</a> <a id="1602" class="Symbol">(</a><a id="1603" href="UF.FunExt-Properties.html#1326" class="Function">r</a> <a id="1605" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="1607" href="UF.FunExt-Properties.html#1427" class="Function">s</a> <a id="1609" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="1611" href="UF.FunExt-Properties.html#1506" class="Function">rs</a><a id="1613" class="Symbol">)</a> <a id="1615" href="UF.FunExt-Properties.html#1242" class="Function">e</a>

<a id="naive-funext-gives-funext"></a><a id="1618" href="UF.FunExt-Properties.html#1618" class="Function">naive-funext-gives-funext</a> <a id="1644" class="Symbol">:</a> <a id="1646" href="UF.FunExt.html#640" class="Function">naive-funext</a> <a id="1659" href="MLTT.Universes.html#3834" class="Generalizable">𝓤</a> <a id="1661" href="MLTT.Universes.html#3834" class="Generalizable">𝓤</a> <a id="1663" class="Symbol">→</a> <a id="1665" href="UF.FunExt.html#850" class="Function">funext</a> <a id="1672" href="MLTT.Universes.html#3834" class="Generalizable">𝓤</a> <a id="1674" href="MLTT.Universes.html#3834" class="Generalizable">𝓤</a>
<a id="1676" href="UF.FunExt-Properties.html#1618" class="Function">naive-funext-gives-funext</a> <a id="1702" href="UF.FunExt-Properties.html#1702" class="Bound">fe</a> <a id="1705" class="Symbol">=</a> <a id="1707" href="UF.FunExt-Properties.html#742" class="Function">naive-funext-gives-funext&#39;</a> <a id="1734" href="UF.FunExt-Properties.html#1702" class="Bound">fe</a> <a id="1737" href="UF.FunExt-Properties.html#1702" class="Bound">fe</a>

<a id="naive-funext-gives-funext₀"></a><a id="1741" href="UF.FunExt-Properties.html#1741" class="Function">naive-funext-gives-funext₀</a> <a id="1768" class="Symbol">:</a> <a id="1770" href="UF.FunExt.html#640" class="Function">naive-funext</a> <a id="1783" href="MLTT.Universes.html#3834" class="Generalizable">𝓤</a> <a id="1785" href="MLTT.Universes.html#3834" class="Generalizable">𝓤</a> <a id="1787" class="Symbol">→</a> <a id="1789" href="UF.FunExt.html#850" class="Function">funext</a> <a id="1796" href="MLTT.Universes.html#3834" class="Generalizable">𝓤</a> <a id="1798" href="Agda.Primitive.html#915" class="Primitive">𝓤₀</a>
<a id="1801" href="UF.FunExt-Properties.html#1741" class="Function">naive-funext-gives-funext₀</a> <a id="1828" href="UF.FunExt-Properties.html#1828" class="Bound">fe</a> <a id="1831" class="Symbol">=</a> <a id="1833" href="UF.FunExt-Properties.html#742" class="Function">naive-funext-gives-funext&#39;</a> <a id="1860" href="UF.FunExt-Properties.html#1828" class="Bound">fe</a> <a id="1863" href="UF.FunExt-Properties.html#1828" class="Bound">fe</a>

<a id="1867" class="Markup">\end{code}</a><a id="1877" class="Background">
</a></pre></body></html>