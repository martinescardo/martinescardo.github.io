<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>UF.FunExt-Properties</title><link rel="stylesheet" href="Agda.css"><script type="text/javascript" src="highlight-hover.js"></script></head><body><pre class="Agda"><a id="1" class="Background">Martin Escardo, 19th May 2018.

Properties of function extensionality.

</a><a id="73" class="Markup">\begin{code}</a>

<a id="87" class="Symbol">{-#</a> <a id="91" class="Keyword">OPTIONS</a> <a id="99" class="Pragma">--safe</a> <a id="106" class="Pragma">--without-K</a> <a id="118" class="Symbol">#-}</a>

<a id="123" class="Keyword">module</a> <a id="130" href="UF.FunExt-Properties.html" class="Module">UF.FunExt-Properties</a> <a id="151" class="Keyword">where</a>

<a id="158" class="Keyword">open</a> <a id="163" class="Keyword">import</a> <a id="170" href="MLTT.Spartan.html" class="Module">MLTT.Spartan</a>
<a id="183" class="Keyword">open</a> <a id="188" class="Keyword">import</a> <a id="195" href="UF.Base.html" class="Module">UF.Base</a>
<a id="203" class="Keyword">open</a> <a id="208" class="Keyword">import</a> <a id="215" href="UF.FunExt.html" class="Module">UF.FunExt</a>
<a id="225" class="Keyword">open</a> <a id="230" class="Keyword">import</a> <a id="237" href="UF.Equiv.html" class="Module">UF.Equiv</a>
<a id="246" class="Keyword">open</a> <a id="251" class="Keyword">import</a> <a id="258" href="UF.Equiv-FunExt.html" class="Module">UF.Equiv-FunExt</a>
<a id="274" class="Keyword">open</a> <a id="279" class="Keyword">import</a> <a id="286" href="UF.Yoneda.html" class="Module">UF.Yoneda</a>
<a id="296" class="Keyword">open</a> <a id="301" class="Keyword">import</a> <a id="308" href="UF.Singleton-Properties.html" class="Module">UF.Singleton-Properties</a>
<a id="332" class="Keyword">open</a> <a id="337" class="Keyword">import</a> <a id="344" href="UF.Subsingletons.html" class="Module">UF.Subsingletons</a>
<a id="361" class="Keyword">open</a> <a id="366" class="Keyword">import</a> <a id="373" href="UF.Retracts.html" class="Module">UF.Retracts</a>

<a id="386" class="Markup">\end{code}</a><a id="396" class="Background">

Vladimir Voevodsky proved in Coq that naive function extensionality
(any two pointwise equal non-dependent functions are equal) implies
function extensionality (happly is an equivalence, for dependent
functions):

  https://github.com/vladimirias/Foundations/blob/master/Generalities/uu0.v

Here is an Agda version, with explicit indication of the universe levels.

</a><a id="764" class="Markup">\begin{code}</a>

<a id="naive-funext-gives-funext&#39;"></a><a id="778" href="UF.FunExt-Properties.html#778" class="Function">naive-funext-gives-funext&#39;</a> <a id="805" class="Symbol">:</a> <a id="807" href="UF.FunExt.html#640" class="Function">naive-funext</a> <a id="820" href="MLTT.Universes.html#3834" class="Generalizable">𝓤</a> <a id="822" class="Symbol">(</a><a id="823" href="MLTT.Universes.html#3834" class="Generalizable">𝓤</a> <a id="825" href="Agda.Primitive.html#961" class="Primitive Operator">⊔</a> <a id="827" href="MLTT.Universes.html#3836" class="Generalizable">𝓥</a><a id="828" class="Symbol">)</a> <a id="830" class="Symbol">→</a> <a id="832" href="UF.FunExt.html#640" class="Function">naive-funext</a> <a id="845" href="MLTT.Universes.html#3834" class="Generalizable">𝓤</a> <a id="847" href="MLTT.Universes.html#3834" class="Generalizable">𝓤</a> <a id="849" class="Symbol">→</a> <a id="851" href="UF.FunExt.html#850" class="Function">funext</a> <a id="858" href="MLTT.Universes.html#3834" class="Generalizable">𝓤</a> <a id="860" href="MLTT.Universes.html#3836" class="Generalizable">𝓥</a>
<a id="862" href="UF.FunExt-Properties.html#778" class="Function">naive-funext-gives-funext&#39;</a> <a id="889" class="Symbol">{</a><a id="890" href="UF.FunExt-Properties.html#890" class="Bound">𝓤</a><a id="891" class="Symbol">}</a> <a id="893" class="Symbol">{</a><a id="894" href="UF.FunExt-Properties.html#894" class="Bound">𝓥</a><a id="895" class="Symbol">}</a> <a id="897" href="UF.FunExt-Properties.html#897" class="Bound">nfe</a> <a id="901" href="UF.FunExt-Properties.html#901" class="Bound">nfe&#39;</a> <a id="906" class="Symbol">=</a> <a id="908" href="UF.Yoneda.html#17253" class="Function">funext-via-singletons</a> <a id="930" href="UF.FunExt-Properties.html#941" class="Function">γ</a>
 <a id="933" class="Keyword">where</a>
  <a id="941" href="UF.FunExt-Properties.html#941" class="Function">γ</a> <a id="943" class="Symbol">:</a> <a id="945" class="Symbol">(</a><a id="946" href="UF.FunExt-Properties.html#946" class="Bound">X</a> <a id="948" class="Symbol">:</a> <a id="950" href="UF.FunExt-Properties.html#890" class="Bound">𝓤</a> <a id="952" href="MLTT.Universes.html#3977" class="Function Operator">̇</a> <a id="954" class="Symbol">)</a> <a id="956" class="Symbol">(</a><a id="957" href="UF.FunExt-Properties.html#957" class="Bound">A</a> <a id="959" class="Symbol">:</a> <a id="961" href="UF.FunExt-Properties.html#946" class="Bound">X</a> <a id="963" class="Symbol">→</a> <a id="965" href="UF.FunExt-Properties.html#894" class="Bound">𝓥</a> <a id="967" href="MLTT.Universes.html#3977" class="Function Operator">̇</a> <a id="969" class="Symbol">)</a>
    <a id="975" class="Symbol">→</a> <a id="977" class="Symbol">((</a><a id="979" href="UF.FunExt-Properties.html#979" class="Bound">x</a> <a id="981" class="Symbol">:</a> <a id="983" href="UF.FunExt-Properties.html#946" class="Bound">X</a><a id="984" class="Symbol">)</a> <a id="986" class="Symbol">→</a> <a id="988" href="UF.Subsingletons.html#1565" class="Function">is-singleton</a> <a id="1001" class="Symbol">(</a><a id="1002" href="UF.FunExt-Properties.html#957" class="Bound">A</a> <a id="1004" href="UF.FunExt-Properties.html#979" class="Bound">x</a><a id="1005" class="Symbol">))</a>
    <a id="1012" class="Symbol">→</a> <a id="1014" href="UF.Subsingletons.html#1565" class="Function">is-singleton</a> <a id="1027" class="Symbol">(</a><a id="1028" href="MLTT.Pi.html#164" class="Function">Π</a> <a id="1030" href="UF.FunExt-Properties.html#957" class="Bound">A</a><a id="1031" class="Symbol">)</a>
  <a id="1035" href="UF.FunExt-Properties.html#941" class="Function">γ</a> <a id="1037" href="UF.FunExt-Properties.html#1037" class="Bound">X</a> <a id="1039" href="UF.FunExt-Properties.html#1039" class="Bound">A</a> <a id="1041" href="UF.FunExt-Properties.html#1041" class="Bound">φ</a> <a id="1043" class="Symbol">=</a> <a id="1045" href="UF.FunExt-Properties.html#1586" class="Function">δ</a>
   <a id="1050" class="Keyword">where</a>
    <a id="1060" href="UF.FunExt-Properties.html#1060" class="Function">f</a> <a id="1062" class="Symbol">:</a> <a id="1064" href="MLTT.Sigma-Type.html#116" class="Record">Σ</a> <a id="1066" href="UF.FunExt-Properties.html#1039" class="Bound">A</a> <a id="1068" class="Symbol">→</a> <a id="1070" href="UF.FunExt-Properties.html#1037" class="Bound">X</a>
    <a id="1076" href="UF.FunExt-Properties.html#1060" class="Function">f</a> <a id="1078" class="Symbol">=</a> <a id="1080" href="MLTT.Sigma-Type.html#198" class="Field">pr₁</a>

    <a id="1089" href="UF.FunExt-Properties.html#1089" class="Function">f-is-equiv</a> <a id="1100" class="Symbol">:</a> <a id="1102" href="UF.Equiv.html#402" class="Function">is-equiv</a> <a id="1111" href="UF.FunExt-Properties.html#1060" class="Function">f</a>
    <a id="1117" href="UF.FunExt-Properties.html#1089" class="Function">f-is-equiv</a> <a id="1128" class="Symbol">=</a> <a id="1130" href="UF.Equiv.html#20735" class="Function">pr₁-is-equiv</a> <a id="1143" href="UF.FunExt-Properties.html#1037" class="Bound">X</a> <a id="1145" href="UF.FunExt-Properties.html#1039" class="Bound">A</a> <a id="1147" href="UF.FunExt-Properties.html#1041" class="Bound">φ</a>

    <a id="1154" href="UF.FunExt-Properties.html#1154" class="Function">g</a> <a id="1156" class="Symbol">:</a> <a id="1158" class="Symbol">(</a><a id="1159" href="UF.FunExt-Properties.html#1037" class="Bound">X</a> <a id="1161" class="Symbol">→</a> <a id="1163" href="MLTT.Sigma-Type.html#116" class="Record">Σ</a> <a id="1165" href="UF.FunExt-Properties.html#1039" class="Bound">A</a><a id="1166" class="Symbol">)</a> <a id="1168" class="Symbol">→</a> <a id="1170" class="Symbol">(</a><a id="1171" href="UF.FunExt-Properties.html#1037" class="Bound">X</a> <a id="1173" class="Symbol">→</a> <a id="1175" href="UF.FunExt-Properties.html#1037" class="Bound">X</a><a id="1176" class="Symbol">)</a>
    <a id="1182" href="UF.FunExt-Properties.html#1154" class="Function">g</a> <a id="1184" href="UF.FunExt-Properties.html#1184" class="Bound">h</a> <a id="1186" class="Symbol">=</a> <a id="1188" href="UF.FunExt-Properties.html#1060" class="Function">f</a> <a id="1190" href="MLTT.Pi.html#513" class="Function Operator">∘</a> <a id="1192" href="UF.FunExt-Properties.html#1184" class="Bound">h</a>

    <a id="1199" href="UF.FunExt-Properties.html#1199" class="Function">g-is-equiv</a> <a id="1210" class="Symbol">:</a> <a id="1212" href="UF.Equiv.html#402" class="Function">is-equiv</a> <a id="1221" href="UF.FunExt-Properties.html#1154" class="Function">g</a>
    <a id="1227" href="UF.FunExt-Properties.html#1199" class="Function">g-is-equiv</a> <a id="1238" class="Symbol">=</a> <a id="1240" href="UF.Equiv-FunExt.html#1862" class="Function">equiv-post</a> <a id="1251" href="UF.FunExt-Properties.html#897" class="Bound">nfe</a> <a id="1255" href="UF.FunExt-Properties.html#901" class="Bound">nfe&#39;</a> <a id="1260" href="UF.FunExt-Properties.html#1060" class="Function">f</a> <a id="1262" href="UF.FunExt-Properties.html#1089" class="Function">f-is-equiv</a>

    <a id="1278" href="UF.FunExt-Properties.html#1278" class="Function">e</a> <a id="1280" class="Symbol">:</a> <a id="1282" href="UF.Subsingletons.html#8927" class="Function">∃!</a> <a id="1285" href="UF.FunExt-Properties.html#1285" class="Bound">h</a> <a id="1287" href="UF.Subsingletons.html#8927" class="Function">꞉</a> <a id="1289" class="Symbol">(</a><a id="1290" href="UF.FunExt-Properties.html#1037" class="Bound">X</a> <a id="1292" class="Symbol">→</a> <a id="1294" href="MLTT.Sigma-Type.html#116" class="Record">Σ</a> <a id="1296" href="UF.FunExt-Properties.html#1039" class="Bound">A</a><a id="1297" class="Symbol">)</a> <a id="1299" href="UF.Subsingletons.html#8927" class="Function">,</a> <a id="1301" href="UF.FunExt-Properties.html#1060" class="Function">f</a> <a id="1303" href="MLTT.Pi.html#513" class="Function Operator">∘</a> <a id="1305" href="UF.FunExt-Properties.html#1285" class="Bound">h</a> <a id="1307" href="MLTT.Id.html#193" class="Datatype Operator">＝</a> <a id="1309" href="MLTT.Pi.html#444" class="Function">id</a>
    <a id="1316" href="UF.FunExt-Properties.html#1278" class="Function">e</a> <a id="1318" class="Symbol">=</a> <a id="1320" href="UF.Equiv.html#17767" class="Function">equivs-are-vv-equivs</a> <a id="1341" href="UF.FunExt-Properties.html#1154" class="Function">g</a> <a id="1343" href="UF.FunExt-Properties.html#1199" class="Function">g-is-equiv</a> <a id="1354" href="MLTT.Pi.html#444" class="Function">id</a>

    <a id="1362" href="UF.FunExt-Properties.html#1362" class="Function">r</a> <a id="1364" class="Symbol">:</a> <a id="1366" class="Symbol">(</a><a id="1367" href="MLTT.Sigma.html#423" class="Function">Σ</a> <a id="1369" href="UF.FunExt-Properties.html#1369" class="Bound">h</a> <a id="1371" href="MLTT.Sigma.html#423" class="Function">꞉</a> <a id="1373" class="Symbol">(</a><a id="1374" href="UF.FunExt-Properties.html#1037" class="Bound">X</a> <a id="1376" class="Symbol">→</a> <a id="1378" href="MLTT.Sigma-Type.html#116" class="Record">Σ</a> <a id="1380" href="UF.FunExt-Properties.html#1039" class="Bound">A</a><a id="1381" class="Symbol">)</a> <a id="1383" href="MLTT.Sigma.html#423" class="Function">,</a> <a id="1385" href="UF.FunExt-Properties.html#1060" class="Function">f</a> <a id="1387" href="MLTT.Pi.html#513" class="Function Operator">∘</a> <a id="1389" href="UF.FunExt-Properties.html#1369" class="Bound">h</a> <a id="1391" href="MLTT.Id.html#193" class="Datatype Operator">＝</a> <a id="1393" href="MLTT.Pi.html#444" class="Function">id</a><a id="1395" class="Symbol">)</a> <a id="1397" class="Symbol">→</a> <a id="1399" href="MLTT.Pi.html#164" class="Function">Π</a> <a id="1401" href="UF.FunExt-Properties.html#1039" class="Bound">A</a>
    <a id="1407" href="UF.FunExt-Properties.html#1362" class="Function">r</a> <a id="1409" class="Symbol">(</a><a id="1410" href="UF.FunExt-Properties.html#1410" class="Bound">h</a> <a id="1412" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="1414" href="UF.FunExt-Properties.html#1414" class="Bound">p</a><a id="1415" class="Symbol">)</a> <a id="1417" href="UF.FunExt-Properties.html#1417" class="Bound">x</a> <a id="1419" class="Symbol">=</a> <a id="1421" href="MLTT.Id.html#735" class="Function">transport</a> <a id="1431" href="UF.FunExt-Properties.html#1039" class="Bound">A</a> <a id="1433" class="Symbol">(</a><a id="1434" href="UF.Base.html#11014" class="Function">happly</a> <a id="1441" href="UF.FunExt-Properties.html#1414" class="Bound">p</a> <a id="1443" href="UF.FunExt-Properties.html#1417" class="Bound">x</a><a id="1444" class="Symbol">)</a> <a id="1446" class="Symbol">(</a><a id="1447" href="MLTT.Sigma-Type.html#209" class="Field">pr₂</a> <a id="1451" class="Symbol">(</a><a id="1452" href="UF.FunExt-Properties.html#1410" class="Bound">h</a> <a id="1454" href="UF.FunExt-Properties.html#1417" class="Bound">x</a><a id="1455" class="Symbol">))</a>

    <a id="1463" href="UF.FunExt-Properties.html#1463" class="Function">s</a> <a id="1465" class="Symbol">:</a> <a id="1467" href="MLTT.Pi.html#164" class="Function">Π</a> <a id="1469" href="UF.FunExt-Properties.html#1039" class="Bound">A</a> <a id="1471" class="Symbol">→</a> <a id="1473" class="Symbol">(</a><a id="1474" href="MLTT.Sigma.html#423" class="Function">Σ</a> <a id="1476" href="UF.FunExt-Properties.html#1476" class="Bound">h</a> <a id="1478" href="MLTT.Sigma.html#423" class="Function">꞉</a> <a id="1480" class="Symbol">(</a><a id="1481" href="UF.FunExt-Properties.html#1037" class="Bound">X</a> <a id="1483" class="Symbol">→</a> <a id="1485" href="MLTT.Sigma-Type.html#116" class="Record">Σ</a> <a id="1487" href="UF.FunExt-Properties.html#1039" class="Bound">A</a><a id="1488" class="Symbol">)</a> <a id="1490" href="MLTT.Sigma.html#423" class="Function">,</a> <a id="1492" href="UF.FunExt-Properties.html#1060" class="Function">f</a> <a id="1494" href="MLTT.Pi.html#513" class="Function Operator">∘</a> <a id="1496" href="UF.FunExt-Properties.html#1476" class="Bound">h</a> <a id="1498" href="MLTT.Id.html#193" class="Datatype Operator">＝</a> <a id="1500" href="MLTT.Pi.html#444" class="Function">id</a><a id="1502" class="Symbol">)</a>
    <a id="1508" href="UF.FunExt-Properties.html#1463" class="Function">s</a> <a id="1510" href="UF.FunExt-Properties.html#1510" class="Bound">φ</a> <a id="1512" class="Symbol">=</a> <a id="1514" class="Symbol">(λ</a> <a id="1517" href="UF.FunExt-Properties.html#1517" class="Bound">x</a> <a id="1519" class="Symbol">→</a> <a id="1521" href="UF.FunExt-Properties.html#1517" class="Bound">x</a> <a id="1523" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="1525" href="UF.FunExt-Properties.html#1510" class="Bound">φ</a> <a id="1527" href="UF.FunExt-Properties.html#1517" class="Bound">x</a><a id="1528" class="Symbol">)</a> <a id="1530" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="1532" href="MLTT.Identity-Type.html#157" class="InductiveConstructor">refl</a>

    <a id="1542" href="UF.FunExt-Properties.html#1542" class="Function">rs</a> <a id="1545" class="Symbol">:</a> <a id="1547" class="Symbol">∀</a> <a id="1549" href="UF.FunExt-Properties.html#1549" class="Bound">φ</a> <a id="1551" class="Symbol">→</a> <a id="1553" href="UF.FunExt-Properties.html#1362" class="Function">r</a> <a id="1555" class="Symbol">(</a><a id="1556" href="UF.FunExt-Properties.html#1463" class="Function">s</a> <a id="1558" href="UF.FunExt-Properties.html#1549" class="Bound">φ</a><a id="1559" class="Symbol">)</a> <a id="1561" href="MLTT.Id.html#193" class="Datatype Operator">＝</a> <a id="1563" href="UF.FunExt-Properties.html#1549" class="Bound">φ</a>
    <a id="1569" href="UF.FunExt-Properties.html#1542" class="Function">rs</a> <a id="1572" href="UF.FunExt-Properties.html#1572" class="Bound">φ</a> <a id="1574" class="Symbol">=</a> <a id="1576" href="MLTT.Identity-Type.html#157" class="InductiveConstructor">refl</a>

    <a id="1586" href="UF.FunExt-Properties.html#1586" class="Function">δ</a> <a id="1588" class="Symbol">:</a> <a id="1590" href="UF.Subsingletons.html#1565" class="Function">is-singleton</a> <a id="1603" class="Symbol">(</a><a id="1604" href="MLTT.Pi.html#164" class="Function">Π</a> <a id="1606" href="UF.FunExt-Properties.html#1039" class="Bound">A</a><a id="1607" class="Symbol">)</a>
    <a id="1613" href="UF.FunExt-Properties.html#1586" class="Function">δ</a> <a id="1615" class="Symbol">=</a> <a id="1617" href="UF.Retracts.html#2463" class="Function">retract-of-singleton</a> <a id="1638" class="Symbol">(</a><a id="1639" href="UF.FunExt-Properties.html#1362" class="Function">r</a> <a id="1641" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="1643" href="UF.FunExt-Properties.html#1463" class="Function">s</a> <a id="1645" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="1647" href="UF.FunExt-Properties.html#1542" class="Function">rs</a><a id="1649" class="Symbol">)</a> <a id="1651" href="UF.FunExt-Properties.html#1278" class="Function">e</a>

<a id="naive-funext-gives-funext"></a><a id="1654" href="UF.FunExt-Properties.html#1654" class="Function">naive-funext-gives-funext</a> <a id="1680" class="Symbol">:</a> <a id="1682" href="UF.FunExt.html#640" class="Function">naive-funext</a> <a id="1695" href="MLTT.Universes.html#3834" class="Generalizable">𝓤</a> <a id="1697" href="MLTT.Universes.html#3834" class="Generalizable">𝓤</a> <a id="1699" class="Symbol">→</a> <a id="1701" href="UF.FunExt.html#850" class="Function">funext</a> <a id="1708" href="MLTT.Universes.html#3834" class="Generalizable">𝓤</a> <a id="1710" href="MLTT.Universes.html#3834" class="Generalizable">𝓤</a>
<a id="1712" href="UF.FunExt-Properties.html#1654" class="Function">naive-funext-gives-funext</a> <a id="1738" href="UF.FunExt-Properties.html#1738" class="Bound">fe</a> <a id="1741" class="Symbol">=</a> <a id="1743" href="UF.FunExt-Properties.html#778" class="Function">naive-funext-gives-funext&#39;</a> <a id="1770" href="UF.FunExt-Properties.html#1738" class="Bound">fe</a> <a id="1773" href="UF.FunExt-Properties.html#1738" class="Bound">fe</a>

<a id="naive-funext-gives-funext₀"></a><a id="1777" href="UF.FunExt-Properties.html#1777" class="Function">naive-funext-gives-funext₀</a> <a id="1804" class="Symbol">:</a> <a id="1806" href="UF.FunExt.html#640" class="Function">naive-funext</a> <a id="1819" href="MLTT.Universes.html#3834" class="Generalizable">𝓤</a> <a id="1821" href="MLTT.Universes.html#3834" class="Generalizable">𝓤</a> <a id="1823" class="Symbol">→</a> <a id="1825" href="UF.FunExt.html#850" class="Function">funext</a> <a id="1832" href="MLTT.Universes.html#3834" class="Generalizable">𝓤</a> <a id="1834" href="Agda.Primitive.html#915" class="Primitive">𝓤₀</a>
<a id="1837" href="UF.FunExt-Properties.html#1777" class="Function">naive-funext-gives-funext₀</a> <a id="1864" href="UF.FunExt-Properties.html#1864" class="Bound">fe</a> <a id="1867" class="Symbol">=</a> <a id="1869" href="UF.FunExt-Properties.html#778" class="Function">naive-funext-gives-funext&#39;</a> <a id="1896" href="UF.FunExt-Properties.html#1864" class="Bound">fe</a> <a id="1899" href="UF.FunExt-Properties.html#1864" class="Bound">fe</a>

<a id="1903" class="Markup">\end{code}</a><a id="1913" class="Background">

Added by Evan Cavallo on 13th March 2025.

The equivalence extensionality axiom is the restriction of function
extensionality to equivalences. By an argument similar to the proof of function
extensionality from univalence, it implies full function extensionality.

</a><a id="2180" class="Markup">\begin{code}</a>

<a id="equivext"></a><a id="2194" href="UF.FunExt-Properties.html#2194" class="Function">equivext</a> <a id="2203" class="Symbol">:</a> <a id="2205" class="Symbol">∀</a> <a id="2207" href="UF.FunExt-Properties.html#2207" class="Bound">𝓤</a> <a id="2209" href="UF.FunExt-Properties.html#2209" class="Bound">𝓥</a> <a id="2211" class="Symbol">→</a> <a id="2213" href="UF.FunExt-Properties.html#2207" class="Bound">𝓤</a> <a id="2215" href="Agda.Primitive.html#931" class="Primitive Operator">⁺</a> <a id="2217" href="Agda.Primitive.html#961" class="Primitive Operator">⊔</a> <a id="2219" href="UF.FunExt-Properties.html#2209" class="Bound">𝓥</a> <a id="2221" href="Agda.Primitive.html#931" class="Primitive Operator">⁺</a> <a id="2223" href="MLTT.Universes.html#3977" class="Function Operator">̇</a>
<a id="2225" href="UF.FunExt-Properties.html#2194" class="Function">equivext</a> <a id="2234" href="UF.FunExt-Properties.html#2234" class="Bound">𝓤</a> <a id="2236" href="UF.FunExt-Properties.html#2236" class="Bound">𝓥</a> <a id="2238" class="Symbol">=</a>
 <a id="2241" class="Symbol">{</a><a id="2242" href="UF.FunExt-Properties.html#2242" class="Bound">X</a> <a id="2244" class="Symbol">:</a> <a id="2246" href="UF.FunExt-Properties.html#2234" class="Bound">𝓤</a> <a id="2248" href="MLTT.Universes.html#3977" class="Function Operator">̇</a> <a id="2250" class="Symbol">}</a> <a id="2252" class="Symbol">{</a><a id="2253" href="UF.FunExt-Properties.html#2253" class="Bound">Y</a> <a id="2255" class="Symbol">:</a> <a id="2257" href="UF.FunExt-Properties.html#2236" class="Bound">𝓥</a> <a id="2259" href="MLTT.Universes.html#3977" class="Function Operator">̇</a> <a id="2261" class="Symbol">}</a> <a id="2263" class="Symbol">(</a><a id="2264" href="UF.FunExt-Properties.html#2264" class="Bound">α</a> <a id="2266" href="UF.FunExt-Properties.html#2266" class="Bound">β</a> <a id="2268" class="Symbol">:</a> <a id="2270" href="UF.FunExt-Properties.html#2242" class="Bound">X</a> <a id="2272" href="UF.Equiv.html#1318" class="Function Operator">≃</a> <a id="2274" href="UF.FunExt-Properties.html#2253" class="Bound">Y</a><a id="2275" class="Symbol">)</a>
 <a id="2278" class="Symbol">→</a> <a id="2280" href="UF.Equiv.html#402" class="Function">is-equiv</a> <a id="2289" class="Symbol">(λ</a> <a id="2292" class="Symbol">(</a><a id="2293" href="UF.FunExt-Properties.html#2293" class="Bound">p</a> <a id="2295" class="Symbol">:</a> <a id="2297" href="UF.FunExt-Properties.html#2264" class="Bound">α</a> <a id="2299" href="MLTT.Id.html#193" class="Datatype Operator">＝</a> <a id="2301" href="UF.FunExt-Properties.html#2266" class="Bound">β</a><a id="2302" class="Symbol">)</a> <a id="2304" class="Symbol">→</a> <a id="2306" href="UF.Base.html#11014" class="Function">happly</a> <a id="2313" class="Symbol">(</a><a id="2314" href="MLTT.Id.html#1136" class="Function">ap</a> <a id="2317" href="UF.Equiv.html#2933" class="Function Operator">⌜_⌝</a> <a id="2321" href="UF.FunExt-Properties.html#2293" class="Bound">p</a><a id="2322" class="Symbol">))</a>

<a id="equivext-gives-funext"></a><a id="2326" href="UF.FunExt-Properties.html#2326" class="Function">equivext-gives-funext</a> <a id="2348" class="Symbol">:</a> <a id="2350" href="UF.FunExt-Properties.html#2194" class="Function">equivext</a> <a id="2359" href="MLTT.Universes.html#3834" class="Generalizable">𝓤</a> <a id="2361" href="MLTT.Universes.html#3834" class="Generalizable">𝓤</a> <a id="2363" class="Symbol">→</a> <a id="2365" href="UF.FunExt.html#850" class="Function">funext</a> <a id="2372" href="MLTT.Universes.html#3834" class="Generalizable">𝓤</a> <a id="2374" href="MLTT.Universes.html#3834" class="Generalizable">𝓤</a>
<a id="2376" href="UF.FunExt-Properties.html#2326" class="Function">equivext-gives-funext</a> <a id="2398" class="Symbol">{</a><a id="2399" href="UF.FunExt-Properties.html#2399" class="Bound">𝓤</a><a id="2400" class="Symbol">}</a> <a id="2402" href="UF.FunExt-Properties.html#2402" class="Bound">ee</a> <a id="2405" class="Symbol">=</a>
 <a id="2408" href="UF.Yoneda.html#17253" class="Function">funext-via-singletons</a> <a id="2430" href="UF.FunExt-Properties.html#3524" class="Function">main</a>
 <a id="2436" class="Keyword">where</a>
  <a id="2444" href="UF.FunExt-Properties.html#2444" class="Function">promote</a> <a id="2452" class="Symbol">:</a> <a id="2454" class="Symbol">(</a><a id="2455" href="UF.FunExt-Properties.html#2455" class="Bound">A</a> <a id="2457" class="Symbol">:</a> <a id="2459" href="UF.FunExt-Properties.html#2399" class="Bound">𝓤</a> <a id="2461" href="MLTT.Universes.html#3977" class="Function Operator">̇</a> <a id="2463" class="Symbol">)</a> <a id="2465" class="Symbol">{</a><a id="2466" href="UF.FunExt-Properties.html#2466" class="Bound">X</a> <a id="2468" href="UF.FunExt-Properties.html#2468" class="Bound">Y</a> <a id="2470" class="Symbol">:</a> <a id="2472" href="UF.FunExt-Properties.html#2399" class="Bound">𝓤</a> <a id="2474" href="MLTT.Universes.html#3977" class="Function Operator">̇</a> <a id="2476" class="Symbol">}</a> <a id="2478" class="Symbol">→</a> <a id="2480" href="UF.FunExt-Properties.html#2466" class="Bound">X</a> <a id="2482" href="UF.Equiv.html#1318" class="Function Operator">≃</a> <a id="2484" href="UF.FunExt-Properties.html#2468" class="Bound">Y</a> <a id="2486" class="Symbol">→</a> <a id="2488" class="Symbol">(</a><a id="2489" href="UF.FunExt-Properties.html#2455" class="Bound">A</a> <a id="2491" href="UF.Equiv.html#1318" class="Function Operator">≃</a> <a id="2493" href="UF.FunExt-Properties.html#2466" class="Bound">X</a><a id="2494" class="Symbol">)</a> <a id="2496" href="UF.Equiv.html#1318" class="Function Operator">≃</a> <a id="2498" class="Symbol">(</a><a id="2499" href="UF.FunExt-Properties.html#2455" class="Bound">A</a> <a id="2501" href="UF.Equiv.html#1318" class="Function Operator">≃</a> <a id="2503" href="UF.FunExt-Properties.html#2468" class="Bound">Y</a><a id="2504" class="Symbol">)</a>
  <a id="2508" href="UF.FunExt-Properties.html#2444" class="Function">promote</a> <a id="2516" href="UF.FunExt-Properties.html#2516" class="Bound">A</a> <a id="2518" href="UF.FunExt-Properties.html#2518" class="Bound">α</a> <a id="2520" class="Symbol">=</a>
   <a id="2525" href="UF.Equiv.html#7224" class="Function">qinveq</a>
    <a id="2536" class="Symbol">(</a><a id="2537" href="UF.Equiv.html#2640" class="Function Operator">_●</a> <a id="2540" href="UF.FunExt-Properties.html#2518" class="Bound">α</a><a id="2541" class="Symbol">)</a>
    <a id="2547" class="Symbol">(</a> <a id="2549" class="Symbol">(</a><a id="2550" href="UF.Equiv.html#2640" class="Function Operator">_●</a> <a id="2553" href="UF.Equiv.html#7797" class="Function">≃-sym</a> <a id="2559" href="UF.FunExt-Properties.html#2518" class="Bound">α</a><a id="2560" class="Symbol">)</a>
    <a id="2566" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="2568" class="Symbol">(λ</a> <a id="2571" href="UF.FunExt-Properties.html#2571" class="Bound">β</a> <a id="2573" class="Symbol">→</a> <a id="2575" href="UF.Equiv.html#498" class="Function">inverse</a> <a id="2583" class="Symbol">_</a> <a id="2585" class="Symbol">(</a><a id="2586" href="UF.FunExt-Properties.html#2402" class="Bound">ee</a> <a id="2589" class="Symbol">_</a> <a id="2591" class="Symbol">_)</a> <a id="2594" class="Symbol">(</a><a id="2595" href="UF.Equiv.html#4819" class="Function">inverses-are-retractions</a> <a id="2620" class="Symbol">_</a> <a id="2622" class="Symbol">(</a><a id="2623" href="MLTT.Sigma-Type.html#209" class="Field">pr₂</a> <a id="2627" href="UF.FunExt-Properties.html#2518" class="Bound">α</a><a id="2628" class="Symbol">)</a> <a id="2630" href="MLTT.Pi.html#513" class="Function Operator">∘</a> <a id="2632" href="UF.Equiv.html#2933" class="Function Operator">⌜</a> <a id="2634" href="UF.FunExt-Properties.html#2571" class="Bound">β</a> <a id="2636" href="UF.Equiv.html#2933" class="Function Operator">⌝</a><a id="2637" class="Symbol">))</a>
    <a id="2644" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="2646" class="Symbol">(λ</a> <a id="2649" href="UF.FunExt-Properties.html#2649" class="Bound">γ</a> <a id="2651" class="Symbol">→</a> <a id="2653" href="UF.Equiv.html#498" class="Function">inverse</a> <a id="2661" class="Symbol">_</a> <a id="2663" class="Symbol">(</a><a id="2664" href="UF.FunExt-Properties.html#2402" class="Bound">ee</a> <a id="2667" class="Symbol">_</a> <a id="2669" class="Symbol">_)</a> <a id="2672" class="Symbol">(</a><a id="2673" href="UF.Equiv.html#5785" class="Function">inverses-are-sections</a> <a id="2695" class="Symbol">_</a> <a id="2697" class="Symbol">(</a><a id="2698" href="MLTT.Sigma-Type.html#209" class="Field">pr₂</a> <a id="2702" href="UF.FunExt-Properties.html#2518" class="Bound">α</a><a id="2703" class="Symbol">)</a> <a id="2705" href="MLTT.Pi.html#513" class="Function Operator">∘</a> <a id="2707" href="UF.Equiv.html#2933" class="Function Operator">⌜</a> <a id="2709" href="UF.FunExt-Properties.html#2649" class="Bound">γ</a> <a id="2711" href="UF.Equiv.html#2933" class="Function Operator">⌝</a><a id="2712" class="Symbol">)))</a>

  <a id="2719" class="Keyword">module</a> <a id="2726" href="UF.FunExt-Properties.html#2726" class="Module">_</a> <a id="2728" class="Symbol">(</a><a id="2729" href="UF.FunExt-Properties.html#2729" class="Bound">X</a> <a id="2731" class="Symbol">:</a> <a id="2733" href="UF.FunExt-Properties.html#2399" class="Bound">𝓤</a> <a id="2735" href="MLTT.Universes.html#3977" class="Function Operator">̇</a> <a id="2737" class="Symbol">)</a> <a id="2739" class="Symbol">(</a><a id="2740" href="UF.FunExt-Properties.html#2740" class="Bound">Y</a> <a id="2742" class="Symbol">:</a> <a id="2744" href="UF.FunExt-Properties.html#2729" class="Bound">X</a> <a id="2746" class="Symbol">→</a> <a id="2748" href="UF.FunExt-Properties.html#2399" class="Bound">𝓤</a> <a id="2750" href="MLTT.Universes.html#3977" class="Function Operator">̇</a> <a id="2752" class="Symbol">)</a> <a id="2754" class="Symbol">(</a><a id="2755" href="UF.FunExt-Properties.html#2755" class="Bound">cY</a> <a id="2758" class="Symbol">:</a> <a id="2760" class="Symbol">(</a><a id="2761" href="UF.FunExt-Properties.html#2761" class="Bound">x</a> <a id="2763" class="Symbol">:</a> <a id="2765" href="UF.FunExt-Properties.html#2729" class="Bound">X</a><a id="2766" class="Symbol">)</a> <a id="2768" class="Symbol">→</a> <a id="2770" href="UF.Subsingletons.html#1565" class="Function">is-singleton</a> <a id="2783" class="Symbol">(</a><a id="2784" href="UF.FunExt-Properties.html#2740" class="Bound">Y</a> <a id="2786" href="UF.FunExt-Properties.html#2761" class="Bound">x</a><a id="2787" class="Symbol">))</a> <a id="2790" class="Keyword">where</a>
   <a id="2799" href="UF.FunExt-Properties.html#2799" class="Function">π</a> <a id="2801" class="Symbol">:</a> <a id="2803" class="Symbol">(</a><a id="2804" href="MLTT.Sigma-Type.html#116" class="Record">Σ</a> <a id="2806" href="UF.FunExt-Properties.html#2740" class="Bound">Y</a><a id="2807" class="Symbol">)</a> <a id="2809" href="UF.Equiv.html#1318" class="Function Operator">≃</a> <a id="2811" href="UF.FunExt-Properties.html#2729" class="Bound">X</a>
   <a id="2816" href="UF.FunExt-Properties.html#2799" class="Function">π</a> <a id="2818" class="Symbol">=</a>
    <a id="2824" href="UF.Equiv.html#7224" class="Function">qinveq</a>
     <a id="2836" href="MLTT.Sigma-Type.html#198" class="Field">pr₁</a>
     <a id="2845" class="Symbol">(</a> <a id="2847" class="Symbol">(λ</a> <a id="2850" href="UF.FunExt-Properties.html#2850" class="Bound">x</a> <a id="2852" class="Symbol">→</a> <a id="2854" href="UF.FunExt-Properties.html#2850" class="Bound">x</a> <a id="2856" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="2858" href="MLTT.Sigma-Type.html#198" class="Field">pr₁</a> <a id="2862" class="Symbol">(</a><a id="2863" href="UF.FunExt-Properties.html#2755" class="Bound">cY</a> <a id="2866" href="UF.FunExt-Properties.html#2850" class="Bound">x</a><a id="2867" class="Symbol">))</a>
     <a id="2875" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="2877" class="Symbol">(λ</a> <a id="2880" class="Symbol">(</a><a id="2881" href="UF.FunExt-Properties.html#2881" class="Bound">x</a> <a id="2883" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="2885" href="UF.FunExt-Properties.html#2885" class="Bound">y</a><a id="2886" class="Symbol">)</a> <a id="2888" class="Symbol">→</a> <a id="2890" href="UF.Base.html#14933" class="Function">to-Σ-＝</a> <a id="2897" class="Symbol">(</a><a id="2898" href="MLTT.Identity-Type.html#157" class="InductiveConstructor">refl</a> <a id="2903" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="2905" href="MLTT.Sigma-Type.html#209" class="Field">pr₂</a> <a id="2909" class="Symbol">(</a><a id="2910" href="UF.FunExt-Properties.html#2755" class="Bound">cY</a> <a id="2913" href="UF.FunExt-Properties.html#2881" class="Bound">x</a><a id="2914" class="Symbol">)</a> <a id="2916" href="UF.FunExt-Properties.html#2885" class="Bound">y</a><a id="2917" class="Symbol">))</a>
     <a id="2925" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="2927" href="MLTT.Id.html#1945" class="Function">∼-refl</a><a id="2933" class="Symbol">)</a>

   <a id="2939" href="UF.FunExt-Properties.html#2939" class="Function">sec</a> <a id="2943" class="Symbol">:</a> <a id="2945" href="MLTT.Pi.html#164" class="Function">Π</a> <a id="2947" href="UF.FunExt-Properties.html#2740" class="Bound">Y</a> <a id="2949" class="Symbol">→</a> <a id="2951" href="Notation.General.html#398" class="Function">fiber</a> <a id="2957" href="UF.Equiv.html#2933" class="Function Operator">⌜</a> <a id="2959" href="UF.FunExt-Properties.html#2444" class="Function">promote</a> <a id="2967" href="UF.FunExt-Properties.html#2729" class="Bound">X</a> <a id="2969" href="UF.FunExt-Properties.html#2799" class="Function">π</a> <a id="2971" href="UF.Equiv.html#2933" class="Function Operator">⌝</a> <a id="2973" href="UF.Equiv.html#1576" class="Function">𝕚𝕕</a>
   <a id="2979" href="UF.FunExt-Properties.html#2939" class="Function">sec</a> <a id="2983" href="UF.FunExt-Properties.html#2983" class="Bound">f</a> <a id="2985" class="Symbol">=</a>
    <a id="2991" class="Symbol">(</a> <a id="2993" href="UF.Equiv.html#7224" class="Function">qinveq</a>
       <a id="3007" class="Symbol">(λ</a> <a id="3010" href="UF.FunExt-Properties.html#3010" class="Bound">x</a> <a id="3012" class="Symbol">→</a> <a id="3014" href="UF.FunExt-Properties.html#3010" class="Bound">x</a> <a id="3016" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="3018" href="UF.FunExt-Properties.html#2983" class="Bound">f</a> <a id="3020" href="UF.FunExt-Properties.html#3010" class="Bound">x</a><a id="3021" class="Symbol">)</a>
       <a id="3030" class="Symbol">(</a> <a id="3032" href="MLTT.Sigma-Type.html#198" class="Field">pr₁</a>
       <a id="3043" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="3045" href="MLTT.Id.html#1945" class="Function">∼-refl</a>
       <a id="3059" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="3061" class="Symbol">(λ</a> <a id="3064" class="Symbol">(</a><a id="3065" href="UF.FunExt-Properties.html#3065" class="Bound">x</a> <a id="3067" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="3069" href="UF.FunExt-Properties.html#3069" class="Bound">y</a><a id="3070" class="Symbol">)</a> <a id="3072" class="Symbol">→</a> <a id="3074" href="UF.Base.html#14933" class="Function">to-Σ-＝</a> <a id="3081" class="Symbol">(</a><a id="3082" href="MLTT.Identity-Type.html#157" class="InductiveConstructor">refl</a> <a id="3087" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="3089" href="UF.Subsingletons.html#2083" class="Function">singletons-are-props</a> <a id="3110" class="Symbol">(</a><a id="3111" href="UF.FunExt-Properties.html#2755" class="Bound">cY</a> <a id="3114" href="UF.FunExt-Properties.html#3065" class="Bound">x</a><a id="3115" class="Symbol">)</a> <a id="3117" class="Symbol">_</a> <a id="3119" class="Symbol">_)))</a>
    <a id="3128" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="3130" href="UF.Equiv.html#498" class="Function">inverse</a> <a id="3138" class="Symbol">_</a> <a id="3140" class="Symbol">(</a><a id="3141" href="UF.FunExt-Properties.html#2402" class="Bound">ee</a> <a id="3144" class="Symbol">_</a> <a id="3146" class="Symbol">_)</a> <a id="3149" href="MLTT.Id.html#1945" class="Function">∼-refl</a><a id="3155" class="Symbol">)</a>

   <a id="3161" href="UF.FunExt-Properties.html#3161" class="Function">ret</a> <a id="3165" class="Symbol">:</a> <a id="3167" href="Notation.General.html#398" class="Function">fiber</a> <a id="3173" href="UF.Equiv.html#2933" class="Function Operator">⌜</a> <a id="3175" href="UF.FunExt-Properties.html#2444" class="Function">promote</a> <a id="3183" href="UF.FunExt-Properties.html#2729" class="Bound">X</a> <a id="3185" href="UF.FunExt-Properties.html#2799" class="Function">π</a> <a id="3187" href="UF.Equiv.html#2933" class="Function Operator">⌝</a> <a id="3189" href="UF.Equiv.html#1576" class="Function">𝕚𝕕</a> <a id="3192" class="Symbol">→</a> <a id="3194" href="MLTT.Pi.html#164" class="Function">Π</a> <a id="3196" href="UF.FunExt-Properties.html#2740" class="Bound">Y</a>
   <a id="3201" href="UF.FunExt-Properties.html#3161" class="Function">ret</a> <a id="3205" class="Symbol">(</a><a id="3206" href="UF.FunExt-Properties.html#3206" class="Bound">α</a> <a id="3208" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="3210" href="UF.FunExt-Properties.html#3210" class="Bound">p</a><a id="3211" class="Symbol">)</a> <a id="3213" href="UF.FunExt-Properties.html#3213" class="Bound">x</a> <a id="3215" class="Symbol">=</a> <a id="3217" href="MLTT.Id.html#735" class="Function">transport</a> <a id="3227" href="UF.FunExt-Properties.html#2740" class="Bound">Y</a> <a id="3229" class="Symbol">(</a><a id="3230" href="UF.Base.html#11014" class="Function">happly</a> <a id="3237" class="Symbol">(</a><a id="3238" href="MLTT.Id.html#1136" class="Function">ap</a> <a id="3241" href="UF.Equiv.html#2933" class="Function Operator">⌜_⌝</a> <a id="3245" href="UF.FunExt-Properties.html#3210" class="Bound">p</a><a id="3246" class="Symbol">)</a> <a id="3248" href="UF.FunExt-Properties.html#3213" class="Bound">x</a><a id="3249" class="Symbol">)</a> <a id="3251" class="Symbol">(</a><a id="3252" href="MLTT.Sigma-Type.html#209" class="Field">pr₂</a> <a id="3256" class="Symbol">(</a><a id="3257" href="MLTT.Sigma-Type.html#198" class="Field">pr₁</a> <a id="3261" href="UF.FunExt-Properties.html#3206" class="Bound">α</a> <a id="3263" href="UF.FunExt-Properties.html#3213" class="Bound">x</a><a id="3264" class="Symbol">))</a>

   <a id="3271" href="UF.FunExt-Properties.html#3271" class="Function">inv</a> <a id="3275" class="Symbol">:</a> <a id="3277" href="UF.FunExt-Properties.html#3161" class="Function">ret</a> <a id="3281" href="MLTT.Pi.html#513" class="Function Operator">∘</a> <a id="3283" href="UF.FunExt-Properties.html#2939" class="Function">sec</a> <a id="3287" href="MLTT.Id.html#1891" class="Function Operator">∼</a> <a id="3289" href="MLTT.Pi.html#444" class="Function">id</a>
   <a id="3295" href="UF.FunExt-Properties.html#3271" class="Function">inv</a> <a id="3299" href="UF.FunExt-Properties.html#3299" class="Bound">f</a> <a id="3301" class="Symbol">=</a>
    <a id="3307" href="MLTT.Id.html#1136" class="Function">ap</a> <a id="3310" class="Symbol">(λ</a> <a id="3313" href="UF.FunExt-Properties.html#3313" class="Bound">h</a> <a id="3315" class="Symbol">→</a> <a id="3317" class="Symbol">λ</a> <a id="3319" href="UF.FunExt-Properties.html#3319" class="Bound">x</a> <a id="3321" class="Symbol">→</a> <a id="3323" href="MLTT.Id.html#735" class="Function">transport</a> <a id="3333" href="UF.FunExt-Properties.html#2740" class="Bound">Y</a> <a id="3335" class="Symbol">(</a><a id="3336" href="UF.FunExt-Properties.html#3313" class="Bound">h</a> <a id="3338" href="UF.FunExt-Properties.html#3319" class="Bound">x</a><a id="3339" class="Symbol">)</a> <a id="3341" class="Symbol">(</a><a id="3342" href="MLTT.Sigma-Type.html#209" class="Field">pr₂</a> <a id="3346" class="Symbol">(</a><a id="3347" href="MLTT.Sigma-Type.html#198" class="Field">pr₁</a> <a id="3351" href="UF.FunExt-Properties.html#3382" class="Function">α</a> <a id="3353" href="UF.FunExt-Properties.html#3319" class="Bound">x</a><a id="3354" class="Symbol">)))</a> <a id="3358" href="UF.FunExt-Properties.html#3427" class="Function">cancel</a>
     <a id="3370" class="Keyword">where</a>
      <a id="3382" href="UF.FunExt-Properties.html#3382" class="Function">α</a> <a id="3384" class="Symbol">=</a> <a id="3386" href="MLTT.Sigma-Type.html#198" class="Field">pr₁</a> <a id="3390" class="Symbol">(</a><a id="3391" href="UF.FunExt-Properties.html#2939" class="Function">sec</a> <a id="3395" href="UF.FunExt-Properties.html#3299" class="Bound">f</a><a id="3396" class="Symbol">)</a>
      <a id="3404" href="UF.FunExt-Properties.html#3404" class="Function">p</a> <a id="3406" class="Symbol">=</a> <a id="3408" href="MLTT.Sigma-Type.html#209" class="Field">pr₂</a> <a id="3412" class="Symbol">(</a><a id="3413" href="UF.FunExt-Properties.html#2939" class="Function">sec</a> <a id="3417" href="UF.FunExt-Properties.html#3299" class="Bound">f</a><a id="3418" class="Symbol">)</a>

      <a id="3427" href="UF.FunExt-Properties.html#3427" class="Function">cancel</a> <a id="3434" class="Symbol">:</a> <a id="3436" href="UF.Base.html#11014" class="Function">happly</a> <a id="3443" class="Symbol">(</a><a id="3444" href="MLTT.Id.html#1136" class="Function">ap</a> <a id="3447" href="UF.Equiv.html#2933" class="Function Operator">⌜_⌝</a> <a id="3451" href="UF.FunExt-Properties.html#3404" class="Function">p</a><a id="3452" class="Symbol">)</a> <a id="3454" href="MLTT.Id.html#193" class="Datatype Operator">＝</a> <a id="3456" href="MLTT.Id.html#1945" class="Function">∼-refl</a>
      <a id="3469" href="UF.FunExt-Properties.html#3427" class="Function">cancel</a> <a id="3476" class="Symbol">=</a> <a id="3478" href="UF.Equiv.html#5785" class="Function">inverses-are-sections</a> <a id="3500" class="Symbol">_</a> <a id="3502" class="Symbol">(</a><a id="3503" href="UF.FunExt-Properties.html#2402" class="Bound">ee</a> <a id="3506" class="Symbol">_</a> <a id="3508" class="Symbol">_)</a> <a id="3511" class="Symbol">(</a><a id="3512" href="MLTT.Id.html#1945" class="Function">∼-refl</a><a id="3518" class="Symbol">)</a>

   <a id="3524" href="UF.FunExt-Properties.html#3524" class="Function">main</a> <a id="3529" class="Symbol">:</a> <a id="3531" href="UF.Subsingletons.html#1565" class="Function">is-singleton</a> <a id="3544" class="Symbol">(</a><a id="3545" href="MLTT.Pi.html#164" class="Function">Π</a> <a id="3547" href="UF.FunExt-Properties.html#2740" class="Bound">Y</a><a id="3548" class="Symbol">)</a>
   <a id="3553" href="UF.FunExt-Properties.html#3524" class="Function">main</a> <a id="3558" class="Symbol">=</a>
    <a id="3564" href="UF.Singleton-Properties.html#454" class="Function">singleton-closed-under-retract</a>
     <a id="3600" class="Symbol">(</a><a id="3601" href="MLTT.Pi.html#164" class="Function">Π</a> <a id="3603" href="UF.FunExt-Properties.html#2740" class="Bound">Y</a><a id="3604" class="Symbol">)</a>
     <a id="3611" class="Symbol">(</a><a id="3612" href="Notation.General.html#398" class="Function">fiber</a> <a id="3618" href="UF.Equiv.html#2933" class="Function Operator">⌜</a> <a id="3620" href="UF.FunExt-Properties.html#2444" class="Function">promote</a> <a id="3628" href="UF.FunExt-Properties.html#2729" class="Bound">X</a> <a id="3630" href="UF.FunExt-Properties.html#2799" class="Function">π</a> <a id="3632" href="UF.Equiv.html#2933" class="Function Operator">⌝</a> <a id="3634" href="UF.Equiv.html#1576" class="Function">𝕚𝕕</a><a id="3636" class="Symbol">)</a>
     <a id="3643" class="Symbol">(</a><a id="3644" href="UF.FunExt-Properties.html#3161" class="Function">ret</a> <a id="3648" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="3650" href="UF.FunExt-Properties.html#2939" class="Function">sec</a> <a id="3654" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="3656" href="UF.FunExt-Properties.html#3271" class="Function">inv</a><a id="3659" class="Symbol">)</a>
     <a id="3666" class="Symbol">(</a><a id="3667" href="UF.Equiv.html#17767" class="Function">equivs-are-vv-equivs</a> <a id="3688" class="Symbol">_</a> <a id="3690" class="Symbol">(</a><a id="3691" href="MLTT.Sigma-Type.html#209" class="Field">pr₂</a> <a id="3695" class="Symbol">(</a><a id="3696" href="UF.FunExt-Properties.html#2444" class="Function">promote</a> <a id="3704" href="UF.FunExt-Properties.html#2729" class="Bound">X</a> <a id="3706" href="UF.FunExt-Properties.html#2799" class="Function">π</a><a id="3707" class="Symbol">))</a> <a id="3710" href="UF.Equiv.html#1576" class="Function">𝕚𝕕</a><a id="3712" class="Symbol">)</a>

<a id="3715" class="Markup">\end{code}</a><a id="3725" class="Background">

End of addition.
</a></pre></body></html>