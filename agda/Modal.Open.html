<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Modal.Open</title><link rel="stylesheet" href="Agda.css"><script type="text/javascript" src="highlight-hover.js"></script></head><body><pre class="Agda"><a id="1" class="Background">Andrew Swan, started 12 February 2024

This is an implementation of open modalities. Like the other results
in this directory, it is covered in [1].

[1] Rijke, Shulman, Spitters, Modalities in homotopy type theory,
https://doi.org/10.23638/LMCS-16(1:2)2020


</a><a id="261" class="Markup">\begin{code}</a>
<a id="274" class="Symbol">{-#</a> <a id="278" class="Keyword">OPTIONS</a> <a id="286" class="Pragma">--safe</a> <a id="293" class="Pragma">--without-K</a> <a id="305" class="Symbol">#-}</a>

<a id="310" class="Keyword">open</a> <a id="315" class="Keyword">import</a> <a id="322" href="MLTT.Spartan.html" class="Module">MLTT.Spartan</a>

<a id="336" class="Keyword">open</a> <a id="341" class="Keyword">import</a> <a id="348" href="Modal.Subuniverse.html" class="Module">Modal.Subuniverse</a>

<a id="367" class="Keyword">open</a> <a id="372" class="Keyword">import</a> <a id="379" href="UF.Equiv.html" class="Module">UF.Equiv</a>
<a id="388" class="Keyword">open</a> <a id="393" class="Keyword">import</a> <a id="400" href="UF.EquivalenceExamples.html" class="Module">UF.EquivalenceExamples</a>
<a id="423" class="Keyword">open</a> <a id="428" class="Keyword">import</a> <a id="435" href="UF.Equiv-FunExt.html" class="Module">UF.Equiv-FunExt</a>
<a id="451" class="Keyword">open</a> <a id="456" class="Keyword">import</a> <a id="463" href="UF.FunExt.html" class="Module">UF.FunExt</a>
<a id="473" class="Keyword">open</a> <a id="478" class="Keyword">import</a> <a id="485" href="UF.Subsingletons.html" class="Module">UF.Subsingletons</a>

<a id="503" class="Keyword">module</a> <a id="510" href="Modal.Open.html" class="Module">Modal.Open</a>

<a id="522" class="Markup">\end{code}</a><a id="532" class="Background">

Function extensionality is required for even some quite basic results
about open modalities, so we will assume it throughout.

</a><a id="661" class="Markup">\begin{code}</a>

 <a id="676" class="Symbol">(</a><a id="677" href="Modal.Open.html#677" class="Bound">fe</a> <a id="680" class="Symbol">:</a> <a id="682" href="UF.FunExt.html#850" class="Function">funext</a> <a id="689" href="MLTT.Universes.html#3833" class="Generalizable">ğ“¤</a> <a id="691" href="MLTT.Universes.html#3833" class="Generalizable">ğ“¤</a><a id="692" class="Symbol">)</a>

<a id="695" class="Markup">\end{code}</a><a id="705" class="Background">

There is an open modality for each proposition P. We fix such a
proposition throughout.

</a><a id="796" class="Markup">\begin{code}</a>

 <a id="811" class="Symbol">(</a><a id="812" href="Modal.Open.html#812" class="Bound">P</a> <a id="814" class="Symbol">:</a> <a id="816" href="MLTT.Universes.html#3833" class="Generalizable">ğ“¤</a> <a id="818" href="MLTT.Universes.html#3976" class="Function Operator">Ì‡</a> <a id="820" class="Symbol">)</a>
 <a id="823" class="Symbol">(</a><a id="824" href="Modal.Open.html#824" class="Bound">P-is-prop</a> <a id="834" class="Symbol">:</a> <a id="836" href="UF.Subsingletons.html#469" class="Function">is-prop</a> <a id="844" href="Modal.Open.html#812" class="Bound">P</a><a id="845" class="Symbol">)</a>
 <a id="848" class="Keyword">where</a>

<a id="open-unit"></a><a id="855" href="Modal.Open.html#855" class="Function">open-unit</a> <a id="865" class="Symbol">:</a> <a id="867" class="Symbol">(</a><a id="868" href="Modal.Open.html#868" class="Bound">A</a> <a id="870" class="Symbol">:</a> <a id="872" href="Modal.Open.html#689" class="Bound">ğ“¤</a> <a id="874" href="MLTT.Universes.html#3976" class="Function Operator">Ì‡</a> <a id="876" class="Symbol">)</a> <a id="878" class="Symbol">â†’</a> <a id="880" href="Modal.Open.html#868" class="Bound">A</a> <a id="882" class="Symbol">â†’</a> <a id="884" class="Symbol">(</a><a id="885" href="Modal.Open.html#812" class="Bound">P</a> <a id="887" class="Symbol">â†’</a> <a id="889" href="Modal.Open.html#868" class="Bound">A</a><a id="890" class="Symbol">)</a>
<a id="892" href="Modal.Open.html#855" class="Function">open-unit</a> <a id="902" href="Modal.Open.html#902" class="Bound">A</a> <a id="904" href="Modal.Open.html#904" class="Bound">a</a> <a id="906" class="Symbol">_</a> <a id="908" class="Symbol">=</a> <a id="910" href="Modal.Open.html#904" class="Bound">a</a>

<a id="is-open-modal"></a><a id="913" href="Modal.Open.html#913" class="Function">is-open-modal</a> <a id="927" class="Symbol">:</a> <a id="929" href="Modal.Open.html#689" class="Bound">ğ“¤</a> <a id="931" href="MLTT.Universes.html#3976" class="Function Operator">Ì‡</a> <a id="933" class="Symbol">â†’</a> <a id="935" href="Modal.Open.html#689" class="Bound">ğ“¤</a> <a id="937" href="MLTT.Universes.html#3976" class="Function Operator">Ì‡</a>
<a id="939" href="Modal.Open.html#913" class="Function">is-open-modal</a> <a id="953" href="Modal.Open.html#953" class="Bound">A</a> <a id="955" class="Symbol">=</a> <a id="957" href="UF.Equiv.html#402" class="Function">is-equiv</a> <a id="966" class="Symbol">(</a><a id="967" href="Modal.Open.html#855" class="Function">open-unit</a> <a id="977" href="Modal.Open.html#953" class="Bound">A</a><a id="978" class="Symbol">)</a>

<a id="open-subuniverse"></a><a id="981" href="Modal.Open.html#981" class="Function">open-subuniverse</a> <a id="998" class="Symbol">:</a> <a id="1000" href="Modal.Subuniverse.html#296" class="Function">subuniverse</a> <a id="1012" href="Modal.Open.html#689" class="Bound">ğ“¤</a> <a id="1014" href="Modal.Open.html#689" class="Bound">ğ“¤</a>
<a id="1016" href="Modal.Open.html#981" class="Function">open-subuniverse</a> <a id="1033" class="Symbol">=</a>
 <a id="1036" href="Modal.Open.html#913" class="Function">is-open-modal</a> <a id="1050" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="1052" class="Symbol">Î»</a> <a id="1054" href="Modal.Open.html#1054" class="Bound">A</a> <a id="1056" class="Symbol">â†’</a> <a id="1058" href="UF.Equiv-FunExt.html#6390" class="Function">being-equiv-is-prop&#39;&#39;</a> <a id="1080" href="Modal.Open.html#677" class="Bound">fe</a> <a id="1083" class="Symbol">_</a>

<a id="1086" class="Markup">\end{code}</a><a id="1096" class="Background">

The reflection has a very simple description - it just sends A to the
exponential P â†’ A. We then need to check that it is a reflection.

</a><a id="1235" class="Markup">\begin{code}</a>

<a id="exponential-is-modal"></a><a id="1249" href="Modal.Open.html#1249" class="Function">exponential-is-modal</a> <a id="1270" class="Symbol">:</a> <a id="1272" class="Symbol">(</a><a id="1273" href="Modal.Open.html#1273" class="Bound">A</a> <a id="1275" class="Symbol">:</a> <a id="1277" href="Modal.Open.html#689" class="Bound">ğ“¤</a> <a id="1279" href="MLTT.Universes.html#3976" class="Function Operator">Ì‡</a> <a id="1281" class="Symbol">)</a> <a id="1283" class="Symbol">â†’</a> <a id="1285" href="Modal.Open.html#913" class="Function">is-open-modal</a> <a id="1299" class="Symbol">(</a><a id="1300" href="Modal.Open.html#812" class="Bound">P</a> <a id="1302" class="Symbol">â†’</a> <a id="1304" href="Modal.Open.html#1273" class="Bound">A</a><a id="1305" class="Symbol">)</a>
<a id="1307" href="Modal.Open.html#1249" class="Function">exponential-is-modal</a> <a id="1328" href="Modal.Open.html#1328" class="Bound">A</a> <a id="1330" class="Symbol">=</a>
 <a id="1333" class="Symbol">((Î»</a> <a id="1337" href="Modal.Open.html#1337" class="Bound">f</a> <a id="1339" href="Modal.Open.html#1339" class="Bound">p</a> <a id="1341" class="Symbol">â†’</a> <a id="1343" href="Modal.Open.html#1337" class="Bound">f</a> <a id="1345" href="Modal.Open.html#1339" class="Bound">p</a> <a id="1347" href="Modal.Open.html#1339" class="Bound">p</a><a id="1348" class="Symbol">)</a> <a id="1350" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a>
  <a id="1354" class="Symbol">(Î»</a> <a id="1357" href="Modal.Open.html#1357" class="Bound">f</a> <a id="1359" class="Symbol">â†’</a>
   <a id="1364" href="UF.FunExt.html#1242" class="Function">dfunext</a>
    <a id="1376" href="Modal.Open.html#677" class="Bound">fe</a>
    <a id="1383" class="Symbol">(Î»</a> <a id="1386" href="Modal.Open.html#1386" class="Bound">p</a> <a id="1388" class="Symbol">â†’</a> <a id="1390" href="UF.FunExt.html#1242" class="Function">dfunext</a> <a id="1398" href="Modal.Open.html#677" class="Bound">fe</a> <a id="1401" class="Symbol">(Î»</a> <a id="1404" href="Modal.Open.html#1404" class="Bound">q</a> <a id="1406" class="Symbol">â†’</a> <a id="1408" href="MLTT.Id.html#1136" class="Function">ap</a> <a id="1411" class="Symbol">(Î»</a> <a id="1414" href="Modal.Open.html#1414" class="Bound">r</a> <a id="1416" class="Symbol">â†’</a> <a id="1418" href="Modal.Open.html#1357" class="Bound">f</a> <a id="1420" href="Modal.Open.html#1414" class="Bound">r</a> <a id="1422" href="Modal.Open.html#1404" class="Bound">q</a><a id="1423" class="Symbol">)</a> <a id="1425" class="Symbol">(</a><a id="1426" href="Modal.Open.html#824" class="Bound">P-is-prop</a> <a id="1436" class="Symbol">_</a> <a id="1438" class="Symbol">_)))))</a> <a id="1445" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a>
 <a id="1448" class="Symbol">(Î»</a> <a id="1451" href="Modal.Open.html#1451" class="Bound">f</a> <a id="1453" href="Modal.Open.html#1453" class="Bound">p</a> <a id="1455" class="Symbol">â†’</a> <a id="1457" href="Modal.Open.html#1451" class="Bound">f</a> <a id="1459" href="Modal.Open.html#1453" class="Bound">p</a> <a id="1461" href="Modal.Open.html#1453" class="Bound">p</a><a id="1462" class="Symbol">)</a> <a id="1464" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a>
 <a id="1467" class="Symbol">(Î»</a> <a id="1470" href="Modal.Open.html#1470" class="Bound">f</a> <a id="1472" class="Symbol">â†’</a> <a id="1474" href="MLTT.Identity-Type.html#157" class="InductiveConstructor">refl</a><a id="1478" class="Symbol">)</a>

<a id="exponential-is-reflection"></a><a id="1481" href="Modal.Open.html#1481" class="Function">exponential-is-reflection</a>
 <a id="1508" class="Symbol">:</a> <a id="1510" class="Symbol">(</a><a id="1511" href="Modal.Open.html#1511" class="Bound">A</a> <a id="1513" class="Symbol">:</a> <a id="1515" href="Modal.Open.html#689" class="Bound">ğ“¤</a> <a id="1517" href="MLTT.Universes.html#3976" class="Function Operator">Ì‡</a> <a id="1519" class="Symbol">)</a>
 <a id="1522" class="Symbol">â†’</a> <a id="1524" href="Modal.Subuniverse.html#739" class="Function">is-reflection</a> <a id="1538" href="Modal.Open.html#981" class="Function">open-subuniverse</a>
  <a id="1557" href="Modal.Open.html#1511" class="Bound">A</a>
  <a id="1561" class="Symbol">(((</a><a id="1564" href="Modal.Open.html#812" class="Bound">P</a> <a id="1566" class="Symbol">â†’</a> <a id="1568" href="Modal.Open.html#1511" class="Bound">A</a><a id="1569" class="Symbol">)</a> <a id="1571" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="1573" class="Symbol">(</a><a id="1574" href="Modal.Open.html#1249" class="Function">exponential-is-modal</a> <a id="1595" href="Modal.Open.html#1511" class="Bound">A</a><a id="1596" class="Symbol">))</a> <a id="1599" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="1601" class="Symbol">Î»</a> <a id="1603" href="Modal.Open.html#1603" class="Bound">a</a> <a id="1605" href="Modal.Open.html#1605" class="Bound">_</a> <a id="1607" class="Symbol">â†’</a> <a id="1609" href="Modal.Open.html#1603" class="Bound">a</a><a id="1610" class="Symbol">)</a>
<a id="1612" href="Modal.Open.html#1481" class="Function">exponential-is-reflection</a> <a id="1638" href="Modal.Open.html#1638" class="Bound">A</a> <a id="1640" href="Modal.Open.html#1640" class="Bound">B</a> <a id="1642" href="Modal.Open.html#1642" class="Bound">B-modal</a> <a id="1650" class="Symbol">=</a>
 <a id="1653" href="UF.Equiv.html#7091" class="Function">qinvs-are-equivs</a>
  <a id="1672" class="Symbol">_</a>
  <a id="1676" class="Symbol">((Î»</a> <a id="1680" href="Modal.Open.html#1680" class="Bound">g</a> <a id="1682" class="Symbol">â†’</a> <a id="1684" href="MLTT.Sigma-Type.html#198" class="Field">prâ‚</a> <a id="1688" class="Symbol">(</a><a id="1689" href="MLTT.Sigma-Type.html#209" class="Field">prâ‚‚</a> <a id="1693" href="Modal.Open.html#1642" class="Bound">B-modal</a><a id="1700" class="Symbol">)</a> <a id="1702" href="MLTT.Pi.html#513" class="Function Operator">âˆ˜</a> <a id="1704" class="Symbol">Î»</a> <a id="1706" href="Modal.Open.html#1706" class="Bound">f</a> <a id="1708" class="Symbol">â†’</a> <a id="1710" href="Modal.Open.html#1680" class="Bound">g</a> <a id="1712" href="MLTT.Pi.html#513" class="Function Operator">âˆ˜</a> <a id="1714" href="Modal.Open.html#1706" class="Bound">f</a><a id="1715" class="Symbol">)</a> <a id="1717" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a>
   <a id="1722" class="Symbol">(Î»</a> <a id="1725" href="Modal.Open.html#1725" class="Bound">j</a> <a id="1727" class="Symbol">â†’</a> <a id="1729" href="UF.FunExt.html#1242" class="Function">dfunext</a> <a id="1737" href="Modal.Open.html#677" class="Bound">fe</a> <a id="1740" class="Symbol">(</a><a id="1741" href="Modal.Open.html#1999" class="Function">is-retraction</a> <a id="1755" href="Modal.Open.html#1725" class="Bound">j</a><a id="1756" class="Symbol">))</a> <a id="1759" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a>
   <a id="1764" class="Symbol">Î»</a> <a id="1766" href="Modal.Open.html#1766" class="Bound">g</a> <a id="1768" class="Symbol">â†’</a> <a id="1770" href="UF.FunExt.html#1242" class="Function">dfunext</a> <a id="1778" href="Modal.Open.html#677" class="Bound">fe</a> <a id="1781" class="Symbol">(Î»</a> <a id="1784" href="Modal.Open.html#1784" class="Bound">a</a> <a id="1786" class="Symbol">â†’</a> <a id="1788" href="MLTT.Sigma-Type.html#209" class="Field">prâ‚‚</a> <a id="1792" class="Symbol">(</a><a id="1793" href="MLTT.Sigma-Type.html#209" class="Field">prâ‚‚</a> <a id="1797" href="Modal.Open.html#1642" class="Bound">B-modal</a><a id="1804" class="Symbol">)</a> <a id="1806" class="Symbol">(</a><a id="1807" href="Modal.Open.html#1766" class="Bound">g</a> <a id="1809" href="Modal.Open.html#1784" class="Bound">a</a><a id="1810" class="Symbol">)))</a>
 <a id="1815" class="Keyword">where</a>
  <a id="1823" href="Modal.Open.html#1823" class="Function">lemma</a>
   <a id="1832" class="Symbol">:</a> <a id="1834" class="Symbol">(</a><a id="1835" href="Modal.Open.html#1835" class="Bound">j</a> <a id="1837" class="Symbol">:</a> <a id="1839" class="Symbol">(</a><a id="1840" href="Modal.Open.html#812" class="Bound">P</a> <a id="1842" class="Symbol">â†’</a> <a id="1844" href="Modal.Open.html#1638" class="Bound">A</a><a id="1845" class="Symbol">)</a> <a id="1847" class="Symbol">â†’</a> <a id="1849" href="Modal.Open.html#1640" class="Bound">B</a><a id="1850" class="Symbol">)</a>
   <a id="1855" class="Symbol">â†’</a> <a id="1857" class="Symbol">(Î»</a> <a id="1860" href="Modal.Open.html#1860" class="Bound">f</a> <a id="1862" class="Symbol">â†’</a> <a id="1864" class="Symbol">(</a><a id="1865" href="Modal.Open.html#1835" class="Bound">j</a> <a id="1867" href="MLTT.Pi.html#513" class="Function Operator">âˆ˜</a> <a id="1869" href="Modal.Open.html#855" class="Function">open-unit</a> <a id="1879" href="Modal.Open.html#1638" class="Bound">A</a> <a id="1881" href="MLTT.Pi.html#513" class="Function Operator">âˆ˜</a> <a id="1883" href="Modal.Open.html#1860" class="Bound">f</a><a id="1884" class="Symbol">))</a> <a id="1887" href="MLTT.Id.html#1425" class="Function Operator">âˆ¼</a> <a id="1889" class="Symbol">(</a><a id="1890" href="Modal.Open.html#855" class="Function">open-unit</a> <a id="1900" href="Modal.Open.html#1640" class="Bound">B</a><a id="1901" class="Symbol">)</a> <a id="1903" href="MLTT.Pi.html#513" class="Function Operator">âˆ˜</a> <a id="1905" href="Modal.Open.html#1835" class="Bound">j</a>
  <a id="1909" href="Modal.Open.html#1823" class="Function">lemma</a> <a id="1915" href="Modal.Open.html#1915" class="Bound">j</a> <a id="1917" href="Modal.Open.html#1917" class="Bound">f</a> <a id="1919" class="Symbol">=</a>
   <a id="1924" href="UF.FunExt.html#1242" class="Function">dfunext</a> <a id="1932" href="Modal.Open.html#677" class="Bound">fe</a>
    <a id="1939" class="Symbol">(Î»</a> <a id="1942" href="Modal.Open.html#1942" class="Bound">z</a> <a id="1944" class="Symbol">â†’</a> <a id="1946" href="MLTT.Id.html#1136" class="Function">ap</a> <a id="1949" href="Modal.Open.html#1915" class="Bound">j</a> <a id="1951" class="Symbol">(</a><a id="1952" href="UF.FunExt.html#1242" class="Function">dfunext</a> <a id="1960" href="Modal.Open.html#677" class="Bound">fe</a> <a id="1963" class="Symbol">(Î»</a> <a id="1966" href="Modal.Open.html#1966" class="Bound">z&#39;</a> <a id="1969" class="Symbol">â†’</a> <a id="1971" href="MLTT.Id.html#1136" class="Function">ap</a> <a id="1974" href="Modal.Open.html#1917" class="Bound">f</a> <a id="1976" class="Symbol">(</a><a id="1977" href="Modal.Open.html#824" class="Bound">P-is-prop</a> <a id="1987" href="Modal.Open.html#1942" class="Bound">z</a> <a id="1989" href="Modal.Open.html#1966" class="Bound">z&#39;</a><a id="1991" class="Symbol">))))</a>

  <a id="1999" href="Modal.Open.html#1999" class="Function">is-retraction</a>
   <a id="2016" class="Symbol">:</a> <a id="2018" class="Symbol">(</a><a id="2019" href="Modal.Open.html#2019" class="Bound">j</a> <a id="2021" class="Symbol">:</a> <a id="2023" class="Symbol">(</a><a id="2024" href="Modal.Open.html#812" class="Bound">P</a> <a id="2026" class="Symbol">â†’</a> <a id="2028" href="Modal.Open.html#1638" class="Bound">A</a><a id="2029" class="Symbol">)</a> <a id="2031" class="Symbol">â†’</a> <a id="2033" href="Modal.Open.html#1640" class="Bound">B</a><a id="2034" class="Symbol">)</a>
   <a id="2039" class="Symbol">â†’</a> <a id="2041" href="MLTT.Sigma-Type.html#198" class="Field">prâ‚</a> <a id="2045" class="Symbol">(</a><a id="2046" href="MLTT.Sigma-Type.html#209" class="Field">prâ‚‚</a> <a id="2050" href="Modal.Open.html#1642" class="Bound">B-modal</a><a id="2057" class="Symbol">)</a> <a id="2059" href="MLTT.Pi.html#513" class="Function Operator">âˆ˜</a> <a id="2061" class="Symbol">(Î»</a> <a id="2064" href="Modal.Open.html#2064" class="Bound">f</a> <a id="2066" class="Symbol">â†’</a> <a id="2068" class="Symbol">(</a><a id="2069" href="Modal.Open.html#2019" class="Bound">j</a> <a id="2071" href="MLTT.Pi.html#513" class="Function Operator">âˆ˜</a> <a id="2073" href="Modal.Open.html#855" class="Function">open-unit</a> <a id="2083" href="Modal.Open.html#1638" class="Bound">A</a> <a id="2085" href="MLTT.Pi.html#513" class="Function Operator">âˆ˜</a> <a id="2087" href="Modal.Open.html#2064" class="Bound">f</a><a id="2088" class="Symbol">))</a> <a id="2091" href="MLTT.Id.html#1425" class="Function Operator">âˆ¼</a> <a id="2093" href="Modal.Open.html#2019" class="Bound">j</a>
  <a id="2097" href="Modal.Open.html#1999" class="Function">is-retraction</a> <a id="2111" href="Modal.Open.html#2111" class="Bound">j</a> <a id="2113" href="Modal.Open.html#2113" class="Bound">f</a> <a id="2115" class="Symbol">=</a>
   <a id="2120" href="MLTT.Sigma-Type.html#198" class="Field">prâ‚</a> <a id="2124" class="Symbol">(</a><a id="2125" href="MLTT.Sigma-Type.html#209" class="Field">prâ‚‚</a> <a id="2129" href="Modal.Open.html#1642" class="Bound">B-modal</a><a id="2136" class="Symbol">)</a> <a id="2138" class="Symbol">(</a><a id="2139" href="Modal.Open.html#2111" class="Bound">j</a> <a id="2141" href="MLTT.Pi.html#513" class="Function Operator">âˆ˜</a> <a id="2143" href="Modal.Open.html#855" class="Function">open-unit</a> <a id="2153" href="Modal.Open.html#1638" class="Bound">A</a> <a id="2155" href="MLTT.Pi.html#513" class="Function Operator">âˆ˜</a> <a id="2157" href="Modal.Open.html#2113" class="Bound">f</a><a id="2158" class="Symbol">)</a>
    <a id="2164" href="MLTT.Id.html#2239" class="Function Operator">ï¼âŸ¨</a> <a id="2167" href="MLTT.Id.html#1136" class="Function">ap</a> <a id="2170" class="Symbol">(</a><a id="2171" href="MLTT.Sigma-Type.html#198" class="Field">prâ‚</a> <a id="2175" class="Symbol">(</a><a id="2176" href="MLTT.Sigma-Type.html#209" class="Field">prâ‚‚</a> <a id="2180" href="Modal.Open.html#1642" class="Bound">B-modal</a><a id="2187" class="Symbol">))</a> <a id="2190" class="Symbol">(</a><a id="2191" href="Modal.Open.html#1823" class="Function">lemma</a> <a id="2197" href="Modal.Open.html#2111" class="Bound">j</a> <a id="2199" href="Modal.Open.html#2113" class="Bound">f</a><a id="2200" class="Symbol">)</a> <a id="2202" href="MLTT.Id.html#2239" class="Function Operator">âŸ©</a>
   <a id="2207" href="MLTT.Sigma-Type.html#198" class="Field">prâ‚</a> <a id="2211" class="Symbol">(</a><a id="2212" href="MLTT.Sigma-Type.html#209" class="Field">prâ‚‚</a> <a id="2216" href="Modal.Open.html#1642" class="Bound">B-modal</a><a id="2223" class="Symbol">)</a> <a id="2225" class="Symbol">(</a><a id="2226" href="Modal.Open.html#855" class="Function">open-unit</a> <a id="2236" href="Modal.Open.html#1640" class="Bound">B</a> <a id="2238" class="Symbol">(</a><a id="2239" href="Modal.Open.html#2111" class="Bound">j</a> <a id="2241" href="Modal.Open.html#2113" class="Bound">f</a><a id="2242" class="Symbol">))</a>
    <a id="2249" href="MLTT.Id.html#2239" class="Function Operator">ï¼âŸ¨</a> <a id="2252" href="MLTT.Sigma-Type.html#209" class="Field">prâ‚‚</a> <a id="2256" class="Symbol">(</a><a id="2257" href="MLTT.Sigma-Type.html#209" class="Field">prâ‚‚</a> <a id="2261" href="Modal.Open.html#1642" class="Bound">B-modal</a><a id="2268" class="Symbol">)</a> <a id="2270" class="Symbol">(</a><a id="2271" href="Modal.Open.html#2111" class="Bound">j</a> <a id="2273" href="Modal.Open.html#2113" class="Bound">f</a><a id="2274" class="Symbol">)</a> <a id="2276" href="MLTT.Id.html#2239" class="Function Operator">âŸ©</a>
   <a id="2281" href="Modal.Open.html#2111" class="Bound">j</a> <a id="2283" href="Modal.Open.html#2113" class="Bound">f</a> <a id="2285" href="MLTT.Id.html#2321" class="Function Operator">âˆ</a>

<a id="open-is-reflective"></a><a id="2288" href="Modal.Open.html#2288" class="Function">open-is-reflective</a> <a id="2307" class="Symbol">:</a> <a id="2309" href="Modal.Subuniverse.html#1085" class="Function">subuniverse-is-reflective</a> <a id="2335" href="Modal.Open.html#981" class="Function">open-subuniverse</a>
<a id="2352" href="Modal.Open.html#2288" class="Function">open-is-reflective</a> <a id="2371" href="Modal.Open.html#2371" class="Bound">A</a> <a id="2373" class="Symbol">=</a>
 <a id="2376" class="Symbol">(((</a><a id="2379" href="Modal.Open.html#812" class="Bound">P</a> <a id="2381" class="Symbol">â†’</a> <a id="2383" href="Modal.Open.html#2371" class="Bound">A</a><a id="2384" class="Symbol">)</a> <a id="2386" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="2388" class="Symbol">(</a><a id="2389" href="Modal.Open.html#1249" class="Function">exponential-is-modal</a> <a id="2410" href="Modal.Open.html#2371" class="Bound">A</a><a id="2411" class="Symbol">))</a> <a id="2414" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="2416" class="Symbol">(</a><a id="2417" href="Modal.Open.html#855" class="Function">open-unit</a> <a id="2427" href="Modal.Open.html#2371" class="Bound">A</a><a id="2428" class="Symbol">))</a> <a id="2431" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a>
 <a id="2434" href="Modal.Open.html#1481" class="Function">exponential-is-reflection</a> <a id="2460" href="Modal.Open.html#2371" class="Bound">A</a>

<a id="2463" class="Markup">\end{code}</a><a id="2473" class="Background">

We can show moreover that the reflective subuniverse is replete,
using only function extensionality rather than univalence, and that it
is Î£-closed. This confirms that it is a modality.

</a><a id="2662" class="Markup">\begin{code}</a>

<a id="open-is-replete"></a><a id="2676" href="Modal.Open.html#2676" class="Function">open-is-replete</a> <a id="2692" class="Symbol">:</a> <a id="2694" href="Modal.Subuniverse.html#1203" class="Function">subuniverse-is-replete</a> <a id="2717" href="Modal.Open.html#981" class="Function">open-subuniverse</a>
<a id="2734" href="Modal.Open.html#2676" class="Function">open-is-replete</a> <a id="2750" href="Modal.Open.html#2750" class="Bound">A</a> <a id="2752" href="Modal.Open.html#2752" class="Bound">B</a> <a id="2754" href="Modal.Open.html#2754" class="Bound">e</a> <a id="2756" href="Modal.Open.html#2756" class="Bound">B-modal</a> <a id="2764" class="Symbol">=</a>
 <a id="2767" href="UF.Equiv.html#25473" class="Function">â‰ƒ-2-out-of-3-left</a>
  <a id="2787" class="Symbol">(</a><a id="2788" href="MLTT.Sigma-Type.html#209" class="Field">prâ‚‚</a> <a id="2792" class="Symbol">(</a><a id="2793" href="UF.EquivalenceExamples.html#15803" class="Function">â†’cong&#39;</a> <a id="2800" href="Modal.Open.html#677" class="Bound">fe</a> <a id="2803" href="Modal.Open.html#677" class="Bound">fe</a> <a id="2806" href="Modal.Open.html#2754" class="Bound">e</a><a id="2807" class="Symbol">))</a>
  <a id="2812" class="Symbol">(</a><a id="2813" href="UF.Equiv.html#1615" class="Function">âˆ˜-is-equiv</a> <a id="2824" href="UF.Equiv.html#3039" class="Function Operator">âŒœ</a> <a id="2826" href="Modal.Open.html#2754" class="Bound">e</a> <a id="2828" href="UF.Equiv.html#3039" class="Function Operator">âŒ-is-equiv</a> <a id="2839" href="Modal.Open.html#2756" class="Bound">B-modal</a><a id="2846" class="Symbol">)</a>

<a id="open-is-sigma-closed"></a><a id="2849" href="Modal.Open.html#2849" class="Function">open-is-sigma-closed</a> <a id="2870" class="Symbol">:</a> <a id="2872" href="Modal.Subuniverse.html#1599" class="Function">subuniverse-is-sigma-closed</a> <a id="2900" href="Modal.Open.html#981" class="Function">open-subuniverse</a>
<a id="2917" href="Modal.Open.html#2849" class="Function">open-is-sigma-closed</a> <a id="2938" href="Modal.Open.html#2938" class="Bound">A</a> <a id="2940" href="Modal.Open.html#2940" class="Bound">B</a> <a id="2942" href="Modal.Open.html#2942" class="Bound">A-modal</a> <a id="2950" href="Modal.Open.html#2950" class="Bound">B-modal</a> <a id="2958" class="Symbol">=</a>
 <a id="2961" href="UF.Equiv.html#25473" class="Function">â‰ƒ-2-out-of-3-left</a>
  <a id="2981" href="UF.Equiv.html#3039" class="Function Operator">âŒœ</a> <a id="2983" href="UF.EquivalenceExamples.html#3864" class="Function">Î Î£-distr-â‰ƒ</a> <a id="2994" href="UF.Equiv.html#3039" class="Function Operator">âŒ-is-equiv</a>
  <a id="3007" href="UF.Equiv.html#3039" class="Function Operator">âŒœ</a> <a id="3009" href="Modal.Open.html#3040" class="Function">unit-equiv</a> <a id="3020" href="UF.Equiv.html#3039" class="Function Operator">âŒ-is-equiv</a>
 <a id="3032" class="Keyword">where</a>
  <a id="3040" href="Modal.Open.html#3040" class="Function">unit-equiv</a> <a id="3051" class="Symbol">:</a> <a id="3053" href="MLTT.Sigma-Type.html#116" class="Record">Î£</a> <a id="3055" href="Modal.Open.html#2940" class="Bound">B</a> <a id="3057" href="UF.Equiv.html#1318" class="Function Operator">â‰ƒ</a> <a id="3059" class="Symbol">(</a><a id="3060" href="MLTT.Sigma.html#423" class="Function">Î£</a> <a id="3062" href="Modal.Open.html#3062" class="Bound">f</a> <a id="3064" href="MLTT.Sigma.html#423" class="Function">ê‰</a> <a id="3066" class="Symbol">(</a><a id="3067" href="Modal.Open.html#812" class="Bound">P</a> <a id="3069" class="Symbol">â†’</a> <a id="3071" href="Modal.Open.html#2938" class="Bound">A</a><a id="3072" class="Symbol">)</a> <a id="3074" href="MLTT.Sigma.html#423" class="Function">,</a> <a id="3076" class="Symbol">((</a><a id="3078" href="Modal.Open.html#3078" class="Bound">z</a> <a id="3080" class="Symbol">:</a> <a id="3082" href="Modal.Open.html#812" class="Bound">P</a><a id="3083" class="Symbol">)</a> <a id="3085" class="Symbol">â†’</a> <a id="3087" href="Modal.Open.html#2940" class="Bound">B</a> <a id="3089" class="Symbol">(</a><a id="3090" href="Modal.Open.html#3062" class="Bound">f</a> <a id="3092" href="Modal.Open.html#3078" class="Bound">z</a><a id="3093" class="Symbol">)))</a>
  <a id="3099" href="Modal.Open.html#3040" class="Function">unit-equiv</a> <a id="3110" class="Symbol">=</a>
   <a id="3115" href="UF.EquivalenceExamples.html#20451" class="Function">Î£-bicong</a> <a id="3124" class="Symbol">_</a> <a id="3126" class="Symbol">_</a>
    <a id="3132" class="Symbol">((</a><a id="3134" href="Modal.Open.html#855" class="Function">open-unit</a> <a id="3144" href="Modal.Open.html#2938" class="Bound">A</a><a id="3145" class="Symbol">)</a> <a id="3147" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="3149" href="Modal.Open.html#2942" class="Bound">A-modal</a><a id="3156" class="Symbol">)</a>
    <a id="3162" class="Symbol">(Î»</a> <a id="3165" href="Modal.Open.html#3165" class="Bound">a</a> <a id="3167" class="Symbol">â†’</a> <a id="3169" class="Symbol">(</a><a id="3170" href="Modal.Open.html#855" class="Function">open-unit</a> <a id="3180" class="Symbol">(</a><a id="3181" href="Modal.Open.html#2940" class="Bound">B</a> <a id="3183" href="Modal.Open.html#3165" class="Bound">a</a><a id="3184" class="Symbol">))</a> <a id="3187" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a> <a id="3189" class="Symbol">(</a><a id="3190" href="Modal.Open.html#2950" class="Bound">B-modal</a> <a id="3198" href="Modal.Open.html#3165" class="Bound">a</a><a id="3199" class="Symbol">))</a>

<a id="3203" class="Markup">\end{code}</a><a id="3213" class="Background">

We add a useful lemma for the absoluteness of compactness: if P is a
true proposition then the open modality is trivial, in the sense that
all types are modal.

</a><a id="3376" class="Markup">\begin{code}</a>

<a id="P-true-implies-all-modal"></a><a id="3390" href="Modal.Open.html#3390" class="Function">P-true-implies-all-modal</a>
 <a id="3416" class="Symbol">:</a> <a id="3418" class="Symbol">(</a><a id="3419" href="Modal.Open.html#3419" class="Bound">z</a> <a id="3421" class="Symbol">:</a> <a id="3423" href="Modal.Open.html#812" class="Bound">P</a><a id="3424" class="Symbol">)</a> <a id="3426" class="Symbol">â†’</a> <a id="3428" class="Symbol">(</a><a id="3429" href="Modal.Open.html#3429" class="Bound">A</a> <a id="3431" class="Symbol">:</a> <a id="3433" href="Modal.Open.html#689" class="Bound">ğ“¤</a> <a id="3435" href="MLTT.Universes.html#3976" class="Function Operator">Ì‡</a> <a id="3437" class="Symbol">)</a> <a id="3439" class="Symbol">â†’</a> <a id="3441" href="Modal.Open.html#913" class="Function">is-open-modal</a> <a id="3455" href="Modal.Open.html#3429" class="Bound">A</a>
<a id="3457" href="Modal.Open.html#3390" class="Function">P-true-implies-all-modal</a> <a id="3482" href="Modal.Open.html#3482" class="Bound">z</a> <a id="3484" href="Modal.Open.html#3484" class="Bound">A</a> <a id="3486" class="Symbol">=</a>
 <a id="3489" href="UF.Equiv.html#7091" class="Function">qinvs-are-equivs</a>
  <a id="3508" class="Symbol">(</a><a id="3509" href="Modal.Open.html#855" class="Function">open-unit</a> <a id="3519" href="Modal.Open.html#3484" class="Bound">A</a><a id="3520" class="Symbol">)</a>
  <a id="3524" class="Symbol">((Î»</a> <a id="3528" href="Modal.Open.html#3528" class="Bound">f</a> <a id="3530" class="Symbol">â†’</a> <a id="3532" href="Modal.Open.html#3528" class="Bound">f</a> <a id="3534" href="Modal.Open.html#3482" class="Bound">z</a><a id="3535" class="Symbol">)</a> <a id="3537" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a>
   <a id="3542" class="Symbol">((Î»</a> <a id="3546" href="Modal.Open.html#3546" class="Bound">a</a> <a id="3548" class="Symbol">â†’</a> <a id="3550" href="MLTT.Identity-Type.html#157" class="InductiveConstructor">refl</a><a id="3554" class="Symbol">)</a> <a id="3556" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a>
   <a id="3561" class="Symbol">(Î»</a> <a id="3564" href="Modal.Open.html#3564" class="Bound">f</a> <a id="3566" class="Symbol">â†’</a> <a id="3568" href="UF.FunExt.html#1242" class="Function">dfunext</a> <a id="3576" href="Modal.Open.html#677" class="Bound">fe</a> <a id="3579" class="Symbol">(Î»</a> <a id="3582" href="Modal.Open.html#3582" class="Bound">z&#39;</a> <a id="3585" class="Symbol">â†’</a> <a id="3587" href="MLTT.Id.html#1136" class="Function">ap</a> <a id="3590" href="Modal.Open.html#3564" class="Bound">f</a> <a id="3592" class="Symbol">(</a><a id="3593" href="Modal.Open.html#824" class="Bound">P-is-prop</a> <a id="3603" href="Modal.Open.html#3482" class="Bound">z</a> <a id="3605" href="Modal.Open.html#3582" class="Bound">z&#39;</a><a id="3607" class="Symbol">)))))</a>

<a id="3614" class="Markup">\end{code}</a><a id="3624" class="Background">
</a></pre></body></html>