<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>TypeTopology.SimpleTypes</title><link rel="stylesheet" href="Agda.css"><script type="text/javascript" src="highlight-hover.js"></script></head><body><pre class="Agda"><a id="1" class="Background">Martin Escardo, January 2018

The simple types are the smallest collection of types containing ℕ and
closed under exponentials (function types).  All simple types are
totally separated and retracts of 𝟚. This is used to show that no
simple type is 𝟚-compact, unless WLPO holds. If 𝟚 is included as a
base simple type, then for (X → Y) to be compact it is necessary that
X is discrete and Y is compact. (It is consistent that the converse
holds (Tychonoff Theorem).)

</a><a id="468" class="Markup">\begin{code}</a>

<a id="482" class="Symbol">{-#</a> <a id="486" class="Keyword">OPTIONS</a> <a id="494" class="Pragma">--safe</a> <a id="501" class="Pragma">--without-K</a> <a id="513" class="Symbol">#-}</a>

<a id="518" class="Keyword">open</a> <a id="523" class="Keyword">import</a> <a id="530" href="MLTT.Spartan.html" class="Module">MLTT.Spartan</a>
<a id="543" class="Keyword">open</a> <a id="548" class="Keyword">import</a> <a id="555" href="UF.FunExt.html" class="Module">UF.FunExt</a>
<a id="565" class="Keyword">open</a> <a id="570" class="Keyword">import</a> <a id="577" href="UF.PropTrunc.html" class="Module">UF.PropTrunc</a>

<a id="591" class="Keyword">module</a> <a id="598" href="TypeTopology.SimpleTypes.html" class="Module">TypeTopology.SimpleTypes</a>
        <a id="631" class="Symbol">(</a><a id="632" href="TypeTopology.SimpleTypes.html#632" class="Bound">fe</a> <a id="635" class="Symbol">:</a> <a id="637" href="UF.FunExt.html#995" class="Function">FunExt</a><a id="643" class="Symbol">)</a>
        <a id="653" class="Symbol">(</a><a id="654" href="TypeTopology.SimpleTypes.html#654" class="Bound">pt</a> <a id="657" class="Symbol">:</a> <a id="659" href="UF.PropTrunc.html#486" class="Record">propositional-truncations-exist</a><a id="690" class="Symbol">)</a>
       <a id="699" class="Keyword">where</a>

<a id="706" class="Keyword">open</a> <a id="711" class="Keyword">import</a> <a id="718" href="TypeTopology.DisconnectedTypes.html" class="Module">TypeTopology.DisconnectedTypes</a>
<a id="749" class="Keyword">open</a> <a id="754" class="Keyword">import</a> <a id="761" href="TypeTopology.TotallySeparated.html" class="Module">TypeTopology.TotallySeparated</a>
<a id="791" class="Keyword">open</a> <a id="796" class="Keyword">import</a> <a id="803" href="TypeTopology.WeaklyCompactTypes.html" class="Module">TypeTopology.WeaklyCompactTypes</a> <a id="835" href="TypeTopology.SimpleTypes.html#632" class="Bound">fe</a> <a id="838" href="TypeTopology.SimpleTypes.html#654" class="Bound">pt</a>
            <a id="853" class="Keyword">renaming</a> <a id="862" class="Symbol">(</a><a id="863" href="TypeTopology.WeaklyCompactTypes.html#1775" class="Function">is-Π-compact</a> <a id="876" class="Symbol">to</a> <a id="879" class="Function">is-compact</a><a id="889" class="Symbol">)</a>
<a id="891" class="Keyword">open</a> <a id="896" class="Keyword">import</a> <a id="903" href="UF.DiscreteAndSeparated.html" class="Module">UF.DiscreteAndSeparated</a>
<a id="927" class="Keyword">open</a> <a id="932" class="Keyword">import</a> <a id="939" href="UF.Retracts.html" class="Module">UF.Retracts</a>
<a id="951" class="Keyword">open</a> <a id="956" class="Keyword">import</a> <a id="963" href="UF.Retracts-FunExt.html" class="Module">UF.Retracts-FunExt</a>

<a id="983" class="Keyword">data</a> <a id="simple-type"></a><a id="988" href="TypeTopology.SimpleTypes.html#988" class="Datatype">simple-type</a> <a id="1000" class="Symbol">:</a> <a id="1002" href="Agda.Primitive.html#915" class="Primitive">𝓤₀</a> <a id="1005" href="MLTT.Universes.html#3977" class="Function Operator">̇</a> <a id="1007" class="Symbol">→</a> <a id="1009" href="MLTT.Universes.html#4022" class="Function">𝓤₁</a> <a id="1012" href="MLTT.Universes.html#3977" class="Function Operator">̇</a> <a id="1014" class="Keyword">where</a>
 <a id="simple-type.base"></a><a id="1021" href="TypeTopology.SimpleTypes.html#1021" class="InductiveConstructor">base</a> <a id="1026" class="Symbol">:</a> <a id="1028" href="TypeTopology.SimpleTypes.html#988" class="Datatype">simple-type</a> <a id="1040" href="MLTT.Natural-Numbers-Type.html#96" class="Datatype">ℕ</a>
 <a id="simple-type.step"></a><a id="1043" href="TypeTopology.SimpleTypes.html#1043" class="InductiveConstructor">step</a> <a id="1048" class="Symbol">:</a> <a id="1050" class="Symbol">{</a><a id="1051" href="TypeTopology.SimpleTypes.html#1051" class="Bound">X</a> <a id="1053" href="TypeTopology.SimpleTypes.html#1053" class="Bound">Y</a> <a id="1055" class="Symbol">:</a> <a id="1057" href="Agda.Primitive.html#915" class="Primitive">𝓤₀</a> <a id="1060" href="MLTT.Universes.html#3977" class="Function Operator">̇</a> <a id="1062" class="Symbol">}</a> <a id="1064" class="Symbol">→</a> <a id="1066" href="TypeTopology.SimpleTypes.html#988" class="Datatype">simple-type</a> <a id="1078" href="TypeTopology.SimpleTypes.html#1051" class="Bound">X</a> <a id="1080" class="Symbol">→</a> <a id="1082" href="TypeTopology.SimpleTypes.html#988" class="Datatype">simple-type</a> <a id="1094" href="TypeTopology.SimpleTypes.html#1053" class="Bound">Y</a> <a id="1096" class="Symbol">→</a> <a id="1098" href="TypeTopology.SimpleTypes.html#988" class="Datatype">simple-type</a> <a id="1110" class="Symbol">(</a><a id="1111" href="TypeTopology.SimpleTypes.html#1051" class="Bound">X</a> <a id="1113" class="Symbol">→</a> <a id="1115" href="TypeTopology.SimpleTypes.html#1053" class="Bound">Y</a><a id="1116" class="Symbol">)</a>


<a id="simple-types-are-totally-separated"></a><a id="1120" href="TypeTopology.SimpleTypes.html#1120" class="Function">simple-types-are-totally-separated</a> <a id="1155" class="Symbol">:</a> <a id="1157" class="Symbol">{</a><a id="1158" href="TypeTopology.SimpleTypes.html#1158" class="Bound">X</a> <a id="1160" class="Symbol">:</a> <a id="1162" href="Agda.Primitive.html#915" class="Primitive">𝓤₀</a> <a id="1165" href="MLTT.Universes.html#3977" class="Function Operator">̇</a> <a id="1167" class="Symbol">}</a>
                                   <a id="1204" class="Symbol">→</a> <a id="1206" href="TypeTopology.SimpleTypes.html#988" class="Datatype">simple-type</a> <a id="1218" href="TypeTopology.SimpleTypes.html#1158" class="Bound">X</a>
                                   <a id="1255" class="Symbol">→</a> <a id="1257" href="TypeTopology.TotallySeparated.html#3866" class="Function">is-totally-separated</a> <a id="1278" href="TypeTopology.SimpleTypes.html#1158" class="Bound">X</a>
<a id="1280" href="TypeTopology.SimpleTypes.html#1120" class="Function">simple-types-are-totally-separated</a> <a id="1315" href="TypeTopology.SimpleTypes.html#1021" class="InductiveConstructor">base</a>       <a id="1326" class="Symbol">=</a> <a id="1328" href="TypeTopology.TotallySeparated.html#14605" class="Function">ℕ-is-totally-separated</a>
<a id="1351" href="TypeTopology.SimpleTypes.html#1120" class="Function">simple-types-are-totally-separated</a> <a id="1386" class="Symbol">(</a><a id="1387" href="TypeTopology.SimpleTypes.html#1043" class="InductiveConstructor">step</a> <a id="1392" href="TypeTopology.SimpleTypes.html#1392" class="Bound">s</a> <a id="1394" href="TypeTopology.SimpleTypes.html#1394" class="Bound">t</a><a id="1395" class="Symbol">)</a> <a id="1397" class="Symbol">=</a> <a id="1399" href="TypeTopology.TotallySeparated.html#14052" class="Function">Π-is-totally-separated</a> <a id="1422" class="Symbol">(</a><a id="1423" href="TypeTopology.SimpleTypes.html#632" class="Bound">fe</a> <a id="1426" href="Agda.Primitive.html#915" class="Primitive">𝓤₀</a> <a id="1429" href="Agda.Primitive.html#915" class="Primitive">𝓤₀</a><a id="1431" class="Symbol">)</a>
                                                 <a id="1482" class="Symbol">λ</a> <a id="1484" href="TypeTopology.SimpleTypes.html#1484" class="Bound">_</a> <a id="1486" class="Symbol">→</a> <a id="1488" href="TypeTopology.SimpleTypes.html#1120" class="Function">simple-types-are-totally-separated</a> <a id="1523" href="TypeTopology.SimpleTypes.html#1394" class="Bound">t</a>

<a id="simple-types-pointed"></a><a id="1526" href="TypeTopology.SimpleTypes.html#1526" class="Function">simple-types-pointed</a> <a id="1547" class="Symbol">:</a> <a id="1549" class="Symbol">{</a><a id="1550" href="TypeTopology.SimpleTypes.html#1550" class="Bound">X</a> <a id="1552" class="Symbol">:</a> <a id="1554" href="Agda.Primitive.html#915" class="Primitive">𝓤₀</a> <a id="1557" href="MLTT.Universes.html#3977" class="Function Operator">̇</a> <a id="1559" class="Symbol">}</a> <a id="1561" class="Symbol">→</a> <a id="1563" href="TypeTopology.SimpleTypes.html#988" class="Datatype">simple-type</a> <a id="1575" href="TypeTopology.SimpleTypes.html#1550" class="Bound">X</a> <a id="1577" class="Symbol">→</a> <a id="1579" href="TypeTopology.SimpleTypes.html#1550" class="Bound">X</a>
<a id="1581" href="TypeTopology.SimpleTypes.html#1526" class="Function">simple-types-pointed</a> <a id="1602" href="TypeTopology.SimpleTypes.html#1021" class="InductiveConstructor">base</a>       <a id="1613" class="Symbol">=</a> <a id="1615" href="MLTT.Natural-Numbers-Type.html#113" class="InductiveConstructor">zero</a>
<a id="1620" href="TypeTopology.SimpleTypes.html#1526" class="Function">simple-types-pointed</a> <a id="1641" class="Symbol">(</a><a id="1642" href="TypeTopology.SimpleTypes.html#1043" class="InductiveConstructor">step</a> <a id="1647" href="TypeTopology.SimpleTypes.html#1647" class="Bound">s</a> <a id="1649" href="TypeTopology.SimpleTypes.html#1649" class="Bound">t</a><a id="1650" class="Symbol">)</a> <a id="1652" class="Symbol">=</a> <a id="1654" class="Symbol">λ</a> <a id="1656" href="TypeTopology.SimpleTypes.html#1656" class="Bound">x</a> <a id="1658" class="Symbol">→</a> <a id="1660" href="TypeTopology.SimpleTypes.html#1526" class="Function">simple-types-pointed</a> <a id="1681" href="TypeTopology.SimpleTypes.html#1649" class="Bound">t</a>

<a id="simple-types-r"></a><a id="1684" href="TypeTopology.SimpleTypes.html#1684" class="Function">simple-types-r</a> <a id="1699" class="Symbol">:</a> <a id="1701" class="Symbol">{</a><a id="1702" href="TypeTopology.SimpleTypes.html#1702" class="Bound">X</a> <a id="1704" href="TypeTopology.SimpleTypes.html#1704" class="Bound">A</a> <a id="1706" class="Symbol">:</a> <a id="1708" href="Agda.Primitive.html#915" class="Primitive">𝓤₀</a> <a id="1711" href="MLTT.Universes.html#3977" class="Function Operator">̇</a> <a id="1713" class="Symbol">}</a> <a id="1715" class="Symbol">→</a> <a id="1717" href="UF.Retracts.html#1303" class="Function Operator">retract</a> <a id="1725" href="TypeTopology.SimpleTypes.html#1704" class="Bound">A</a> <a id="1727" href="UF.Retracts.html#1303" class="Function Operator">of</a> <a id="1730" href="MLTT.Natural-Numbers-Type.html#96" class="Datatype">ℕ</a> <a id="1732" class="Symbol">→</a> <a id="1734" href="TypeTopology.SimpleTypes.html#988" class="Datatype">simple-type</a> <a id="1746" href="TypeTopology.SimpleTypes.html#1702" class="Bound">X</a> <a id="1748" class="Symbol">→</a> <a id="1750" href="UF.Retracts.html#1303" class="Function Operator">retract</a> <a id="1758" href="TypeTopology.SimpleTypes.html#1704" class="Bound">A</a> <a id="1760" href="UF.Retracts.html#1303" class="Function Operator">of</a> <a id="1763" href="TypeTopology.SimpleTypes.html#1702" class="Bound">X</a>
<a id="1765" href="TypeTopology.SimpleTypes.html#1684" class="Function">simple-types-r</a> <a id="1780" href="TypeTopology.SimpleTypes.html#1780" class="Bound">rn</a> <a id="1783" href="TypeTopology.SimpleTypes.html#1021" class="InductiveConstructor">base</a>       <a id="1794" class="Symbol">=</a> <a id="1796" href="TypeTopology.SimpleTypes.html#1780" class="Bound">rn</a>
<a id="1799" href="TypeTopology.SimpleTypes.html#1684" class="Function">simple-types-r</a> <a id="1814" href="TypeTopology.SimpleTypes.html#1814" class="Bound">rn</a> <a id="1817" class="Symbol">(</a><a id="1818" href="TypeTopology.SimpleTypes.html#1043" class="InductiveConstructor">step</a> <a id="1823" href="TypeTopology.SimpleTypes.html#1823" class="Bound">s</a> <a id="1825" href="TypeTopology.SimpleTypes.html#1825" class="Bound">t</a><a id="1826" class="Symbol">)</a> <a id="1828" class="Symbol">=</a> <a id="1830" href="UF.Retracts-FunExt.html#1647" class="Function">retracts-of-closed-under-exponentials</a>
                                 <a id="1901" class="Symbol">(</a><a id="1902" href="TypeTopology.SimpleTypes.html#632" class="Bound">fe</a> <a id="1905" href="Agda.Primitive.html#915" class="Primitive">𝓤₀</a> <a id="1908" href="Agda.Primitive.html#915" class="Primitive">𝓤₀</a><a id="1910" class="Symbol">)</a>
                                 <a id="1945" class="Symbol">(</a><a id="1946" href="TypeTopology.SimpleTypes.html#1526" class="Function">simple-types-pointed</a> <a id="1967" href="TypeTopology.SimpleTypes.html#1823" class="Bound">s</a><a id="1968" class="Symbol">)</a>
                                 <a id="2003" class="Symbol">(</a><a id="2004" href="TypeTopology.SimpleTypes.html#1684" class="Function">simple-types-r</a> <a id="2019" href="TypeTopology.SimpleTypes.html#1814" class="Bound">rn</a> <a id="2022" href="TypeTopology.SimpleTypes.html#1823" class="Bound">s</a><a id="2023" class="Symbol">)</a>
                                 <a id="2058" class="Symbol">(</a><a id="2059" href="TypeTopology.SimpleTypes.html#1684" class="Function">simple-types-r</a> <a id="2074" href="TypeTopology.SimpleTypes.html#1814" class="Bound">rn</a> <a id="2077" href="TypeTopology.SimpleTypes.html#1825" class="Bound">t</a><a id="2078" class="Symbol">)</a>

<a id="cfdbce"></a><a id="2081" href="TypeTopology.SimpleTypes.html#2081" class="Function">cfdbce</a> <a id="2088" class="Symbol">:</a> <a id="2090" class="Symbol">{</a><a id="2091" href="TypeTopology.SimpleTypes.html#2091" class="Bound">X</a> <a id="2093" href="TypeTopology.SimpleTypes.html#2093" class="Bound">Y</a> <a id="2095" class="Symbol">:</a> <a id="2097" href="Agda.Primitive.html#915" class="Primitive">𝓤₀</a> <a id="2100" href="MLTT.Universes.html#3977" class="Function Operator">̇</a> <a id="2102" class="Symbol">}</a>
       <a id="2111" class="Symbol">→</a> <a id="2113" href="TypeTopology.SimpleTypes.html#988" class="Datatype">simple-type</a> <a id="2125" href="TypeTopology.SimpleTypes.html#2091" class="Bound">X</a>
       <a id="2134" class="Symbol">→</a> <a id="2136" href="TypeTopology.SimpleTypes.html#988" class="Datatype">simple-type</a> <a id="2148" href="TypeTopology.SimpleTypes.html#2093" class="Bound">Y</a>
       <a id="2157" class="Symbol">→</a> <a id="2159" href="TypeTopology.SimpleTypes.html#879" class="Function">is-compact</a> <a id="2170" class="Symbol">(</a><a id="2171" href="TypeTopology.SimpleTypes.html#2091" class="Bound">X</a> <a id="2173" class="Symbol">→</a> <a id="2175" href="TypeTopology.SimpleTypes.html#2093" class="Bound">Y</a><a id="2176" class="Symbol">)</a>
       <a id="2185" class="Symbol">→</a> <a id="2187" href="UF.DiscreteAndSeparated.html#1853" class="Function">is-discrete</a> <a id="2199" href="TypeTopology.SimpleTypes.html#2091" class="Bound">X</a> <a id="2201" href="MLTT.Sigma.html#558" class="Function Operator">×</a> <a id="2203" href="TypeTopology.SimpleTypes.html#879" class="Function">is-compact</a> <a id="2214" href="TypeTopology.SimpleTypes.html#2093" class="Bound">Y</a>
<a id="2216" href="TypeTopology.SimpleTypes.html#2081" class="Function">cfdbce</a> <a id="2223" href="TypeTopology.SimpleTypes.html#2223" class="Bound">s</a> <a id="2225" href="TypeTopology.SimpleTypes.html#2225" class="Bound">t</a> <a id="2227" href="TypeTopology.SimpleTypes.html#2227" class="Bound">c</a> <a id="2229" class="Symbol">=</a> <a id="2231" href="TypeTopology.WeaklyCompactTypes.html#14930" class="Function">tscd₀</a>
                <a id="2253" class="Symbol">(</a><a id="2254" href="TypeTopology.SimpleTypes.html#1120" class="Function">simple-types-are-totally-separated</a> <a id="2289" href="TypeTopology.SimpleTypes.html#2223" class="Bound">s</a><a id="2290" class="Symbol">)</a>
                <a id="2308" class="Symbol">(</a><a id="2309" href="TypeTopology.SimpleTypes.html#1684" class="Function">simple-types-r</a> <a id="2324" href="TypeTopology.DisconnectedTypes.html#3662" class="Function">ℕ-is-disconnected</a> <a id="2342" href="TypeTopology.SimpleTypes.html#2225" class="Bound">t</a><a id="2343" class="Symbol">)</a> <a id="2345" href="TypeTopology.SimpleTypes.html#2227" class="Bound">c</a> <a id="2347" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a>
               <a id="2364" href="TypeTopology.WeaklyCompactTypes.html#10535" class="Function">Π-compact-exponential-with-pointed-domain-has-Π-compact-domain</a>
                <a id="2443" class="Symbol">(</a><a id="2444" href="TypeTopology.SimpleTypes.html#1526" class="Function">simple-types-pointed</a> <a id="2465" href="TypeTopology.SimpleTypes.html#2223" class="Bound">s</a><a id="2466" class="Symbol">)</a> <a id="2468" href="TypeTopology.SimpleTypes.html#2227" class="Bound">c</a>
<a id="2470" class="Markup">\end{code}</a><a id="2480" class="Background">

TODO: prove that WLPO&#39; is equivalent to WLPO. But notice that WLPO&#39; is
the original formulation of WLPO by Bishop (written in type theory).

We have that simple types are &quot;not&quot; compact:

</a><a id="2669" class="Markup">\begin{code}</a>

<a id="WLPO&#39;"></a><a id="2683" href="TypeTopology.SimpleTypes.html#2683" class="Function">WLPO&#39;</a> <a id="2689" class="Symbol">:</a> <a id="2691" href="Agda.Primitive.html#915" class="Primitive">𝓤₀</a> <a id="2694" href="MLTT.Universes.html#3977" class="Function Operator">̇</a>
<a id="2696" href="TypeTopology.SimpleTypes.html#2683" class="Function">WLPO&#39;</a> <a id="2702" class="Symbol">=</a> <a id="2704" href="TypeTopology.SimpleTypes.html#879" class="Function">is-compact</a> <a id="2715" href="MLTT.Natural-Numbers-Type.html#96" class="Datatype">ℕ</a>

<a id="stcwlpo"></a><a id="2718" href="TypeTopology.SimpleTypes.html#2718" class="Function">stcwlpo</a> <a id="2726" class="Symbol">:</a> <a id="2728" class="Symbol">{</a><a id="2729" href="TypeTopology.SimpleTypes.html#2729" class="Bound">X</a> <a id="2731" class="Symbol">:</a> <a id="2733" href="Agda.Primitive.html#915" class="Primitive">𝓤₀</a> <a id="2736" href="MLTT.Universes.html#3977" class="Function Operator">̇</a> <a id="2738" class="Symbol">}</a> <a id="2740" class="Symbol">→</a> <a id="2742" href="TypeTopology.SimpleTypes.html#988" class="Datatype">simple-type</a> <a id="2754" href="TypeTopology.SimpleTypes.html#2729" class="Bound">X</a> <a id="2756" class="Symbol">→</a> <a id="2758" href="TypeTopology.SimpleTypes.html#879" class="Function">is-compact</a> <a id="2769" href="TypeTopology.SimpleTypes.html#2729" class="Bound">X</a> <a id="2771" class="Symbol">→</a> <a id="2773" href="TypeTopology.SimpleTypes.html#2683" class="Function">WLPO&#39;</a>
<a id="2779" href="TypeTopology.SimpleTypes.html#2718" class="Function">stcwlpo</a> <a id="2787" href="TypeTopology.SimpleTypes.html#1021" class="InductiveConstructor">base</a> <a id="2792" href="TypeTopology.SimpleTypes.html#2792" class="Bound">c</a> <a id="2794" class="Symbol">=</a> <a id="2796" href="TypeTopology.SimpleTypes.html#2792" class="Bound">c</a>
<a id="2798" href="TypeTopology.SimpleTypes.html#2718" class="Function">stcwlpo</a> <a id="2806" class="Symbol">(</a><a id="2807" href="TypeTopology.SimpleTypes.html#1043" class="InductiveConstructor">step</a> <a id="2812" href="TypeTopology.SimpleTypes.html#2812" class="Bound">s</a> <a id="2814" href="TypeTopology.SimpleTypes.html#2814" class="Bound">t</a><a id="2815" class="Symbol">)</a> <a id="2817" href="TypeTopology.SimpleTypes.html#2817" class="Bound">c</a> <a id="2819" class="Symbol">=</a> <a id="2821" href="TypeTopology.SimpleTypes.html#2718" class="Function">stcwlpo</a> <a id="2829" href="TypeTopology.SimpleTypes.html#2814" class="Bound">t</a> <a id="2831" class="Symbol">(</a><a id="2832" href="MLTT.Sigma-Type.html#209" class="Field">pr₂</a> <a id="2836" class="Symbol">(</a><a id="2837" href="TypeTopology.SimpleTypes.html#2081" class="Function">cfdbce</a> <a id="2844" href="TypeTopology.SimpleTypes.html#2812" class="Bound">s</a> <a id="2846" href="TypeTopology.SimpleTypes.html#2814" class="Bound">t</a> <a id="2848" href="TypeTopology.SimpleTypes.html#2817" class="Bound">c</a><a id="2849" class="Symbol">))</a>

<a id="2853" class="Markup">\end{code}</a><a id="2863" class="Background">

But, of course, the last consequence can be proved more directly by
simply showing that ℕ is a retract of every simple type, using the
fact that compactness is inherited by retracts, which doesn&#39;t rely
on the notion of total separatedness:

</a><a id="3106" class="Markup">\begin{code}</a>

<a id="ℕ-is-retract-of-any-simple-type"></a><a id="3120" href="TypeTopology.SimpleTypes.html#3120" class="Function">ℕ-is-retract-of-any-simple-type</a> <a id="3152" class="Symbol">:</a> <a id="3154" class="Symbol">{</a><a id="3155" href="TypeTopology.SimpleTypes.html#3155" class="Bound">X</a> <a id="3157" class="Symbol">:</a> <a id="3159" href="Agda.Primitive.html#915" class="Primitive">𝓤₀</a> <a id="3162" href="MLTT.Universes.html#3977" class="Function Operator">̇</a> <a id="3164" class="Symbol">}</a> <a id="3166" class="Symbol">→</a> <a id="3168" href="TypeTopology.SimpleTypes.html#988" class="Datatype">simple-type</a> <a id="3180" href="TypeTopology.SimpleTypes.html#3155" class="Bound">X</a> <a id="3182" class="Symbol">→</a> <a id="3184" href="UF.Retracts.html#1303" class="Function Operator">retract</a> <a id="3192" href="MLTT.Natural-Numbers-Type.html#96" class="Datatype">ℕ</a> <a id="3194" href="UF.Retracts.html#1303" class="Function Operator">of</a> <a id="3197" href="TypeTopology.SimpleTypes.html#3155" class="Bound">X</a>
<a id="3199" href="TypeTopology.SimpleTypes.html#3120" class="Function">ℕ-is-retract-of-any-simple-type</a> <a id="3231" class="Symbol">=</a> <a id="3233" href="TypeTopology.SimpleTypes.html#1684" class="Function">simple-types-r</a> <a id="3248" href="UF.Retracts.html#2956" class="Function">identity-retraction</a>

<a id="stcwlpo&#39;"></a><a id="3269" href="TypeTopology.SimpleTypes.html#3269" class="Function">stcwlpo&#39;</a> <a id="3278" class="Symbol">:</a> <a id="3280" class="Symbol">{</a><a id="3281" href="TypeTopology.SimpleTypes.html#3281" class="Bound">X</a> <a id="3283" class="Symbol">:</a> <a id="3285" href="Agda.Primitive.html#915" class="Primitive">𝓤₀</a> <a id="3288" href="MLTT.Universes.html#3977" class="Function Operator">̇</a> <a id="3290" class="Symbol">}</a> <a id="3292" class="Symbol">→</a> <a id="3294" href="TypeTopology.SimpleTypes.html#988" class="Datatype">simple-type</a> <a id="3306" href="TypeTopology.SimpleTypes.html#3281" class="Bound">X</a> <a id="3308" class="Symbol">→</a> <a id="3310" href="TypeTopology.SimpleTypes.html#879" class="Function">is-compact</a> <a id="3321" href="TypeTopology.SimpleTypes.html#3281" class="Bound">X</a> <a id="3323" class="Symbol">→</a> <a id="3325" href="TypeTopology.SimpleTypes.html#2683" class="Function">WLPO&#39;</a>
<a id="3331" href="TypeTopology.SimpleTypes.html#3269" class="Function">stcwlpo&#39;</a> <a id="3340" href="TypeTopology.SimpleTypes.html#3340" class="Bound">s</a> <a id="3342" class="Symbol">=</a> <a id="3344" href="TypeTopology.WeaklyCompactTypes.html#9913" class="Function">retract-is-Π-compact</a> <a id="3365" class="Symbol">(</a><a id="3366" href="TypeTopology.SimpleTypes.html#3120" class="Function">ℕ-is-retract-of-any-simple-type</a> <a id="3398" href="TypeTopology.SimpleTypes.html#3340" class="Bound">s</a><a id="3399" class="Symbol">)</a>

<a id="3402" class="Markup">\end{code}</a><a id="3412" class="Background">

To make this less trivial, we include 𝟚 as a base type in the
definition of simple types:

</a><a id="3505" class="Markup">\begin{code}</a>

<a id="3519" class="Keyword">data</a> <a id="simple-type₂"></a><a id="3524" href="TypeTopology.SimpleTypes.html#3524" class="Datatype">simple-type₂</a> <a id="3537" class="Symbol">:</a> <a id="3539" href="Agda.Primitive.html#915" class="Primitive">𝓤₀</a> <a id="3542" href="MLTT.Universes.html#3977" class="Function Operator">̇</a> <a id="3544" class="Symbol">→</a> <a id="3546" href="MLTT.Universes.html#4022" class="Function">𝓤₁</a> <a id="3549" href="MLTT.Universes.html#3977" class="Function Operator">̇</a> <a id="3551" class="Keyword">where</a>
 <a id="simple-type₂.base₂"></a><a id="3558" href="TypeTopology.SimpleTypes.html#3558" class="InductiveConstructor">base₂</a> <a id="3564" class="Symbol">:</a> <a id="3566" href="TypeTopology.SimpleTypes.html#3524" class="Datatype">simple-type₂</a> <a id="3579" href="MLTT.Two.html#124" class="Datatype">𝟚</a>
 <a id="simple-type₂.base"></a><a id="3582" href="TypeTopology.SimpleTypes.html#3582" class="InductiveConstructor">base</a>  <a id="3588" class="Symbol">:</a> <a id="3590" href="TypeTopology.SimpleTypes.html#3524" class="Datatype">simple-type₂</a> <a id="3603" href="MLTT.Natural-Numbers-Type.html#96" class="Datatype">ℕ</a>
 <a id="simple-type₂.step"></a><a id="3606" href="TypeTopology.SimpleTypes.html#3606" class="InductiveConstructor">step</a>  <a id="3612" class="Symbol">:</a> <a id="3614" class="Symbol">{</a><a id="3615" href="TypeTopology.SimpleTypes.html#3615" class="Bound">X</a> <a id="3617" href="TypeTopology.SimpleTypes.html#3617" class="Bound">Y</a> <a id="3619" class="Symbol">:</a> <a id="3621" href="Agda.Primitive.html#915" class="Primitive">𝓤₀</a> <a id="3624" href="MLTT.Universes.html#3977" class="Function Operator">̇</a> <a id="3626" class="Symbol">}</a> <a id="3628" class="Symbol">→</a> <a id="3630" href="TypeTopology.SimpleTypes.html#3524" class="Datatype">simple-type₂</a> <a id="3643" href="TypeTopology.SimpleTypes.html#3615" class="Bound">X</a> <a id="3645" class="Symbol">→</a> <a id="3647" href="TypeTopology.SimpleTypes.html#3524" class="Datatype">simple-type₂</a> <a id="3660" href="TypeTopology.SimpleTypes.html#3617" class="Bound">Y</a> <a id="3662" class="Symbol">→</a> <a id="3664" href="TypeTopology.SimpleTypes.html#3524" class="Datatype">simple-type₂</a> <a id="3677" class="Symbol">(</a><a id="3678" href="TypeTopology.SimpleTypes.html#3615" class="Bound">X</a> <a id="3680" class="Symbol">→</a> <a id="3682" href="TypeTopology.SimpleTypes.html#3617" class="Bound">Y</a><a id="3683" class="Symbol">)</a>

<a id="3686" class="Markup">\end{code}</a><a id="3696" class="Background">

Then now, potentially, there are compact types such as the Cantor
space (ℕ → 𝟚), and more generally (X → Y) with X discrete and Y
compact, if Tychonoff holds (which is consistent but not
provable). The following then says that this is the only possibility:
If X and Y simple types in this generalized sense, for (X → Y) to be
compact, it is necessary that X is discrete and Y is compact.

</a><a id="4087" class="Markup">\begin{code}</a>

<a id="simple-types₂-totally-separated"></a><a id="4101" href="TypeTopology.SimpleTypes.html#4101" class="Function">simple-types₂-totally-separated</a> <a id="4133" class="Symbol">:</a> <a id="4135" class="Symbol">{</a><a id="4136" href="TypeTopology.SimpleTypes.html#4136" class="Bound">X</a> <a id="4138" class="Symbol">:</a> <a id="4140" href="Agda.Primitive.html#915" class="Primitive">𝓤₀</a> <a id="4143" href="MLTT.Universes.html#3977" class="Function Operator">̇</a> <a id="4145" class="Symbol">}</a>
                                <a id="4179" class="Symbol">→</a> <a id="4181" href="TypeTopology.SimpleTypes.html#3524" class="Datatype">simple-type₂</a> <a id="4194" href="TypeTopology.SimpleTypes.html#4136" class="Bound">X</a>
                                <a id="4228" class="Symbol">→</a> <a id="4230" href="TypeTopology.TotallySeparated.html#3866" class="Function">is-totally-separated</a> <a id="4251" href="TypeTopology.SimpleTypes.html#4136" class="Bound">X</a>
<a id="4253" href="TypeTopology.SimpleTypes.html#4101" class="Function">simple-types₂-totally-separated</a> <a id="4285" href="TypeTopology.SimpleTypes.html#3558" class="InductiveConstructor">base₂</a>       <a id="4297" class="Symbol">=</a> <a id="4299" href="TypeTopology.TotallySeparated.html#13971" class="Function">𝟚-is-totally-separated</a>
<a id="4322" href="TypeTopology.SimpleTypes.html#4101" class="Function">simple-types₂-totally-separated</a> <a id="4354" href="TypeTopology.SimpleTypes.html#3582" class="InductiveConstructor">base</a>        <a id="4366" class="Symbol">=</a> <a id="4368" href="TypeTopology.TotallySeparated.html#14605" class="Function">ℕ-is-totally-separated</a>
<a id="4391" href="TypeTopology.SimpleTypes.html#4101" class="Function">simple-types₂-totally-separated</a> <a id="4423" class="Symbol">(</a><a id="4424" href="TypeTopology.SimpleTypes.html#3606" class="InductiveConstructor">step</a> <a id="4429" href="TypeTopology.SimpleTypes.html#4429" class="Bound">s</a> <a id="4431" href="TypeTopology.SimpleTypes.html#4431" class="Bound">t</a><a id="4432" class="Symbol">)</a>  <a id="4435" class="Symbol">=</a>
 <a id="4438" href="TypeTopology.TotallySeparated.html#14052" class="Function">Π-is-totally-separated</a> <a id="4461" class="Symbol">(</a><a id="4462" href="TypeTopology.SimpleTypes.html#632" class="Bound">fe</a> <a id="4465" href="Agda.Primitive.html#915" class="Primitive">𝓤₀</a> <a id="4468" href="Agda.Primitive.html#915" class="Primitive">𝓤₀</a><a id="4470" class="Symbol">)</a>
  <a id="4474" class="Symbol">(λ</a> <a id="4477" href="TypeTopology.SimpleTypes.html#4477" class="Bound">_</a> <a id="4479" class="Symbol">→</a> <a id="4481" href="TypeTopology.SimpleTypes.html#4101" class="Function">simple-types₂-totally-separated</a> <a id="4513" href="TypeTopology.SimpleTypes.html#4431" class="Bound">t</a><a id="4514" class="Symbol">)</a>

<a id="simple-types₂-pointed"></a><a id="4517" href="TypeTopology.SimpleTypes.html#4517" class="Function">simple-types₂-pointed</a> <a id="4539" class="Symbol">:</a> <a id="4541" class="Symbol">{</a><a id="4542" href="TypeTopology.SimpleTypes.html#4542" class="Bound">X</a> <a id="4544" class="Symbol">:</a> <a id="4546" href="Agda.Primitive.html#915" class="Primitive">𝓤₀</a> <a id="4549" href="MLTT.Universes.html#3977" class="Function Operator">̇</a> <a id="4551" class="Symbol">}</a> <a id="4553" class="Symbol">→</a> <a id="4555" href="TypeTopology.SimpleTypes.html#3524" class="Datatype">simple-type₂</a> <a id="4568" href="TypeTopology.SimpleTypes.html#4542" class="Bound">X</a> <a id="4570" class="Symbol">→</a> <a id="4572" href="TypeTopology.SimpleTypes.html#4542" class="Bound">X</a>
<a id="4574" href="TypeTopology.SimpleTypes.html#4517" class="Function">simple-types₂-pointed</a> <a id="4596" href="TypeTopology.SimpleTypes.html#3558" class="InductiveConstructor">base₂</a>      <a id="4607" class="Symbol">=</a> <a id="4609" href="MLTT.Two.html#140" class="InductiveConstructor">₀</a>
<a id="4611" href="TypeTopology.SimpleTypes.html#4517" class="Function">simple-types₂-pointed</a> <a id="4633" href="TypeTopology.SimpleTypes.html#3582" class="InductiveConstructor">base</a>       <a id="4644" class="Symbol">=</a> <a id="4646" href="MLTT.Natural-Numbers-Type.html#113" class="InductiveConstructor">zero</a>
<a id="4651" href="TypeTopology.SimpleTypes.html#4517" class="Function">simple-types₂-pointed</a> <a id="4673" class="Symbol">(</a><a id="4674" href="TypeTopology.SimpleTypes.html#3606" class="InductiveConstructor">step</a> <a id="4679" href="TypeTopology.SimpleTypes.html#4679" class="Bound">s</a> <a id="4681" href="TypeTopology.SimpleTypes.html#4681" class="Bound">t</a><a id="4682" class="Symbol">)</a> <a id="4684" class="Symbol">=</a> <a id="4686" class="Symbol">λ</a> <a id="4688" href="TypeTopology.SimpleTypes.html#4688" class="Bound">x</a> <a id="4690" class="Symbol">→</a> <a id="4692" href="TypeTopology.SimpleTypes.html#4517" class="Function">simple-types₂-pointed</a> <a id="4714" href="TypeTopology.SimpleTypes.html#4681" class="Bound">t</a>

<a id="simple-types₂-disconnected"></a><a id="4717" href="TypeTopology.SimpleTypes.html#4717" class="Function">simple-types₂-disconnected</a> <a id="4744" class="Symbol">:</a> <a id="4746" class="Symbol">{</a><a id="4747" href="TypeTopology.SimpleTypes.html#4747" class="Bound">X</a> <a id="4749" class="Symbol">:</a> <a id="4751" href="Agda.Primitive.html#915" class="Primitive">𝓤₀</a> <a id="4754" href="MLTT.Universes.html#3977" class="Function Operator">̇</a> <a id="4756" class="Symbol">}</a> <a id="4758" class="Symbol">→</a> <a id="4760" href="TypeTopology.SimpleTypes.html#3524" class="Datatype">simple-type₂</a> <a id="4773" href="TypeTopology.SimpleTypes.html#4747" class="Bound">X</a> <a id="4775" class="Symbol">→</a> <a id="4777" href="TypeTopology.DisconnectedTypes.html#3477" class="Function">is-disconnected</a> <a id="4793" href="TypeTopology.SimpleTypes.html#4747" class="Bound">X</a>
<a id="4795" href="TypeTopology.SimpleTypes.html#4717" class="Function">simple-types₂-disconnected</a> <a id="4822" href="TypeTopology.SimpleTypes.html#3558" class="InductiveConstructor">base₂</a>      <a id="4833" class="Symbol">=</a> <a id="4835" href="UF.Retracts.html#2956" class="Function">identity-retraction</a>
<a id="4855" href="TypeTopology.SimpleTypes.html#4717" class="Function">simple-types₂-disconnected</a> <a id="4882" href="TypeTopology.SimpleTypes.html#3582" class="InductiveConstructor">base</a>       <a id="4893" class="Symbol">=</a> <a id="4895" href="TypeTopology.DisconnectedTypes.html#3662" class="Function">ℕ-is-disconnected</a>
<a id="4913" href="TypeTopology.SimpleTypes.html#4717" class="Function">simple-types₂-disconnected</a> <a id="4940" class="Symbol">(</a><a id="4941" href="TypeTopology.SimpleTypes.html#3606" class="InductiveConstructor">step</a> <a id="4946" href="TypeTopology.SimpleTypes.html#4946" class="Bound">s</a> <a id="4948" href="TypeTopology.SimpleTypes.html#4948" class="Bound">t</a><a id="4949" class="Symbol">)</a> <a id="4951" class="Symbol">=</a> <a id="4953" href="UF.Retracts-FunExt.html#1647" class="Function">retracts-of-closed-under-exponentials</a>
                                         <a id="5032" class="Symbol">(</a><a id="5033" href="TypeTopology.SimpleTypes.html#632" class="Bound">fe</a> <a id="5036" href="Agda.Primitive.html#915" class="Primitive">𝓤₀</a> <a id="5039" href="Agda.Primitive.html#915" class="Primitive">𝓤₀</a><a id="5041" class="Symbol">)</a>
                                         <a id="5084" class="Symbol">(</a><a id="5085" href="TypeTopology.SimpleTypes.html#4517" class="Function">simple-types₂-pointed</a> <a id="5107" href="TypeTopology.SimpleTypes.html#4946" class="Bound">s</a><a id="5108" class="Symbol">)</a>
                                         <a id="5151" class="Symbol">(</a><a id="5152" href="TypeTopology.SimpleTypes.html#4717" class="Function">simple-types₂-disconnected</a> <a id="5179" href="TypeTopology.SimpleTypes.html#4946" class="Bound">s</a><a id="5180" class="Symbol">)</a>
                                         <a id="5223" class="Symbol">(</a><a id="5224" href="TypeTopology.SimpleTypes.html#4717" class="Function">simple-types₂-disconnected</a> <a id="5251" href="TypeTopology.SimpleTypes.html#4948" class="Bound">t</a><a id="5252" class="Symbol">)</a>

<a id="cfdbce₂"></a><a id="5255" href="TypeTopology.SimpleTypes.html#5255" class="Function">cfdbce₂</a> <a id="5263" class="Symbol">:</a> <a id="5265" class="Symbol">{</a><a id="5266" href="TypeTopology.SimpleTypes.html#5266" class="Bound">X</a> <a id="5268" href="TypeTopology.SimpleTypes.html#5268" class="Bound">Y</a> <a id="5270" class="Symbol">:</a> <a id="5272" href="Agda.Primitive.html#915" class="Primitive">𝓤₀</a> <a id="5275" href="MLTT.Universes.html#3977" class="Function Operator">̇</a> <a id="5277" class="Symbol">}</a>
        <a id="5287" class="Symbol">→</a> <a id="5289" href="TypeTopology.SimpleTypes.html#3524" class="Datatype">simple-type₂</a> <a id="5302" href="TypeTopology.SimpleTypes.html#5266" class="Bound">X</a>
        <a id="5312" class="Symbol">→</a> <a id="5314" href="TypeTopology.SimpleTypes.html#3524" class="Datatype">simple-type₂</a> <a id="5327" href="TypeTopology.SimpleTypes.html#5268" class="Bound">Y</a>
        <a id="5337" class="Symbol">→</a> <a id="5339" href="TypeTopology.SimpleTypes.html#879" class="Function">is-compact</a> <a id="5350" class="Symbol">(</a><a id="5351" href="TypeTopology.SimpleTypes.html#5266" class="Bound">X</a> <a id="5353" class="Symbol">→</a> <a id="5355" href="TypeTopology.SimpleTypes.html#5268" class="Bound">Y</a><a id="5356" class="Symbol">)</a>
        <a id="5366" class="Symbol">→</a> <a id="5368" href="UF.DiscreteAndSeparated.html#1853" class="Function">is-discrete</a> <a id="5380" href="TypeTopology.SimpleTypes.html#5266" class="Bound">X</a> <a id="5382" href="MLTT.Sigma.html#558" class="Function Operator">×</a> <a id="5384" href="TypeTopology.SimpleTypes.html#879" class="Function">is-compact</a> <a id="5395" href="TypeTopology.SimpleTypes.html#5268" class="Bound">Y</a>
<a id="5397" href="TypeTopology.SimpleTypes.html#5255" class="Function">cfdbce₂</a> <a id="5405" href="TypeTopology.SimpleTypes.html#5405" class="Bound">s</a> <a id="5407" href="TypeTopology.SimpleTypes.html#5407" class="Bound">t</a> <a id="5409" href="TypeTopology.SimpleTypes.html#5409" class="Bound">c</a> <a id="5411" class="Symbol">=</a> <a id="5413" href="TypeTopology.WeaklyCompactTypes.html#14930" class="Function">tscd₀</a> <a id="5419" class="Symbol">(</a><a id="5420" href="TypeTopology.SimpleTypes.html#4101" class="Function">simple-types₂-totally-separated</a> <a id="5452" href="TypeTopology.SimpleTypes.html#5405" class="Bound">s</a><a id="5453" class="Symbol">)</a>
                      <a id="5477" class="Symbol">(</a><a id="5478" href="TypeTopology.SimpleTypes.html#4717" class="Function">simple-types₂-disconnected</a> <a id="5505" href="TypeTopology.SimpleTypes.html#5407" class="Bound">t</a><a id="5506" class="Symbol">)</a> <a id="5508" href="TypeTopology.SimpleTypes.html#5409" class="Bound">c</a> <a id="5510" href="MLTT.Sigma.html#395" class="InductiveConstructor Operator">,</a>
                <a id="5528" href="TypeTopology.WeaklyCompactTypes.html#10535" class="Function">Π-compact-exponential-with-pointed-domain-has-Π-compact-domain</a>
                 <a id="5608" class="Symbol">(</a><a id="5609" href="TypeTopology.SimpleTypes.html#4517" class="Function">simple-types₂-pointed</a> <a id="5631" href="TypeTopology.SimpleTypes.html#5405" class="Bound">s</a><a id="5632" class="Symbol">)</a>
                 <a id="5651" href="TypeTopology.SimpleTypes.html#5409" class="Bound">c</a>

<a id="5654" class="Markup">\end{code}</a><a id="5664" class="Background">
</a></pre></body></html>