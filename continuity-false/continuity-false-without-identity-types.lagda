Martin Escardo 2015.

If all functions (â„• â†’ â„•) â†’ â„• are continuous then 0=1.

Based on the version of 27 Nov 2013
http://www.cs.bham.ac.uk/~mhe/continuity-false/continuity-false.html

In this version we don't use identity types, but we use a universe
instead, in order to define equality on â„•. This is the only equality
type we consider, and we name it â‰¡.

Standard preliminaries:

\begin{code}

{-# OPTIONS --without-K #-}

U = Set

data Î£ {X : U} (Y : X â†’ U) : U where
 _,_ : (x : X)(y : Y x) â†’ Î£ {X} Y

_Ã—_ : U â†’ U â†’ U
X Ã— Y = Î£ \(x : X) â†’ Y

Ï€â‚€ : {X : U} {Y : X â†’ U} â†’ (Î£ \(x : X) â†’ Y x) â†’ X
Ï€â‚€(x , y) = x

Ï€â‚ : {X : U} {Y : X â†’ U} (t : Î£ \(x : X) â†’ Y x) â†’ Y(Ï€â‚€ t)
Ï€â‚(x , y) = y

data âˆ… : U where

âˆ…-rec : {X : U} â†’ âˆ… â†’ X
âˆ…-rec ()

data ğŸ™ : U where
 * : ğŸ™ 

ğŸ™-rec : {X : U} â†’ X â†’ ğŸ™ â†’ X
ğŸ™-rec x * = x

data â„• : U where 
 zero : â„•              
 succ : â„• â†’ â„•       

\end{code}

We define equality on â„• by induction and show that it satisfies the
defining properties of the identity type Id â„•.

\begin{code}

{-# BUILTIN NATURAL â„• #-}
infix 3 _â‰¡_

_â‰¡_ : â„• â†’ â„• â†’ U
0        â‰¡ 0        = ğŸ™ 
(succ m) â‰¡ 0        = âˆ… 
0        â‰¡ (succ n) = âˆ… 
(succ m) â‰¡ (succ n) = m â‰¡ n 

refl : âˆ€ n â†’ n â‰¡ n
refl 0 = *
refl (succ n) = refl n

â‰¡-ind : (A : (m n : â„•) â†’ m â‰¡ n â†’ U)
      â†’ (âˆ€ n â†’ A n n (refl n)) â†’  âˆ€ m n p â†’ A m n p
-- â‰¡-ind A r 0 0 p = ğŸ™-rec (r 0) p -- doesn't type check.
â‰¡-ind A r 0 0 * = r 0
â‰¡-ind A r (succ m) 0 p = âˆ…-rec p
â‰¡-ind A r 0 (succ n) p = âˆ…-rec p 
â‰¡-ind A r (succ m) (succ n) p = â‰¡-ind (Î» m n â†’ A (succ m) (succ n)) (Î» n â†’ r(succ n)) m n p

-- We only use â‰¡-ind to define â‰¡-rec:

â‰¡-rec : (A : â„• â†’ â„• â†’ U) â†’ (âˆ€ n â†’ A n n) â†’  âˆ€ m n â†’ m â‰¡ n â†’ A m n
â‰¡-rec A = â‰¡-ind (Î» m n _ â†’ A m n)

-- We only use â‰¡-rec to define transport:

transport : (A : â„• â†’ U) â†’ {x y : â„•} â†’ x â‰¡ y â†’ A x â†’ A y
transport A {x} {y} = â‰¡-rec (Î» x y â†’ A x â†’ A y) (Î» _ a â†’ a) x y

-- From transport we prove all properties of equality we need here:

sym : âˆ€{x y} â†’ x â‰¡ y â†’ y â‰¡ x
sym {x} {y} p = transport (Î» z â†’ z â‰¡ x) {x} {y} p (refl x)

euclidean : âˆ€{a b c} â†’ b â‰¡ c â†’ b â‰¡ a â†’ c â‰¡ a
euclidean {a} {b} {c} = transport (Î» d â†’ d â‰¡ a) {b} {c}

trans : âˆ€{x y z} â†’ x â‰¡ y â†’ y â‰¡ z â†’ x â‰¡ z
trans {x} {y} {z} p = euclidean {z} {y} (sym {x} p)

ap : âˆ€(f : â„• â†’ â„•) â†’ âˆ€{x y} â†’ x â‰¡ y â†’ f x â‰¡ f y
ap f {x} {y} p = transport (Î» z â†’ f x â‰¡ f z) {x} {y} p (refl(f x))

\end{code}

The Baire space of infinite sequences of natural numbers, ranged over
by Î± and Î²:

\begin{code}

Baire : U
Baire = â„• â†’ â„•

head : {X : â„• â†’ U} â†’ ((i : â„•) â†’ X i) â†’ X 0
head Î± = Î± 0

tail : {X : â„• â†’ U} â†’ ((i : â„•) â†’ X i) â†’ ((i : â„•) â†’ X(succ i))
tail Î± = Î» i â†’ Î±(succ i)

_â‰¡[_]_ : Baire â†’ â„• â†’ Baire â†’ U
Î± â‰¡[ zero ] Î² = ğŸ™
Î± â‰¡[ succ n ] Î² = (head Î± â‰¡ head Î²) Ã— (tail Î± â‰¡[ n ] tail Î²)

-- The sequence consisting of n zeros followed by infinitely many k's
-- is written "n zeros-and-then k":

_zeros-and-then_ : â„• â†’ â„• â†’ Baire
( 0       zeros-and-then k)  i       = k
((succ n) zeros-and-then k)  0       = 0
((succ n) zeros-and-then k) (succ i) = (n zeros-and-then k) i

zeros-and-then-specâ‚€ : âˆ€ n k â†’ (n zeros-and-then k) n â‰¡ k
zeros-and-then-specâ‚€  0       k = refl k 
zeros-and-then-specâ‚€ (succ n) k = zeros-and-then-specâ‚€ n k

-- The sequence consisting of infinitely many zeros:

O : Baire
O = Î» i â†’ 0

zeros-and-then-specâ‚ : âˆ€ n k â†’ O â‰¡[ n ] (n zeros-and-then k)
zeros-and-then-specâ‚ zero k = *
zeros-and-then-specâ‚ (succ n) k = * , (zeros-and-then-specâ‚ n k)

\end{code}

We now come to the subject of this file. We define the Curry-Howard
interpretation of a Brouwerian continuity principle, and show that not
all functions are continuous. Notice that, by definition, 0â‰¡1 is âˆ….

\begin{code}

continuous : (Baire â†’ â„•) â†’ U
continuous f = âˆ€ Î± â†’ Î£ \n â†’ âˆ€ Î² â†’ Î± â‰¡[ n ] Î² â†’ f Î± â‰¡ f Î²

theorem : (âˆ€(f : Baire â†’ â„•) â†’ continuous f) â†’ 0 â‰¡ 1
theorem continuity = zero-is-one
 where
  -- The modulus-of-continuity functional at the point O : Baire:
  M : (Baire â†’ â„•) â†’ â„• 
  M f = Ï€â‚€(continuity f O)

  continuityâ‚€ : âˆ€ f Î² â†’ O â‰¡[ M f ] Î² â†’ f O â‰¡ f Î²
  continuityâ‚€ f = Ï€â‚(continuity f O)

  -- Any number is a modulus of continuity of a constant function. Our
  -- hypothetical modulus-of-continuity functional chooses m:
  m : â„•
  m = M(Î» Î± â†’ 0)

  -- To get the desired contradiction, we define a non-continuous
  -- function using M (twice):
  f : Baire â†’ â„•
  f Î² = M(Î» Î± â†’ Î²(Î± m))

  -- With definitional/judgemental equality, we have
  --
  --    f O = M(Î» Î± â†’ O(Î± m)) = M(Î» Î± â†’ 0) = m, 
  -- 
  -- because O(Î± m) = 0. Agda performs this conversion silently:
  crucial-observation : f O â‰¡ m
  crucial-observation = refl(f O)
  -- In a system based on combinators S and K rather than the
  -- Î»-calculus, this conversion is not available. Often HA^Ï‰ is taken
  -- in combinatory form, in which case one needs some form of
  -- extensionality to conclude that f O = m.

  -- The following crucial fact silently uses the above observation,
  -- without the need to invoke its proof refl(f O), because the
  -- equality f O = m is judgemental:
  crucial-fact : âˆ€ Î² â†’ O â‰¡[ M f ] Î² â†’ m â‰¡ f Î²
  crucial-fact = continuityâ‚€ f 

  lemmaâ‚€ : M f â‰¡ 0 â†’ 0 â‰¡ 1
  lemmaâ‚€ p = zero-is-one
   where
    câ‚€ : âˆ€ Î² â†’ O â‰¡[ M f ] Î²
    câ‚€ Î² = transport (Î» n â†’ O â‰¡[ n ] Î²) (sym {M f} p) (refl 0)  
    câ‚ : âˆ€ Î² â†’ m â‰¡ f Î²
    câ‚ Î² = crucial-fact Î² (câ‚€ Î²)
    câ‚‚ : M(Î» Î± â†’ Î± m) â‰¡ m
    câ‚‚ = sym {m} (câ‚ (Î» i â†’ i))
    câ‚ƒ : âˆ€ Î± â†’ O â‰¡[ M(Î» Î± â†’ Î± m) ] Î± â†’ 0 â‰¡ Î± m
    câ‚ƒ = continuityâ‚€ (Î» Î± â†’ Î± m) 
    câ‚„ : âˆ€ Î± â†’ O â‰¡[ m ] Î± â†’ 0 â‰¡ Î± m
    câ‚„ = transport (Î» n â†’ âˆ€ Î± â†’ O â‰¡[ n ] Î± â†’ 0 â‰¡ Î± m) câ‚‚ câ‚ƒ  
    Î± : Baire
    Î± = m zeros-and-then 1
    Î±-propertyâ‚€ : Î± m â‰¡ 1
    Î±-propertyâ‚€ = zeros-and-then-specâ‚€ m 1
    Î±-propertyâ‚ : O â‰¡[ m ] Î±
    Î±-propertyâ‚ = zeros-and-then-specâ‚ m 1
    câ‚… : 0 â‰¡ Î± m
    câ‚… = câ‚„ Î± Î±-propertyâ‚ 
    zero-is-one : 0 â‰¡ 1
    zero-is-one = trans {0} {Î± m} câ‚… Î±-propertyâ‚€

  lemmaâ‚ : (Î£ \n â†’ M f â‰¡ succ n) â†’ 0 â‰¡ 1
  lemmaâ‚ (n , p) = zero-is-one
   where
    Î² : Baire
    Î² = (M f) zeros-and-then 1
    Î²-propertyâ‚€ : Î²(M f) â‰¡ 1
    Î²-propertyâ‚€ = zeros-and-then-specâ‚€ (M f) 1
    Î²-propertyâ‚ : O â‰¡[ M f ] Î²
    Î²-propertyâ‚ = zeros-and-then-specâ‚ (M f) 1
    câ‚€ : f Î² â‰¡ m
    câ‚€ = sym {m} (crucial-fact Î² Î²-propertyâ‚)
    câ‚ : âˆ€ Î± â†’ O â‰¡[ f Î² ] Î± â†’ Î² 0 â‰¡ Î²(Î± m) 
    câ‚ Î± = continuityâ‚€ (Î» Î± â†’ Î²(Î± m)) Î±
    câ‚‚ : âˆ€ Î± â†’ O â‰¡[ m ] Î± â†’ Î² 0 â‰¡ Î²(Î± m) 
    câ‚‚ = transport (Î» n â†’ âˆ€ Î± â†’ O â‰¡[ n ] Î± â†’ Î² 0 â‰¡ Î²(Î± m)) câ‚€ câ‚ 
    Î± : Baire
    Î± = m zeros-and-then (M f)
    Î±-propertyâ‚€ : Î± m â‰¡ M f
    Î±-propertyâ‚€ = zeros-and-then-specâ‚€ m (M f)
    Î±-propertyâ‚ : O â‰¡[ m ] Î±
    Î±-propertyâ‚ = zeros-and-then-specâ‚ m (M f)
    câ‚ƒ : Î² 0 â‰¡ Î²(Î± m)
    câ‚ƒ = câ‚‚ Î± Î±-propertyâ‚
    câ‚… : Î²(Î± m) â‰¡ Î²(M f)
    câ‚… = ap Î² Î±-propertyâ‚€
    câ‚† : Î² (Î± m) â‰¡ 1
    câ‚† = trans {Î²(Î± m)} câ‚… Î²-propertyâ‚€
    câ‚„ : Î² 0 â‰¡ 1
    câ‚„ = trans {Î² 0} câ‚ƒ câ‚†
    câ‚ˆ : O â‰¡[ succ n ] Î²
    câ‚ˆ = transport (Î» n â†’ O â‰¡[ n ] Î²) p Î²-propertyâ‚
    câ‚‰ : O â‰¡[ succ n ] Î² â†’ 0 â‰¡ Î² 0
    câ‚‰ e = Ï€â‚€ e
    câ‚‡ : 0 â‰¡ Î² 0
    câ‚‡ = câ‚‰ câ‚ˆ
    zero-is-one : 0 â‰¡ 1
    zero-is-one = trans {0} {Î² 0} câ‚‡ câ‚„

  lemma : (Î£ \n â†’ M f â‰¡ n) â†’ 0 â‰¡ 1
  lemma (0      , p) = lemmaâ‚€ p
  lemma (succ n , p) = lemmaâ‚(n , p)

  zero-is-one : 0 â‰¡ 1
  zero-is-one = lemma(M f , refl(M f)) 

\end{code}

The following observation was communicated to me independently by
each of Altenkirch, Coquand and Martin-LÓ§f.

A continuous function is extensional in the sense that it assigns the
same value to pointwise equal inputs:

\begin{code}

continuous-functions-are-extensional : 
 âˆ€(f : Baire â†’ â„•) â†’ continuous f â†’ âˆ€ Î± Î² â†’ (âˆ€ i â†’ Î± i â‰¡ Î² i) â†’ f Î± â‰¡ f Î²
continuous-functions-are-extensional f f-continuous Î± Î² e = g Î² (h Î± Î² e n)
 where
  n : â„•
  n = Ï€â‚€(f-continuous Î±)
  g : âˆ€ Î² â†’ Î± â‰¡[ n ] Î² â†’ f Î± â‰¡ f Î² 
  g = Ï€â‚(f-continuous Î±)
  h : âˆ€ Î± Î² â†’ (âˆ€ i â†’ Î± i â‰¡ Î² i) â†’ âˆ€ n â†’ Î± â‰¡[ n ] Î²
  h Î± Î² e zero = *
  h Î± Î² e (succ n) = (e zero) , (h (tail Î±) (tail Î²) (tail e) n)

\end{code}

So there is some amount of extensionality built-in in the definition
of continuity.

And here is a simplification suggested by an anonymous TLCA'2015
referee, which we incorporated in the TLCA final version of the paper:

  "Considering \beta = 0^(Mf+1) 1^\omega and \alpha = 0^m
   (Mf+1)^\omega, one can avoid the proof case Mf = 0 and use just the
   other one."

\begin{code}

â‰¡[]-lemma : {Î± Î² : Baire} (n : â„•) â†’ Î± â‰¡[ succ n ] Î² â†’ Î± â‰¡[ n ] Î²
â‰¡[]-lemma zero     _       = *
â‰¡[]-lemma (succ n) (p , q) = p , â‰¡[]-lemma n q

theorem' : (âˆ€(f : Baire â†’ â„•) â†’ continuous f) â†’ 0 â‰¡ 1
theorem' continuity = zero-is-one
 where
  M : (Baire â†’ â„•) â†’ â„• 
  M f = Ï€â‚€(continuity f O)
  continuityâ‚€ : âˆ€ f Î² â†’ O â‰¡[ M f ] Î² â†’ f O â‰¡ f Î²
  continuityâ‚€ f = Ï€â‚(continuity f O)
  m : â„•
  m = M(Î» Î± â†’ 0)
  f : Baire â†’ â„•
  f Î² = M(Î» Î± â†’ Î²(Î± m))
  Î² : Baire
  Î² = (succ(M f)) zeros-and-then 1
  Î²-propertyâ‚€ : Î²(succ(M f)) â‰¡ 1
  Î²-propertyâ‚€ = zeros-and-then-specâ‚€ (succ(M f)) 1
  Î²-propertyâ‚ : O â‰¡[ succ(M f) ] Î²
  Î²-propertyâ‚ = zeros-and-then-specâ‚ (succ(M f)) 1
  Î²-propertyâ‚‚ : O â‰¡[ M f ] Î²
  Î²-propertyâ‚‚ = â‰¡[]-lemma (M f) Î²-propertyâ‚
  câ‚€ : f Î² â‰¡ m
  câ‚€ = sym {m} (continuityâ‚€ f Î² Î²-propertyâ‚‚)
  câ‚ : âˆ€ Î± â†’ O â‰¡[ f Î² ] Î± â†’ Î² 0 â‰¡ Î²(Î± m) 
  câ‚ = continuityâ‚€ (Î» Î± â†’ Î²(Î± m))
  câ‚‚ : âˆ€ Î± â†’ O â‰¡[ m ] Î± â†’ Î² 0 â‰¡ Î²(Î± m) 
  câ‚‚ = transport (Î» n â†’ âˆ€ Î± â†’ O â‰¡[ n ] Î± â†’ Î² 0 â‰¡ Î²(Î± m)) câ‚€ câ‚ 
  Î± : Baire
  Î± = m zeros-and-then (succ(M f))
  Î±-propertyâ‚€ : Î± m â‰¡ succ(M f)
  Î±-propertyâ‚€ = zeros-and-then-specâ‚€ m (succ(M f))
  Î±-propertyâ‚ : O â‰¡[ m ] Î±
  Î±-propertyâ‚ = zeros-and-then-specâ‚ m (succ(M f))
  câ‚ƒ : Î² 0 â‰¡ Î²(Î± m)
  câ‚ƒ = câ‚‚ Î± Î±-propertyâ‚
  câ‚„ : Î²(Î± m) â‰¡ Î²(succ(M f))
  câ‚„ = ap Î² {Î± m} {succ(M f)} Î±-propertyâ‚€
  câ‚… : Î²(Î± m) â‰¡ 1
  câ‚… = trans {Î²(Î± m)} câ‚„ Î²-propertyâ‚€
  câ‚† : Î² 0 â‰¡ 1
  câ‚† = trans {Î² 0} {Î² (Î± m)} {1} câ‚ƒ câ‚…
  câ‚‡ : 0 â‰¡ Î² 0
  câ‚‡ = Ï€â‚€ Î²-propertyâ‚
  zero-is-one : 0 â‰¡ 1
  zero-is-one = trans {0} {Î² 0} {1} câ‚‡ câ‚†

\end{code}
