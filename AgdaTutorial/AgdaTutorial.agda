-- A brief Agda tutorial.
-- MartÃ­n EscardÃ³, 7 Sep 2012 (updated to be compatible with Agda 2.4.2 2 Oct 2014), written for a talk in our Theory Group.
--
-- (There also this 2017 tutorial:
-- http://www.cs.bham.ac.uk/~mhe/fp-learning-2017-2018/html/Agda-in-a-Hurry.html)
--
-- Agda is a computer-implemented dialect of Martin-LÃ¶f type theory.
-- It can both check and run proofs. 
--
-- Propositions are types (also called sets, indicated by the keyword
-- Set), and their witnesses or proofs are programs.
--
-- If one ignores this view/encoding of propositions, Agda is simply a
-- strongly typed, pure, functional programming language with
-- dependent types and other kinds of fancy types.
--
-- All programs normalize in Agda. 
---
-- GÃ¶del's system T is included in Agda, but Platek-Scott-Plotkin's
-- PCF is not.  But Agda is much more expressive than system T: it
-- defines many more functions â„• â†’ â„•, for example, and encodes much
-- higher ordinals.

-- The Agda wiki http://wiki.portal.chalmers.se/agda/agda.php tells
-- you how to install Agda and get started with editing.
--
-- I recommend the tutorial paper 
---    http://www.cse.chalmers.se/~ulfn/papers/afp08/tutorial.pdf,
-- among others available at 
--     http://wiki.portal.chalmers.se/agda/agda.php?n=Main.Documentation
--
-- See also the standard library
--     http://www.cse.chalmers.se/~nad/listings/lib-0.6/README.html
-- which we will not use in this tutorial.

module AgdaTutorial where

-- An inductive definition:

data â„• : Set where
 zero : â„•
 succ : â„• â†’ â„•

-- This notation uses unicode UTF-8. To enter unicode characters in emacs in Agda mode, see 
-- http://wiki.portal.chalmers.se/agda/agda.php?n=Main.QuickGuideToEditingTypeCheckingAndCompilingAgdaCode
-- (usually LaTeX syntax works).

-- Could instead write the following, which is equivalent with a
-- different notation, using ascii characters only:

data N : Set where
 zero : N
 succ : N -> N

-- But we will use the first definition. (Later we may prove that â„•
-- and N are isomorphic, as an exercise.)

-- Our first function, inductively defined:

_+_ : â„• â†’ â„• â†’ â„•
x + zero = x
x + succ y = succ(x + y)

-- We can define a simple-recursion combinator:

rec : (X : Set) â†’ X â†’ (X â†’ X) â†’ (â„• â†’ X)
rec X x f zero = x
rec X x f (succ n) = f(rec X x f n)

-- Or the primitive-recursion combinator:

prim-rec : (X : Set) â†’ X â†’ (â„• â†’ X â†’ X) â†’ â„• â†’ X 
prim-rec X x f zero = x
prim-rec X x f (succ n) = f n (prim-rec X x f n)

-- Addition can then be instead defined as:

_+â‚_ : â„• â†’ â„• â†’ â„•
x +â‚ y = rec â„• x succ y

-- We are using a subscript to indicate a different version. An
-- identifier is a sequence of unicode (UTF-8 encoding) characters not
-- containing white space or reserved characters @.(){};_. 
-- 
-- Quoting from the agda wiki,
-- http://wiki.portal.chalmers.se/agda/pmwiki.php?n=ReferenceManual.LexicalMatters:
--
-- Furthermore, the following set of reserved words cannot be used as name parts.
--
-- ->         :         =        ?        \           |          â†’         âˆ€   --     Î»
-- abstract   data      forall   hiding   import      in         infix     infixl   infixr
-- let        module    mutual   open     postulate   primitive  private   public   record
-- renaming   rewrite   using    where    with
-- Prop       Set[0â€“9]* [0â€“9]+
--
-- This means that strings like x:A and Aâ†’B are valid names. To write
-- the type signature and the function type, white space have to be
-- inserted: x : A, and A â†’ B.
--

-- To illustrate some features, another equivalent definition is this:

recâ‚ : (X : Set) â†’ X â†’ (X â†’ X) â†’ (â„• â†’ X)
recâ‚ X x f = h
 where 
  h : â„• â†’ X
  h zero = x
  h (succ n) = f(h n)

-- Indentation matters.

-- The parameter X of the definition of rec/recâ‚ may be made implicit,
-- and this is sensible:

recâ‚‚ : {X : Set} â†’ X â†’ (X â†’ X) â†’ (â„• â†’ X)
recâ‚‚ {X} x f = h
 where 
  h : â„• â†’ X
  h zero = x
  h (succ n) = f(h n)

-- Now we can define addition omitting the type â„•:

_+â‚‚_ : â„• â†’ â„• â†’ â„•
x +â‚‚ y = recâ‚‚ x succ y

-- Agda then infers (by unification) that this is the only possibly
-- choice for the implicit argument. 

-- In cases when the inference fails, or when you want to implicitly
-- supply the implicit parameter for emphasis, you can write:

_+â‚ƒ_ : â„• â†’ â„• â†’ â„•
x +â‚ƒ y = recâ‚‚ {â„•} x succ y

-- You can also write:

_+â‚„_ : â„• â†’ â„• â†’ â„•
_+â‚„_ x y = recâ‚‚ {â„•} x succ y

-- or even, using Î·-contraction:

_+â‚…_ : â„• â†’ â„• â†’ â„•
_+â‚…_ x = recâ‚‚ {â„•} x succ

-- or:

_+â‚†_ : â„• â†’ â„• â†’ â„•
_+â‚†_ = Î» x â†’ recâ‚‚ {â„•} x succ

-- or, using only ascii characters:

_+â‚‡_ : â„• â†’ â„• â†’ â„•
_+â‚‡_ = \x -> recâ‚‚ {â„•} x succ

-- I prefer the original definition.

-- You can also declare an associativity and precedence for (the
-- various versions of) _+_ if you wish (valid from now on):

infixl 5 _+_

-- Since we are discussing syntax, let also do:

{-# BUILTIN NATURAL â„• #-}

-- This allows you to write e.g. 0 to mean zero and 3 to mean
-- Succ(Succ(Succ zero)).

-- So far we have a few definitions. We want to prove something about
-- them.


-- Agda uses the Brouwer-Heyting-Kolmogorov-Curry-Howard
-- representation of propositions as types. 
-- 
-- Types are called sets in Agda syntax, with the keyword "Set". 
-- (And in the early accounts by Martin-LÃ¶f too.)
--
-- So a proposition is a set.
--
--    * A set may be empty: this represents "false".
--
--    * Or it may have an element: any inhabited set represents "true".
--
-- So a proposition may be true in many ways: an element of its
-- representing set is called a "witness" or a "realizer" or "a
-- proof".
--
--
-- A predicate on a type X is a function A : X â†’ Set.  
-- For each element x : X it gives a set A x representing a
-- proposition.
--
--
-- * The BHKCH intepretation of implication:
--
--     A realizer of the proposition "A implies B" is a function that
--     given any realizer of A produces a realizer of B.
--  
--     Thus, the function type (A â†’ B), built-in in Martin-LÃ¶f theory
--     and in Agda, codes implication.
--
--
-- * The BHKCH intepretation of universal quantification:
--
--     A realizer of "for all x in X, A x" is a function that
--     transforms any x in X into a realizer of A x.
--
--     The interpretation of universal quantification is given by
--     dependent products, again built-in in Martin-LÃ¶f theory and in
--     Agda.
--
--     In Martin-LÃ¶f type theory, it is written Î .  In Agda, the
--     notation (x : X) â†’ A x is used, where A : X â†’ Set.
--
--     It is the type of functions that map any x : X to an element y : A x.
--     Notice that the type of the output depends on the input.
--
--     Agda allows you to write âˆ€(x : X) â†’ A x to mean (x : X) â†’ A x.
--     When types can be inferred by Agda, you can also write âˆ€ x â†’ A x.
--
-- * The BHKCH intepretation of existential quantification:
--
--     A realizer of "there is x in X s.t. A x" is a pair (x , a)
--     where x is in X and a is a realizer of A x.
--
--     In Martin-LÃ¶f type theory, Î£ is used to define the
--     interpretation of existential quantification.
--
--     In Agda we need to (inductively) define it ourselves:

data Î£ {X : Set} (A : X â†’ Set) : Set where
     _,_ : (xâ‚€ : X) â†’ A xâ‚€ â†’ Î£ \(x : X) â†’ A x

-- Read Î£ \(x : X) â†’ A x as the sum of the sets A x for x : X.  Agda
-- is "intensional", but it uses the Î· rule.  So Î£ \(x : X) â†’ A x is
-- the same as Î£ {X} A, because A is the same thing as \(x : X) â†’ A x,
-- and because X can be inferred now, as it is given in the definition
-- \(x : X) â†’ A x.

Ï€â‚€ : {X : Set} {A : X â†’ Set} â†’ (Î£ \(x : X) â†’ A x) â†’ X
Ï€â‚€(x , a) = x

Ï€â‚ : {X : Set} {A : X â†’ Set} â†’ (z : Î£ \(x : X) â†’ A x) â†’ A(Ï€â‚€ z)
Ï€â‚(x , a) = a

-- Martin-LÃ¶f instead works with the following elimination rule, which
-- is a dependently type version of "uncurrying":

Î£-elim : {X : Set} â†’ {Y : X â†’ Set} â†’ 
         {A : (Î£ \(x : X) â†’ Y x) â†’ Set}

   â†’ (âˆ€(x : X) â†’ âˆ€(y : Y x) â†’ A(x , y))
   â†’ âˆ€(t : (Î£ \(x : X) â†’ Y x)) â†’ A t

Î£-elim f(x , y) = f x y

-- Notice that Î£-elim defines the projections:

Ï€â‚€' : {X : Set} {A : X â†’ Set} â†’ (Î£ \(x : X) â†’ A x) â†’ X
Ï€â‚€' = Î£-elim (Î» x a â†’ x) 

Ï€â‚' : {X : Set} {A : X â†’ Set} â†’ (z : Î£ \(x : X) â†’ A x) â†’ A(Ï€â‚€ z)
Ï€â‚' = Î£-elim (Î» x a â†’ a) 

-- The converse holds if we assume "surjective-pairing", a form of the
-- Î·-rule for sums. Now this may be confusing: the way we defined Î£
-- using "data" doesn't give you that. But if you define Î£ using a
-- record, then you do get that. See
-- http://www.cs.bham.ac.uk/~mhe/agda/SetsAndFunctions.html

-- Cartesian products are a special case of dependent sums, where Y
-- doesn't depend on x : X, which represent conjunctions in the BHKCH
-- interpretation of logic:

_Ã—_ : Set â†’ Set â†’ Set
X Ã— Y = Î£ \(x : X) â†’ Y

-- It is also useful to introduce âˆƒ-notation:

âˆƒ : {X : Set} â†’ (A : X â†’ Set) â†’ Set
âˆƒ = Î£

-- We have developed enough material to be able to formulate and prove
-- our first theorem: the Axiom of Choice:


AC : {X Y : Set} {A : X â†’ Y â†’ Set} â†’

 (âˆ€(x : X) â†’ âˆƒ \(y : Y) â†’ A x y) â†’ âˆƒ \(f : X â†’ Y) â†’ âˆ€(x : X) â†’ A x (f x)

AC g = (Î» x â†’ Ï€â‚€(g x)) , (Î» x â†’ Ï€â‚(g x))

-- We have a dependently typed version as well (not to be confused
-- with dependent choice, which I will discuss later), with the same
-- proof:

AC' : {X : Set} {Y : X â†’ Set} {A : (x : X) â†’ Y x â†’ Set} â†’

 (âˆ€(x : X) â†’ âˆƒ \(y : Y x) â†’ A x y) â†’ âˆƒ \(f : ((x : X) â†’ Y x)) â†’ âˆ€(x : X) â†’ A x (f x)

AC' g = ((Î» x â†’ Ï€â‚€(g x)) , (Î» x â†’ Ï€â‚(g x)))

-- I find the following use of an implicit argument useful in order
-- to achieve a notation close to Martin-LÃ¶f's.

Î  : {X : Set} â†’ (Y : X â†’ Set) â†’ Set
Î  {X} Y = (x : X) â†’ Y x

-- For example the axiom of choice becomes less surprising when we
-- realize that it amounts to

AC-in-ML-notation : {X : Set} {Y : X â†’ Set} {A : (x : X) â†’ Y x â†’ Set} â†’

 (Î  \(x : X) â†’ Î£ \(y : Y x) â†’ A x y) â†’ Î£ \(f : ((x : X) â†’ Y x)) â†’ Î  \(x : X) â†’ A x (f x)

AC-in-ML-notation g = ((Î» x â†’ Ï€â‚€(g x)) , (Î» x â†’ Ï€â‚(g x)))

-- (The axiom of choice usually fails in a topos, but the above always
-- holds. In fact, the function AC-in-ML-notation is a
-- (pointwise) isomorphism. Exercise: define its inverse in Agda.)


-- Let's return to the natural numbers. 
-- We can also write an induction combinator:

induction : {A : â„• â†’ Set} â†’ 

 A zero â†’ (âˆ€(k : â„•) â†’ A k â†’ A(succ k)) â†’ âˆ€(n : â„•) â†’ A n 

induction base step 0 = base
induction base step (succ n) = step n (induction base step n)

-- Notice that the realizer of the principle of induction has the same
-- definition as primitive recursion. Only the types are different:
-- that of primitive recursion is less general, and that of induction
-- is the dependent-type generalization.

prim-rec' : {X : Set} â†’ X â†’ (â„• â†’ X â†’ X) â†’ â„• â†’ X 
prim-rec' = induction

-- The empty type, representing the false proposition. 

data âˆ… : Set where
 -- no constructors given

-- The "elimintation rule of false", or "ex-falso quod libet", or the
-- function from the empty set to any set:


from-âˆ… : {X : Set} â†’ âˆ… â†’ X
from-âˆ… ()

-- This is our first encounter of "()" patterns. They assert that a
-- case is impossible (and Agda checks that), or that no constructor
-- is available to perform a match. All uses of () can be reduced to
-- from-âˆ…, but sometimes it is clearer to use the "()" pattern.

-- The type with one element, giving one way of representing the true
-- proposition:

data ğŸ™ : Set where
 O : ğŸ™ 

-- This is something like the terminal object:

to-ğŸ™ : {X : Set} â†’ X â†’ ğŸ™
to-ğŸ™ x = O

-- O The BHKCH intepretation of disjunction is the binary co-product,
--   inductively defined. It is natural to use the symbol "+" to
--   denote the co-product, but in Agda there is no overloading other than
--   constructors defined in (different) "data" definitions. So let's
--   use "â¨„" as in the standard library


data _â¨„_ (Xâ‚€ Xâ‚ : Set) : Set where
 inâ‚€ : Xâ‚€ â†’ Xâ‚€ â¨„ Xâ‚
 inâ‚ : Xâ‚ â†’ Xâ‚€ â¨„ Xâ‚

-- These constructors correspond to the introduction rules of disjunction.
-- Definition by cases corresponds to the elimination rule:

cases : {Xâ‚€ Xâ‚ Y : Set} â†’ (Xâ‚€ â†’ Y) â†’ (Xâ‚ â†’ Y) â†’ (Xâ‚€ â¨„ Xâ‚ â†’ Y)
cases fâ‚€ fâ‚ (inâ‚€ xâ‚€) = fâ‚€ xâ‚€
cases fâ‚€ fâ‚ (inâ‚ xâ‚) = fâ‚ xâ‚

-- See Bove & Dybjer's paper "Dependent types at work" for 
-- a dependently typed version of this:
-- http://www.cse.chalmers.se/~ulfn/papers/afp08/tutorial.pdf
-- Alternatively, work it out yourself as an exercise.

-- Binary co-products can be alternatively defined from sums and the
-- booleans in the presence of the "universe" Set.
--
-- First define the set of binary digits (the booleans with another
-- notation and perspective):

data â‘¡ : Set where
 â‚€ : â‘¡
 â‚ : â‘¡

-- This amounts to if-then-else:

â‘¡-cases : {X : Set} â†’ X â†’ X â†’ â‘¡ â†’ X  
â‘¡-cases xâ‚€ xâ‚ â‚€ = xâ‚€
â‘¡-cases xâ‚€ xâ‚ â‚ = xâ‚

-- Here is the dependently-typed version:

dep-â‘¡-cases : {X : â‘¡ â†’ Set} â†’ X â‚€ â†’ X â‚ â†’ ((i : â‘¡) â†’ X i)
dep-â‘¡-cases xâ‚€ xâ‚ â‚€ = xâ‚€
dep-â‘¡-cases xâ‚€ xâ‚ â‚ = xâ‚

-- The following has the same definition but a different type:

â‘¡-Cases : Set â†’ Set â†’ â‘¡ â†’ Set  
â‘¡-Cases Xâ‚€ Xâ‚ â‚€ = Xâ‚€
â‘¡-Cases Xâ‚€ Xâ‚ â‚ = Xâ‚

-- Agda has universe polymorphism, but I won't mention it in this
-- tutorial. It requires to rewrite all code we have written and we
-- will write in this tutorial, everytime with a parameter for a
-- universe level. Explore the standard library to see how this works.


_â¨„'_ : Set â†’ Set â†’ Set
Xâ‚€ â¨„' Xâ‚ = Î£ \(i : â‘¡) â†’ â‘¡-Cases Xâ‚€ Xâ‚ i

inâ‚€' : {Xâ‚€ Xâ‚ : Set} â†’ Xâ‚€ â†’ Xâ‚€ â¨„' Xâ‚
inâ‚€' xâ‚€ = (â‚€ , xâ‚€)

inâ‚' : {Xâ‚€ Xâ‚ : Set} â†’ Xâ‚ â†’ Xâ‚€ â¨„' Xâ‚
inâ‚' xâ‚ = (â‚ , xâ‚)

cases' : {Xâ‚€ Xâ‚ Y : Set} â†’ (Xâ‚€ â†’ Y) â†’ (Xâ‚ â†’ Y) â†’ (Xâ‚€ â¨„' Xâ‚ â†’ Y)
cases' {Xâ‚€} {Xâ‚} {Y} fâ‚€ fâ‚ = h 
 where
  Y' : Xâ‚€ â¨„' Xâ‚ â†’ Set
  Y' z = Y 

  f : (i : â‘¡) â†’ (x : â‘¡-Cases Xâ‚€ Xâ‚ i) â†’ Y'(i , x)
  f = dep-â‘¡-cases fâ‚€ fâ‚

  g : (z : Xâ‚€ â¨„' Xâ‚) â†’ Y' z
  g = Î£-elim {â‘¡} {â‘¡-Cases Xâ‚€ Xâ‚} {Y'} f

  h : Xâ‚€ â¨„' Xâ‚ â†’ Y
  h = g

-- O The BHKCH intepretation of equality:
--   
--     Howards idea: x = y is interpreted by a type that is empty if x
--     and y are equal, and has precisely one element if not. 
--
--     In set theory (classical or constructive), this can be written
--     { z âˆˆ 1 | x = y }, where 1 is a singleton, say {0}.
--
--     But notice that this cannot be written down in Martin-LÃ¶f's
--     type theory, and already presuposes a notion of equality to be
--     available.
-- 
--     Martin-LÃ¶f's idea: inductively define this type. This is the
--     so-called identity type, which is complicated and hence we will
--     look at it later. 


-- Using the universe Set, one can easily define equality on the
-- natural numbers by induction and show that the Martin-LÃ¶f induction
-- priciple J holds for this notion of equality on â„•.

-- Notice that we use four stacked bars to denote the equality type on
-- the type â„•. Later we will use three to denote the equality type, or
-- identity type, for any type (and in particular â„• again).

infix 3 _â‰£_

_â‰£_ : â„• â†’ â„• â†’ Set
0 â‰£ 0 = ğŸ™ 
(succ m) â‰£ 0 = âˆ… 
0 â‰£ (succ n) = âˆ… 
(succ m) â‰£ (succ n) = m â‰£ n 

Reflâ„• : âˆ€ n â†’ n â‰£ n
Reflâ„• 0 = O
Reflâ„• (succ n) = IH
 -- Notice that we needed to inhabit the set ((succ m) â‰£ (succ n))
 -- in this case, but we instead inhabited the set (m â‰£ n) using IH.
 -- This works because, by definition, ((succ m) â‰£ (succ n)) is (m â‰£ n).
 where
  IH : n â‰£ n
  IH = Reflâ„• n

-- We next show that _â‰£_ is the least reflexive relation on â„•:

weak-Jâ„• : (A : â„• â†’ â„• â†’ Set) â†’ (âˆ€ n â†’ A n n) â†’  âˆ€ m n â†’ m â‰£ n â†’ A m n
weak-Jâ„• A Ï† 0 0 O = Ï† 0
weak-Jâ„• A Ï† 0 (succ n) ()
weak-Jâ„• A Ï† (succ m) 0 ()
weak-Jâ„• A Ï† (succ m) (succ n) e = weak-Jâ„• A' Ï†' m n e
 where
  A' : â„• â†’ â„• â†’ Set 
  A' m n = A (succ m) (succ n)
  Ï†' : âˆ€ n â†’ A' n n
  Ï†' n = Ï†(succ n)

-- If you don't like "()" patterns, you can use the function from-âˆ…
-- (defined above using "()" patterns):

weak-Jâ„•' : (A : â„• â†’ â„• â†’ Set) â†’ (âˆ€ n â†’ A n n) â†’  âˆ€ m n â†’ m â‰£ n â†’ A m n
weak-Jâ„•' A Ï† 0 0 O = Ï† 0
weak-Jâ„•' A Ï† 0 (succ n) e = from-âˆ… e
weak-Jâ„•' A Ï† (succ m) 0 e = from-âˆ… e
weak-Jâ„•' A Ï† (succ m) (succ n) e = 
 weak-Jâ„•' (Î» m n â†’ A(succ m)(succ n)) (Î» n â†’ Ï†(succ n)) m n e

-- There is a stronger, dependent(ly typed) version of weak-Jâ„•:

Jâ„• : (A : (m n : â„•) â†’ m â‰£ n â†’ Set)

   â†’ (âˆ€ n â†’ A n n (Reflâ„• n)) â†’  âˆ€ m n â†’ âˆ€(e : m â‰£ n) â†’ A m n e

Jâ„• A Ï† 0 0 O = Ï† 0
Jâ„• A Ï† 0 (succ n) ()
Jâ„• A Ï† (succ m) 0 ()
Jâ„• A Ï† (succ m) (succ n) e = 
 Jâ„• (Î» m n â†’ A (succ m) (succ n)) (Î» n â†’ Ï†(succ n)) m n e

-- Of course we could have defined instead the weak version from the
-- strong one:

weak-Jâ„•'' : (A : (m n : â„•) â†’ Set) â†’ (âˆ€ n â†’ A n n) â†’  âˆ€ m n â†’ m â‰£ n â†’ A m n
weak-Jâ„•'' A = Jâ„• (Î» m n e â†’ A m n) 

-- Jâ„• can be regarded as an induction principle for equality on the
-- type â„•. Several properties of â‰£ can be proved using J without
-- reference to the inductive structure of the the type â„•, and often
-- its weak version suffices.

symâ„• : (x y : â„•) â†’ x â‰£ y â†’ y â‰£ x
symâ„• = weak-Jâ„• A Ï† 
 where
  A : (x y : â„•) â†’ Set
  A x y = y â‰£ x
  Ï† : (x : â„•) â†’ x â‰£ x
  Ï† = Reflâ„•

transâ„• : (x y z : â„•) â†’ x â‰£ y â†’ y â‰£ z â†’ x â‰£ z
transâ„• x y z r s =  transâ„•' x y r z s
 where
  transâ„•' : (x y : â„•) â†’ x â‰£ y â†’ (z : â„•) â†’ y â‰£ z â†’ x â‰£ z
  transâ„•' = weak-Jâ„• A Ï†
   where
    A : (x y : â„•) â†’ Set
    A x y = âˆ€(z : â„•) â†’ y â‰£ z â†’ x â‰£ z
    Ï† : (x : â„•) â†’ A x x 
    Ï† x z s = s

substâ„• : (P : â„• â†’ Set) â†’ (x y : â„•) â†’ x â‰£ y â†’ P x â†’ P y
substâ„• P = weak-Jâ„• A Ï†
 where
  A : (x y : â„•) â†’ Set
  A x y = P x â†’ P y
  Ï† : (x : â„•) â†’ A x x 
  Ï† x p = p

-- Transitivity can be proved using substitution:

sym-transâ„• : (x y z : â„•) â†’ x â‰£ y â†’ x â‰£ z â†’ y â‰£ z
sym-transâ„• x y z = rearrange z x y
 where
  rearrange : (z x y : â„•) â†’ x â‰£ y â†’ x â‰£ z â†’ y â‰£ z
  rearrange z = substâ„• (Î» x â†’ x â‰£ z)

transâ„•' : (x y z : â„•) â†’ x â‰£ y â†’ y â‰£ z â†’ x â‰£ z
transâ„•' x y z r s = sym-transâ„• y x z (symâ„• x y r) s

congâ„•â†’â„• : (f : â„• â†’ â„•) â†’ (x x' : â„•) â†’ x â‰£ x' â†’ f x â‰£ f x'
congâ„•â†’â„• f = weak-Jâ„• (Î» x x' â†’ f x â‰£ f x') (Î» x â†’ Reflâ„•(f x)) 

-- As another example, we show that addition is commutative:

zero-is-left-neutral : âˆ€ n â†’ 0 + n â‰£ n 
zero-is-left-neutral 0 = O
zero-is-left-neutral (succ n) = IH  
             -- We need to inhabit the type (0 + succ n â‰£ succ n). 
             -- Expanding the definitions,
             --   (0 + succ n â‰£ succ n) = 
             --   (succ(0 + n) â‰£ succ n) = 
             --   (0 + n â‰£ n).
             -- Here "=" is definitional equality, silently applied by Agda.
 where
  IH : 0 + n â‰£ n
  IH = zero-is-left-neutral n

-- Equivalently:

zero-is-left-neutral' : âˆ€ n â†’ 0 + n â‰£ n
zero-is-left-neutral' = induction base step
 where
  base : ğŸ™
  base = O
  step : âˆ€ n â†’ 0 + n â‰£ n â†’ 0 + n â‰£ n
  step n e = e

-- This with the following shows that, of course, it is equivalent to
-- define addition by induction on the first argument. The proof is by
-- induction on the second argument, following the definition of _+_.

addition-on-first : âˆ€ m n â†’ (succ m) + n â‰£ succ(m + n)
addition-on-first m 0 = Reflâ„• m
addition-on-first m (succ n) = IH
 where
  IH : succ m + n â‰£ succ(m + n)
  IH = addition-on-first m n

-- Because the situation is symmetric, we can choose any of the two
-- arguments to perform the induction in the following theorem:

addition-commutative : âˆ€ m n â†’ m + n â‰£ n + m
addition-commutative 0 n = zero-is-left-neutral n 
addition-commutative (succ m) n = lemma
 where
  IH : m + n â‰£ n + m
  IH = addition-commutative m n
  claim : succ(m + n) â‰£ succ(n + m)
  claim = congâ„•â†’â„• succ (m + n) (n + m) IH
  lemma : succ m + n â‰£ succ (n + m)
  lemma = transâ„• (succ m + n) (succ(m + n)) (succ (n + m)) 
                 (addition-on-first m n) claim


-- Exercise. Prove the Peano axioms that are not covered above. 

-- The above theorem "Jâ„•" motivates Martin-LÃ¶f's inductive definition
-- of the identity type for any type X:

data _â‰¡_ {X : Set} : X â†’ X â†’ Set where
 refl : {x : X} â†’ x â‰¡ x

-- Martin-LÃ¶f's notation is the following:

Id : (X : Set) â†’ X â†’ X â†’ Set
Id X x y  =  x â‰¡ y

Refl : (X : Set) â†’ (x : X) â†’ Id X x x
Refl X x = refl {X} {x}

-- The induction principle is as for equality on â„• defined earlier:

J : {X : Set} â†’ (A : (x x' : X) â†’ x â‰¡ x' â†’ Set)

   â†’ (âˆ€(x : X) â†’ A x x refl) â†’  âˆ€{x x' : X} â†’ âˆ€(r : x â‰¡ x') â†’ A x x' r

J A f {x} refl = f x

-- In Agda, one can prove the unique-of-identity proofs principle K by
-- pattern matching:

K : {X : Set} â†’ âˆ€{x x' : X} â†’ âˆ€(r s : x â‰¡ x') â†’ r â‰¡ s
K refl refl = refl

-- This is not provable in intensional Martin-LÃ¶f type theory (Hofmann
-- & Streicher's groupoid interpretation paper).
--
-- However, it is known that the following is provable in MLTT:

pseudo-K : {X : Set} â†’ âˆ€{x x' : X} â†’ âˆ€(r : x â‰¡ x') â†’ (x , refl) â‰¡ (x' , r)
pseudo-K {X} = J {X} A (Î» x â†’ refl)
 where 
  A : âˆ€(x x' : X) â†’ x â‰¡ x' â†’ Set
  A x x' r = _â‰¡_ {Î£ \(x' : X) â†’ x â‰¡ x'} (x , refl) (x' , r)

-- It has been shown that types with decidable equality, such as â„• and
-- the booleans, satisfy the K-rule. Moreover, for â„• it can be easily
-- proved by induction (exercise, which we may eventually do).

-- Conor McBride proved that having the K-rule is equivalent to having
-- pattern matching over refl, which Agda does. But this can be
-- disabled in Agda using the pragma {-# OPTIONS --without-K #-}.
-- Without this pragma, many definitions by pattern matching,
-- including that of J, are accepted, but that of K, and other
-- definitions that require K, are not.

-- Exercise: formulate and prove sym, subst, trans, cong, generalizing
-- the above development for â„•. Define a weak version of J from J, and
-- use this in your proofs.

-- The propositional "axiom of function extensionality" is

Extensionality : Setâ‚
Extensionality = 
 âˆ€(X Y : Set) â†’ âˆ€(f g : X â†’ Y) â†’ (âˆ€(x : X) â†’ f x â‰¡ g x) â†’ f â‰¡ g

-- Here we have used the second universe. The first is Set = Setâ‚€.  We
-- have Setâ‚€ : Setâ‚, Setâ‚ : Setâ‚‚, and so on. The "small" sets (or
-- types) live in Setâ‚€. The large set Extensionality lives in Setâ‚
-- because it quantifies over elements of Setâ‚€.

-- It is neither possible to show that the set Extensionality is
-- inhabited or that it is empty. That is, the extensionality axiom is
-- independent of MLTT. We can, if we wish, postulate it, or rather
-- postulate an inhabitant:

postulate ext : Extensionality

-- But then ext behaves like a constant, with no computation rules for
-- it. Reduction gets stuck when it encounters this case.

-- The principle of excluded middle is independent of MLTT.

EM : Setâ‚
EM = âˆ€(X : Set) â†’ X â¨„ (X â†’ âˆ…)

-- Notice that (X â†’ âˆ…) represents negation because âˆ… represents false.
-- Notice also that the independence of EM follows from that of
-- Extensionality if we don't have the above postulate.

-- It would be good, too good, to have an inhabitant of EM without
-- violating the computational character of MLTT. It would solve
-- Hilbert's decision problem for large fragments of mathematics,
-- including Peano arithmetic. It would amount to an algorithm for
-- deciding whether a proposition has a realizer, or its negation has
-- a realizer, and would also give the corresponding realizer. As is
-- well known, this is not possible, and this is why EM cannot be
-- inhabited in MLTT.

-- This is not to say that EM is false in MLTT. 
-- In fact the set (EM â†’ âˆ…) cannot be inhabited in MLTT either. This
-- is because MLTT is compatible with classical mathematics (like
-- Bishop mathematics). Set theory, say ZF with an inaccessible
-- cardinal modeling the universe, or with a stack of inaccessible
-- cardinals modelling a hierarchy of universes, can be used to give a
-- model of MLTT in which EM is inhabited (by simply using the
-- principle of excluded middle of ZF, whose consistency is not
-- disputed, even by constructive mathematicians).

-- The negation of EM is also consistent, because recursive models
-- of MLTT, in which an inhabitant of EM needs to be given by an
-- algorithm, exist, so that EM is empty and hence (EM â†’ âˆ…) is
-- inhabited in such models. There are also (classical and
-- constructive) models in which all functions are continuous. In
-- those models, (EM â†’ âˆ…) has an inhabitant too.

-- So it is very inaccurate to say that excluded middle "doesn't hold"
-- in Martin-LÃ¶f type theory.  What we have is that EM is independent
-- of MLTT. Independence is a meta-theoretical property, which cannot
-- be written down in MLTT itself, of course. Formalized versions of
-- the assertions "all functions are computable" and "all functions
-- are continuous" are also independent. In constructive mathematics
-- in the style of Bishop or Martin-LÃ¶f, in which compatibility with
-- classical and computational mathematics is desired, it is very easy
-- to find independent statements, as there are plenty of theorems
-- that hold classically but have no computable realizers (EM is one).

-- To be continued.

Jâ„•eq : (A : (m n : â„•) â†’ m â‰£ n â†’ Set) â†’ (Ï† : (âˆ€ n â†’ A n n (Reflâ„• n))) â†’  âˆ€ m 
     â†’ Jâ„• A Ï† m m (Reflâ„• m) â‰¡ Ï† m
Jâ„•eq A Ï† zero = refl
Jâ„•eq A Ï† (succ m) = Jâ„•eq (Î» m n â†’ A (succ m) (succ n)) (Î» n â†’ Ï†(succ n)) m

Jâ„•eq17 : (A : (m n : â„•) â†’ m â‰£ n â†’ Set) â†’ (Ï† : (âˆ€ n â†’ A n n (Reflâ„• n)))
     â†’ Jâ„• A Ï† 17 17 (Reflâ„• 17) â‰¡ Ï† 17
Jâ„•eq17 A Ï† = refl
