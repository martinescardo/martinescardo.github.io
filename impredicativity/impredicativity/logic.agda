-- Martin Escardo, 3rd August 2015, updated 6th August 2015

{-
   We develop some logic in the type Prp of propositions, where we
   define the logical connectives and their introduction and
   elimination rules following the ideas of the HoTT book. We then
   prove that

      false â‰¡ âˆ€ r. r
      p âˆ§ q â‰¡ âˆ€ r. (p â‡’ q â‡’ r) â‡’ r
      p âˆ¨ q â‰¡ âˆ€ r. (p â‡’ r) â‡’ (q â‡’ r) â‡’ r
      âˆƒ p   â‰¡ âˆ€ r. (âˆ€ x. p(x) â‡’ r) â‡’ r
-}

{-# OPTIONS --without-K #-}

module logic where

open import preliminaries
open import prop
open import proptrunc

open import propisset
open import propua

-- The two canonical truth values:

true : Prp
true = â‚ ğŸ™ , ğŸ™-isProp â‚

false : Prp
false = â‚ ğŸ˜ , ğŸ˜-isProp â‚

-- Propositional univalence gives that p holds â‰¡ (p â‰¡ true):

holdsâ†’equal-true : {p : Prp} â†’ p holds â†’ p â‰¡ true
holdsâ†’equal-true {p} h = Prp-â‰¡ (prop-ua' (holdsIsProp p) (holdsIsProp true) (Î» _ â†’ zero) (Î» _ â†’ h))

equal-trueâ†’holds : {p : Prp} â†’ p â‰¡ true â†’ p holds
equal-trueâ†’holds {p} e = transport (Î» X â†’ X) (sym a) zero
  where
   a : p holds â‰¡ ğŸ™
   a = ap _holds e

holds-is-equal-true : (p : Prp) â†’ p holds â‰¡ (p â‰¡ true)
holds-is-equal-true p = prop-ua (holdsIsProp p) Prp-isSet holdsâ†’equal-true equal-trueâ†’holds

-- The logical connectives:

infixr 42 _â‡’_
infixr 41 _âˆ¨_
infixr 40 _âˆ§_

_âˆ§_ _âˆ¨_ _â‡’_ : Prp â†’ Prp â†’ Prp

p âˆ§ q = â‚ p holds Ã— q holds , isProp-closed-under-Î£ (holdsIsProp p) (Î» _ â†’ holdsIsProp q) â‚

p â‡’ q = â‚ (p holds â†’ q holds) , isProp-exponential-ideal (Î» _ â†’ holdsIsProp q) â‚

p âˆ¨ q = â‚ âˆ¥ p holds + q holds âˆ¥ , âˆ¥âˆ¥-isProp â‚

-- The quantifiers.
--
-- Because "âˆ€" is taken, and "âˆƒ" has a slightly different meaning in
-- the HoTT book, we will have to use something else. We write a
-- small, almost invisible, dot on top of them:

âˆ€Ì‡ âˆƒÌ‡ : {X : U} â†’ (X â†’ Prp) â†’ Prp

âˆ€Ì‡ p = â‚ (âˆ€ x â†’ p x holds) , isProp-exponential-ideal (Î» x â†’ holdsIsProp(p x)) â‚

âˆƒÌ‡ p = â‚ âˆ¥(Î£ \x â†’ p x holds)âˆ¥ , âˆ¥âˆ¥-isProp â‚

-- Introduction principles:

true-intro : true holds
true-intro = zero

âˆ§-intro : {p q : Prp} â†’ p holds â†’ q holds â†’ p âˆ§ q holds
âˆ§-intro = _,_

âˆ¨-intro-L : {p q : Prp} â†’ p holds â†’ p âˆ¨ q holds
âˆ¨-intro-L h = âˆ£ inl h âˆ£

âˆ¨-intro-R : {p q : Prp} â†’ q holds â†’ p âˆ¨ q holds
âˆ¨-intro-R k = âˆ£ inr k âˆ£

â‡’-intro : {p q : Prp} â†’ (p holds â†’ q holds) â†’ p â‡’ q holds
â‡’-intro = Î» f â†’ f

âˆƒÌ‡-intro : {X : U} {p : X â†’ Prp} (x : X) â†’ p x holds â†’ âˆƒÌ‡ p holds
âˆƒÌ‡-intro x h = âˆ£ x , h âˆ£

âˆ€Ì‡-intro : {X : U} {p : X â†’ Prp} â†’ ((x : X) â†’ p x holds) â†’ âˆ€Ì‡ p holds
âˆ€Ì‡-intro = Î» f â†’ f

-- Elimination principles:

âˆ§-elim-L : {p q : Prp} â†’ p âˆ§ q holds â†’ p holds
âˆ§-elim-L = prâ‚

âˆ§-elim-R : {p q : Prp} â†’ p âˆ§ q holds â†’ q holds
âˆ§-elim-R = prâ‚‚

false-elim : {p : Prp} â†’ false holds â†’ p holds
false-elim = Î»()

âˆ¨-elim : {p q r : Prp} â†’ (p holds â†’ r holds) â†’ (q holds â†’ r holds)
       â†’ p âˆ¨ q holds â†’ r holds
âˆ¨-elim {p} {q} {r} f g = âˆ¥âˆ¥-rec (holdsIsProp r) (+-rec f g)

â‡’-elim : {p q : Prp} â†’ (p holds â†’ q holds) â†’ p holds â†’ q holds
â‡’-elim = Î» f â†’ f

âˆƒÌ‡-elim : {X : U} {p : X â†’ Prp} {r : Prp} â†’ ((x : X) â†’ p x holds â†’ r holds)
        â†’ âˆƒÌ‡ p holds â†’ r holds
âˆƒÌ‡-elim {X} {p} {r} f = âˆ¥âˆ¥-rec (holdsIsProp r) (Î£-rec f)

âˆ€Ì‡-elim : {X : U} {p : X â†’ Prp} â†’ âˆ€Ì‡ p holds â†’ (x : X) â†’ p x holds
âˆ€Ì‡-elim = Î» f â†’ f

-- Characterizations in terms of _â‡’_ and âˆ€Ì‡:

false-charac : false â‰¡ âˆ€Ì‡ \r â†’ r
false-charac = propext a b
 where
  a : false holds â†’ (âˆ€Ì‡ \r â†’ r) holds
  a = false-elim {(âˆ€Ì‡ \r â†’ r)}
  b : (âˆ€Ì‡ \r â†’ r) holds â†’ false holds
  b Ï† = Ï† false

âˆ§-charac : (p q : Prp) â†’ p âˆ§ q â‰¡ âˆ€Ì‡ \r â†’ (p â‡’ q â‡’ r) â‡’ r
âˆ§-charac p q = propext a b
 where
  a : p âˆ§ q holds â†’ (âˆ€Ì‡ \r â†’ (p â‡’ q â‡’ r) â‡’ r) holds
  a pq = Î» r pqr â†’ pqr (âˆ§-elim-L {p} {q} pq) (âˆ§-elim-R {p} {q} pq)
  b : (âˆ€Ì‡ \r â†’ (p â‡’ q â‡’ r) â‡’ r) holds â†’ p âˆ§ q holds
  b Ï† = âˆ§-intro {p} {q} (Ï† p (Î» x y â†’ x)) (Ï† q (Î» x y â†’ y))

âˆ¨-charac : (p q : Prp) â†’ p âˆ¨ q â‰¡ âˆ€Ì‡ \r â†’ (p â‡’ r) â‡’ (q â‡’ r) â‡’ r
âˆ¨-charac p q = propext a b
 where
   a : p âˆ¨ q holds â†’ (âˆ€Ì‡ \r â†’ (p â‡’ r) â‡’ (q â‡’ r) â‡’ r) holds
   a pq = Î» r pr qr â†’ âˆ¨-elim {p} {q} {r} pr qr pq
   b : (âˆ€Ì‡ \r â†’ (p â‡’ r) â‡’ (q â‡’ r) â‡’ r) holds â†’ p âˆ¨ q holds
   b Ï† = Î» r f â†’ Ï† r (Î» x â†’ f (inl x)) (Î» y â†’ f (inr y))

âˆƒÌ‡-charac : {X : U} (p : X â†’ Prp) â†’ âˆƒÌ‡ p â‰¡ âˆ€Ì‡ \r â†’ (âˆ€Ì‡ \x â†’ p x â‡’ r) â‡’ r
âˆƒÌ‡-charac {X} p = propext a b
 where
  a : (âˆƒÌ‡ p) holds â†’ (âˆ€Ì‡ \r â†’ (âˆ€Ì‡ \x â†’ (p x â‡’ r)) â‡’ r) holds
  a ep = Î» r f â†’ âˆƒÌ‡-elim {X} {p} {r} (Î» x px â†’ f x px) ep
  b : (âˆ€Ì‡ \r â†’ (âˆ€Ì‡ \x â†’ (p x â‡’ r)) â‡’ r) holds â†’ (âˆƒÌ‡ p) holds
  b Ï† = Î» r f â†’ Ï† r (Î» x px â†’ f (x , px))
