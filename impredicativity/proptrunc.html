<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>proptrunc</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">-- Martin Escardo, 3rd August 2015</a>

<a id="37" class="Comment">-- Propositional truncation.</a>

<a id="67" class="Symbol">{-#</a> <a id="71" class="Keyword">OPTIONS</a> <a id="79" class="Pragma">--without-K</a> <a id="91" class="Symbol">#-}</a>

<a id="96" class="Keyword">module</a> <a id="103" href="proptrunc.html" class="Module">proptrunc</a> <a id="113" class="Keyword">where</a>

<a id="120" class="Keyword">open</a> <a id="125" class="Keyword">import</a> <a id="132" href="preliminaries.html" class="Module">preliminaries</a>
<a id="146" class="Keyword">open</a> <a id="151" class="Keyword">import</a> <a id="158" href="prop.html" class="Module">prop</a>

<a id="164" class="Comment">-- Propositional truncation first:</a>

<a id="∥_∥"></a><a id="200" href="proptrunc.html#200" class="Function Operator">∥_∥</a> <a id="204" class="Symbol">:</a> <a id="206" href="preliminaries.html#254" class="Function">U</a> <a id="208" class="Symbol">→</a> <a id="210" href="preliminaries.html#254" class="Function">U</a>
<a id="212" href="proptrunc.html#200" class="Function Operator">∥</a> <a id="214" href="proptrunc.html#214" class="Bound">X</a> <a id="216" href="proptrunc.html#200" class="Function Operator">∥</a> <a id="218" class="Symbol">=</a> <a id="220" class="Symbol">(</a><a id="221" href="proptrunc.html#221" class="Bound">p</a> <a id="223" class="Symbol">:</a> <a id="225" href="prop.html#926" class="Function">Prp</a><a id="228" class="Symbol">)</a> <a id="230" class="Symbol">→</a> <a id="232" class="Symbol">(</a><a id="233" href="proptrunc.html#214" class="Bound">X</a> <a id="235" class="Symbol">→</a> <a id="237" href="proptrunc.html#221" class="Bound">p</a> <a id="239" href="prop.html#1565" class="Function Operator">holds</a><a id="244" class="Symbol">)</a> <a id="246" class="Symbol">→</a> <a id="248" href="proptrunc.html#221" class="Bound">p</a> <a id="250" href="prop.html#1565" class="Function Operator">holds</a>

<a id="257" class="Keyword">infix</a> <a id="263" class="Number">1</a> <a id="265" href="proptrunc.html#200" class="Function Operator">∥_∥</a>
<a id="269" class="Keyword">infix</a> <a id="275" class="Number">1</a> <a id="277" href="proptrunc.html#324" class="Function Operator">∣_∣</a>

<a id="282" class="Comment">-- Here ∥ X ∥ is ranged over by s and t.</a>

<a id="∣_∣"></a><a id="324" href="proptrunc.html#324" class="Function Operator">∣_∣</a> <a id="328" class="Symbol">:</a> <a id="330" class="Symbol">{</a><a id="331" href="proptrunc.html#331" class="Bound">X</a> <a id="333" class="Symbol">:</a> <a id="335" href="preliminaries.html#254" class="Function">U</a><a id="336" class="Symbol">}</a> <a id="338" class="Symbol">→</a> <a id="340" href="proptrunc.html#331" class="Bound">X</a> <a id="342" class="Symbol">→</a> <a id="344" href="proptrunc.html#200" class="Function Operator">∥</a> <a id="346" href="proptrunc.html#331" class="Bound">X</a> <a id="348" href="proptrunc.html#200" class="Function Operator">∥</a>
<a id="350" href="proptrunc.html#324" class="Function Operator">∣</a> <a id="352" href="proptrunc.html#352" class="Bound">x</a> <a id="354" href="proptrunc.html#324" class="Function Operator">∣</a> <a id="356" class="Symbol">=</a> <a id="358" class="Symbol">λ</a> <a id="360" href="proptrunc.html#360" class="Bound">_</a> <a id="362" href="proptrunc.html#362" class="Bound">f</a> <a id="364" class="Symbol">→</a> <a id="366" href="proptrunc.html#362" class="Bound">f</a> <a id="368" href="proptrunc.html#352" class="Bound">x</a>

<a id="∥∥-rec"></a><a id="371" href="proptrunc.html#371" class="Function">∥∥-rec</a> <a id="378" class="Symbol">:</a> <a id="380" class="Symbol">{</a><a id="381" href="proptrunc.html#381" class="Bound">X</a> <a id="383" href="proptrunc.html#383" class="Bound">P</a> <a id="385" class="Symbol">:</a> <a id="387" href="preliminaries.html#254" class="Function">U</a><a id="388" class="Symbol">}</a> <a id="390" class="Symbol">→</a> <a id="392" href="isprop.html#168" class="Function">isProp</a> <a id="399" href="proptrunc.html#383" class="Bound">P</a> <a id="401" class="Symbol">→</a> <a id="403" class="Symbol">(</a><a id="404" href="proptrunc.html#381" class="Bound">X</a> <a id="406" class="Symbol">→</a> <a id="408" href="proptrunc.html#383" class="Bound">P</a><a id="409" class="Symbol">)</a> <a id="411" class="Symbol">→</a> <a id="413" href="proptrunc.html#200" class="Function Operator">∥</a> <a id="415" href="proptrunc.html#381" class="Bound">X</a> <a id="417" href="proptrunc.html#200" class="Function Operator">∥</a> <a id="419" class="Symbol">→</a> <a id="421" href="proptrunc.html#383" class="Bound">P</a>
<a id="423" href="proptrunc.html#371" class="Function">∥∥-rec</a> <a id="430" class="Symbol">{</a><a id="431" href="proptrunc.html#431" class="Bound">X</a><a id="432" class="Symbol">}</a> <a id="434" class="Symbol">{</a><a id="435" href="proptrunc.html#435" class="Bound">P</a><a id="436" class="Symbol">}</a> <a id="438" href="proptrunc.html#438" class="Bound">isp</a> <a id="442" href="proptrunc.html#442" class="Bound">φ</a> <a id="444" href="proptrunc.html#444" class="Bound">s</a> <a id="446" class="Symbol">=</a> <a id="448" href="proptrunc.html#444" class="Bound">s</a> <a id="450" href="prop.html#1423" class="Function Operator">₍</a> <a id="452" href="proptrunc.html#435" class="Bound">P</a> <a id="454" href="prop.html#1423" class="Function Operator">,</a> <a id="456" href="proptrunc.html#438" class="Bound">isp</a> <a id="460" href="prop.html#1423" class="Function Operator">₎</a> <a id="462" href="proptrunc.html#442" class="Bound">φ</a>

<a id="∥∥-rec-comp"></a><a id="465" href="proptrunc.html#465" class="Function">∥∥-rec-comp</a> <a id="477" class="Symbol">:</a> <a id="479" class="Symbol">{</a><a id="480" href="proptrunc.html#480" class="Bound">X</a> <a id="482" href="proptrunc.html#482" class="Bound">P</a> <a id="484" class="Symbol">:</a> <a id="486" href="preliminaries.html#254" class="Function">U</a><a id="487" class="Symbol">}</a> <a id="489" class="Symbol">→</a> <a id="491" class="Symbol">{</a><a id="492" href="proptrunc.html#492" class="Bound">isp</a> <a id="496" class="Symbol">:</a> <a id="498" href="isprop.html#168" class="Function">isProp</a> <a id="505" href="proptrunc.html#482" class="Bound">P</a><a id="506" class="Symbol">}</a> <a id="508" class="Symbol">(</a><a id="509" href="proptrunc.html#509" class="Bound">φ</a> <a id="511" class="Symbol">:</a> <a id="513" href="proptrunc.html#480" class="Bound">X</a> <a id="515" class="Symbol">→</a> <a id="517" href="proptrunc.html#482" class="Bound">P</a><a id="518" class="Symbol">)</a> <a id="520" class="Symbol">(</a><a id="521" href="proptrunc.html#521" class="Bound">x</a> <a id="523" class="Symbol">:</a> <a id="525" href="proptrunc.html#480" class="Bound">X</a><a id="526" class="Symbol">)</a> <a id="528" class="Symbol">→</a>  <a id="531" href="proptrunc.html#371" class="Function">∥∥-rec</a> <a id="538" href="proptrunc.html#492" class="Bound">isp</a> <a id="542" href="proptrunc.html#509" class="Bound">φ</a> <a id="544" href="proptrunc.html#324" class="Function Operator">∣</a> <a id="546" href="proptrunc.html#521" class="Bound">x</a> <a id="548" href="proptrunc.html#324" class="Function Operator">∣</a> <a id="550" href="preliminaries.html#1472" class="Datatype Operator">≡</a> <a id="552" href="proptrunc.html#509" class="Bound">φ</a> <a id="554" href="proptrunc.html#521" class="Bound">x</a>
<a id="556" href="proptrunc.html#465" class="Function">∥∥-rec-comp</a> <a id="568" href="proptrunc.html#568" class="Bound">φ</a> <a id="570" href="proptrunc.html#570" class="Bound">x</a> <a id="572" class="Symbol">=</a> <a id="574" href="preliminaries.html#1524" class="InductiveConstructor">refl</a><a id="578" class="Symbol">(</a><a id="579" href="proptrunc.html#568" class="Bound">φ</a> <a id="581" href="proptrunc.html#570" class="Bound">x</a><a id="582" class="Symbol">)</a>

<a id="585" class="Comment">-- To have that ∥ X ∥ is a proposition we need function extensionality.</a>
<a id="657" class="Comment">-- In fact, assuming that ∥ X ∥ is a proposition gives function extensionality</a>
<a id="736" class="Comment">-- (proof omitted here).</a>

<a id="762" class="Keyword">open</a> <a id="767" class="Keyword">import</a> <a id="774" href="funext.html" class="Module">funext</a>

<a id="∥∥-isProp"></a><a id="782" href="proptrunc.html#782" class="Function">∥∥-isProp</a> <a id="792" class="Symbol">:</a> <a id="794" class="Symbol">{</a><a id="795" href="proptrunc.html#795" class="Bound">X</a> <a id="797" class="Symbol">:</a> <a id="799" href="preliminaries.html#254" class="Function">U</a><a id="800" class="Symbol">}</a> <a id="802" class="Symbol">→</a> <a id="804" href="isprop.html#168" class="Function">isProp</a> <a id="811" href="proptrunc.html#200" class="Function Operator">∥</a> <a id="813" href="proptrunc.html#795" class="Bound">X</a> <a id="815" href="proptrunc.html#200" class="Function Operator">∥</a>
<a id="817" href="proptrunc.html#782" class="Function">∥∥-isProp</a> <a id="827" href="proptrunc.html#827" class="Bound">s</a> <a id="829" href="proptrunc.html#829" class="Bound">t</a> <a id="831" class="Symbol">=</a> <a id="833" href="proptrunc.html#848" class="Function">lemma</a>
 <a id="840" class="Keyword">where</a>
  <a id="848" href="proptrunc.html#848" class="Function">lemma</a> <a id="854" class="Symbol">:</a> <a id="856" href="proptrunc.html#827" class="Bound">s</a> <a id="858" href="preliminaries.html#1472" class="Datatype Operator">≡</a> <a id="860" href="proptrunc.html#829" class="Bound">t</a>
  <a id="864" href="proptrunc.html#848" class="Function">lemma</a> <a id="870" class="Symbol">=</a> <a id="872" href="funext.html#88" class="Postulate">funext</a> <a id="879" class="Symbol">(λ</a> <a id="882" href="proptrunc.html#882" class="Bound">p</a> <a id="884" class="Symbol">→</a> <a id="886" href="funext.html#88" class="Postulate">funext</a> <a id="893" class="Symbol">(λ</a> <a id="896" href="proptrunc.html#896" class="Bound">f</a> <a id="898" class="Symbol">→</a> <a id="900" href="prop.html#1615" class="Function">holdsIsProp</a> <a id="912" href="proptrunc.html#882" class="Bound">p</a> <a id="914" class="Symbol">(</a><a id="915" href="proptrunc.html#827" class="Bound">s</a> <a id="917" href="proptrunc.html#882" class="Bound">p</a> <a id="919" href="proptrunc.html#896" class="Bound">f</a><a id="920" class="Symbol">)</a> <a id="922" class="Symbol">(</a><a id="923" href="proptrunc.html#829" class="Bound">t</a> <a id="925" href="proptrunc.html#882" class="Bound">p</a> <a id="927" href="proptrunc.html#896" class="Bound">f</a><a id="928" class="Symbol">)))</a>

<a id="933" class="Comment">-- Induction on ∥ X ∥ follows from recursion:</a>

<a id="∥∥-ind"></a><a id="980" href="proptrunc.html#980" class="Function">∥∥-ind</a> <a id="987" class="Symbol">:</a> <a id="989" class="Symbol">{</a><a id="990" href="proptrunc.html#990" class="Bound">X</a> <a id="992" class="Symbol">:</a> <a id="994" href="preliminaries.html#254" class="Function">U</a><a id="995" class="Symbol">}</a> <a id="997" class="Symbol">{</a><a id="998" href="proptrunc.html#998" class="Bound">P</a> <a id="1000" class="Symbol">:</a> <a id="1002" href="proptrunc.html#200" class="Function Operator">∥</a> <a id="1004" href="proptrunc.html#990" class="Bound">X</a> <a id="1006" href="proptrunc.html#200" class="Function Operator">∥</a> <a id="1008" class="Symbol">→</a> <a id="1010" href="preliminaries.html#254" class="Function">U</a><a id="1011" class="Symbol">}</a> <a id="1013" class="Symbol">→</a> <a id="1015" class="Symbol">((</a><a id="1017" href="proptrunc.html#1017" class="Bound">s</a> <a id="1019" class="Symbol">:</a> <a id="1021" href="proptrunc.html#200" class="Function Operator">∥</a> <a id="1023" href="proptrunc.html#990" class="Bound">X</a> <a id="1025" href="proptrunc.html#200" class="Function Operator">∥</a><a id="1026" class="Symbol">)</a> <a id="1028" class="Symbol">→</a> <a id="1030" href="isprop.html#168" class="Function">isProp</a><a id="1036" class="Symbol">(</a><a id="1037" href="proptrunc.html#998" class="Bound">P</a> <a id="1039" href="proptrunc.html#1017" class="Bound">s</a><a id="1040" class="Symbol">))</a>
       <a id="1050" class="Symbol">→</a> <a id="1052" class="Symbol">((</a><a id="1054" href="proptrunc.html#1054" class="Bound">x</a> <a id="1056" class="Symbol">:</a> <a id="1058" href="proptrunc.html#990" class="Bound">X</a><a id="1059" class="Symbol">)</a> <a id="1061" class="Symbol">→</a> <a id="1063" href="proptrunc.html#998" class="Bound">P</a> <a id="1065" href="proptrunc.html#324" class="Function Operator">∣</a> <a id="1067" href="proptrunc.html#1054" class="Bound">x</a> <a id="1069" href="proptrunc.html#324" class="Function Operator">∣</a><a id="1070" class="Symbol">)</a> <a id="1072" class="Symbol">→</a>  <a id="1075" class="Symbol">(</a><a id="1076" href="proptrunc.html#1076" class="Bound">s</a> <a id="1078" class="Symbol">:</a> <a id="1080" href="proptrunc.html#200" class="Function Operator">∥</a> <a id="1082" href="proptrunc.html#990" class="Bound">X</a> <a id="1084" href="proptrunc.html#200" class="Function Operator">∥</a><a id="1085" class="Symbol">)</a> <a id="1087" class="Symbol">→</a> <a id="1089" href="proptrunc.html#998" class="Bound">P</a> <a id="1091" href="proptrunc.html#1076" class="Bound">s</a>
<a id="1093" href="proptrunc.html#980" class="Function">∥∥-ind</a> <a id="1100" class="Symbol">{</a><a id="1101" href="proptrunc.html#1101" class="Bound">X</a><a id="1102" class="Symbol">}</a> <a id="1104" class="Symbol">{</a><a id="1105" href="proptrunc.html#1105" class="Bound">P</a><a id="1106" class="Symbol">}</a> <a id="1108" href="proptrunc.html#1108" class="Bound">isp</a> <a id="1112" href="proptrunc.html#1112" class="Bound">φ</a> <a id="1114" href="proptrunc.html#1114" class="Bound">s</a> <a id="1116" class="Symbol">=</a> <a id="1118" href="proptrunc.html#371" class="Function">∥∥-rec</a> <a id="1125" class="Symbol">(</a><a id="1126" href="proptrunc.html#1108" class="Bound">isp</a> <a id="1130" href="proptrunc.html#1114" class="Bound">s</a><a id="1131" class="Symbol">)</a> <a id="1133" href="proptrunc.html#1146" class="Function">ψ</a> <a id="1135" href="proptrunc.html#1114" class="Bound">s</a>
 <a id="1138" class="Keyword">where</a>
  <a id="1146" href="proptrunc.html#1146" class="Function">ψ</a> <a id="1148" class="Symbol">:</a> <a id="1150" href="proptrunc.html#1101" class="Bound">X</a> <a id="1152" class="Symbol">→</a> <a id="1154" href="proptrunc.html#1105" class="Bound">P</a> <a id="1156" href="proptrunc.html#1114" class="Bound">s</a>
  <a id="1160" href="proptrunc.html#1146" class="Function">ψ</a> <a id="1162" href="proptrunc.html#1162" class="Bound">x</a> <a id="1164" class="Symbol">=</a> <a id="1166" href="preliminaries.html#2002" class="Function">transport</a> <a id="1176" href="proptrunc.html#1105" class="Bound">P</a> <a id="1178" class="Symbol">(</a><a id="1179" href="proptrunc.html#782" class="Function">∥∥-isProp</a> <a id="1189" href="proptrunc.html#324" class="Function Operator">∣</a> <a id="1191" href="proptrunc.html#1162" class="Bound">x</a> <a id="1193" href="proptrunc.html#324" class="Function Operator">∣</a> <a id="1195" href="proptrunc.html#1114" class="Bound">s</a><a id="1196" class="Symbol">)</a> <a id="1198" class="Symbol">(</a><a id="1199" href="proptrunc.html#1112" class="Bound">φ</a> <a id="1201" href="proptrunc.html#1162" class="Bound">x</a><a id="1202" class="Symbol">)</a>

<a id="1205" class="Comment">-- There should be a way of defining ∥∥-ind so that its computation</a>
<a id="1273" class="Comment">-- rule holds definitionally (using the ideas of the file hsetfunext</a>
<a id="1342" class="Comment">-- elsewhere). For the above definition, it only holds</a>
<a id="1397" class="Comment">-- propositionally:</a>

<a id="∥∥-ind-comp"></a><a id="1418" href="proptrunc.html#1418" class="Function">∥∥-ind-comp</a> <a id="1430" class="Symbol">:</a> <a id="1432" class="Symbol">{</a><a id="1433" href="proptrunc.html#1433" class="Bound">X</a> <a id="1435" class="Symbol">:</a> <a id="1437" href="preliminaries.html#254" class="Function">U</a><a id="1438" class="Symbol">}</a> <a id="1440" class="Symbol">{</a><a id="1441" href="proptrunc.html#1441" class="Bound">P</a> <a id="1443" class="Symbol">:</a> <a id="1445" href="proptrunc.html#200" class="Function Operator">∥</a> <a id="1447" href="proptrunc.html#1433" class="Bound">X</a> <a id="1449" href="proptrunc.html#200" class="Function Operator">∥</a> <a id="1451" class="Symbol">→</a> <a id="1453" href="preliminaries.html#254" class="Function">U</a><a id="1454" class="Symbol">}</a> <a id="1456" class="Symbol">(</a><a id="1457" href="proptrunc.html#1457" class="Bound">isp</a> <a id="1461" class="Symbol">:</a> <a id="1463" class="Symbol">(</a><a id="1464" href="proptrunc.html#1464" class="Bound">s</a> <a id="1466" class="Symbol">:</a> <a id="1468" href="proptrunc.html#200" class="Function Operator">∥</a> <a id="1470" href="proptrunc.html#1433" class="Bound">X</a> <a id="1472" href="proptrunc.html#200" class="Function Operator">∥</a><a id="1473" class="Symbol">)</a> <a id="1475" class="Symbol">→</a> <a id="1477" href="isprop.html#168" class="Function">isProp</a><a id="1483" class="Symbol">(</a><a id="1484" href="proptrunc.html#1441" class="Bound">P</a> <a id="1486" href="proptrunc.html#1464" class="Bound">s</a><a id="1487" class="Symbol">))</a> <a id="1490" class="Symbol">(</a><a id="1491" href="proptrunc.html#1491" class="Bound">φ</a> <a id="1493" class="Symbol">:</a> <a id="1495" class="Symbol">(</a><a id="1496" href="proptrunc.html#1496" class="Bound">x</a> <a id="1498" class="Symbol">:</a> <a id="1500" href="proptrunc.html#1433" class="Bound">X</a><a id="1501" class="Symbol">)</a> <a id="1503" class="Symbol">→</a> <a id="1505" href="proptrunc.html#1441" class="Bound">P</a> <a id="1507" href="proptrunc.html#324" class="Function Operator">∣</a> <a id="1509" href="proptrunc.html#1496" class="Bound">x</a> <a id="1511" href="proptrunc.html#324" class="Function Operator">∣</a><a id="1512" class="Symbol">)</a>
            <a id="1526" class="Symbol">→</a> <a id="1528" class="Symbol">(</a><a id="1529" href="proptrunc.html#1529" class="Bound">x</a> <a id="1531" class="Symbol">:</a> <a id="1533" href="proptrunc.html#1433" class="Bound">X</a><a id="1534" class="Symbol">)</a> <a id="1536" class="Symbol">→</a> <a id="1538" href="proptrunc.html#980" class="Function">∥∥-ind</a> <a id="1545" href="proptrunc.html#1457" class="Bound">isp</a> <a id="1549" href="proptrunc.html#1491" class="Bound">φ</a> <a id="1551" href="proptrunc.html#324" class="Function Operator">∣</a> <a id="1553" href="proptrunc.html#1529" class="Bound">x</a> <a id="1555" href="proptrunc.html#324" class="Function Operator">∣</a> <a id="1557" href="preliminaries.html#1472" class="Datatype Operator">≡</a> <a id="1559" href="proptrunc.html#1491" class="Bound">φ</a> <a id="1561" href="proptrunc.html#1529" class="Bound">x</a>
<a id="1563" href="proptrunc.html#1418" class="Function">∥∥-ind-comp</a> <a id="1575" href="proptrunc.html#1575" class="Bound">isp</a> <a id="1579" href="proptrunc.html#1579" class="Bound">φ</a> <a id="1581" href="proptrunc.html#1581" class="Bound">x</a> <a id="1583" class="Symbol">=</a> <a id="1585" href="proptrunc.html#1575" class="Bound">isp</a> <a id="1589" href="proptrunc.html#324" class="Function Operator">∣</a> <a id="1591" href="proptrunc.html#1581" class="Bound">x</a> <a id="1593" href="proptrunc.html#324" class="Function Operator">∣</a> <a id="1595" class="Symbol">(</a><a id="1596" href="proptrunc.html#980" class="Function">∥∥-ind</a> <a id="1603" href="proptrunc.html#1575" class="Bound">isp</a> <a id="1607" href="proptrunc.html#1579" class="Bound">φ</a> <a id="1609" href="proptrunc.html#324" class="Function Operator">∣</a> <a id="1611" href="proptrunc.html#1581" class="Bound">x</a> <a id="1613" href="proptrunc.html#324" class="Function Operator">∣</a><a id="1614" class="Symbol">)</a> <a id="1616" class="Symbol">(</a><a id="1617" href="proptrunc.html#1579" class="Bound">φ</a> <a id="1619" href="proptrunc.html#1581" class="Bound">x</a><a id="1620" class="Symbol">)</a>
</pre></body></html>