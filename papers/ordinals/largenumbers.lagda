Martin Escardo, February 2015. 
Comment at the very end updated 11 Mar 2015.

Perhaps just for amusement.

We define the rather large number 

   2^2^2^2^2^2^2^2^2^2 : â„• 

without using any kind of recursion. 
-----------------------------------

Of course, this can be done with a program of length
2^2^2^2^2^2^2^2^2^2 multiplied by a small factor. 

The program succ(succ(succ(...succ(zero)))) will do, by suitably
expanding the dots.

Our program below is shorter.

If you have problems with unicode, read this here instead:
 http://www.cs.bham.ac.uk/~mhe/papers/ordinals/largenumbers.html
 http://www.cs.bham.ac.uk/~mhe/papers/ordinals/largenumbers.lagda

We use Agda, but what we do takes place in its GÓ§del's system T
fragment. In fact, this works in system T *without* the primitive
recursion (or iteration) combinator.

We need a natural numbers type â„• with zero and successor and nothing
else, in particular no primitive recursion.

So we may work in the simply typed lambda calculus with a base type â„•
and constants zero:â„• and succ:â„•â†’â„•.

The only way to do recursion in Agda is via pattern matching. We
disable it, to be sure:

\begin{code}

{-# OPTIONS --no-pattern-matching #-}

U = Set

data â„• : U where
 zero : â„•
 succ : â„• â†’ â„•

\end{code}

The idea is to work with Church encodings of natural numbers. But the
goal, as stated above, is to define an element of â„•. 

Church encodings will help us to avoid recursion in defining such an
element of â„•.

But we have a problem. Schwichtenberg (1976) showed that the
exponential function is not definable in the simply typed lambda
calculus via Church encodings. 

Only polynomials can be defined.

   Helmut Schwichtenberg. "Functions definable in the simply typed
   lambda calculus". Arch. Math. Logik 17 (1976). English translation
   by RJ Irwin. http://www.cis.syr.edu/~royer/htc96/fdst.ps

We have to side-step this.

Schwichtenberg considers a base type o, and his Church encodings live
in the type (o â†’ o) â†’ (o â†’ o).

We instead consider Church encodings based on an arbitrary type X.

\begin{code}

N : U â†’ U
N X = (X â†’ X) â†’ (X â†’ X)

ðŸ˜ ðŸ™ ðŸš ðŸ› : {X : U} â†’ N X
ðŸ˜ = Î» s z â†’ z
ðŸ™ = Î» s z â†’ s z
ðŸš = Î» s z â†’ s(s z)
ðŸ› = Î» s z â†’ s(s(s z))

suc : {X : U} â†’ N X â†’ N X
suc n = Î» s z â†’ s(n s z)

add : {X : U} â†’ N X â†’ N X â†’ N X
add m n = Î» s z â†’ m s (n s z)

\end{code}

The following codes primitive recursion without using any kind of
recursion:

\begin{code}

N-rec : {X : U} â†’ (X â†’ X) â†’ X â†’ (N X â†’ X)
N-rec s z m = m s z

\end{code}

We live with Schwichtenberg's result by giving different types to
multiplication and exponentiation. The idea is to iterate N when our
definitions don't type check, to make them type check:

\begin{code}

mul : {X : U} â†’ N X â†’ N(N X) â†’ N X
mul m = N-rec (add m) ðŸ˜

exp : {X : U} â†’ N(N X) â†’ N(N X) â†’ N X
exp n = N-rec (Î» m â†’ mul m n) ðŸ™ 

\end{code}

We defined exp in several steps to try to be understandable. Expanding
the definitions, we get the direct, shorter, equivalent definition,
which we include for the record:

\begin{code}

exp' : {X : U} â†’ N(N X) â†’ N(N X) â†’ N X
exp' n l = l (Î» m â†’ n (Î» k s z â†’ m s (k s z)) (Î» s z â†’ z)) (Î» s z â†’ s z)

\end{code}

We can now define our promised large natural number with a short
expression, without using recursion:

\begin{code}

decode : N â„• â†’ â„•
decode = N-rec succ zero

large : â„•
large = decode
 (exp ðŸš 
 (exp ðŸš 
 (exp ðŸš 
 (exp ðŸš 
 (exp ðŸš 
 (exp ðŸš 
 (exp ðŸš 
 (exp ðŸš 
 (exp ðŸš 
      ðŸš)))))))))

\end{code}

Oh, but the above is cheating! There are implicit parameters. In fact,
if I write "?" for them and then I automatically fill them with
ctrl-c-ctrl-a, I get a 28000-line agda file. The implicit parameters
are rather large!

Very big, but still much smaller than 2^2^2^2^2^2^2^2^2^2.

However, we can make the implicit parameters small, 
using our "macro" N:

\begin{code}

large' : â„•
large' = decode
 (exp{â„•}                        (ðŸš{N â„•}) 
 (exp{N â„•}                      (ðŸš{N(N â„•)}) 
 (exp{N(N â„•)}                   (ðŸš{N(N(N â„•))}) 
 (exp{N(N(N â„•))}                (ðŸš{N(N(N(N â„•)))}) 
 (exp{N(N(N(N â„•)))}             (ðŸš{N(N(N(N(N â„•))))}) 
 (exp{N(N(N(N(N â„•))))}          (ðŸš{N(N(N(N(N(N â„•)))))}) 
 (exp{N(N(N(N(N(N â„•)))))}       (ðŸš{N(N(N(N(N(N(N â„•))))))}) 
 (exp{N(N(N(N(N(N(N â„•))))))}    (ðŸš{N(N(N(N(N(N(N(N â„•)))))))}) 
 (exp{N(N(N(N(N(N(N(N â„•)))))))} (ðŸš{N(N(N(N(N(N(N(N(N â„•))))))))}) 
                                 (ðŸš{N(N(N(N(N(N(N(N(N â„•))))))))}))
 )))))))) 

\end{code}

This illustrates that, when we regard our definitions as taking place
in system T, the definition of exp is not uniform. In the definition
of "large", each occurrence of exp has a different type.

Of course, don't try to run the above. For testing, we need to be more
modest:

\begin{code}

number16 : â„•
number16 = decode(exp ðŸš (exp ðŸš ðŸš))

number81 : â„•
number81 = decode(exp ðŸ› (exp ðŸš ðŸš))

number1296 : â„•
number1296 = decode(mul(exp ðŸš (exp ðŸš ðŸš))(exp ðŸ› (exp ðŸš ðŸš)))

{-# BUILTIN NATURAL â„• #-}

\end{code}

You should get 16, 81, and 1296 when you normalize the above terms
with ctrl-c-ctrl-n.

I decided to write the above because some people were asking me about
ordinals in system T, following
http://www.cs.bham.ac.uk/~mhe/papers/ordinals/ordinals.html

What happens there is similar: we can get as close as we wish to the
ordinal Îµâ‚€ in system T without using recursion on ordinals (but this
time using recursion on â„•). Of course, this is not my own finding.

I thought it would be worthwhile to abstract away from the ordinals,
and show how this works for â„•, as explained above.

However, the above use of iterations of N may be new (but I can't be
sure).

To the last parenthetical remark, Aaron Stump added the following
comment in the Agda mailing list, Wed Mar 11 02:29:56 CET 2015
https://lists.chalmers.se/pipermail/agda/2015/007591.html

> I believe you can find this (presented quite a bit more densely!) in
> some papers by Daniel Leivant, exploring the computational power of
> predicative polymorphism.  See his CMU tech report "Finitely
> stratified polymorphism" (there is also a LICS '89 paper on the
> topic): http://repository.cmu.edu/compsci/1962/

Indeed, the definition of our N is at the top of page 6. 
